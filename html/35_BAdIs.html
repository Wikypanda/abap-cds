<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>35 BAdIs - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>Enhancements Using BAdIs</h1>
<ul>
<li><a href="#enhancements-using-badis">Enhancements Using BAdIs</a></li>
<li><a href="#about-badis">About BAdIs</a></li>
<li><a href="#excursion-demo-badis">Excursion: Demo BAdIs</a></li>
<li><a href="#demo-badi-1">Demo BAdI 1</a></li>
<li><a href="#demo-badi-2">Demo BAdI 2</a></li>
<li><a href="#badi-related-abap-statements">BAdI-Related ABAP Statements</a></li>
<li><a href="#more-information">More Information</a></li>
<li><a href="#executable-example">Executable Example</a></li>
</ul>
<p>This ABAP cheat sheet provides a high-level overview of enhancements using BAdIs (Business Add-Ins), focusing on the relevant ABAP syntax. The cheat sheet aims to illustrate the BAdI-related ABAP syntax with simple, self-created demo BAdIs, whose creation is outlined in tutorial-style descriptions. The descriptions are intended to get an idea about the involved repository objects. The repository objects serve as the basis for the ABAP example code snippets in the <a href="#executable-example">executable example</a>.</p>
<h2>About BAdIs</h2>
<ul>
<li>A BAdI (Business Add-In) is an object-oriented option for enhancing the functionality of standard SAP or custom objects without modifying the underlying source code.</li>
<li>These enhancement options are realized through specific positions (enhancement spots) that allow custom logic to be plugged in and executed at runtime.</li>
<li>A BAdI mainly consists of a BAdI interface, which describes a template for BAdI objects.</li>
<li>The BAdI interface includes the specification of the predefined tag interface <code>IF_BADI_INTERFACE</code> and the declaration of BAdI methods. Note that the specification of variable attributes is not allowed.</li>
<li>BAdIs offer various properties to be set, including:</li>
<li>Filter conditions</li>
<li>You can specify one or more filters by providing filter names and associating them with basic data types (for example, integer or string).</li>
<li>This allows you to select different BAdI implementations. Callers can dynamically control which BAdI implementations are used by specifying filter values.</li>
<li>Defining a BAdI for single or multiple use</li>
<li>Single use: Only one BAdI implementation can be used in an internal session, and one BAdI implementation must be available for each use. In single-use cases, it is advisable to specify and implement an optional fallback class for scenarios where no (default) BAdI implementation is found or when the caller-provided filter values do not match existing filters.</li>
<li>Multiple use: Multiple BAdI implementations (or none) can exist in parallel, enabling the execution of multiple functionalities. Note the restrictions with this setting: The parameter interface of a BAdI method can only contain importing and changing parameters. With multiple functionalities being executed, there cannot be a single output parameter. Changing parameters incorporate input and output parameter capabilities, allowing an existing data object to be altered sequentially.</li>
<li>Instantiation-related properties such as new instance creation, instance reuse, or context-dependent instantiation</li>
<li>With the first two properties set, BAdIs are considered context-free.</li>
<li>Context-dependent instantiation means the context controls instantiation, allowing only one instance for each context and implementing class. The prerequisite is the implementation of the tag interface <code>IF_BADI_CONTEXT</code>. The <code>GET BADI</code> statement includes the <code>CONTEXT</code> addition relevant here, which is not covered in this cheat sheet. Refer to the documentation for more details.</li>
<li>BAdI methods are implemented in BAdI implementations.</li>
<li>A BAdI implementation consists of a BAdI implementation class whose instances enhance the functionality at runtime in the form of an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_plugin_glosry.htm">object plug-in</a>.</li>
<li>Multiple BAdI implementations can be assigned to a single BAdI.</li>
<li>BAdI methods can be called in ABAP programs exclusively via the associated BAdI object using <code>CALL BADI</code>.</li>
</ul>
<div class="note">[!NOTE] The steps and syntax outlined in this document focus on the kernel-based BAdI concept, which integrates BAdIs in ABAP for use with related ABAP syntax. Legacy classic BAdIs found in [classic ABAP](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclassic_abap_glosry.htm) are not covered here. In classic ABAP, the BAdI builder, an ABAP Workbench tool, allows you to create and maintain BAdIs. The relevant transaction codes are `SE18` for defining and `SE19` for implementing BAdIs.</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursion: Demo BAdIs</h2>
<p>This section provides high-level walkthrough examples to illustrate simplified BAdIs, which serve as examples for demonstrating the ABAP syntax below. The step-by-step procedure includes the creation of the following elements:</p>
<ul>
<li><strong>BAdI enhancement spot</strong></li>
<li>Serves as a container for the BAdI.</li>
<li><strong>BAdI</strong> (<strong>BAdI definition</strong>)</li>
<li>For a BAdI, you create a BAdI definition and provide a dedicated BAdI name.</li>
<li>This BAdI must be assigned to the BAdI enhancement spot.</li>
<li>Various properties can be set, such as filter conditions, single/multiple use, and more. Optionally, you can specify and implement a fallback class.</li>
<li><strong>BAdI interface</strong></li>
<li>For the BAdI, you create a BAdI interface to define the BAdI methods that implementation classes will implement.</li>
<li>The BAdI Interface must include the interface <code>IF_BADI_INTERFACE</code>.</li>
<li><strong>BAdI enhancement implementation</strong></li>
<li>Since the BAdI only defines an interface, you need a class that implements the interface to create a usable BAdI object.</li>
<li>The BAdI enhancement implementation serves as a container for <strong>BAdI implementations</strong>.</li>
<li>You can include multiple BAdI implementations, all assigned to a specific BAdI enhancement spot.</li>
<li>A <strong>BAdI implementation</strong> specifies properties such as the <strong>BAdI implementation class</strong>, which contains the concrete code enhancement, and filter combinations (that is, when a specific filter is selected, BAdI methods in that specific BAdI implementation class should be called).</li>
</ul>
<div class="note">[!NOTE] - The demo BAdIs outlined here are simplified and non-semantic, intended solely for [exploration, experimentation, and demonstration](./README.md#%EF%B8%8F-disclaimer). They represent self-contained examples on which the BAdI-related ABAP statements are illustrated in the cheat sheet. - As a prerequisite for walking through the creation steps, you have: - Opened ADT. - Logged in to your SAP BTP ABAP Environment. - Created a package for demo content. - The procedure description includes the creation of demo repository objects step by step . You can also create objects separately and add them to the appropriate fields on the maintenance screens in ADT. - When enhancing existing functionality, you need to know about the existing BAdI and proceed with creating custom implementations.</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Demo BAdI 1</h3>
<div class="note">[!NOTE] - Context of the simplified demo: Calculations - The example includes subtraction and division (and addition provided through a fallback class). The idea is that the functionality can be enhanced by, for example, introducing additional calculation options, such as multiplication. - The BAdI is defined for single use, including filter conditions and a fallback class.</div>
<ul>
<li>In your ADT package, create a new object by right-clicking the package, selecting <em>New</em>, then <em>Other ABAP Repository Object</em>, and filtering for <em>BAdI Enhancement Spot</em>.</li>
<li>Name the enhancement spot _ZES_DEMO_ABAP_CALCULATOR_ and provide a description.</li>
<li>Follow the wizard prompts. Once created, the <em>BAdI Enhancement Spot</em> screen will display.</li>
</ul>
<ol>
</ol>
<ul>
<li>With the BAdI enhancement spot repository object open from the previous step, you are on the <em>BAdI Enhancement Spot</em> screen.</li>
<li>Choose the <em>Add BAdI</em> button.</li>
<li>In the <em>New BAdI Definition</em> pop-up, enter the name _ZBADI_DEMO_ABAP_CALCULATOR_ and confirm.</li>
<li>The new BAdI will appear in the <em>BAdI Definitions</em> list on the left. A message will prompt you to add an interface to the BAdI.</li>
<li>Set the following options:</li>
<li>Deselect the <em>Multiple Use</em> checkbox, as the BAdI is intended for single use.</li>
<li>Select the <em>Use Fallback</em> checkbox.</li>
<li>Leave other settings unchanged (the <em>Interface</em> and <em>Fallback Class</em> fields will be covered later).</li>
<li>Choose the <em>Add Filter</em> button.</li>
<li>In the <em>New Filter</em> pop-up, enter the name <em>OPERATOR</em> and choose <em>Add</em>.</li>
<li>The filter will appear below the _ZBADI_DEMO_ABAP_CALCULATOR_ node on the left.</li>
<li>In the <em>Filter Details</em> on the right, select <em>Character</em> as the <em>Type</em>. Additional settings, like value checks, are not included in this example.</li>
<li>Return to the detail view for _ZBADI_DEMO_ABAP_CALCULATOR_ by selecting the _ZBADI_DEMO_ABAP_CALCULATOR_ node on the left.</li>
</ul>
<ol>
</ol>
<ul>
<li>The BAdI enhancement spot repository object is opened.</li>
<li>Choose the <em>Interface</em> link in the <em>Configuration</em> section.</li>
<li>Follow the <em>New ABAP Interface</em> wizard:</li>
<li>Enter the name _ZIF_DEMO_ABAP_CALCULATOR_ and provide a description.</li>
<li>Ensure that the interface _IF_BADI_INTERFACE_ is included in the <em>Interfaces</em> list.</li>
<li>Add the following code and activate the interface. Note that the returning parameter in this simplified example is intentionally typed as <code>string</code>, and an appropriate error handling is not covered.</li>
</ul>
<ol>
</ol>
<pre><code>      INTERFACE zif_demo_abap_calculator
        PUBLIC .

        INTERFACES if_badi_interface .

        METHODS calculate IMPORTING num1          TYPE i
                                    num2          TYPE i
                          RETURNING VALUE(result) TYPE string.
      ENDINTERFACE.</code></pre>
<ul>
<li>The BAdI enhancement spot repository object is opened.</li>
<li>Choose the <em>Fallback Class</em> link in the <em>Configuration</em> section.</li>
<li>Walk through the <em>New ABAP Class</em> wizard:</li>
<li>Enter the name _ZCL_DEMO_ABAP_CALCULATOR_FB_ and add a description.</li>
<li>Ensure the interface _ZIF_DEMO_ABAP_CALCULATOR_ is included in the <em>Interfaces</em> list.</li>
<li>Add the following code and activate the class.</li>
</ul>
<ol>
</ol>
<pre><code>       CLASS zcl_demo_abap_calculator_fb DEFINITION
         PUBLIC
         FINAL
         CREATE PUBLIC .

         PUBLIC SECTION.

           INTERFACES if_badi_interface .
           INTERFACES zif_demo_abap_calculator .
         PROTECTED SECTION.
         PRIVATE SECTION.
       ENDCLASS.



       CLASS zcl_demo_abap_calculator_fb IMPLEMENTATION.
         METHOD zif_demo_abap_calculator~calculate.
           result = |{ num1 + num2 }|.
         ENDMETHOD.
       ENDCLASS.</code></pre>
<ul>
<li>Ensure you activate the BAdI if you have not done already.</li>
<li>In your package in ADT, find the enhancement spot _ZES_DEMO_ABAP_CALCULATOR_. Right-click it and select <em>New BAdI Enhancement Implementation</em>.</li>
<li>Follow the <em>New BAdI Enhancement Implementation</em> wizard:</li>
<li>Add a suitable target package.</li>
<li>Enter the name _ZEI_DEMO_ABAP_CALCULATOR_ and a description.</li>
<li>Ensure the enhancement spot _ZES_DEMO_ABAP_CALCULATOR_ is included in the <em>Enhancement Spot</em> input field.</li>
<li>After creation, the <em>BAdI Enhancement Implementation</em> screen will appear.</li>
<li>Click the <em>Add BAdI Implementation</em> button.</li>
<li>In the <em>Add BAdI Implementation</em> pop-up, enter the following:</li>
<li><em>BAdI Definition</em>: _ZBADI_DEMO_ABAP_CALCULATOR_</li>
<li><em>BAdI Implementation Name</em>: _ZBADI_IMPL_DEMO_ABAP_CALC_SUB_</li>
<li>In the <em>BAdI Implementation Details</em> section on the right, choose the <em>Implementing Class</em> link.</li>
<li>Follow the <em>New ABAP Class</em> wizard:</li>
<li>Enter the name _ZCL_DEMO_ABAP_CALCULATOR_SUBTR_ and a description.</li>
<li>Ensure the interface _ZIF_DEMO_ABAP_CALCULATOR_ is added to the Interfaces list.</li>
<li>Add the following code and activate the class:</li>
</ul>
<ol>
</ol>
<pre><code>      CLASS zcl_demo_abap_calculator_subtr DEFINITION
        PUBLIC
        FINAL
        CREATE PUBLIC .

        PUBLIC SECTION.

          INTERFACES if_badi_interface .
          INTERFACES zif_demo_abap_calculator .
        PROTECTED SECTION.
        PRIVATE SECTION.
      ENDCLASS.



      CLASS zcl_demo_abap_calculator_subtr IMPLEMENTATION.
        METHOD zif_demo_abap_calculator~calculate.
          result = |{ num1 - num2 }|.
        ENDMETHOD.
      ENDCLASS.</code></pre>
<ul>
<li>Set filter conditions</li>
<li>Back on the <em>BAdI Enhancement Implementation</em> screen, choose the <em>Add Filter Combination</em> button.</li>
<li>A <em>Filter Values</em> node should be added below the _ZBADI_IMPL_DEMO_ABAP_CALC_SUB_ node.</li>
<li>In the <em>Filter Details</em> section on the right, insert <em>-</em> (minus character) in the <em>Value</em> field.</li>
</ul>
<ul>
<li>Repeat the previous steps and create another BAdI implementation to reflect division:</li>
<li><em>BAdI Definition</em>: _ZBADI_DEMO_ABAP_CALCULATOR_</li>
<li><em>BAdI Implementation Name</em>: _ZBADI_IMPL_DEMO_ABAP_CALC_DIV_</li>
<li>Implementing class: _ZCL_DEMO_ABAP_CALCULATOR_DIV_</li>
</ul>
<ol>
</ol>
<pre><code>    CLASS zcl_demo_abap_calculator_div DEFINITION
      PUBLIC
      FINAL
      CREATE PUBLIC .

      PUBLIC SECTION.

        INTERFACES if_badi_interface .
        INTERFACES zif_demo_abap_calculator .
      PROTECTED SECTION.
      PRIVATE SECTION.
    ENDCLASS.



    CLASS zcl_demo_abap_calculator_div IMPLEMENTATION.
      METHOD zif_demo_abap_calculator~calculate.
        TRY.
            IF num1 = 0 AND num2 = 0.
              RAISE EXCEPTION TYPE cx_sy_zerodivide.
            ENDIF.

            result = |{ CONV decfloat34( num1 / num2 ) STYLE = SIMPLE }|.
          CATCH cx_sy_zerodivide.
            result = |Dividing { num1 } by { num2 } is not possible.|.
        ENDTRY.
      ENDMETHOD.
    ENDCLASS.</code></pre>
<ul>
<li><em>Filter Combination</em>/<em>Value</em>: <em>/</em></li>
</ul>
<h3>Demo BAdI 2</h3>
<div class="note">[!NOTE] - Context of the simplified demo: Data object content conversion - The example includes XML and JSON conversion. The idea is that the functionality can be enhanced by, for example, introducing additional conversion options. - The BAdI is defined for multiple use, without filter, and without fallback class. - The descriptions here are reduced. The creation follows the same steps as above (except for not specifying filter conditions and a fallback class).</div>
<ul>
<li>Create an enhancement spot with the name _ZES_DEMO_ABAP_CONVERTER_.</li>
</ul>
<ol>
</ol>
<ul>
<li>Create a BAdI definition with the name _ZBADI_DEMO_ABAP_CONVERTER_.</li>
<li>Set the following options:</li>
<li>Select the <em>Multiple use</em> checkbox since the BAdI is intended for multiple use.</li>
<li>Leave the other settings unchanged (only the <em>Interface</em> field is relevant).</li>
</ul>
<ol>
</ol>
<ul>
<li>Create a BAdI interface with the name _ZIF_DEMO_ABAP_CONVERTER_.</li>
<li>Note that only importing and changing parameters are possible here. For display purposes, the method definition includes an importing parameter having a reference to the classrun output. In the executable example class, the calling class implements the classrun and supplies a bound interface reference variable when calling the BAdI method.</li>
</ul>
<ol>
</ol>
<pre><code>        INTERFACE zif_demo_abap_converter
          PUBLIC .

          INTERFACES if_badi_interface .
          METHODS convert IMPORTING content TYPE any
                                    out     TYPE REF TO if_oo_adt_classrun_out.
        ENDINTERFACE.</code></pre>
<ul>
<li>Name: _ZEI_DEMO_ABAP_CONVERTER_</li>
<li>BAdI implementations (using the _ZBADI_DEMO_ABAP_CONVERTER_ BAdI definition)</li>
<li>_ZBADI_IMPL_DEMO_ABAP_CONV_XML_</li>
<li>Implementing class: _ZCL_DEMO_ABAP_CONVERTER_XML_</li>
<li>_ZBADI_IMPL_DEMO_ABAP_CONV_JSON_</li>
<li>Implementing class: _ZCL_DEMO_ABAP_CONVERTER_JSON_</li>
<li>Add the following code to the classes activate them:</li>
<li>_ZCL_DEMO_ABAP_CONVERTER_XML_</li>
</ul>
<ol>
</ol>
<pre><code>          CLASS zcl_demo_abap_converter_xml DEFINITION
            PUBLIC
            FINAL
            CREATE PUBLIC .

            PUBLIC SECTION.

              INTERFACES if_badi_interface .
              INTERFACES zif_demo_abap_converter .
            PROTECTED SECTION.
            PRIVATE SECTION.
          ENDCLASS.



          CLASS zcl_demo_abap_converter_xml IMPLEMENTATION.
            METHOD zif_demo_abap_converter~convert.
              IF out IS NOT BOUND.
                RETURN.
              ENDIF.

              DATA xml TYPE xstring.

              DATA(tdo) = cl_abap_typedescr=&gt;describe_by_data( content ).
              CASE TYPE OF tdo.
                WHEN TYPE cl_abap_elemdescr.
                  CALL TRANSFORMATION id
                    SOURCE var = content
                    RESULT XML xml.
                WHEN TYPE cl_abap_structdescr.
                  CALL TRANSFORMATION id
                    SOURCE structure = content
                    RESULT XML xml.
                WHEN TYPE cl_abap_tabledescr.
                  CALL TRANSFORMATION id
                    SOURCE table = content
                    RESULT XML xml.
                WHEN OTHERS.
                  RETURN.
              ENDCASE.

              DATA(xml_str) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml ).

              out-&gt;write( xml_str ).
              out-&gt;write( |\n| ).
            ENDMETHOD.
          ENDCLASS.</code></pre>
<ul>
<li>_ZCL_DEMO_ABAP_CONVERTER_JSON_</li>
</ul>
<pre><code>          CLASS zcl_demo_abap_converter_json DEFINITION
            PUBLIC
            FINAL
            CREATE PUBLIC .

            PUBLIC SECTION.

              INTERFACES if_badi_interface .
              INTERFACES zif_demo_abap_converter .
            PROTECTED SECTION.
            PRIVATE SECTION.
          ENDCLASS.



          CLASS zcl_demo_abap_converter_json IMPLEMENTATION.
            METHOD zif_demo_abap_converter~convert.
              IF out IS NOT BOUND.
                RETURN.
              ENDIF.

              DATA(json) = /ui2/cl_json=&gt;serialize( data = content
                                                    format_output = abap_true ).

              out-&gt;write( json ).
              out-&gt;write( |\n| ).    
            ENDMETHOD.
          ENDCLASS.</code></pre>
<h2>BAdI-Related ABAP Statements</h2>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Statement &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>GET BADI</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Creates a new BAdI object and assigns the BAdI reference to a BAdI reference variable.</li>
<li>If the BAdI includes filters, the addition <code>FILTERS</code> must be specified, followed by assignments. The arguments for the filters must by type-compliant. Note that filters can also be defined to use only constants. In this case, only literals and constants can be specified. If a BAdI does not define filters, the <code>FILTERS</code> addition cannot be specified.</li>
<li>After instantiation, the system searches for BAdI implementation classes. Only active classes that match the filter criteria are included in the search. If no matching implementations are found, the system will look for standard implementations. If none are found, it will use the fallback class, if available. For more information, refer to the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABAPGET_BADI.html">ABAP Keyword Documentation</a>.</li>
<li>In addition to a static variant, a dynamic variant is also available:</li>
<li>The dynamic variant uses the addition <code>TYPE</code> and requires the name of a BAdI as a character-like data object in parentheses.</li>
<li>The static type of the reference variable must be <code>CL_BADI_BASE</code>, the superclass of all BAdI classes.</li>
<li>The <code>FILTERS</code> addition can also be applied in the dynamic variant. Alternatively, the <code>FILTER-TABLE</code> addition can be used, requiring a table of type <code>badi_filter_bindings</code>. Its components are <code>name</code> (type c, length 30; the filter name should be in uppercase) and <code>value</code> (type <code>REF TO data</code>; it expects a pointer to a matching data object).</li>
<li>For context-dependent BAdIs, the <code>CONTEXT</code> addition is available. Find more details in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABAPGET_BADI.html">ABAP Keyword Documentation</a>.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>CALL BADI</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Calls a BAdI method using the BAdI reference variable.</li>
<li>Expects a list of parameters to assign actual parameters to the method&#x27;s formal parameters.</li>
<li>In addition to a static variant, a dynamic variant is available:</li>
<li>The method name comes after the BAdI reference variable, which has the static type of <code>CL_BADI_BASE</code>, and is specified as a character-like data object within parentheses.</li>
<li>You can either specify the parameter list as in static statements or use a parameter table with <code>PARAMETER-TABLE</code>, which requires a table of type <code>abap_parmbind_tab</code> (refer to dynamic <code>CALL METHOD</code> statements in the <em>Dynamic Programming</em> cheat sheet).</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<div class="note">[!NOTE] Note that various catchable exceptions can be raised when using `GET BADI` and `CALL BADI`.</div>
<p>The following snippet uses the demo BAdIs outlined earlier. For more example snippets, see the <a href="#executable-example">executable example</a>.</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Static variant of the GET BADI and CALL BADI statements
*&amp;---------------------------------------------------------------------*

&quot;Declaring a BAdI reference variable
&quot;The variable references the multiple-use demo BAdI definition.
DATA badi_a TYPE REF TO zbadi_demo_abap_converter.

&quot;Creating a BAdI object
&quot;The demo BAdI does not specify filters.
GET BADI badi_a.

&quot;Calling a BAdI method through the BAdI reference variable
&quot;and providing actual parameters to formal parameters.
CALL BADI badi_a-&gt;convert
  EXPORTING
    content = `Hello world`
    out     = out.

&quot;Declaring another BAdI reference variable
&quot;The variable references the single-use demo BAdI.
DATA badi_b TYPE REF TO zbadi_demo_abap_calculator.

&quot;The demo BAdI specifies a filter condition. Therefore, the GET BADI
&quot;statement requires to specify the FILTERS addition.
GET BADI badi_b FILTERS operator = &#x27;-&#x27;.

&quot;Not that unlike methods of multiple-use BAdIs, methods of single-use
&quot;BAdIs can include output parameters in the signature.
CALL BADI badi_b-&gt;calculate
  EXPORTING
    num1   = 10
    num2   = 8
  RECEIVING
    result = DATA(res).

*&amp;---------------------------------------------------------------------*
*&amp; Dynamic variants of the GET BADI and CALL BADI statements
*&amp;---------------------------------------------------------------------*

&quot;Character-like data objects to be used in the dynamic statements
DATA(badi_name) = &#x27;ZBADI_DEMO_ABAP_CALCULATOR&#x27;.
DATA(method_name) = &#x27;CALCULATE&#x27;.
DATA dyn_result TYPE string.

&quot;Declaring a BAdI reference variable
&quot;The static type of the reference variable must reference CL_BADI_BASE,
&quot;the superclass of all BAdI classes.
DATA badi_dyn TYPE REF TO cl_badi_base.

&quot;Dynamic GET BADI statement specifying the TYPE addition and
&quot;the name of a BAdI (a data object containing it) in parentheses.
GET BADI badi_dyn TYPE (badi_name) FILTERS operator = &#x27;-&#x27;.

&quot;Dynamic CALL BADI statement specifying the method name
&quot;dyanmically (i.e. a data object containing the name) in
&quot;parentheses following object component selector -&gt;.
CALL BADI badi_dyn-&gt;(method_name)
  EXPORTING
    num1   = 10
    num2   = 8
  RECEIVING
    result = dyn_result.

&quot;Using the FILTER-TABLE addition in dynamic GET BADI statements
&quot;After the addition, a filter table of type badi_filter_bindings is
&quot;expected.
DATA(op) = &#x27;/&#x27;.
DATA(filter_name) = CONV badi_filter_name( &#x27;OPERATOR&#x27; ).

DATA(filter_tab) = VALUE badi_filter_bindings( ( name  = filter_name
                                                 value = REF #( op ) ) ).

GET BADI badi_dyn TYPE (badi_name) FILTER-TABLE filter_tab.

CALL BADI badi_dyn-&gt;(method_name)
  EXPORTING
    num1   = 10
    num2   = 5
  RECEIVING
    result = dyn_result.

&quot;Using the PARAMETER-TABLE addition in dynamic CALL BADI statements
&quot;Similar to dynamic CALL METHOD statements, dynamic CALL BADI statements
&quot;can specify a parameter table of type abap_parmbind_tab.
DATA(ptab) = VALUE abap_parmbind_tab( ( name  = &#x27;NUM1&#x27;
                                        kind  = cl_abap_objectdescr=&gt;exporting
                                        value = NEW i( 10 ) )
                                      ( name  = &#x27;NUM2&#x27;
                                        kind  = cl_abap_objectdescr=&gt;exporting
                                        value = NEW i( 5 ) )
                                      ( name  = &#x27;RESULT&#x27;
                                        kind  = cl_abap_objectdescr=&gt;returning
                                        value = REF #( dyn_result ) ) ).

CALL BADI badi_dyn-&gt;(method_name) PARAMETER-TABLE ptab.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>More Information</h2>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbadi_enhancement.htm">Enhancements Using BAdIs (ABAP Keyword Documentation)</a></li>
<li><a href="https://help.sap.com/docs/ABAP_PLATFORM_NEW/46a2cfc13d25463b8b9a3d2a3c3ba0d9/8ff2e540f8648431e10000000a1550b0.html?version=LATEST&amp;locale=en-US">Business Add-Ins (BAdIs) on SAP Help Portal</a> (Note that the context of the documentation is classic ABAP)</li>
<li><a href="https://www.sap.com/documents/2022/10/52e0cd9b-497e-0010-bca6-c68f7e60039b.html">Extend SAP S/4HANA in the cloud and on premise with ABAP based extensions</a></li>
<li>Exploring usable standard BAdIs:</li>
<li>You can go to <a href="https://api.sap.com/">https://api.sap.com/</a>.</li>
<li>Then, you can, for example, choose <em>Explore</em> from the menu -&gt; <em>Categories -&gt; Business Add-Ins (BAdIs)</em>. Apply appropriate filtering if required.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Executable Example</h2>
<div class="note">[!NOTE] - The commented example illustrates various `GET BADI` and `CALL BADI` statements. - As a prerequisite, you have walked through the [previous steps](#excursion-demo-badis) and created the demo BAdIs. - To try the example out, create a demo class named `zcl_demo_abap` and paste the code into it. After activation, choose *F9* in ADT to execute the class. The example is set up to display output in the console. - [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
    TYPES c1 TYPE c LENGTH 1.
    DATA: operand1       TYPE i,
          operand2       TYPE i,
          op             TYPE c1,
          example_number TYPE i.
    METHODS set_example_divider IMPORTING out  TYPE REF TO if_oo_adt_classrun_out
                                          text TYPE string.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

*&amp;---------------------------------------------------------------------*
*&amp; Multiple-use demo BAdI
*&amp;---------------------------------------------------------------------*

    &quot;The multiple-use demo BAdI does not specify filters. Therefore, the syntax
    &quot;does not allow the use of the FILTERS addition. Two demo BAdI implementations
    &quot;are available in the demo, converting data objects to XML and JSON. When
    &quot;calling the BAdI method, it executes both implementations. Consequently,
    &quot;the output includes data objects converted to both XML and JSON.

    example_number += 1.
    set_example_divider( out  = out
                         text = |{ example_number }) Multiple-use BAdI| ).

    &quot;Creating demo data objects for the example to work with
    DATA str TYPE string VALUE `Hello world`.

    DATA: BEGIN OF struct,
            comp1 TYPE string VALUE `ABAP`,
            comp2 TYPE i VALUE 987,
            comp3 TYPE n LENGTH 5 VALUE &#x27;12345&#x27;,
            comp4 TYPE abap_boolean VALUE abap_true,
          END OF struct.

    DATA itab LIKE TABLE OF struct WITH EMPTY KEY.
    itab = VALUE #( ( struct )
                    ( comp1 = `BAdI` comp2 = 321 comp3 = &#x27;67890&#x27; comp4 = abap_false ) ).

    &quot;Declaring a BAdI reference variable
    &quot;The variable references the multiple-use demo BAdI definition.
    DATA badi_m TYPE REF TO zbadi_demo_abap_converter.

    &quot;Creating a BAdI object
    GET BADI badi_m.

    out-&gt;write( |---------- Converting elementary data objects ----------\n| ).

    CALL BADI badi_m-&gt;convert
      EXPORTING
        content = str
        out     = out.

    out-&gt;write( |---------- Converting a structure ----------\n| ).

    CALL BADI badi_m-&gt;convert
      EXPORTING
        content = struct
        out     = out.

    out-&gt;write( |---------- Converting an internal table ----------\n| ).

    CALL BADI badi_m-&gt;convert
      EXPORTING
        content = itab
        out     = out.

*&amp;---------------------------------------------------------------------*
*&amp; Single-use demo BAdI
*&amp;---------------------------------------------------------------------*

    &quot;The single-use demo BAdI specifies filters. Therefore, the GET BADI
    &quot;statement requires to specify the FILTERS addition.
    &quot;Additionally, a fallback class is created for the demo BAdI. It
    &quot;means that if an implementation class is not found that matches
    &quot;the filter condition, the BAdI method implemented in the fallback
    &quot;class is called.

    example_number += 1.
    set_example_divider( out  = out
                         text = |{ example_number }) Single-use demo BAdI| ).

    &quot;Declaring a BAdI reference variable
    &quot;The variable references the single-use demo BAdI definition.
    DATA badi_s TYPE REF TO zbadi_demo_abap_calculator.

    &quot;Assigning demo values to data objects used for the BAdI
    &quot;method calls
    operand1 = 10.
    operand2 = 8.
    op = &#x27;-&#x27;.

    &quot;Creating a BAdI object
    &quot;Note that the demo BAdI requires filters to be specified.
    GET BADI badi_s FILTERS operator = op.

    CALL BADI badi_s-&gt;calculate
      EXPORTING
        num1   = operand1
        num2   = operand2
      RECEIVING
        result = DATA(result).

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

    op = &#x27;/&#x27;.

    GET BADI badi_s FILTERS operator = op.

    CALL BADI badi_s-&gt;calculate
      EXPORTING
        num1   = operand1
        num2   = operand2
      RECEIVING
        result = result.

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

    operand1 = 5.
    operand2 = 0.

    CALL BADI badi_s-&gt;calculate
      EXPORTING
        num1   = operand1
        num2   = operand2
      RECEIVING
        result = result.

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

    &quot;Using a non-specified filter
    &quot;In this case, the BAdI method in the fallback class is called,
    &quot;reflecting addition.
    op = &#x27;?&#x27;.

    GET BADI badi_s FILTERS operator = op.

    operand1 = 3.
    operand2 = 2.

    CALL BADI badi_s-&gt;calculate
      EXPORTING
        num1   = operand1
        num2   = operand2
      RECEIVING
        result = result.

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

*&amp;---------------------------------------------------------------------*
*&amp; Dynamic specifications
*&amp;---------------------------------------------------------------------*

    example_number += 1.
    set_example_divider( out  = out
                         text = |{ example_number }) Dynamic specifications| ).

    &quot;Dynamic variant of the GET BADI statement
    &quot;Creating character-like data objects holding the names of a BAdI
    &quot;and BAdI method
    DATA(badi_name) = &#x27;ZBADI_DEMO_ABAP_CALCULATOR&#x27;.
    DATA(method_name) = &#x27;CALCULATE&#x27;.

    operand1 = 15.
    operand2 = 9.
    op = &#x27;-&#x27;.

    &quot;The dynamic variant of the GET BADI statement specifies the TYPE addition,
    &quot;followed by the BAdI name (contained in a character-like data object) in
    &quot;parentheses. The BAdI reference variable must have the static type CL_BADI_BASE,
    &quot;the superclass of all BAdI classes.
    DATA badi_dyn TYPE REF TO cl_badi_base.

    GET BADI badi_dyn TYPE (badi_name) FILTERS operator = op.

    &quot;The dynamic variant of the CALL BADI statement specifies the object component
    &quot;selector -&gt; followed by the BAdI method name (contained in a character-like
    &quot;data object) in parentheses. The following statement explicitly specifies the
    &quot;parameters, similar to dynamic CALL METHOD statements.
    CALL BADI badi_dyn-&gt;(method_name)
      EXPORTING
        num1   = operand1
        num2   = operand2
      RECEIVING
        result = result.

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

    &quot;Using the FILTER-TABLE addition in dynamic GET BADI statements
    &quot;After the addition, a filter table of type badi_filter_bindings is
    &quot;expected.
    DATA filter_tab TYPE badi_filter_bindings.
    DATA badi_filter_name TYPE badi_filter_name.

    badi_filter_name = &#x27;OPERATOR&#x27;.
    op = &#x27;/&#x27;.
    operand1 = 30.
    operand2 = 6.

    filter_tab = VALUE #( ( name  = badi_filter_name
                            value = NEW c1( op ) ) ).

    GET BADI badi_dyn TYPE (badi_name) FILTER-TABLE filter_tab.

    CALL BADI badi_dyn-&gt;(method_name)
      EXPORTING
        num1   = operand1
        num2   = operand2
      RECEIVING
        result = result.

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

    &quot;Using the PARAMETER-TABLE addition in dynamic CALL BADI statements
    &quot;Similar to dynamic CALL METHOD statements, dynamic CALL BADI statements
    &quot;can specify a parameter table of type abap_parmbind_tab.
    badi_filter_name = &#x27;OPERATOR&#x27;.
    op = &#x27;/&#x27;.
    operand1 = 1.
    operand2 = 8.

    filter_tab = VALUE #( ( name  = badi_filter_name
                            value = NEW c1( op ) ) ).

    GET BADI badi_dyn TYPE (badi_name) FILTER-TABLE filter_tab.

    DATA(ptab) = VALUE abap_parmbind_tab( ( name  = &#x27;NUM1&#x27;
                                            kind  = cl_abap_objectdescr=&gt;exporting
                                            value = REF #( operand1 ) )
                                          ( name  = &#x27;NUM2&#x27;
                                            kind  = cl_abap_objectdescr=&gt;exporting
                                            value = REF #( operand2 ) )
                                          ( name  = &#x27;RESULT&#x27;
                                            kind  = cl_abap_objectdescr=&gt;returning
                                            value = REF #( result ) ) ).

    CALL BADI badi_dyn-&gt;(method_name) PARAMETER-TABLE ptab.

    out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
    out-&gt;write( |\n| ).

    &quot;Note:
    &quot;The following statement is not possible as the BAdI reference variable
    &quot;is typed with type ref to zbadi_demo_abap_calculator. A reference to
    &quot;the superclass CL_BADI_BASE is expected.
    &quot;GET BADI badi_s TYPE (name) FILTERS operator = &#x27;-&#x27;.

*&amp;---------------------------------------------------------------------*
*&amp; Exceptions
*&amp;---------------------------------------------------------------------*

    example_number += 1.
    set_example_divider( out  = out
                         text = |{ example_number }) Demonstrating selected exceptions| ).

    DATA error TYPE REF TO cx_root.

    &quot;1) Filter not bound
    CLEAR filter_tab.
    TRY.
        GET BADI badi_dyn TYPE (badi_name) FILTER-TABLE filter_tab.
      CATCH cx_badi_filter_error INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).

    &quot;2) Filter not bound (not specifying filters at all)
    TRY.
        GET BADI badi_dyn TYPE (badi_name).
      CATCH cx_badi_filter_error INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).

    &quot;3) Non-existant BAdI name
    DATA badi_dyn_2 TYPE REF TO cl_badi_base.
    badi_name = &#x27;NOPE&#x27;.

    TRY.
        GET BADI badi_dyn_2 TYPE (badi_name).
      CATCH cx_badi_filter_error INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).

    &quot;4) Wrong parameters (you might know the exception class
    &quot;   from regular dynamic method calls)
    badi_name = &#x27;ZBADI_DEMO_ABAP_CONVERTER&#x27;.
    method_name = &#x27;CONVERT&#x27;.

    GET BADI badi_dyn_2 TYPE (badi_name).

    TRY.
        CALL BADI badi_dyn_2-&gt;(method_name)
          EXPORTING
            nope1 = str
            nope2 = out.
      CATCH cx_sy_dyn_call_param_missing INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).

    &quot;5) Wrong method name (you might know the exception class
    &quot;   from regular dynamic method calls)
    TRY.
        CALL BADI badi_dyn_2-&gt;(&#x27;NOPE&#x27;)
          EXPORTING
            content = str
            out     = out.
      CATCH cx_sy_dyn_call_illegal_method INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).

    &quot;6) Initial BAdI reference variable
    CLEAR badi_dyn_2.

    TRY.
        CALL BADI badi_dyn_2-&gt;(&#x27;CONVERT&#x27;)
          EXPORTING
            content = str
            out     = out.
      CATCH cx_badi_initial_reference INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).

    &quot;7) No exception raised here, but demonstrating the following:
    &quot;   In case of an initial BAdI reference variable whose static type
    &quot;   refers to a multiple-use BAdI, there is no effect of the statement
    &quot;   and no exception raised. In the console, there should not be any
    &quot;   output displayed between the two dividers.
    GET BADI badi_m.

    CLEAR badi_m.

    out-&gt;write( `----------` ).

    CALL BADI badi_m-&gt;convert
      EXPORTING
        content = 123
        out     = out.

    out-&gt;write( `----------` ).
    out-&gt;write( |\n| ).

    &quot;8) BAdI reference variable whose static type refers to a single-use
    &quot;   BAdI: Exception is raised in case of an initial BAdI reference variable.
    op = &#x27;-&#x27;.
    operand1 = 3.
    operand2 = 2.

    GET BADI badi_s FILTERS operator = op.

    CLEAR badi_s.

    TRY.
        CALL BADI badi_s-&gt;calculate
          EXPORTING
            num1   = operand1
            num2   = operand2
          RECEIVING
            result = result.

        out-&gt;write( |{ operand1 } { op } { operand2 } = { result }| ).
      CATCH cx_badi_initial_reference INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

  ENDMETHOD.

  METHOD set_example_divider.
    out-&gt;write( |*&amp;{ repeat( val = `-` occ = 70 ) }*| ).
    out-&gt;write( |*&amp; { text }| ).
    out-&gt;write( |*&amp;{ repeat( val = `-` occ = 70 ) }*| ).
    out-&gt;write( |\n| ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
</body>
</html>