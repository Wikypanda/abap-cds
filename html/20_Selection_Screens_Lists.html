<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>20 Selection Screens Lists - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>Selection Screens and Classic Lists</h1>
<ul>
<li><a href="#selection-screens-and-classic-lists">Selection Screens and Classic Lists</a></li>
<li><a href="#introduction">Introduction</a></li>
<li><a href="#selection-screens">Selection Screens</a></li>
<li><a href="#classic-lists">Classic Lists</a></li>
<li><a href="#abap-statements-for-selection-screens">ABAP Statements for Selection Screens</a></li>
<li><a href="#parameters">PARAMETERS</a></li>
<li><a href="#select-options">SELECT-OPTIONS</a></li>
<li><a href="#selection-screen">SELECTION-SCREEN</a></li>
<li><a href="#creating-selection-screens">Creating Selection Screens</a></li>
<li><a href="#adapting-the-selection-screen-layout">Adapting the Selection Screen Layout</a></li>
<li><a href="#using-elements-from-other-selection-screens">Using Elements from Other Selection Screens</a></li>
<li><a href="#calling-selection-screens">Calling Selection Screens</a></li>
<li><a href="#excursion-submit-statements">Excursion: SUBMIT Statements</a></li>
<li><a href="#abap-statements-for-classic-lists">ABAP Statements for Classic Lists</a></li>
<li><a href="#creating-lists-with-write-statements">Creating Lists with WRITE Statements</a></li>
<li><a href="#formatting-output-using-format-statements">Formatting Output Using FORMAT Statements</a></li>
<li><a href="#list-creation-related-abap-keywords-and-additions">List Creation-Related ABAP Keywords and Additions</a></li>
<li><a href="#processing-lists-in-list-buffers">Processing Lists in List Buffers</a></li>
<li><a href="#event-blocks">Event Blocks</a></li>
<li><a href="#excursion-sap-list-viewer-alv">Excursion: SAP List Viewer (ALV)</a></li>
<li><a href="#getting-started">Getting Started</a></li>
<li><a href="#optional-layout-settings-and-functionality">Optional Layout Settings and Functionality</a></li>
<li><a href="#more-information">More Information</a></li>
<li><a href="#executable-examples">Executable Examples</a></li>
</ul>
<h2>Introduction</h2>
<div class="note">[!NOTE] The content of this cheat sheet and the executable examples are only relevant to [classic ABAP](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenclassic_abap_glosry.htm).</div>
<p><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_screen_glosry.htm">Selection screens</a> and <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenclassic_list_glosry.htm">classic lists</a> are among the classic ABAP user interfaces. They are integrated into the ABAP language itself, which means that special ABAP statements are available to create and handle them.</p>
<p>This cheat sheet provides a high-level overview of selection screens and classic lists, focusing on a selection of related statements, supported by executable examples to explore the syntax in action. It includes an excursion into the SAP List Viewer (ALV).</p>
<p>For more detailed information and syntax options, see the topics <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_screen.htm">Selection Screens</a> and <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_dynpro_list.htm">Classic Lists</a> in the ABAP Keyword Documentation.</p>
<div class="note">[!NOTE] - Although they are considered outdated for application programs, you will still find classic ABAP UIs frequently in classic ABAP. - Classic ABAP UIs cannot be created in [ABAP Cloud](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_cloud_glosry.htm). - This cheat sheet ... - is not intended to encourage you to start creating classic ABAP UIs for programming new applications. - does not cover all facets, techniques, and syntax options (there's a rich variety of keywords and additions) in great detail. - is intended to cover a selection of related syntax that you may encounter in older ABAP code. If you need more information, always consult the ABAP Keyword Documentation. - Links to the ABAP Keyword Documentation in this cheat sheet refer to the documentation for [Standard ABAP](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenstandard_abap_glosry.htm) (latest version).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Selection Screens</h3>
<ul>
<li>Are special <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendynpro_glosry.htm">dynpros</a> in <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenexecutable_program_glosry.htm">executable programs</a> (<em>&quot;reports&quot;</em>; they&#x27;re also possible in <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenfunction_group_glosry.htm">function groups</a> and <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenmodul_pool_glosry.htm">module pools</a>, but the focus here is on executable programs).</li>
<li>Used for data entry in an executable program, i.e. they allow users to ...</li>
<li>enter parameters (for entering single values).</li>
<li>provide selection criteria (complex selection options for value lists and value ranges) that can be used to supply an executable program with values to work with.</li>
<li>Can be created using special ABAP statements in the global declaration part of executable programs</li>
<li>Are processed by the ABAP runtime framework, which triggers <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_screen_event_glosry.htm">selection screen events</a> (<a href="#event-blocks">see further down</a>).</li>
<li>When you create a selection screen and run an ABAP program, the dynpros are generated automatically. These generated dynpros cannot be edited directly.</li>
<li>Further ways in which selections screens are different from <em>regular</em> dynpros:</li>
<li>Unlike dynpros, you can create selection screens without using the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenscreen_painter_glosry.htm">screen painter</a> tool. The <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_editor_glosry.htm">ABAP Editor</a> is the only tool you use to define a selection screen (using ABAP statements).</li>
<li>They have a more limited set of features, for example, the layout of selection screens is more restricted. However, selection screens offer various types of input fields, which can also be expressed as checkboxes or radio buttons, and which can be grouped and labeled.</li>
<li>Modification options are limited. For example, you can only use some predefined function codes.</li>
<li>You cannot define <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendialog_module_glosry.htm">dialog modules</a> in the ABAP program.</li>
<li>There are two types of selection screens:</li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenstand-alone_sel_screen_glosry.htm">Standalone selection screens</a></li>
<li>Defined between the statements <code>SELECTION-SCREEN BEGIN OF SCREEN</code> and <code>SELECTION-SCREEN END OF SCREEN</code></li>
<li>Can be defined in all programs that can contain dynpros</li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenstandard_selscreen_glosry.htm">Standard selection screens</a></li>
<li>Each executable program (and only there) contains a standard selection screen with the dynpro number 1000. Note: If you create a standalone selection screen, you cannot use dynpro number 1000.</li>
<li>The screen elements on the standard selection screen are defined by all <code>PARAMETERS</code>, <code>SELECT-OPTIONS</code>, and <code>SELECTION-SCREEN</code> statements that are defined outside of the statements mentioned above for creating standalone selection screens.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Classic Lists</h3>
<ul>
<li>Used to output data in a structured and formatted way.</li>
<li>This output can be ...</li>
<li>created in the so-called <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenlist_buffer_glosry.htm">list buffer</a>, a memory area for storing <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenscreen_list_glosry.htm">screen lists</a>, i.e. a list that is displayed on a predefined list dynpro (list dynpro that is accessed implicitly when a program is executed; note: this dynpro is not part of the executed program).</li>
<li>sent to the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abensap_spool_system_glosry.htm">SAP spool system</a> as a spool list (i.e. a list that is not stored as a screen list but is intended for printing or archiving; the focus of this cheat sheet is on screen lists).</li>
<li>Typical flow of executable programs that include selection screens and content to be displayed on the list dynpro:</li>
<li>Programs are executed using the <em>Execute</em> button (F8).</li>
<li>Selection screens are displayed, users usually make entries for whatever purpose, which are further processed in the program.</li>
<li>Output result is written to the list buffer and finally displayed (on a list dynpro; note: the list processing is automatically started when running executable programs).</li>
<li>The list can be further implemented to respond to user interaction, such as clicking a line in the list.</li>
<li>More modern alternatives for classic lists are available, such as the classes of the SAP List Viewer (ALV), for example <code>CL_SALV_TABLE</code>.</li>
</ul>
<div class="note">[!NOTE] The program is grouped into so-called [event blocks](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenevent_block_glosry.htm), which can contain implementations for various events (e.g. a [selection screen event](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_screen_event_glosry.htm) when a user selects a radio button, or a [list event](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenlist_event_glosry.htm) when a user double-clicks a line in the list). These event blocks are introduced by special ABAP statements. When a particular event is triggered, the corresponding event block is called and the appropriate code can be implemented there to react to the user action. See more information [here](#event-blocks).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>ABAP Statements for Selection Screens</h2>
<p>Selection screens can be created by using special ABAP statements in the global declaration part of executable programs:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapparameters.htm"><code>PARAMETERS</code></a></li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapselect-options.htm"><code>SELECT-OPTIONS</code></a></li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapselection-screen.htm"><code>SELECTION-SCREEN</code></a></li>
</ul>
<h3>PARAMETERS</h3>
<ul>
<li>Creates ...</li>
<li>a program-global variable (similar to <code>DATA</code> statements)</li>
<li>an input field on the selection screen automatically, i.e. the content of the program-global variable can be changed via the input field.</li>
<li>The parameter name can be up to eight characters long.</li>
<li>Multiple additions are available to define the appearance (e.g. <code>AS CHECKBOX</code>) or influence the user interface (e.g. <code>OBLIGATORY</code>).</li>
</ul>
<p>The following table includes code snippets with a selection of available syntax options:</p>
<div class="note">[!NOTE] - Various combinations of multiple additions are possible with `PARAMETERS` statement, and some are not. See the ABAP Keyword Documentation for details. - To try out the code examples, you can create a demo executable program, copy and paste the code snippets, and run the programs using F8. They are designed to output content using classic lists. - The code snippets anticipate topics outlined further down, for example, event blocks and `WRITE` statements.</div>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Additions &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Type specification options&lt;br&gt;&lt;br&gt; Additions <code>TYPE</code> and <code>LIKE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can specify data types for parameters similar to <code>DATA</code> statements.</li>
<li>This includes the <code>TYPE</code> and <code>LIKE</code> additions. Types requiring a length specification use the <code>TYPE ... LENGTH ...</code> additions.</li>
<li>Regarding the type <code>c</code>, there are syntax variants to specify the length and the type itself (such as leaving out the explicit type or length specification; length specification in parentheses).</li>
<li>Note that without the <code>LOWER CASE</code> addition, inserted character-like values are transformed to uppercase.</li>
<li>For type <code>p</code>, you can specify the <code>DECIMALS</code> addition.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

&quot;TYPE addition
&quot;The example uses a built-in ABAP type
&quot;Note the LOWER CASE addition further down.
PARAMETERS p_str TYPE string.

&quot;LIKE addition
&quot;Referring to an existing data object
DATA some_number TYPE i.
PARAMETERS p_num LIKE some_number.

&quot;TYPE ... LENGTH for types that require a length specification
&quot;such as c, n, p, and x
PARAMETERS p_clen10 TYPE c LENGTH 10.
PARAMETERS p_nlen5 TYPE n LENGTH 5.

&quot;Length specification variants
&quot;The length can be specified in parentheses, however, specifying LENGTH
&quot;explicitly is recommended for better readability.
PARAMETERS p_lenpar(3) TYPE c.
&quot;No length specified means LENGTH 1 by default
PARAMETERS p_wo_len TYPE c.
&quot;No explicit type specification means TYPE c by default
PARAMETERS p_wo_c_a(20).
PARAMETERS p_wo_c_b LENGTH 20.
&quot;No explicit type and length specification means TYPE c LENGTH 1 by default
PARAMETERS p_woclen.

START-OF-SELECTION.

  WRITE / `Inserted values (note that inserted characters are capitalized):`.
  SKIP.
  WRITE / |p_str: &quot;{ p_str }&quot;|.
  WRITE / |p_num: &quot;{ p_num }&quot;|.
  WRITE / |p_clen10: &quot;{ p_clen10 }&quot;|.
  WRITE / |p_nlen5: &quot;{ p_nlen5 }&quot;|.
  WRITE / |p_lenpar: &quot;{ p_lenpar }&quot;|.
  WRITE / |p_wo_len: &quot;{ p_wo_len }&quot;|.
  WRITE / |p_wo_c_a: &quot;{ p_wo_c_a }&quot;|.
  WRITE / |p_wo_c_b: &quot;{ p_wo_c_b }&quot;|.
  WRITE / |p_woclen: &quot;{ p_woclen }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Parameters using ABAP Dicitionary input helps</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>When referring to data types from the ABAP Dictionary, all screen-relvant properties of that type are adopted.</li>
<li>If the referred type includes input help functionality, a callable field help is automatically created.</li>
<li>The input helps inlcude search helps, check tables, fixed values and calendar/clock helps. Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_dynpros_value_help_auto.htm">here</a>.</li>
<li>Note that there is no automatic value check. For that, use the <code>VALUE CHECK</code> addition.</li>
<li>The code snippet includes input helps from a DDIC structure of an ABAP Keyword Documentation example.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

&quot;carrier1 is based on DDIC data element demof4de, which has the search help
&quot;demo_f4_de assigned
PARAMETERS p_carr1 TYPE demof4help-carrier1.

&quot;carrier2 is assigned to check table scarr, which is assigned to the
&quot;search help h_scarr
PARAMETERS p_carr2 TYPE demof4help-carrier2.

&quot;DDIC data element that has the search help s_carrier_id assigned
PARAMETERS p_carr3 TYPE s_carr_id.

&quot;connid is assigned to the search help demo_f4_field
PARAMETERS p_connid TYPE demof4help-connid.

&quot;Component of demo database table that specifies a foreign key
PARAMETERS p_plane TYPE sflight-planetype.

&quot;Fixed values as input help
PARAMETERS p_num TYPE demo_numbers.

&quot;ABAP Dictionary types such as DATS and TIMS have
&quot;a predefined input help (calendar and clock).
PARAMETERS p_dats TYPE dats.
PARAMETERS p_tims TYPE tims.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |p_carr1: &quot;{ p_carr1 }&quot;|.
  WRITE / |p_carr2: &quot;{ p_carr2 }&quot;|.
  WRITE / |p_carr3: &quot;{ p_carr3 }&quot;|.
  WRITE / |p_connid: &quot;{ p_connid }&quot;|.
  WRITE / |p_plane: &quot;{ p_plane }&quot;|.
  WRITE / |p_num: &quot;{ p_num }&quot;|.
  WRITE / |p_dats: &quot;{ p_dats }&quot;|.
  WRITE / |p_tims: &quot;{ p_tims }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Dynamically specifying the type&lt;br&gt;&lt;br&gt; Addition <code>LIKE (...)</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The dynamic specification works with <code>LIKE</code> followed by a data object name in parentheses.</li>
<li>As name, a character-like data object that contains the name of a structure component in the ABAP Dictionary in uppercase letters.</li>
<li>The parameter is then of type <code>c</code> length 132, however, the properties such as length and input help are taken from the type specified in parentheses.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

&quot;In the example, the currently loaded text pool does not contain a selection text 
&quot;for the parameters. Here, the output field displays the field label from the
&quot;ABAP Dictionary.
DATA some_dobj TYPE c LENGTH 30.
PARAMETERS p_dyn1 LIKE (some_dobj).

DATA comp TYPE c LENGTH 20.
PARAMETERS p_dyn2 LIKE (comp).

&quot;Parameter on the screen with type c length 132
&quot;Here, no selection text is created for the example, and no field
&quot;label field can be referred to. The output field displays the text 
&quot;&#x27;Dynamic Parameter&#x27;.
DATA another_dobj like some_dobj.
PARAMETERS p_dyn3 LIKE (another_dobj).

&quot;Text field literal, however, its content is not evaluated.
&quot;Also here, the parameter on the screen is of type
&quot;c length 132.
PARAMETERS p_dyn4 LIKE (&#x27;SPFLI-CARRID&#x27;).

INITIALIZATION.
  some_dobj = &#x27;SPFLI-CARRID&#x27;.
  comp = &#x27;SFLIGHT-PLANETYPE&#x27;.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |p_dyn1: &quot;{ p_dyn1 }&quot;|.
  WRITE / |p_dyn2: &quot;{ p_dyn2 }&quot;|.
  WRITE / |p_dyn3: &quot;{ p_dyn3 }&quot;|.
  WRITE / |p_dyn4: &quot;{ p_dyn4 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying value options&lt;br&gt;&lt;br&gt; Additions <code>DEFAULT</code>, <code>LOWER CASE</code>, <code>MATCHCODE OBJECT</code>, <code>MEMORY ID</code>, <code>VALUE CHECK</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>DEFAULT</code>: Defines a start value (can also be a data object instead of a literal)</li>
<li><code>LOWER CASE</code>: Prevents the effect of capitalizing the entry made when the content is transported to the data object</li>
<li><code>MATCHCODE OBJECT</code>: Links an input field with a DDIC search help</li>
<li><code>MEMORY ID</code>: Links an input field with an SPA/GPA parameter in the user memory, i.e. data objects in the user memory accessible by ABAP programs. When calling the selection screen, the input field receives the SPA/GPA parameter value if the <code>PARAMETERS</code>&#x27;s data object is initial after processing of the <code>AT SELECTION-SCREEN OUTPUT</code> event block. The SPA/GPA parameters are set using <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapset_parameter.htm"><code>SET PARAMETER</code></a> and read using <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapget_parameter.htm"><code>GET PARAMETER</code></a> statements. See the names of the parameters in the <code>TPARA</code> database table.</li>
<li><code>VALUE CHECK</code>: Checks input against fixed values defined for the domain of a data type. It can only be used for DDIC data types. Checks are also available for data types being components of foreign key tables. It is recommended to specify the <code>VALUE CHECK</code> addition with the <code>OBLIGATORY</code> addition as the check is also applied in case of empty input fields.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

*&amp;---------------------------------------------------------------------*
*&amp; DEFAULT addition
*&amp;---------------------------------------------------------------------*

PARAMETERS p_deflti TYPE i DEFAULT 12345.
PARAMETERS p_deflts TYPE string DEFAULT `This is some demo text`.

*&amp;---------------------------------------------------------------------*
*&amp; LOWER CASE addition
*&amp;---------------------------------------------------------------------*

PARAMETERS p_upper TYPE string DEFAULT `Hello World`.
PARAMETERS p_lower TYPE string DEFAULT `Hello World` LOWER CASE.

*&amp;---------------------------------------------------------------------*
*&amp; MATCHCODE OBJECT addition
*&amp;---------------------------------------------------------------------*

&quot;The following example uses a search help of an ABAP Keyword Documentation example.
PARAMETERS p_carr1 TYPE s_carr_id MATCHCODE OBJECT demo_f4_de.
&quot;Another predelivered demo search help.
PARAMETERS p_carr2 TYPE c LENGTH 3 MATCHCODE OBJECT s_carrier_id.
&quot;Non-existent search help (triggers a message in the status line)
PARAMETERS p_nope TYPE s_carr_id MATCHCODE OBJECT demo_f4_de###.

*&amp;---------------------------------------------------------------------*
*&amp; MEMORY ID addition
*&amp;---------------------------------------------------------------------*

&quot;The example use the memory id &#x27;rid&#x27; that stores the name of the program
&quot;processed last. The demo explicitly sets the value to a dummy program
&quot;name in the AT SELECTION-SCREEN OUTPUT event block using a SET PARAMETER ID
&quot;statement.
PARAMETERS p_prog TYPE sy-repid MEMORY ID rid.

*&amp;---------------------------------------------------------------------*
*&amp; VALUE CHECK addition
*&amp;---------------------------------------------------------------------*

&quot;The example selection parameters are specified with reference to a
&quot;demo database table field for which the check table scarr is specified.
&quot;So, you can only enter values that are also contained in scarr.
&quot;When running the example program, you can ...
&quot;- make entries that are not contained in the list and execute the program 
&quot;  to demonstrate that the value check is applied.
&quot;- leave the input for pvalchk1 empty to demonstrate that the value check is 
&quot;  also applied on empty fields. Therefore, also specifying the OBLIGATORY 
&quot;  addition is recommended.
PARAMETERS pvalchk1 TYPE spfli-carrid VALUE CHECK.
PARAMETERS pvalchk2 TYPE spfli-carrid VALUE CHECK OBLIGATORY.

AT SELECTION-SCREEN OUTPUT.
  SET PARAMETER ID &#x27;RID&#x27; FIELD &#x27;SOME_TEST_REPORT&#x27;.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |p_deflti: &quot;{ p_deflti }&quot;|.
  WRITE / |p_deflts: &quot;{ p_deflts }&quot;|.
  WRITE / |p_upper: &quot;{ p_upper }&quot;|.
  WRITE / |p_lower: &quot;{ p_lower }&quot;|.
  WRITE / |p_carr1: &quot;{ p_carr1 }&quot;|.
  WRITE / |p_nope: &quot;{ p_nope }&quot;|.
  WRITE / |p_prog: &quot;{ p_prog }&quot;|.
  WRITE / |pvalchk1: &quot;{ pvalchk1 }&quot;|.
  WRITE / |pvalchk2: &quot;{ pvalchk2 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying screen options&lt;br&gt;&lt;br&gt; Additions <code>OBLIGATORY</code>, <code>NO-DISPLAY</code>, <code>VISIBLE LENGTH</code>, <code>AS CHECKBOX</code>, <code>RADIOBUTTON GROUP</code>, <code>AS LISTBOX VISIBLE LENGTH</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>OBLIGATORY</code>: Declares the input field as a required field. If there is no entry, the program cannot proceed when choosing <em>Execute</em>. A message will be displayed.</li>
<li><code>NO-DISPLAY</code>: Hides the input field on the selection screen. A value can be supplied when calling the program with <code>SUBMIT</code> and the <code>WITH</code> addition. Note that with the <code>NO-DISPLAY</code> addition, the parameter can have any data types except for reference/enumerated types, unlike in the other additions which require flat types (except type <code>string</code>).</li>
<li><code>VISIBLE LENGTH</code>: Defines the visible length of the field. If you specify the parameter with type <code>c</code> and length 10, and you specify the visible length as 3, the input field size is reduced accordingly, but you can still insert 10 characters.</li>
<li><code>AS CHECKBOX</code>: Displays an input field as checkbox. Type <code>c</code> and length 1 is expected, but the explicit length specification is not allowed. The checkbox is selected if the value has the value <code>X</code> or <code>x</code>.</li>
<li><code>RADIOBUTTON GROUP</code>: Defines a radio button group for parameters. Note:</li>
<li>The group name can have a maximum of four characters.</li>
<li>Regarding the data type, the same applies as for <code>AS CHECKBOX</code>.</li>
<li>Only one parameter can be defined with the <code>DEFAULT</code> addition.</li>
<li>If <code>DEFAULT</code> is not specified, the first parameter of the group is set to the value <code>X</code>.</li>
<li><code>AS LISTBOX VISIBLE LENGTH</code>: Creates a dropdown list box. You can use the function module <code>VRM_SET_VALUES</code> by passing a suitable list at the</li>
</ul>
<p><code>AT SELECTION-SCREEN OUTPUT</code> or <code>AT SELECTION-SCREEN ON VALUE-REQUEST</code> events. You may want to specify the <code>OBLIGATORY</code> addition here, too, as the check is also applied to empty fields.</p>
<div class="note">[!NOTE] The `USER-COMMAND` addition can be specified together with `AS CHECKBOX`, `RADIOBUTTON GROUP` and `AS LISTBOX VISIBLE LENGTH` to assign a function code to the selection parameter.</div>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

*&amp;---------------------------------------------------------------------*
*&amp; OBLIGATORY addition
*&amp;---------------------------------------------------------------------*

PARAMETERS p_oblgty TYPE c LENGTH 10 OBLIGATORY.

*&amp;---------------------------------------------------------------------*
*&amp; NO-DISPLAY addition
*&amp;---------------------------------------------------------------------*

PARAMETERS p_hidden TYPE c LENGTH 10 NO-DISPLAY.
&quot;With NO-DISPLAY, also complex types (except reference types) can be
&quot;specified.
PARAMETERS p_tabtyp TYPE string_table NO-DISPLAY.

*&amp;---------------------------------------------------------------------*
*&amp; VISIBLE LENGTH addition
*&amp;---------------------------------------------------------------------*

PARAMETERS p_vislen TYPE c LENGTH 10 VISIBLE LENGTH 3.

*&amp;---------------------------------------------------------------------*
*&amp; AS CHECKBOX addition
*&amp;---------------------------------------------------------------------*

&quot;Implicit type c
PARAMETERS p_chkbx1 AS CHECKBOX.
&quot;Explicit type specification (but no explicit length)
PARAMETERS p_chkbx2 TYPE c AS CHECKBOX.

*&amp;---------------------------------------------------------------------*
*&amp; RADIOBUTTON GROUP addition
*&amp;---------------------------------------------------------------------*

&quot;The following example uses a chained statement.
PARAMETERS: p_rb1a RADIOBUTTON GROUP rbg1,
            p_rb1b TYPE c RADIOBUTTON GROUP rbg1,       &quot;Explicit type specification
            p_rb1c RADIOBUTTON GROUP rbg1 DEFAULT &#x27;X&#x27;.  &quot;Select this radiobutton by default

&quot;Separate specifications, no DEFAULT specification
&quot;Here, the first radio button is selected by default.
PARAMETERS p_rb2a RADIOBUTTON GROUP rbg2.
PARAMETERS p_rb2b RADIOBUTTON GROUP rbg2.
PARAMETERS p_rb2c RADIOBUTTON GROUP rbg2.
PARAMETERS p_rb2d RADIOBUTTON GROUP rbg2.

*&amp;---------------------------------------------------------------------*
*&amp; AS LISTBOX VISIBLE LENGTH
*&amp;---------------------------------------------------------------------*

&quot;See the implementation in the AT SELECTION-SCREEN OUTPUT event block
PARAMETERS plistbox TYPE i AS LISTBOX VISIBLE LENGTH 10 OBLIGATORY.

AT SELECTION-SCREEN OUTPUT.

  &quot;The following implementation is relevant for the AS LISTBOX VISIBLE LENGTH
  &quot;addition. Note:
  &quot;- The function module VRM_SET_VALUES is used.
  &quot;- Column TEXT: What is displayed in the list box.
  &quot;- Colum KEY: When a line is selected, the KEY value is added to the parameter
  &quot;- In the example, the numbers 1 - 10 are added as values.
  CALL FUNCTION &#x27;VRM_SET_VALUES&#x27;
    EXPORTING
      id     = CONV vrm_id( &#x27;PLISTBOX&#x27; )
      values = VALUE vrm_values( FOR i = 1 UNTIL i &gt; 10 ( key = i text = |Value { i }| ) ).

  &quot;Filling the string table for the NO-DISPLAY example statement.
  p_tabtyp = VALUE #( ( `Some` ) ( `demo` ) ( `strings` ) ).

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |p_oblgty: &quot;{ p_oblgty }&quot;|.
  WRITE / |p_hidden: &quot;{ p_hidden }&quot;|.

  LOOP AT p_tabtyp into data(wa).
    WRITE / |p_tabtyp (line { sy-tabix }): &quot;{ wa }&quot;|.
  endloop.

  IF p_chkbx1 IS INITIAL.
    WRITE / `Checkbox p_chkbx1 was not selected.`.
  ELSE.
    WRITE / `Checkbox p_chkbx1 was selected.`.
  ENDIF.

  IF p_chkbx2 IS INITIAL.
    WRITE / `Checkbox p_chkbx2 was not selected.`.
  ELSE.
    WRITE / `Checkbox p_chkbx2 was selected.`.
  ENDIF.

  WRITE / |Radio button rbg1 selection: &quot;{ COND #( WHEN p_rb1a IS NOT INITIAL THEN &#x27;p_rb1a&#x27; WHEN p_rb1b IS NOT INITIAL THEN &#x27;p_rb1b&#x27; ELSE &#x27;p_rb1c&#x27; ) }&quot;| .
  WRITE / |Radio button rbg2 selection: &quot;{ COND #( WHEN p_rb2a IS NOT INITIAL THEN &#x27;p_rb2a&#x27; WHEN p_rb2b IS NOT INITIAL THEN &#x27;p_rb2b&#x27; WHEN p_rb2c IS NOT INITIAL THEN &#x27;p_rb2c&#x27; ELSE &#x27;p_rb2d&#x27; ) }&quot;| .

  WRITE / |p_list: &quot;{ plistbox }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Assigning function codes to selection parameters&lt;br&gt;&lt;br&gt; Addition <code>USER-COMMAND</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can assign function codes to selection parameters using the <code>USER-COMMAND</code> addition.</li>
<li>The addition is supported by <code>PARAMETERS</code> statements specifying the <code>AS CHECKBOX</code>, <code>RADIOBUTTON GROUP</code> and <code>AS LISTBOX VISIBLE LENGTH</code> additions.</li>
<li>As a prerequisite, you must specify a <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abaptables.htm"><code>TABLES</code></a> statement to declare an <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abeninterface_work_area_glosry.htm">interface work area</a> of the <code>sscrfields</code> DDIC structure.</li>
<li>On user selection (e.g. a checkbox or radio button is selected), the <code>AT SELECTION-SCREEN</code> event is raised, and the function code is passed to the <code>ucomm</code> component of <code>sscrfields</code>. For evaluation purposes, evaluate <code>sscrfields-ucomm</code> instead of <code>sy-ucomm</code> as it is not guaranteed that the latter is always passed the correct value during selection screen processing.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

&quot;Declaring interface work area
TABLES sscrfields.

*&amp;---------------------------------------------------------------------*
*&amp; AS CHECKBOX USER-COMMAND
*&amp;---------------------------------------------------------------------*

PARAMETERS p_chkbox AS CHECKBOX USER-COMMAND box_cmd.

*&amp;---------------------------------------------------------------------*
*&amp; RADIOBUTTON GROUP group USER-COMMAND
*&amp;---------------------------------------------------------------------*

PARAMETERS: p_rb1 RADIOBUTTON GROUP rbg USER-COMMAND rb_cmd,
            p_rb2 RADIOBUTTON GROUP rbg,
            p_rb3 RADIOBUTTON GROUP rbg.

*&amp;---------------------------------------------------------------------*
*&amp; AS LISTBOX VISIBLE LENGTH ... USER-COMMAND
*&amp;---------------------------------------------------------------------*

PARAMETERS plistbox TYPE i AS LISTBOX VISIBLE LENGTH 10 USER-COMMAND list_cmd.

AT SELECTION-SCREEN OUTPUT.

  CALL FUNCTION &#x27;VRM_SET_VALUES&#x27;
    EXPORTING
      id     = CONV vrm_id( &#x27;PLISTBOX&#x27; )
      values = VALUE vrm_values( FOR i = 1 UNTIL i &gt; 10 ( key = i text = |Value { i }| ) ).

AT SELECTION-SCREEN.

  CASE sscrfields-ucomm.
    WHEN &#x27;BOX_CMD&#x27;.
      MESSAGE |Hallo { sy-uname }. You selected a checkbox.| TYPE &#x27;I&#x27;.
    WHEN &#x27;RB_CMD&#x27;.
      MESSAGE |Hallo { sy-uname }. You selected a radio button.| TYPE &#x27;I&#x27;.
    WHEN &#x27;LIST_CMD&#x27;.
      MESSAGE |Hallo { sy-uname }. You selected an item from the list box.| TYPE &#x27;I&#x27;.
  ENDCASE.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.

  IF p_chkbox IS INITIAL.
    WRITE / `Checkbox p_chkbox was not selected.`.
  ELSE.
    WRITE / `Checkbox p_chkbox was selected.`.
  ENDIF.

  WRITE / |Radio button rbg selection: &quot;{ COND #( WHEN p_rb1 IS NOT INITIAL THEN &#x27;p_rb1&#x27; WHEN p_rb2 IS NOT INITIAL THEN &#x27;p_rb2&#x27; ELSE &#x27;p_rb3&#x27; ) }&quot;| .

  WRITE / |p_list: &quot;{ plistbox }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Assigning a screen element of a selection screen to a modification group&lt;br&gt;&lt;br&gt; Addition <code>MODIF ID</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The <code>PARAMETERS</code> statement can be specified with the <code>MODIF ID</code> addition to assign a screen element of a selection screen to a modification group, which represents a group of multiple screen elements of a dynpro having a three-character ID. This ID is used to modify the display properties of all those elements specifying the ID before they are actually displayed. For that purpose, you can use the <code>MODIFY SCREEN</code> statement. The ID is assigned to the <code>screen-group1</code> component that can be evaluated.</li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenmodification_group_glosry.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

TABLES sscrfields.

PARAMETERS showhide AS CHECKBOX USER-COMMAND box_cmd1.

PARAMETERS p_input1 TYPE c LENGTH 20.
PARAMETERS p_input2 TYPE c LENGTH 20 MODIF ID id1.
PARAMETERS p_input3 TYPE c LENGTH 20.
PARAMETERS p_input4 TYPE c LENGTH 20 MODIF ID id1.

PARAMETERS colored AS CHECKBOX USER-COMMAND box_cmd2.

PARAMETERS p_input5 TYPE c LENGTH 20 DEFAULT &#x27;abcdefg&#x27;.
PARAMETERS p_input6 TYPE c LENGTH 20 MODIF ID id2 DEFAULT &#x27;hijklm&#x27;.
PARAMETERS p_input7 TYPE c LENGTH 20 MODIF ID id2 DEFAULT &#x27;nopqrs&#x27;.
PARAMETERS p_input8 TYPE c LENGTH 20 DEFAULT &#x27;tuvwxyz&#x27;.

AT SELECTION-SCREEN.

  CASE sscrfields-ucomm.
    WHEN &#x27;BOX_CMD1&#x27;.
      MESSAGE |Hallo { sy-uname }. You selected the SHOWHIDE checkbox. Screen elements are about to be shown or hidden.| TYPE &#x27;I&#x27;.
    WHEN &#x27;BOX_CMD2&#x27;.
      MESSAGE |Hallo { sy-uname }. You selected the COLORED checkbox. The colors of output fields are about to be intensified or reverted.| TYPE &#x27;I&#x27;.
  ENDCASE.

AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN INTO DATA(screen_wa).
    IF showhide = &#x27;X&#x27; AND screen_wa-group1 = &#x27;ID1&#x27;.
      screen_wa-active = &#x27;0&#x27;.
    ENDIF.
    IF colored = &#x27;X&#x27; AND screen_wa-group1 = &#x27;ID2&#x27;.
      screen_wa-intensified = &#x27;1&#x27;.
    ENDIF.
    MODIFY SCREEN FROM screen_wa.
  ENDLOOP.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |p_input1: &quot;{ p_input1 }&quot;|.
  WRITE / |p_input2: &quot;{ p_input2 }&quot;|.
  WRITE / |p_input3: &quot;{ p_input3 }&quot;|.
  WRITE / |p_input4: &quot;{ p_input4 }&quot;|.
  WRITE / |p_input5: &quot;{ p_input5 }&quot;|.
  WRITE / |p_input6: &quot;{ p_input6 }&quot;|.
  WRITE / |p_input7: &quot;{ p_input7 }&quot;|.
  WRITE / |p_input8: &quot;{ p_input8 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Replacing the technical name displayed on the screen</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The parameter name is a technical name. You probably do not want to display technical names on the user interface. You can define proper names (<a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abentext_element_glosry.htm">text elements</a>) in the ABAP Editor, e.g. for translation purposes.</li>
<li>For the example code snippet, proceed as follows:</li>
<li>In ADT, make a right-click inside your demo report you copied the code to. Choose <em>Open Others -&gt; Text Elements</em>.</li>
<li>At the bottom of the opened window, select the <em>Selection Texts</em> tab.</li>
<li>Insert <code>PARAM=Enter some text</code> and activate. Leave the <em>number</em> parameter unchanged.</li>
<li>You can also use <code>SELECTION-SCREEN</code> statements such as the <code>... COMMENT ...</code> statement, as outlined below, and assign a text value to the screen comment.</li>
<li>When you execute the program, you will see the technical names replaced by <em>Enter some text</em> and <em>Enter a number</em>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

PARAMETERS param type string.

SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT (33) txt.
  PARAMETERS number type i.
SELECTION-SCREEN END OF LINE.

INITIALIZATION.

  txt = &#x27;Enter a number&#x27;.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param: &quot;{ param }&quot;|.
  WRITE / |number: &quot;{ number }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>SELECT-OPTIONS</h3>
<ul>
<li>Declares selection criteria for a data object</li>
<li>Unlike the <code>PARAMETERS</code> statement, which specifies a single value for a variable, the <code>SELECT-OPTIONS</code> statement allows you to specify complex criteria, such as an value range or a list of single values, to include or exclude values, that can be evaluated.</li>
<li>The selection criteria are assigned to a <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_table_glosry.htm">selection table</a>:</li>
<li>Such a table contains four columns - <code>LOW</code>, <code>HIGH</code>, <code>OPTION</code>, <code>SIGN</code> - for determining range conditions. Each line of such a table represents a condition.</li>
<li>Typically, the content of the selection table can be evaluated in <code>SELECT</code> statements using the <code>IN</code> operator in the <code>WHERE</code> clause.</li>
<li>If the selection table is empty, all lines are respected.</li>
<li>Selection tables have the same layout as <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenranges_table_glosry.htm">ranges table</a> that can be created using the syntax <code>TYPE RANGE OF</code>.</li>
<li>For historical reasons, the selection table is a table with <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenheader_line_glosry.htm">header line</a>. Therefore, if you want to address the table content (beyond the use in a <code>SELECT ... WHERE ... IN ...</code> statement), use the syntax <code>a[]</code>.</li>
<li>Multiple additions are available, and combinations of them are possible.</li>
</ul>
<p>The following table includes code snippets with a selection of available syntax options:</p>
<div class="note">[!NOTE] - Various combinations of multiple additions are possible with `SELECT-OPTIONS` statements. - To try out the code examples, you can create a demo executable program, copy and paste the code snippets, and run the programs using F8. They are designed to output content using classic lists. - The code snippets anticipate topics outlined further down, for example, event blocks and `WRITE` statements.</div>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Addition &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Determining the <code>low</code> and <code>high</code> columns in the selection table&lt;br&gt;&lt;br&gt; Addition <code>FOR</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Determines the <code>low</code> and <code>high</code> columns in the selection table.</li>
<li>Besides a statically defined data object from the program or public attributes of global classes, you can also dynamically specify a DDIC data type within parentheses.</li>
<li>Note:</li>
<li>Without the addition <code>NO_DISPLAY</code>, the data types must be elementary and flat (except type <code>f</code> and enumerated types).</li>
<li>When you specify a reference to a DDIC type, its screen-related properties are used.</li>
<li>In case of the dynamic specification, the columns in the selection table are created using type <code>c</code> length 45. A constant or variable containing the name of component of a flat structure from the DDIC can be specified. Literals are not evaluated.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>A demo internal table is created. It is filled in the <code>INITIALIZATION</code> event block.</li>
<li>Two <code>SELECT-OPTIONS</code> statements are included. The first references a statically data object after <code>FOR</code>, the second a dynamically specified DDIC type. The latter demonstrates that screen-relevant properties are inherited.</li>
<li>When running the program, you can make entries. <code>SELECT</code> statements retrieve data from the internal table based on the specifications in the ranges table. To visualize the selection result, <code>WRITE</code> statements display table entries in a classic list.</li>
</ul>
<pre><code>PROGRAM.

DATA: BEGIN OF demo_structure,
        carrid TYPE c LENGTH 3,
        num    TYPE i,
      END OF demo_structure.

DATA itab LIKE TABLE OF demo_structure WITH EMPTY KEY.

*&amp;---------------------------------------------------------------------*
*&amp; Referencing a statically defined data object
*&amp;---------------------------------------------------------------------*

&quot;In the example, a structure component is specified.
SELECT-OPTIONS sel1 FOR demo_structure-num.

*&amp;---------------------------------------------------------------------*
*&amp; Referencing a dynamically specified DDIC type
*&amp;---------------------------------------------------------------------*

&quot;Here, the name of a component of a flat structure from the DDIC
&quot;is passed. The example type involves search help functionality.
&quot;Screen-relevant properties are inherited.
DATA dyn_spec TYPE c LENGTH 12.
SELECT-OPTIONS sel2 FOR (dyn_spec).

INITIALIZATION.

  itab = VALUE #( ( carrid = &#x27;AA&#x27; num = 1 )
                  ( carrid = &#x27;AC&#x27; num = 2 )
                  ( carrid = &#x27;AF&#x27; num = 3 )
                  ( carrid = &#x27;AZ&#x27; num = 4 )
                  ( carrid = &#x27;BA&#x27; num = 5 )
                  ( carrid = &#x27;FJ&#x27; num = 6 )
                  ( carrid = &#x27;JL&#x27; num = 7 )
                  ( carrid = &#x27;LH&#x27; num = 8 )                  
                  ( carrid = &#x27;NW&#x27; num = 9 )
                  ( carrid = &#x27;QF&#x27; num = 10 )
                  ( carrid = &#x27;SQ&#x27; num = 11 )
                  ( carrid = &#x27;UA&#x27; num = 12 ) ).

  dyn_spec = &#x27;SCARR-CARRID&#x27;.

START-OF-SELECTION.

  WRITE / `Selected values (sel1):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel1
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (sel2):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE carrid IN @sel2
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying screen options&lt;br&gt;&lt;br&gt; Additions <code>OBLIGATORY</code>, <code>NO-DISPLAY</code>, <code>VISIBLE LENGTH</code>, <code>NO-EXTENSION</code>, <code>NO INTERVALS</code>, <code>MODIF ID</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>OBLIGATORY</code>: Declares the first input field as a required field. If there is no entry, the program cannot proceed when choosing Execute. A message will be displayed.</li>
<li><code>NO-DISPLAY</code>: Hides screen elements. Values (any flat types) can be supplied using <code>SUBMIT</code> statements. Note that there is no restriction to 45 characters.</li>
<li><code>VISIBLE LENGTH</code>: Defines the visible length of the input field.</li>
<li><code>NO-EXTENSION</code>: Cancels the option for multiple selection on the screen.</li>
<li><code>NO INTERVALS</code>: Removes the second input field on the screen.</li>
<li><code>MODIF ID</code>: See a description in the previous section about <code>PARAMETERS</code>.</li>
<li>Note that combinations of the additions are possible.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>A demo internal table is created. It is filled in the <code>INITIALIZATION</code> event block.</li>
<li>Multiple <code>SELECT-OPTIONS</code> statements are included that specify additions.</li>
<li>When running the program, you can make entries. <code>SELECT</code> statements retrieve data from the internal table based on the specifications in the ranges table. To visualize the selection result, <code>WRITE</code> statements display table entries in a classic list.</li>
</ul>
<pre><code>PROGRAM.

DATA: BEGIN OF demo_structure,
        carrid TYPE c LENGTH 3,
        num    TYPE i,
      END OF demo_structure.

DATA itab LIKE TABLE OF demo_structure WITH EMPTY KEY.

*&amp;---------------------------------------------------------------------*
*&amp; OBLIGATORY
*&amp;---------------------------------------------------------------------*

SELECT-OPTIONS sel_oblg FOR demo_structure-num OBLIGATORY.

*&amp;---------------------------------------------------------------------*
*&amp; NO-DISPLAY
*&amp;---------------------------------------------------------------------*

SELECT-OPTIONS sel_nodi FOR demo_structure-num NO-DISPLAY.

*&amp;---------------------------------------------------------------------*
*&amp; VISIBLE LENGTH
*&amp;---------------------------------------------------------------------*

SELECT-OPTIONS sel_visl FOR demo_structure-carrid VISIBLE LENGTH 2.

*&amp;---------------------------------------------------------------------*
*&amp; NO-EXTENSION
*&amp;---------------------------------------------------------------------*

SELECT-OPTIONS sel_noet FOR demo_structure-num NO-EXTENSION.

*&amp;---------------------------------------------------------------------*
*&amp; NO INTERVALS
*&amp;---------------------------------------------------------------------*

SELECT-OPTIONS sel_noin FOR demo_structure-num NO INTERVALS.

INITIALIZATION.

  itab = VALUE #( ( carrid = &#x27;AA&#x27; num = 1 )
                  ( carrid = &#x27;AC&#x27; num = 2 )
                  ( carrid = &#x27;AF&#x27; num = 3 )
                  ( carrid = &#x27;AZ&#x27; num = 4 )
                  ( carrid = &#x27;BA&#x27; num = 5 )
                  ( carrid = &#x27;FJ&#x27; num = 6 )
                  ( carrid = &#x27;JL&#x27; num = 7 )
                  ( carrid = &#x27;LH&#x27; num = 8 )                  
                  ( carrid = &#x27;NW&#x27; num = 9 )
                  ( carrid = &#x27;QF&#x27; num = 10 )
                  ( carrid = &#x27;SQ&#x27; num = 11 )
                  ( carrid = &#x27;UA&#x27; num = 12 ) ).

START-OF-SELECTION.

  WRITE / `Selected values (OBLIGATORY addition):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_oblg
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (NO-DISPLAY addition):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_nodi
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (VISIBLE LENGTH addition):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE carrid IN @sel_visl
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (NO-EXTENSION addition):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_noet
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (NO INTERVALS addition):`.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_noin
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying value options&lt;br&gt;&lt;br&gt; Additions <code>DEFAULT a [TO b] [OPTION c] [SIGN d]</code>, <code>LOWER CASE</code>, <code>MATCHCODE OBJECT</code>, <code>MEMORY ID</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>DEFAULT a [TO b] [OPTION c] [SIGN d]</code>: Defines start values for the first line of the selection table.</li>
<li><code>a</code> specifies the <code>low</code> column</li>
<li><code>b</code> specifies the <code>high</code> column</li>
<li><code>c</code> specifies the <code>option</code> column (specify the comparison expressions <code>eq</code>, <code>ne</code>, <code>ge</code>, <code>gt</code>, <code>le</code>, <code>lt</code>, <code>cp</code>, <code>np</code>, <code>bt</code>, <code>nb</code> directly after <code>OPTION</code>; restrictions apply if you do not specify <code>TO</code>; not specifying <code>OPTION</code> means <code>eq</code> or <code>bt</code> by default; to use patterns with <code>cp</code> and <code>np</code>, specify wildcard characters <code>*</code> or <code>+</code>)</li>
<li><code>d</code> specifies the <code>sign</code> column (specify either <code>i</code> or <code>e</code> to include or exclude; not specifying <code>SIGN</code> means <code>i</code> is used by default)</li>
<li><code>LOWER CASE</code>: Avoid autoamtic transformation to uppercase letters.</li>
<li><code>MATCHCODE OBJECT</code>: Links an input field with a DDIC search help</li>
<li><code>MEMORY ID</code>: See a description in the previous section about <code>PARAMETERS</code>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>A demo internal table is created. It is filled in the <code>INITIALIZATION</code> event block.</li>
<li>Multiple <code>SELECT-OPTIONS</code> statements are included that specify additions.</li>
<li>When running the program, you can make entries. <code>SELECT</code> statements retrieve data from the internal table based on the specifications in the ranges table. To visualize the selection result, <code>WRITE</code> statements display table entries in a classic list. Additionally, the content of the ranges tables is displayed.</li>
</ul>
<pre><code>PROGRAM.

DATA: BEGIN OF demo_structure,
        carrid TYPE c LENGTH 3,
        num    TYPE i,
      END OF demo_structure.

DATA itab LIKE TABLE OF demo_structure WITH EMPTY KEY.

*&amp;---------------------------------------------------------------------*
*&amp; DEFAULT a [TO b] [OPTION c] [SIGN d]
*&amp;---------------------------------------------------------------------*

SELECT-OPTIONS sel_dfl1 FOR demo_structure-num DEFAULT 4.
SELECT-OPTIONS sel_dfl2 FOR demo_structure-num DEFAULT 2 TO 8.
SELECT-OPTIONS sel_dfl3 FOR demo_structure-num DEFAULT 5 TO 10 OPTION BT SIGN I.
SELECT-OPTIONS sel_dfl4 FOR demo_structure-num DEFAULT 5 TO 10 OPTION BT SIGN E.
SELECT-OPTIONS sel_dfl5 FOR demo_structure-carrid DEFAULT &#x27;A*&#x27; OPTION CP SIGN I.

*&amp;---------------------------------------------------------------------*
*&amp; LOWER CASE
*&amp;---------------------------------------------------------------------*

&quot;The example is specified with DEFAULT ... TO ... and includes values
&quot;with uppercase and lowercase. If you leave the demo values, all table
&quot;entries are read (starting from AA which is the first) as there is no
&quot;lh value. It should be specified as LH in uppercase.
SELECT-OPTIONS sel_lc FOR demo_structure-carrid DEFAULT &#x27;AA&#x27; TO &#x27;lh&#x27; LOWER CASE.

*&amp;---------------------------------------------------------------------*
*&amp; MATCHCODE OBJECT
*&amp;---------------------------------------------------------------------*

&quot;The specified type includes a search help
SELECT-OPTIONS sel_mtch FOR demo_structure-carrid MATCHCODE OBJECT s_carrier_id.

INITIALIZATION.

  itab = VALUE #( ( carrid = &#x27;AA&#x27; num = 1 )
                  ( carrid = &#x27;AC&#x27; num = 2 )
                  ( carrid = &#x27;AF&#x27; num = 3 )
                  ( carrid = &#x27;AZ&#x27; num = 4 )
                  ( carrid = &#x27;BA&#x27; num = 5 )
                  ( carrid = &#x27;FJ&#x27; num = 6 )
                  ( carrid = &#x27;JL&#x27; num = 7 )
                  ( carrid = &#x27;LH&#x27; num = 8 )
                  ( carrid = &#x27;NW&#x27; num = 9 )
                  ( carrid = &#x27;QF&#x27; num = 10 )
                  ( carrid = &#x27;SQ&#x27; num = 11 )
                  ( carrid = &#x27;UA&#x27; num = 12 ) ).

START-OF-SELECTION.

  WRITE / `Selected values (only DEFAULT):`.
  LOOP AT sel_dfl1 ASSIGNING FIELD-SYMBOL(&lt;st&gt;).
    WRITE / |low: { &lt;st&gt;-low }, high: { &lt;st&gt;-high }, option: { &lt;st&gt;-option }, sign: { &lt;st&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_dfl1
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (DEFAULT, TO):`.
  LOOP AT sel_dfl2 ASSIGNING &lt;st&gt;.
    WRITE / |low: { &lt;st&gt;-low }, high: { &lt;st&gt;-high }, option: { &lt;st&gt;-option }, sign: { &lt;st&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_dfl2
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (DEFAULT, TO, OPTION, SIGN include):`.
  LOOP AT sel_dfl3 ASSIGNING &lt;st&gt;.
    WRITE / |low: { &lt;st&gt;-low }, high: { &lt;st&gt;-high }, option: { &lt;st&gt;-option }, sign: { &lt;st&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_dfl3
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (DEFAULT, TO, OPTION, SIGN exclude):`.
  LOOP AT sel_dfl4 ASSIGNING &lt;st&gt;.
    WRITE / |low: { &lt;st&gt;-low }, high: { &lt;st&gt;-high }, option: { &lt;st&gt;-option }, sign: { &lt;st&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE num IN @sel_dfl4
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (DEFAULT, OPTION using CP for patterns, SIGN include):`.
  LOOP AT sel_dfl5 ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    WRITE / |low: { &lt;fs&gt;-low }, high: { &lt;fs&gt;-high }, option: { &lt;fs&gt;-option }, sign: { &lt;fs&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE carrid IN @sel_dfl5
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (LOWER CASE):`.
  LOOP AT sel_lc ASSIGNING &lt;fs&gt;.
    WRITE / |low: { &lt;fs&gt;-low }, high: { &lt;fs&gt;-high }, option: { &lt;fs&gt;-option }, sign: { &lt;fs&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE carrid IN @sel_lc
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.

  SKIP.
  SKIP.
  WRITE / `Selected values (MATCHCODE OBJECT):`.
  LOOP AT sel_mtch ASSIGNING &lt;fs&gt;.
    WRITE / |low: { &lt;fs&gt;-low }, high: { &lt;fs&gt;-high }, option: { &lt;fs&gt;-option }, sign: { &lt;fs&gt;-sign }|.
  ENDLOOP.
  SKIP.

  SELECT *
         FROM @itab AS tab
         WHERE carrid IN @sel_mtch
         INTO @demo_structure.
    WRITE / |{ demo_structure-carrid } { demo_structure-num }|.
  ENDSELECT.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>SELECTION-SCREEN</h3>
<pre><code>  PARAMETERS pa TYPE string.
  SELECTION-SCREEN COMMENT /5(10) t1.
  &quot;SELECTION-SCREEN COMMENT 10(20) t2.
  &quot;SELECTION-SCREEN COMMENT 20(20) t3.</code></pre>
<ul>
<li><code>SELECTION-SCREEN</code> statements can be used to create and modify the layout of standalone selection screens.</li>
<li>Note: The standard selection screen for executable programs is created automatically. That is, each executable program contains a standard selection screen with dynpro number 1000. The screen elements there are defined by all <code>PARAMETERS</code>, <code>SELECT-OPTIONS</code>, and <code>SELECTION-SCREEN</code> statements that are <strong>not</strong> specified within the <code>SELECTION-SCREEN BEGIN OF SCREEN ... SELECTION-SCREEN END OF SCREEN ...</code> block.</li>
<li>Selection screens can be created as regular dynpros or as subscreen dynpros.</li>
<li>Multiple syntax variants with <code>SELECTION-SCREEN</code> are available. Most of them do not create selection screens, but are used to change the layout of selection screens, create additional screen elements, and so on.</li>
<li>Example: The <code>PARAMETERS</code> and <code>SELECT-OPTIONS</code> statements create input fields on an individual line. Using the variants of the <code>SELECTION-SCREEN</code> statement, you can arrange the layout differently.</li>
<li>However, the selection screen cannot be generated if there are conflicts with existing screen elements.</li>
<li>See the effect with the following snippet. Run a program with the code snippet and comment in the third line. The program terminates. The text to be created (in the same line as the first text) interferes with the first text. However, the fourth line would be possible because there is no overlap.</li>
</ul>
<h4>Creating Selection Screens</h4>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Statement &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating a selection screen as regular dynpro&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN BEGIN OF SCREEN ...</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Creates a selection screen as regular dynpro by specifying a dynpro number.</li>
<li>The dynpro number must be unique in the program. Do not use 1000.</li>
<li>Additions (which can be combined):</li>
<li><code>... TITLE ...</code>: Defines a title (e.g. a random name or the name of a text symbol) for the title bar</li>
<li><code>... AS WINDOW</code>: Displays the selection screen in a modal dialog box</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

SELECTION-SCREEN BEGIN OF SCREEN 9000.
  PARAMETERS param TYPE string.
  PARAMETERS number TYPE i.
SELECTION-SCREEN END OF SCREEN 9000.

START-OF-SELECTION.

  CALL SELECTION-SCREEN 9000.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param: &quot;{ param }&quot;|.
  WRITE / |number: &quot;{ number }&quot;|.</code></pre>
<p>&lt;br&gt;</p>
<p><code>TITLE</code> addition For the example, proceed as follows:</p>
<pre><code>  @MaxLength:64
  001=Selection screen demo with the TITLE addition</code></pre>
<ul>
<li>Copy and paste the code snippet to a demo executable program.</li>
<li>Create a text symbol by making a right-click in the source code, and choose <em>Open Others -&gt; Text Elements</em>.</li>
<li>Select the <em>Text Symbols</em> tab.</li>
<li>Add the following code and activate.</li>
</ul>
<pre><code>PROGRAM.

SELECTION-SCREEN BEGIN OF SCREEN 9001 TITLE TEXT-001.
  PARAMETERS param TYPE string.
  PARAMETERS number TYPE i.
SELECTION-SCREEN END OF SCREEN 9001.

START-OF-SELECTION.

  CALL SELECTION-SCREEN 9001.

  WRITE / `Text of text symbol:`.
  WRITE / TEXT-001.
  SKIP.
  SKIP.
  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param: &quot;{ param }&quot;|.
  WRITE / |number: &quot;{ number }&quot;|.</code></pre>
<p>&lt;br&gt;</p>
<p><code>AS WINDOW</code> addition</p>
<ul>
<li>The example code includes multiple <code>SELECTION-SCREEN</code> statements that specify the <code>AS WINDOW</code> addition. They also specify the <code>TITLE</code> addition. In these cases, a random name is used. The name is assigned a value in the <code>INITIALIZATION</code> event block.</li>
<li>You can select a radio button. Depending on the selection a selection screen is called.</li>
<li>The example demonstrates selection screens displayed as modal dialog box. They are called with <code>CALL SELECTION-SCREEN ... STARTING AT ...</code> statements (except one example, <code>p_winno</code> selection, that does not open a modal dialog box).</li>
</ul>
<pre><code>PROGRAM.

PARAMETERS: p_winat1 RADIOBUTTON GROUP rbg,
            p_winat2 RADIOBUTTON GROUP rbg,
            p_noat  RADIOBUTTON GROUP rbg.

SELECTION-SCREEN BEGIN OF SCREEN 9001 TITLE seltitl1 AS WINDOW.
  PARAMETERS param1 TYPE string.
SELECTION-SCREEN END OF SCREEN 9001.

SELECTION-SCREEN BEGIN OF SCREEN 9002 TITLE seltitl2 AS WINDOW.
  PARAMETERS param2 TYPE string.
SELECTION-SCREEN END OF SCREEN 9002.

INITIALIZATION.

  seltitl1 = &#x27;Selection screen demo with the AS WINDOW addition (1)&#x27;.
  seltitl2 = &#x27;Another selection screen with the AS WINDOW addition (2)&#x27;.

START-OF-SELECTION.

  CASE &#x27;X&#x27;.
    WHEN p_winat1.
      CALL SELECTION-SCREEN 9001 STARTING AT 10 10.
      WRITE / `Inserted value:`.
      SKIP.
      WRITE / |param1: &quot;{ param1 }&quot;|.
    WHEN p_winat2.
      CALL SELECTION-SCREEN 9002 STARTING AT 1 5.
      WRITE / `Inserted value:`.
      SKIP.
      WRITE / |param2: &quot;{ param2 }&quot;|.
    WHEN p_noat.
      CALL SELECTION-SCREEN 9001.
      WRITE / `Inserted value:`.
      SKIP.
      WRITE / |param1: &quot;{ param1 }&quot;|.
  ENDCASE.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating a selection screen as subscreen dynpro&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN BEGIN OF SCREEN ... AS SUBSCREEN</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Can be included in other dynpros or selection screens, or in subscreen areas or tab pages.</li>
<li>They cannot be called explicitly using <code>CALL SELECTION-SCREEN</code> statements.</li>
<li>Additions:</li>
<li><code>NO INTERVALS</code>: <code>NO INTERVALS</code> is implicitly used in <code>SELECT-OPTIONS</code> statements</li>
<li><code>NESTING LEVEL</code> Adjusts the subscreen width (a number between 0 and 4 is ecpected) when the subscreen is part of one or more frames in tabstrip controls</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>Multiple selection screens are created as subscreen dynpros, each including a <code>PARAMETERS</code> statement.</li>
<li>They are included in a tabstrip control on the standard selection screen of an executable program. The <code>TABBED BLOCK</code> syntax is covered further down. In the example, the first tabbed block specifies default screens. The second does not. There, the switch on tab click is realized by evaluating <code>sscrfields-ucomm</code>.</li>
</ul>
<pre><code>PROGRAM.

TABLES sscrfields.

SELECTION-SCREEN BEGIN OF SCREEN 9001 AS SUBSCREEN.
  PARAMETERS param1 TYPE c LENGTH 20.
SELECTION-SCREEN END OF SCREEN 9001.

SELECTION-SCREEN BEGIN OF SCREEN 9002 AS SUBSCREEN.
  PARAMETERS param2 TYPE c LENGTH 20.
SELECTION-SCREEN END OF SCREEN 9002.

SELECTION-SCREEN BEGIN OF SCREEN 9003 AS SUBSCREEN.
  PARAMETERS param3 TYPE c LENGTH 20.
SELECTION-SCREEN END OF SCREEN 9003.

SELECTION-SCREEN BEGIN OF SCREEN 9004 AS SUBSCREEN.
  PARAMETERS param4 TYPE c LENGTH 20.
SELECTION-SCREEN END OF SCREEN 9004.

SELECTION-SCREEN: BEGIN OF TABBED BLOCK tabbl1 FOR 2 LINES,
TAB (20) tab1 USER-COMMAND push1 DEFAULT SCREEN 9001,
TAB (20) tab2 USER-COMMAND push2 DEFAULT SCREEN 9002,
END OF BLOCK tabbl1.

SELECTION-SCREEN: BEGIN OF TABBED BLOCK tabbl2 FOR 2 LINES,
TAB (20) tab3 USER-COMMAND push3,
TAB (20) tab4 USER-COMMAND push4,
END OF BLOCK tabbl2.

INITIALIZATION.

  tab1 = &#x27;Subscreen 1&#x27;.
  tab2 = &#x27;Subscreen 2&#x27;.
  tab3 = &#x27;Subscreen 3&#x27;.
  tab4 = &#x27;Subscreen 4&#x27;.

  tabbl2-prog = sy-repid.
  tabbl2-dynnr = 9003.
  tabbl2-activetab = &#x27;TAB3&#x27;.

AT SELECTION-SCREEN.

  CASE sscrfields-ucomm.
    WHEN &#x27;PUSH1&#x27;.
      tab1 = &#x27;New subscreen title&#x27;.
    WHEN &#x27;PUSH2&#x27;.
      tab2 = &#x27;Some tab&#x27;.
    WHEN &#x27;PUSH3&#x27;.
      tabbl2-dynnr = 9003.
    WHEN &#x27;PUSH4&#x27;.
      tabbl2-dynnr = 9004.
  ENDCASE.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  WRITE / |param3: &quot;{ param3 }&quot;|.
  WRITE / |param4: &quot;{ param4 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Adapting the Selection Screen Layout</h4>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Statement &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Adding blank lines&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN SKIP</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Creates a specified number of lines</li>
<li>If a number is not specified, one blank line is created by default.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PARAMETERS param1 TYPE c LENGTH 20.
SELECTION-SCREEN SKIP.
PARAMETERS param2 TYPE c LENGTH 20.
SELECTION-SCREEN SKIP 3.
PARAMETERS param3 TYPE c LENGTH 20.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  WRITE / |param3: &quot;{ param3 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating horizontal lines&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN ULINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Formatting options are available:</li>
<li>Position from where to start the line and length specification</li>
<li>Either the position (1-83) is specified directly as number or with the additions <code>POS_LOW</code> (position of first input field) and <code>POS_HIGH</code> (position of second inut field).</li>
<li><code>/</code> creates the line a new line; cannot be specified if multiple elements are specified in a line</li>
<li>If no formatting options are specified, a line is created that goes accross the whole screen below the lines that are already filled.</li>
<li>The addition <code>MODIF ID</code> is supported.</li>
<li>Find more details <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapselection-screen_uline.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

PARAMETERS param1 TYPE c LENGTH 20.
SELECTION-SCREEN ULINE.
PARAMETERS param2 TYPE c LENGTH 20.
&quot;/ means the line is created in a new line
&quot;/ is followed by the position and the length in parentheses
SELECTION-SCREEN ULINE /5(10).
PARAMETERS param3 TYPE c LENGTH 20.
&quot;SELECTION-SCREEN ULINE 5(10).
PARAMETERS param4 TYPE c LENGTH 20.
SELECTION-SCREEN ULINE /pos_low(15).
PARAMETERS param5 TYPE c LENGTH 20.
SELECTION-SCREEN ULINE /pos_high(15).
&quot;/ can be ommitted if the elements are in the same line
SELECTION-SCREEN BEGIN OF LINE.
  PARAMETERS param6 TYPE c LENGTH 1.
  SELECTION-SCREEN ULINE 4(5).
  PARAMETERS param7 TYPE c LENGTH 1.
  SELECTION-SCREEN ULINE pos_low(20).
  PARAMETERS param8 TYPE c LENGTH 1.
  SELECTION-SCREEN ULINE pos_high(40).
SELECTION-SCREEN END OF LINE.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  WRITE / |param3: &quot;{ param3 }&quot;|.
  WRITE / |param4: &quot;{ param4 }&quot;|.
  WRITE / |param5: &quot;{ param5 }&quot;|.
  WRITE / |param6: &quot;{ param6 }&quot;|.
  WRITE / |param7: &quot;{ param7 }&quot;|.
  WRITE / |param8: &quot;{ param8 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating an output field/comment on the selection screen&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN COMMENT</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Creates text content in a field</li>
<li>The statement expects a random name (maximum 8 characters; a global variable with type <code>c</code> and length 83 is implicitly created) or a text symbol</li>
<li>Additions</li>
<li>Position-related additions as outlined for <code>SELECTION-SCREEN ULINE</code></li>
<li><code>FOR FIELD</code>: Links output field to elements specified with <code>PARAMETERS</code> or <code>SELECT-OPTIONS</code></li>
<li><code>VISIBLE LENGTH</code>: Defines the visible length of the output field</li>
<li><code>MODIF ID</code>: See above.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

*&amp;---------------------------------------------------------------------*
*&amp; Position-related additions
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN COMMENT /1(20) txt1.
SELECTION-SCREEN ULINE /1(50).
PARAMETERS: rb1 RADIOBUTTON GROUP rbgr,
            rb2 RADIOBUTTON GROUP rbgr,
            rb3 RADIOBUTTON GROUP rbgr.
SELECTION-SCREEN ULINE /1(50).

SELECTION-SCREEN COMMENT /pos_high(40) txt2.

&quot;Providing a text for a parameter
SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT (15) txt3.
  PARAMETERS param1 TYPE c LENGTH 10.
SELECTION-SCREEN END OF LINE.

&quot;Using a text symbol
SELECTION-SCREEN COMMENT /1(20) TEXT-001.

*&amp;---------------------------------------------------------------------*
*&amp; VISIBLE LENGTH
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN COMMENT /1(20) txt4 VISIBLE LENGTH 4.

*&amp;---------------------------------------------------------------------*
*&amp; MODIF ID
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN COMMENT /1(50) txt5 MODIF ID mid.
SELECTION-SCREEN COMMENT /1(50) txt6.
SELECTION-SCREEN COMMENT /1(50) txt7 MODIF ID mid.

AT SELECTION-SCREEN OUTPUT.
  txt1 = &#x27;Make a selection:&#x27;.
  txt2 = &#x27;Text with pos_high&#x27;.
  txt3 = &#x27;Make an entry&#x27;.
  txt4 = &#x27;Some long text&#x27;.
  txt5 = &#x27;First intensified text&#x27;.
  txt6 = &#x27;Not intensified text&#x27;.
  txt7 = &#x27;Second intensified tex&#x27;.
  LOOP AT SCREEN INTO DATA(screen_wa).
    IF screen_wa-group1 = &#x27;MID&#x27;.
      screen_wa-intensified = &#x27;1&#x27;.
      MODIFY SCREEN FROM screen_wa.
    ENDIF.
  ENDLOOP.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |Radio button selection: &quot;{ COND #( WHEN rb1 IS NOT INITIAL THEN &#x27;rb1&#x27; WHEN rb2 IS NOT INITIAL THEN &#x27;rb2&#x27; ELSE &#x27;rb3&#x27; ) }&quot;|.
  WRITE / |param1: &quot;{ param1 }&quot;|.

  IF TEXT-001 IS INITIAL.
    WRITE / |Text symbol TEXT-001 does not exist.|.
  ENDIF.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying multiple elements in one line and their position in the line&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN BEGIN OF LINE. ... SELECTION-SCREEN END OF LINE.</code> and <code>SELECTION-SCREEN POSITION</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Places all <code>PARAMETERS</code>, <code>SELECT-OPTIONS</code> and <code>SELECTION-SCREEN</code> statements in one line without spaces</li>
<li>The statement <code>SELECTION-SCREEN POSITION</code> can only be specified in the statement block. The position can be specified as number (1-83) or using the additions <code>POS_LOW</code> and <code>POS_HIGH</code>. The statement relates to positioning the following statement.</li>
<li>Note that if there are positioning conflicts, the selection screen cannot be created.</li>
<li>In the statement block, certain restrictions apply. See <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapselection-screen_line.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT (10) txt1.
  SELECTION-SCREEN POSITION 15.
  PARAMETERS param1 TYPE c LENGTH 3.
  SELECTION-SCREEN COMMENT (10) txt2.
  SELECTION-SCREEN POSITION 45.
  PARAMETERS param2 TYPE c LENGTH 3.
  SELECTION-SCREEN COMMENT (10) txt3.
  SELECTION-SCREEN POSITION 70.
  SELECTION-SCREEN COMMENT (10) txt4.
SELECTION-SCREEN END OF LINE.

DATA dyn_spec TYPE c LENGTH 12.
&quot;Chained statement
&quot;The example emphasizes potential positioning
&quot;conflicts that can prevent the selection screen
&quot;from being created. For demo purposes, you can
&quot;comment in param3 and run the program, resulting
&quot;in a runtime error. Then you can comment in the
&quot;NO-EXTENSION and NO INTERVALS additions to the
&quot;SELECT-OPTIONS statement, and run the program
&quot;again. Since the extra screen elements are not
&quot;displayed, the selection screen can be created.
SELECTION-SCREEN: BEGIN OF LINE,
POSITION 5,
COMMENT (10) txt5.
SELECT-OPTIONS sel FOR (dyn_spec)
&quot;NO-EXTENSION
&quot;NO INTERVALS
.
&quot;PARAMETERS param3 TYPE c LENGTH 3.
SELECTION-SCREEN: POSITION 70,
COMMENT (10) txt6,
END OF LINE.

INITIALIZATION.
  txt1 = &#x27;Some text&#x27;.
  txt2 = &#x27;Hello&#x27;.
  txt3 = sy-uname.
  txt4 = sy-datum.
  txt5 = &#x27;Select&#x27;.
  txt6 = &#x27;More text&#x27;.

  dyn_spec = &#x27;SCARR-CARRID&#x27;.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  SKIP.
  WRITE / `Selection table content:`.
  LOOP AT sel ASSIGNING FIELD-SYMBOL(&lt;st&gt;).
    WRITE / |low: { &lt;st&gt;-low }, high: { &lt;st&gt;-high }, option: { &lt;st&gt;-option }, sign: { &lt;st&gt;-sign }|.
  ENDLOOP.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating pushbuttons&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN PUSHBUTTON</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Typically, pushbuttons are used to modify the selection screen, not to control the program (e.g. to exit the program).</li>
<li>Additions:</li>
<li>Position-related additions as outlined above</li>
<li><code>USER-COMMAND</code>: Used to assign a function code. The pushbutton is enabled by implementing a <code>TABLES sscrfields.</code> statement. In doing so, an interface work area is created. When a pushbutton is clicked, the <code>AT SELECTION-SCREEN</code> event is raised, and the function code can be evaluated using <code>sscrfields-ucomm</code> (do not use <code>sy-ucomm</code>).</li>
<li><code>VISIBLE LENGTH</code>: Defines the visible length of the pushbutton</li>
<li><code>MODIF ID</code>: See above</li>
<li>You can use the function module <code>ICON_CREATE</code> to assign an icon, a tooltip, and a text to a pushbutton.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

TABLES sscrfields.

SELECTION-SCREEN PUSHBUTTON /1(15) btn_a USER-COMMAND cmd1.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN PUSHBUTTON /1(15) btn_b USER-COMMAND cmd2.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN PUSHBUTTON /1(30) btn_c USER-COMMAND cmd3 VISIBLE LENGTH 7.

INITIALIZATION.

  btn_a = &#x27;Button A&#x27;.
  btn_c = &#x27;Button C with long text&#x27;.

  CALL FUNCTION &#x27;ICON_CREATE&#x27;
    EXPORTING
      name   = icon_information
      text   = &#x27;Button B&#x27;
      info   = &#x27;Some info&#x27;
    IMPORTING
      result = btn_b
    EXCEPTIONS
      OTHERS = 0.

AT SELECTION-SCREEN.
  CASE sscrfields-ucomm.
    WHEN &#x27;CMD1&#x27;.
      MESSAGE |Hello { sy-uname }. You clicked button A.| TYPE &#x27;I&#x27;.
    WHEN &#x27;CMD2&#x27;.
      MESSAGE |Hello { sy-uname }. You clicked button B.| TYPE &#x27;I&#x27;.
    WHEN &#x27;CMD3&#x27;.
      MESSAGE |Hello { sy-uname }. You clicked button C.| TYPE &#x27;I&#x27;.
  ENDCASE.

START-OF-SELECTION.

  WRITE / `No content to display in this example.`.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Defining a block&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN BEGIN OF BLOCK ... SELECTION-SCREEN END OF BLOCK ...</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can also create blocks within blocks.</li>
<li>The blocks raise the <code>AT SELECTION-SCREEN ON BLOCK</code>. There, the entries of a block can be processed together.</li>
<li>Additions:</li>
<li><code>WITH FRAME [TITLE ...]</code>: Draws a frame round a block. You can optionally specify a title either by specifying a random name or a text symbol.</li>
<li><code>NO INTERVALS</code>: When specified, <code>NO INTERVALS</code> is used implicitly for <code>SELECT-OPTIONS</code> (in all nested blocks).</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>Multiple blocks are created using <code>SELECTION-SCREEN ... BLOCK ...</code> statements, including different additions or none. One block contains nested blocks.</li>
<li>The raising of <code>AT SELECTION-SCREEN ON BLOCK</code> events for the blocks defined is demonstrated using messages displayed when the program is run.</li>
</ul>
<pre><code>PROGRAM.

*&amp;---------------------------------------------------------------------*
*&amp; No addition
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK block1.
  PARAMETERS param1 TYPE c LENGTH 10 DEFAULT &#x27;abc&#x27; LOWER CASE.
  PARAMETERS param2 TYPE c LENGTH 10 DEFAULT &#x27;def&#x27; LOWER CASE.
SELECTION-SCREEN END OF BLOCK block1.

*&amp;---------------------------------------------------------------------*
*&amp; WITH FRAME
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK block2 WITH FRAME.
  PARAMETERS param3 TYPE c LENGTH 10 DEFAULT &#x27;hij&#x27; LOWER CASE.
  PARAMETERS param4 TYPE c LENGTH 10 DEFAULT &#x27;klm&#x27; LOWER CASE.
SELECTION-SCREEN END OF BLOCK block2.

*&amp;---------------------------------------------------------------------*
*&amp; WITH FRAME WITH TITLE
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK block3 WITH FRAME TITLE titl.
  PARAMETERS: rb1 RADIOBUTTON GROUP gr,
              rb2 RADIOBUTTON GROUP gr,
              rb3 RADIOBUTTON GROUP gr.
SELECTION-SCREEN END OF BLOCK block3.

*&amp;---------------------------------------------------------------------*
*&amp; Nested blocks
*&amp;---------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK block4a WITH FRAME TITLE titl4a.
  PARAMETERS param5 TYPE c LENGTH 10 DEFAULT &#x27;nop&#x27; LOWER CASE.
  SELECTION-SCREEN SKIP.
  SELECTION-SCREEN BEGIN OF BLOCK block4b WITH FRAME TITLE titl4b.
    PARAMETERS param6 TYPE c LENGTH 10 DEFAULT &#x27;qrs&#x27; LOWER CASE.
    SELECTION-SCREEN SKIP.
    SELECTION-SCREEN BEGIN OF BLOCK block4c WITH FRAME TITLE titl4c.
      PARAMETERS param7 TYPE c LENGTH 10 DEFAULT &#x27;tuv&#x27; LOWER CASE.
      SELECTION-SCREEN SKIP.
    SELECTION-SCREEN END OF BLOCK block4c.
  SELECTION-SCREEN END OF BLOCK block4b.
SELECTION-SCREEN END OF BLOCK block4a.

&quot;Demo flags for the AT SELECTION-SCREEN ON BLOCK implementations
DATA block1_flag TYPE abap_boolean.
DATA block2_flag TYPE abap_boolean.
DATA block3_flag TYPE abap_boolean.
DATA block4a_flag TYPE abap_boolean.
DATA block4b_flag TYPE abap_boolean.
DATA block4c_flag TYPE abap_boolean.

INITIALIZATION.
  titl = &#x27;Radiobutton Selection&#x27;.
  titl4a = &#x27;Title 1&#x27;.
  titl4b = &#x27;Title 2&#x27;.
  titl4c = &#x27;Title 3&#x27;.

AT SELECTION-SCREEN ON BLOCK block1.
  block1_flag = abap_true.
  MESSAGE |Event AT SELECTION-SCREEN ON BLOCK block1 raised. Others already raised? | &amp;&amp;
     |block2: &quot;{ COND #( WHEN block2_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block3: &quot;{ COND #( WHEN block3_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4a: &quot;{ COND #( WHEN block4a_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4b: &quot;{ COND #( WHEN block4b_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4c: &quot;{ COND #( WHEN block4c_flag = abap_true THEN abap_true ) }&quot; | TYPE &#x27;I&#x27;.

AT SELECTION-SCREEN ON BLOCK block2.
  block2_flag = abap_true.
  MESSAGE |Event AT SELECTION-SCREEN ON BLOCK block2 raised. Others already raised? | &amp;&amp;
     |block1: &quot;{ COND #( WHEN block1_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block3: &quot;{ COND #( WHEN block3_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4a: &quot;{ COND #( WHEN block4a_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4b: &quot;{ COND #( WHEN block4b_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4c: &quot;{ COND #( WHEN block4c_flag = abap_true THEN abap_true ) }&quot; | TYPE &#x27;I&#x27;.

AT SELECTION-SCREEN ON BLOCK block3.
  block3_flag = abap_true.
  MESSAGE |Event AT SELECTION-SCREEN ON BLOCK block3 raised. Others already raised? | &amp;&amp;
     |block1: &quot;{ COND #( WHEN block1_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block2: &quot;{ COND #( WHEN block2_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4a: &quot;{ COND #( WHEN block4a_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4b: &quot;{ COND #( WHEN block4b_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4c: &quot;{ COND #( WHEN block4c_flag = abap_true THEN abap_true ) }&quot; | TYPE &#x27;I&#x27;.

AT SELECTION-SCREEN ON BLOCK block4a.
  block4a_flag = abap_true.
  MESSAGE |Event AT SELECTION-SCREEN ON BLOCK block4a raised. Others already raised? | &amp;&amp;
     |block1: &quot;{ COND #( WHEN block1_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block2: &quot;{ COND #( WHEN block2_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block3: &quot;{ COND #( WHEN block3_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4b: &quot;{ COND #( WHEN block4b_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4c: &quot;{ COND #( WHEN block4c_flag = abap_true THEN abap_true ) }&quot; | TYPE &#x27;I&#x27;.

AT SELECTION-SCREEN ON BLOCK block4b.
  block4b_flag = abap_true.

  MESSAGE |Event AT SELECTION-SCREEN ON BLOCK block4b raised. Others already raised? | &amp;&amp;
     |block1: &quot;{ COND #( WHEN block1_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block2: &quot;{ COND #( WHEN block2_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block3: &quot;{ COND #( WHEN block3_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4a: &quot;{ COND #( WHEN block4a_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
     |block4c: &quot;{ COND #( WHEN block4c_flag = abap_true THEN abap_true ) }&quot; | TYPE &#x27;I&#x27;.

AT SELECTION-SCREEN ON BLOCK block4c.
  block4c_flag = abap_true.
  MESSAGE |Event AT SELECTION-SCREEN ON BLOCK block4c raised. Others already raised? | &amp;&amp;
   |block1: &quot;{ COND #( WHEN block1_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
   |block2: &quot;{ COND #( WHEN block2_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
   |block3: &quot;{ COND #( WHEN block3_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
   |block4a: &quot;{ COND #( WHEN block4a_flag = abap_true THEN abap_true ) }&quot; | &amp;&amp;
   |block4b: &quot;{ COND #( WHEN block4b_flag = abap_true THEN abap_true ) }&quot; | TYPE &#x27;I&#x27;.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  WRITE / |param3: &quot;{ param3 }&quot;|.
  WRITE / |param4: &quot;{ param4 }&quot;|.
  WRITE / |param5: &quot;{ param5 }&quot;|.
  WRITE / |param6: &quot;{ param6 }&quot;|.
  WRITE / |param7: &quot;{ param7 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Defining tabstrips&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN BEGIN OF TABBED BLOCK ... SELECTION-SCREEN END OF BLOCK ...</code> and <code>SELECTION-SCREEN TAB</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Within the statement block, you can only specify <code>SELECTION-SCREEN ... TAB ...</code> statements (they define tab titles).</li>
<li>Additions to <code>... TABBED BLOCK ...</code> statements:</li>
<li><code>FOR ... LINES</code>: Defines the number of lines in the tabstrip area</li>
<li><code>NO INTERVALS</code>: To narrow the size for subscreen dynpros.</li>
<li>Additions to <code>... TAB ...</code> statements:</li>
<li><code>(...)</code>: Length specification</li>
<li><code>USER-COMMAND</code>: To assign a function code. See above.</li>
<li><code>DEFAULT [PROGRAM ...] SCREEN</code>:</li>
<li>The tab titles must be assigned a subscreen dynpro.</li>
<li>For the tabstrip areas, structures are implicitly created having the specified name. They have 3 components: <code>prog</code>, <code>dynnr</code> and <code>activetab</code>. Not specifying <code>DEFAULT ...</code> means the values must be passed before the selection screen is sent. Find <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapselection-screen_tabbed.htm">here</a> more information.</li>
<li><code>MODIF ID</code>: See above</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>See the example for <code>SELECTION-SCREEN BEGIN OF SCREEN ... AS SUBSCREEN</code> that uses <code>... TABBED BLOCK ...</code> and <code>... TAB ...</code> statements.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Adding pushbuttons in the application toolbar&lt;br&gt;&lt;br&gt; <code>SELECTION-SCREEN FUNCTION KEY</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The statement activates pushbuttons in the application toolbar for specific function codes (<code>FC0n</code>).</li>
<li>There are five inactive pushbuttons to which the function codes <code>FC01</code>, <code>FC02</code> up to <code>FC05</code> are assigned.</li>
<li>To enable the pushbutton, include the <code>TABLES sscrfields.</code> statement in the code for the interface work area.</li>
<li>When the button is clicked, the <code>AT SELECTION-SCREEN</code> event is raised and the function code is passed to <code>sscrfields-ucomm</code>, which can be evaluated and reacted upon accordingly.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>Five pushbuttons are added to the application toolbar.</li>
<li>To provide text for the buttons, values are assigned to the <code>sscrfields-functxt_0n</code> component.</li>
<li>In the <code>AT SELECTION-SCREEN</code> event block, <code>sscrfields-ucomm</code> is evaluated. Depending on the function code, parameter values are assigned or messages are raised for demonstration purposes.</li>
</ul>
<pre><code>PROGRAM.

TABLES sscrfields.
PARAMETERS param1 TYPE c LENGTH 20.
PARAMETERS param2 TYPE c LENGTH 20.
PARAMETERS param3 TYPE c LENGTH 20.

SELECTION-SCREEN: FUNCTION KEY 1,  &quot;Stands for FC01
                  FUNCTION KEY 2,  &quot;FC02
                  FUNCTION KEY 3,  &quot;FC03
                  FUNCTION KEY 4,  &quot;FC04
                  FUNCTION KEY 5.  &quot;FC05

INITIALIZATION.
  sscrfields-functxt_01 = &#x27;Insert name&#x27;.
  sscrfields-functxt_02 = &#x27;Insert time&#x27;.
  sscrfields-functxt_03 = &#x27;Insert date&#x27;.
  sscrfields-functxt_04 = &#x27;Say hi&#x27;.
  sscrfields-functxt_05 = &#x27;Error message&#x27;.

AT SELECTION-SCREEN.
  CASE sscrfields-ucomm.
    WHEN &#x27;FC01&#x27;.
      param1 = sy-uname.
    WHEN &#x27;FC02&#x27;.
      param2 = sy-uzeit.
    WHEN &#x27;FC03&#x27;.
      param3 = sy-datum.
    WHEN &#x27;FC04&#x27;.
      MESSAGE |Hello { sy-uname }. You clicked the fourth button.| TYPE &#x27;I&#x27;.
    WHEN &#x27;FC05&#x27;.
      MESSAGE |Error message raised by clicking the fifth button.| TYPE &#x27;E&#x27;.
  ENDCASE.

START-OF-SELECTION.

  WRITE / `Inserted values:`.
  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  WRITE / |param3: &quot;{ param3 }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Using Elements from Other Selection Screens</h4>
<ul>
<li>You can include screen elements created in other selection screens.</li>
<li>Statements include the <code>INCLUDE</code> addition. These are possible:</li>
<li><code>SELECTION-SCREEN INCLUDE PARAMETERS</code></li>
<li><code>SELECTION-SCREEN INCLUDE SELECT-OPTIONS</code></li>
<li><code>SELECTION-SCREEN INCLUDE COMMENT</code></li>
<li><code>SELECTION-SCREEN INCLUDE PUSHBUTTON</code></li>
<li><code>SELECTION-SCREEN INCLUDE BLOCKS</code></li>
<li>Find more information in the subtopics <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapselection-screen_include.htm">here</a>.</li>
</ul>
<p>The following code snippet, copyable to a demo program and executable using F8, implements the following:</p>
<ul>
<li>Various screen elements are created. Many of them - all possible syntax options are reflected - are reused and included in another selection screen (9000).</li>
<li>The selection screen with number 9000 is called if a checkbox is selected. This selection screen then opens in a modal dialog box.</li>
</ul>
<pre><code>PROGRAM.

TABLES sscrfields.

SELECTION-SCREEN COMMENT /1(30) txt1.
SELECTION-SCREEN SKIP.
PARAMETERS param1 TYPE c LENGTH 10.
SELECTION-SCREEN SKIP.
DATA num TYPE c LENGTH 10.
SELECT-OPTIONS sel FOR num.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN COMMENT /1(30) txt2.
SELECTION-SCREEN PUSHBUTTON /1(15) btn USER-COMMAND cmd.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN COMMENT /1(70) txt3.
PARAMETERS toincl AS CHECKBOX DEFAULT &#x27;X&#x27;.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN: BEGIN OF BLOCK blck,
                  COMMENT /1(30) txt4,
                  ULINE.
   PARAMETERS param2 TYPE c LENGTH 10.
SELECTION-SCREEN END OF BLOCK blck.

SELECTION-SCREEN: BEGIN OF SCREEN 9000 AS WINDOW,
                  INCLUDE PARAMETERS param1,
                  INCLUDE SELECT-OPTIONS sel,
                  INCLUDE COMMENT /1(10) txt2,
                  INCLUDE PUSHBUTTON /1(15) btn,
                  INCLUDE BLOCKS blck,
END OF SCREEN 9000.

INITIALIZATION.
  txt1 = &#x27;Standard Selection Screen&#x27;.
  txt2 = &#x27;Pushbutton:&#x27;.
  txt3 = &#x27;Select the checkbox to demonstrate SELECTION-SCREEN ... INCLUDE&#x27;.
  txt4 = &#x27;Some text&#x27;.
  btn = &#x27;Button&#x27;.

AT SELECTION-SCREEN.
  CASE sscrfields-ucomm.
    WHEN &#x27;CMD&#x27;.
      MESSAGE |Hello { sy-uname }. You clicked the pushbutton| TYPE &#x27;I&#x27;.
  ENDCASE.

START-OF-SELECTION.

  IF toincl = &#x27;X&#x27;.
    CALL SELECTION-SCREEN 9000 STARTING AT 10 10.
    WRITE / `The checkbox was selected, and the modal dialog box opened.`.
    WRITE / `This modal dialog box contains multiple included elements.`.
  ELSE.
    WRITE / `The checkbox was not selected, so the modal dialog box was not opened.`.
    WRITE / `This modal dialog box contains multiple included elements.`.
  ENDIF.

  SKIP.
  WRITE / |param1: &quot;{ param1 }&quot;|.
  WRITE / |param2: &quot;{ param2 }&quot;|.
  WRITE / `Selection table content:`.
  LOOP AT sel ASSIGNING FIELD-SYMBOL(&lt;st&gt;).
    WRITE / |low: { &lt;st&gt;-low }, high: { &lt;st&gt;-high }, option: { &lt;st&gt;-option }, sign: { &lt;st&gt;-sign }|.
  ENDLOOP.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Calling Selection Screens</h3>
<ul>
<li>There are several ways of how to call selection screens (see <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_screen_call.htm">here</a>), among them <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapcall_selection_screen.htm"><code>CALL SELECTION-SCREEN</code></a> statements.</li>
<li>These statements call selection screens by specifying the dynpro number.</li>
<li>Any selection screen of the main program (and only from there; including 1000) can be called.</li>
</ul>
<pre><code>CALL SELECTION-SCREEN 9876.

&quot;STARTING AT/ENDING AT additions for creating a modal dialog box
&quot;The numbers determine how it should be displayed. 
&quot;STARTING AT: upper left corner (column and line); ENDING AT: bottom right 
&quot;corner (if not specified, it is set automatically)
CALL SELECTION-SCREEN 9345 STARTING AT 10 10.
CALL SELECTION-SCREEN 9345 STARTING AT a b ENDING AT c d.

&quot;You can also predefine selection criteria for a selection screen using 
&quot;the addition USING SELECTION-SET.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursion: SUBMIT Statements</h2>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapsubmit.htm"><code>SUBMIT</code></a> statements call executable programs and, therefore, selection screens.</li>
<li>In general, every executable program is started implicitly with <code>SUBMIT</code>.</li>
<li>Selection screens can be considered a parameter interface if the program containing them is executed using <code>SUBMIT</code>.</li>
<li>Note that when using <code>SUBMIT</code>, an authorization check for the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenauthorization_group_glosry.htm">authorization group</a> is performed using the <code>S_PROGRAM</code> authorization object.</li>
</ul>
<p>The following table includes a selection of additions and code snippets. Two programs use the demo names <code>zdemo_abap_report</code> for the calling program and <code>zdemo_abap_report_submit</code> for the called program.</p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Addition &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Calling a program, ending the current program</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The program name is specified directly.</li>
<li>If the program does not exist, a runtime error occurs.</li>
<li>Without the <code>AND RETURN</code> addition, the current <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abensap_luw_glosry.htm">SAP LUW</a> is terminated.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following code snippets, copyable to demo programs and executable using F8 (execute program <code>zdemo_abap_report</code>), implements the following:</p>
<ul>
<li>Using radio buttons in the calling program, you can determine which <code>SUBMIT</code> statement to execute.</li>
<li>One statement specifies the <code>AND RETURN</code> addition, the others do not.</li>
<li>Regarding the third radiobutton:</li>
<li>The example is implemented to demonstrate the program interruption triggered by the <code>AND RETURN</code> addition. In the calling program, the SAP LUW key is retrieved using a system class before and after the <code>SUBMIT</code> statement. Both values are the same, demonstrating that the SAP LUW has been kept and is still the same. However, the called program has its own SAP LUW. The code of the called program also includes the retrieval of the SAP LUW key. This value differs from the SAP LUW key of the calling program, thus, showing that two different SAP LUWs have been started.</li>
<li>As there is no &quot;returning parameter-like&quot; functionality when returning to the calling program, the called program uses an <code>EXPORT ... TO MEMORY ID</code> statement to store the SAP LUW key value in the ABAP memory. For display and demonstration, the calling program retrieves this stored content with an <code>IMPORT ... TO MEMORY ID</code> statement. It then writes the content to a list using a <code>WRITE</code> statement. Find more information and criteria for the use <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendata_cluster.htm">here</a>.</li>
</ul>
<pre><code>PROGRAM.

PARAMETERS: noreturn RADIOBUTTON GROUP rbg,
            w_return RADIOBUTTON GROUP rbg,
            w_retkey RADIOBUTTON GROUP rbg.

START-OF-SELECTION.

  CASE &#x27;X&#x27;.
    WHEN noreturn.

      SUBMIT zdemo_abap_report_submit.
      &quot;Text will not be written to the list since the calling program is ended.
      WRITE / |This text was written to the list in program { sy-repid }.|.

    WHEN w_return.

      SUBMIT zdemo_abap_report_submit AND RETURN.
      &quot;Text will be written to the list since the calling program is only interrupted.
      WRITE / |This text was written to the list in program { sy-repid }.|.

    WHEN w_retkey.

      &quot;SAP LUW key information
      DATA(sap_luw_key_calling_prog1) = cl_system_transaction_state=&gt;get_sap_luw_key( ).
      SUBMIT zdemo_abap_report_submit AND RETURN.
      DATA(sap_luw_key_calling_prog2) = cl_system_transaction_state=&gt;get_sap_luw_key( ).

      DATA luw_key_called_prog TYPE sychar32.
      IMPORT key = luw_key_called_prog FROM MEMORY ID &#x27;ID4LUWKEY&#x27;.

      WRITE / |This text was written to the list in program { sy-repid }.|.
      SKIP.
      ASSERT sap_luw_key_calling_prog1 &lt;&gt; luw_key_called_prog.
      WRITE: / |SAP LUW key in calling program (1): { sap_luw_key_calling_prog1 }|.
      WRITE: / |SAP LUW key in calling program (2): { sap_luw_key_calling_prog2 }|.
      WRITE: / |SAP LUW key in called program: { luw_key_called_prog }|.

  ENDCASE.</code></pre>
<p><code>zdemo_abap_report</code></p>
<p>&lt;br&gt;</p>
<pre><code>PROGRAM.

START-OF-SELECTION.

DATA(sap_luw_key_called_prog) = cl_system_transaction_state=&gt;get_sap_luw_key( ).
EXPORT key = sap_luw_key_called_prog TO MEMORY ID &#x27;ID4LUWKEY&#x27;.
MESSAGE |Hello from program { sy-repid }, { sy-uname }. The SAP LUW key value is &quot;{ sap_luw_key_called_prog }&quot;.| &amp;&amp;
        | If you selected the first radiobutton (no &quot;AND RETURN&quot;), you must run the program again to check out more radiobuttons.| TYPE &#x27;I&#x27;.</code></pre>
<p><code>zdemo_abap_report_submit</code></p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Calling a program, interrupting the current program and return&lt;br&gt;&lt;br&gt; Addition <code>AND RETURN</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The calling program is interrupted.</li>
<li>With the <code>AND RETURN</code> addition, the called program starts in a new <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abeninternal_session_glosry.htm">internal session</a> and has its own <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abensap_luw_glosry.htm">SAP LUW</a>. The current internal session and <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abensap_luw_glosry.htm">SAP LUW</a> is kept. When the called program is ended, the execution of the calling program continues.</li>
<li>Find <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapsubmit.htm">here</a> more information regarding the impact of the involved SAP LUWs.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>See above.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Dynamically calling a program</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>A parenthesized flat character-like data object (literal, constannt or variable) is specified.</li>
<li>The name must be specified in uppercase.</li>
<li>Note the security risks and consequences a program submitted from outside can cause. You can use the class <code>CL_ABAP_DYN_PRG</code> to tackle security risks.</li>
<li>As there is no &quot;returning parameter-like&quot; functionality when returning to the calling program, the called program uses an <code>EXPORT ... TO MEMORY ID</code> statement to store parameter values in a string table within the ABAP memory. For display and demonstration, the calling program retrieves this stored content with an <code>IMPORT ... TO MEMORY ID</code> statement. It then writes the content to a list using <code>WRITE</code> statements. Find more information and criteria for the use <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendata_cluster.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following code snippets, copyable to demo programs and executable using F8 (execute program <code>zdemo_abap_report</code>), implements the following:</p>
<ul>
<li>Using radio buttons in the calling program, you can determine which dynamic <code>SUBMIT</code> statement to execute.</li>
<li>Some statements specify the <code>AND RETURN</code> addition, some do not.</li>
<li>Regarding the fourth radiobutton, see the description in the first example.</li>
</ul>
<pre><code>PROGRAM.

PARAMETERS: noreturn RADIOBUTTON GROUP rbg,
            no_retdo RADIOBUTTON GROUP rbg,
            w_return RADIOBUTTON GROUP rbg,
            w_retkey RADIOBUTTON GROUP rbg.

START-OF-SELECTION.

  CASE &#x27;X&#x27;.
    WHEN noreturn.

      &quot;Program name specified as literal
      SUBMIT (&#x27;ZDEMO_ABAP_REPORT_SUBMIT&#x27;).
      &quot;Text will not be written to the list since the calling program is ended.
      WRITE / |This text was written to the list in program { sy-repid }.|.

    WHEN no_retdo.

      &quot;Specifying the program with a variable
      DATA prog_name TYPE syrepid VALUE &#x27;ZDEMO_ABAP_REPORT_SUBMIT&#x27;.
      SUBMIT (prog_name).
      &quot;Text will not be written to the list since the calling program is ended.
      WRITE / |This text was written to the list in program { sy-repid }.|.

    WHEN w_return.

      SUBMIT (&#x27;ZDEMO_ABAP_REPORT_SUBMIT&#x27;) AND RETURN.
      &quot;Text will be written to the list since the calling program is only interrupted.
      WRITE / |This text was written to the list in program { sy-repid }.|.

    WHEN w_retkey.

      &quot;SAP LUW key information
      DATA(sap_luw_key_calling_prog1) = cl_system_transaction_state=&gt;get_sap_luw_key( ).
      SUBMIT (&#x27;ZDEMO_ABAP_REPORT_SUBMIT&#x27;) AND RETURN.
      DATA(sap_luw_key_calling_prog2) = cl_system_transaction_state=&gt;get_sap_luw_key( ).

      DATA luw_key_called_prog TYPE sychar32.
      IMPORT key = luw_key_called_prog FROM MEMORY ID &#x27;ID4LUWKEY&#x27;.

      WRITE / |This text was written to the list in program { sy-repid }.|.
      SKIP.
      ASSERT sap_luw_key_calling_prog1 &lt;&gt; luw_key_called_prog.
      WRITE: / |SAP LUW key in calling program (1): { sap_luw_key_calling_prog1 }|.
      WRITE: / |SAP LUW key in calling program (2): { sap_luw_key_calling_prog2 }|.
      WRITE: / |SAP LUW key in called program: { luw_key_called_prog }|.

  ENDCASE.</code></pre>
<p><code>zdemo_abap_report</code></p>
<p>&lt;br&gt;</p>
<pre><code>PROGRAM.

START-OF-SELECTION.

DATA(sap_luw_key_called_prog) = cl_system_transaction_state=&gt;get_sap_luw_key( ).
EXPORT key = sap_luw_key_called_prog TO MEMORY ID &#x27;ID4LUWKEY&#x27;.
MESSAGE |Hello from program { sy-repid }, { sy-uname }. The SAP LUW key value is &quot;{ sap_luw_key_called_prog }&quot;.| &amp;&amp;
        | If you selected one of the first two radiobuttons (no &quot;AND RETURN&quot;), you must run the program again to check out more radiobuttons.| TYPE &#x27;I&#x27;.</code></pre>
<p><code>zdemo_abap_report_submit</code></p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying a particular selection screen to be called and display it or not&lt;br&gt;&lt;br&gt; Additions <code>USING SELECTION-SCREEN</code> and <code>VIA SELECTION-SCREEN</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>USING SELECTION-SCREEN</code>: Specifies the selection screen. If the addition is not specified, the standard selection screen 1000 is called. The specified seclection screen must exist, otherwise a runtime error occurs.</li>
<li><code>VIA SELECTION-SCREEN</code>: Determines whether the selection is displayed or not.</li>
<li>The following sections outline options to pass parameter and selection criteria values.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following code snippets, copyable to demo programs and executable using F8 (execute program <code>zdemo_abap_report</code>), implements the following:</p>
<ul>
<li>Using radio buttons in the calling program, you can determine which <code>SUBMIT</code> statement to execute.</li>
<li>All statements use the <code>AND RETURN</code> addition.</li>
<li>Some statements anticipate the <code>WITH</code> addition with which you can pass values for parameters and selection criteria.</li>
<li>As there is no &quot;returning parameter-like&quot; functionality when returning to the calling program, the called program uses an <code>EXPORT ... TO MEMORY ID</code> statement to store parameter values in a string table within the ABAP memory. For display and demonstration, the calling program retrieves this stored content with an <code>IMPORT ... TO MEMORY ID</code> statement. It then writes the content to a list using <code>WRITE</code> statements. Find more information and criteria for the use <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendata_cluster.htm">here</a>.</li>
</ul>
<pre><code>PROGRAM.

PARAMETERS: use_sel  RADIOBUTTON GROUP rbg,
            use_selp RADIOBUTTON GROUP rbg,
            use_sels RADIOBUTTON GROUP rbg,
            sel_via  RADIOBUTTON GROUP rbg,
            onlyvia  RADIOBUTTON GROUP rbg.

DATA param1 TYPE c LENGTH 10.
DATA param2 TYPE c LENGTH 10.

START-OF-SELECTION.

  CASE &#x27;X&#x27;.
    WHEN use_sel.

      &quot;In this example, the called selection screen is not displayed. As no parameter values
      &quot;are passed (see next example), none of the parameters in the called program are assigned
      &quot;values.
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9000 AND RETURN.

    WHEN use_selp.

      &quot;The following statement does not specify VIA SELECTION-SCREEN either, but anticipates
      &quot;the WITH addition to pass parameter values.
      SUBMIT zdemo_abap_report_submit WITH param2 = &#x27;hello&#x27;
                                      USING SELECTION-SCREEN 9000 AND RETURN.

    WHEN use_sels.

      &quot;This example statement explicitly calls the standard selection screen.
      SUBMIT zdemo_abap_report_submit WITH param1 = &#x27;hi&#x27;
                                      USING SELECTION-SCREEN 1000 AND RETURN.

    WHEN sel_via.

      &quot;The specified selection screen is called.
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9000
                                      VIA SELECTION-SCREEN
                                      AND RETURN.

    WHEN onlyvia.

      &quot;The standard selection screen 1000 is called.
      SUBMIT zdemo_abap_report_submit VIA SELECTION-SCREEN
                                      AND RETURN.


  ENDCASE.

  DATA input_values TYPE string_table.
  IMPORT input = input_values FROM MEMORY ID &#x27;DEMO_ID&#x27;.

  WRITE / |This text was written to the list in program { sy-repid }.|.
  LOOP AT input_values INTO DATA(wa).
    WRITE / wa.
  ENDLOOP.</code></pre>
<p><code>zdemo_abap_report</code></p>
<p>&lt;br&gt;</p>
<pre><code>PROGRAM.

PARAMETERS param1 TYPE c LENGTH 10 LOWER CASE.

SELECTION-SCREEN BEGIN OF SCREEN 9000.
  PARAMETERS param2 TYPE c LENGTH 10 LOWER CASE.
SELECTION-SCREEN END OF SCREEN 9000.

START-OF-SELECTION.

  DATA input_values TYPE string_table.
  APPEND |param1: &quot;{ param1 }&quot;| TO input_values.
  APPEND |param2: &quot;{ param2 }&quot;| TO input_values.

  EXPORT input = input_values TO MEMORY ID &#x27;DEMO_ID&#x27;.

  MESSAGE |Hello from program { sy-repid }, { sy-uname }. The parameter values are param1 = &quot;{ param1 }&quot; and param2 = &quot;{ param2 }&quot;.| TYPE &#x27;I&#x27;.</code></pre>
<p><code>zdemo_abap_report_submit</code></p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Passing values to a called selection screen&lt;br&gt;&lt;br&gt; Additions <code>WITH SELECTION-TABLE</code>, <code>WITH ...</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>WITH SELECTION-TABLE</code>: Passes values specified in an internal of a special line type (<code>RSPARAMS</code>, <code>RSPARAMSL_255</code>, without secondary table keys). The line type has specific component, among them: <code>selname</code> (parameter name in uppercase), <code>kind</code> (<code>P</code> for parameters, <code>S</code> for selection criteria), <code>sign</code>/<code>option</code>/<code>low</code>/<code>high</code> (ranges table components; note that for parameters, the value must be specified for <code>low</code>)</li>
<li><code>WITH ...</code> : Supplying individual values</li>
<li>More additions are available. Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapsubmit_selscreen_parameters.htm">here</a>:</li>
<li><code>WITH FREE SELECTIONS</code>: For dynamic selections of selection screens of <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenlogical_data_base_glosry.htm">logical databases</a></li>
<li><code>USING SELECTION-SET</code>: Passes values of a <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenvariant_glosry.htm">selection screen variant</a></li>
<li><code>USING SELECTION-SETS OF PROGRAM</code>: Passes values of a selection screen variant of a particular program</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following code snippets, copyable to demo programs and executable using F8 (execute program <code>zdemo_abap_report</code>), implements the following:</p>
<ul>
<li>Using radio buttons in the calling program, you can determine which <code>SUBMIT</code> statement to execute.</li>
<li>The <code>SUBMIT</code> statements do not specify the <code>VIA SELECTION-SCREEN</code> addition on purpose. Parameter values and selection criteria are predefined in the calling program and then used by the <code>SUBMIT</code> statements.</li>
<li>As there is no &quot;returning parameter-like&quot; functionality when returning to the calling program, the called program uses an <code>EXPORT ... TO MEMORY ID</code> statement to store parameter values in a string table within the ABAP memory. For display and demonstration, the calling program retrieves this stored content with an <code>IMPORT ... TO MEMORY ID</code> statement. It then writes the content to a list using <code>WRITE</code> statements. Find more information and criteria for the use <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendata_cluster.htm">here</a>.</li>
</ul>
<pre><code>PROGRAM.

PARAMETERS: wseltab1 RADIOBUTTON GROUP rbg,
            wseltab2 RADIOBUTTON GROUP rbg,
            wseltab3 RADIOBUTTON GROUP rbg,
            simple_w RADIOBUTTON GROUP rbg,
            with_sel RADIOBUTTON GROUP rbg,
            singlrt  RADIOBUTTON GROUP rbg,
            intv_rt  RADIOBUTTON GROUP rbg,
            rangest  RADIOBUTTON GROUP rbg.

START-OF-SELECTION.

  CASE &#x27;X&#x27;.
    WHEN wseltab1.

      DATA seltab1 TYPE TABLE OF rsparams WITH EMPTY KEY.
      seltab1 = VALUE #( ( selname = &#x27;PARAM2&#x27; kind = &#x27;P&#x27; low = &#x27;Test&#x27; ) ).
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9000 WITH SELECTION-TABLE seltab1 AND RETURN.

    WHEN wseltab2.

      DATA seltab2 TYPE TABLE OF rsparams WITH EMPTY KEY.
      seltab2 = VALUE #( ( selname = &#x27;SEL1&#x27; kind = &#x27;S&#x27; low = &#x27;A&#x27; high = &#x27;M&#x27; option = &#x27;BT&#x27; sign = &#x27;I&#x27; ) ).
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9001 WITH SELECTION-TABLE seltab2 AND RETURN.

    WHEN wseltab3.

      DATA seltab3 TYPE TABLE OF rsparams WITH EMPTY KEY.
      seltab3 = VALUE #( ( selname = &#x27;PARAM3&#x27; kind = &#x27;P&#x27; low = &#x27;Hello&#x27; )
                         ( selname = &#x27;SEL2&#x27; kind = &#x27;S&#x27; low = 1 high = 10 option = &#x27;BT&#x27; sign = &#x27;I&#x27; ) ).
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9002 WITH SELECTION-TABLE seltab3 AND RETURN.

    WHEN simple_w.

      &quot;Calls the standard selection screen
      SUBMIT zdemo_abap_report_submit WITH param1 = &#x27;ABAP&#x27; AND RETURN.

    WHEN with_sel.

      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9000 WITH param2 = &#x27;lorem&#x27; AND RETURN.

    WHEN singlrt.

      &quot;Single value passed for ranges table
      &quot;Note:
      &quot;- Specifying SIGN is optional. The default value for sign is &#x27;I&#x27;.
      &quot;- Specify the comparions operations EQ|NE|CP|NP|GT|GE|LT|LE and =|INCL instead of EQ
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9001 WITH sel1 = &#x27;A&#x27; SIGN &#x27;I&#x27; AND RETURN.

    WHEN intv_rt.

      &quot;Passed interval for ranges table
      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9001 WITH sel1 BETWEEN &#x27;A&#x27; AND &#x27;M&#x27; SIGN &#x27;I&#x27; AND RETURN.

    WHEN rangest.

      &quot;Passing a ranges table
      TYPES c10 TYPE c LENGTH 10.
      DATA rangetab TYPE RANGE OF c10.
      rangetab = VALUE #( sign   = &#x27;I&#x27;
                          option = &#x27;BT&#x27; ( low = &#x27;A&#x27;  high = &#x27;C&#x27; )
                                        ( low = &#x27;F&#x27;  high = &#x27;M&#x27; )
                                        ( low = &#x27;S&#x27;  high = &#x27;U&#x27; )
                          option = &#x27;GE&#x27; ( low = &#x27;X&#x27; ) ).

      SUBMIT zdemo_abap_report_submit USING SELECTION-SCREEN 9001 WITH sel1 IN rangetab AND RETURN.

  ENDCASE.

  DATA input_tab TYPE string_table.
  IMPORT values = input_tab FROM MEMORY ID &#x27;ID4INPUT&#x27;.

  LOOP AT input_tab INTO DATA(wa).
    SET BLANK LINES ON.
    WRITE / wa.
  ENDLOOP.</code></pre>
<p><code>zdemo_abap_report</code></p>
<p>&lt;br&gt;</p>
<pre><code>PROGRAM.

PARAMETERS param1 TYPE c LENGTH 10 LOWER CASE.

SELECTION-SCREEN BEGIN OF SCREEN 9000.
  PARAMETERS param2 TYPE c LENGTH 10 LOWER CASE.
SELECTION-SCREEN END OF SCREEN 9000.

DATA txt TYPE c LENGTH 10.
SELECTION-SCREEN BEGIN OF SCREEN 9001.
  SELECT-OPTIONS: sel1 FOR txt.
SELECTION-SCREEN END OF SCREEN 9001.

DATA num TYPE i.
SELECTION-SCREEN BEGIN OF SCREEN 9002.
  PARAMETERS param3 TYPE c LENGTH 10 LOWER CASE.
  SELECT-OPTIONS: sel2 FOR num.
SELECTION-SCREEN END OF SCREEN 9002.

START-OF-SELECTION.

  DATA input_values TYPE string_table.

  APPEND |param1: &quot;{ param1 }&quot;| TO input_values.
  APPEND INITIAL LINE TO input_values.
  APPEND |param2: &quot;{ param2 }&quot;| TO input_values.
  APPEND INITIAL LINE TO input_values.
  APPEND |param3: &quot;{ param3 }&quot;| TO input_values.
  APPEND INITIAL LINE TO input_values.

  IF sel1 IS INITIAL.
    APPEND `No sel1 selection criteria.` TO input_values.
  ELSE.
    APPEND `sel1 selection criteria:` TO input_values.
    LOOP AT sel1 ASSIGNING FIELD-SYMBOL(&lt;st1&gt;).
      APPEND |low: { &lt;st1&gt;-low }, high: { &lt;st1&gt;-high }, option: { &lt;st1&gt;-option }, sign: { &lt;st1&gt;-sign }| TO input_values.
    ENDLOOP.
  ENDIF.
  APPEND INITIAL LINE TO input_values.
  IF sel2 IS INITIAL.
    APPEND `No sel2 selection criteria.` TO input_values.
  ELSE.
    APPEND `sel2 selection criteria:` TO input_values.
    LOOP AT sel2 ASSIGNING FIELD-SYMBOL(&lt;st2&gt;).
      APPEND |low: { &lt;st2&gt;-low }, high: { &lt;st2&gt;-high }, option: { &lt;st2&gt;-option }, sign: { &lt;st2&gt;-sign }| TO input_values.
    ENDLOOP.
  ENDIF.

  EXPORT values = input_values TO MEMORY ID &#x27;ID4INPUT&#x27;.</code></pre>
<p><code>zdemo_abap_report_submit</code></p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Handling the list of the called program&lt;br&gt;&lt;br&gt; Addition <code>EXPORTING LIST TO MEMORY</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>After a program has been called, the output statements of that program write to the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenbasic_list_glosry.htm">basic list</a>.</li>
<li>If this addition is used, the output list is stored in the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_memory_glosry.htm">ABAP memory</a> in an internal table of type <code>abaplist</code>. If it is not used, the list of the called program is displayed. That&#x27;s why the examples above do not use <code>WRITE</code> statements in the called program.</li>
<li>The addition must be used with <code>AND RETURN</code>.</li>
<li>You can access the stored list in the calling program using these function modules:</li>
<li><code>LIST_FROM_MEMORY</code>: Retrieves the list from the ABAP memory in an internal table of type <code>abaplist</code></li>
<li><code>WRITE_LIST</code>: Writes the content of the table (of type <code>abaplist</code>) to the current list</li>
<li><code>DISPLAY_LIST</code>: Displays the content the content of the table (of type <code>abaplist</code>) in a separate <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenlist_dynpro_glosry.htm">list dynpro</a></li>
<li><code>LIST_TO_ASCI</code>: Converts the content the content of the table (of type <code>abaplist</code>) to ASCII representation</li>
<li>Find more information in the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapsubmit_list_options.htm">ABAP Keyword Documentation</a>. More additions are available to handle lists:</li>
<li><code>TO SAP-SPOOL</code>: Sends the output list to the SAP spool system as spool list</li>
<li><code>LINE-SIZE</code>: Defines the line width of the basic list</li>
<li><code>LINE-COUNT</code>: Defines the page length the basic list</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following code snippets, copyable to demo programs and executable using F8 (execute program <code>zdemo_abap_report</code>), implements the following:</p>
<ul>
<li>Using radio buttons in the calling program, you can determine which <code>SUBMIT</code> statement to execute and how the list is handled afterwards.</li>
<li>The examples cover the calling of the function modules mentioned above.</li>
</ul>
<p><code>zdemo_abap_report</code></p>
<pre><code>PROGRAM.

PARAMETERS: expwrite RADIOBUTTON GROUP rbg,
            expdispl RADIOBUTTON GROUP rbg,
            expascii RADIOBUTTON GROUP rbg.

DATA list_tab TYPE TABLE OF abaplist.

START-OF-SELECTION.

  CASE &#x27;X&#x27;.
    WHEN expwrite.

      SUBMIT zdemo_abap_report_submit WITH param = &#x27;WRITE_LIST&#x27;
                                      EXPORTING LIST TO MEMORY AND RETURN.

      CALL FUNCTION &#x27;LIST_FROM_MEMORY&#x27;
        TABLES
          listobject = list_tab
        EXCEPTIONS
          not_found  = 1
          OTHERS     = 2.

      IF sy-subrc = 0.
        CALL FUNCTION &#x27;WRITE_LIST&#x27;
          TABLES
            listobject = list_tab.
      ELSE.
        WRITE / `List not found.`.
      ENDIF.

      WRITE / |This text was written in the calling program { sy-repid }.|.

    WHEN expdispl.

      SUBMIT zdemo_abap_report_submit WITH param = &#x27;DISPLAY_LIST&#x27;
                                      EXPORTING LIST TO MEMORY AND RETURN.

      CALL FUNCTION &#x27;LIST_FROM_MEMORY&#x27;
        TABLES
          listobject = list_tab
        EXCEPTIONS
          not_found  = 1
          OTHERS     = 2.

      IF sy-subrc = 0.
        CALL FUNCTION &#x27;DISPLAY_LIST&#x27;
          TABLES
            listobject = list_tab
          EXCEPTIONS
            empty_list = 1
            OTHERS     = 2.
        IF sy-subrc &lt;&gt; 0.
          WRITE / `Cannot display list.`.
        ENDIF.
      ELSE.
        WRITE / `List not found.`.
      ENDIF.

      WRITE / |This text was written in the calling program { sy-repid }.|.

    WHEN expascii.

      SUBMIT zdemo_abap_report_submit WITH param = &#x27;LIST_TO_ASCI&#x27;
                                      EXPORTING LIST TO MEMORY AND RETURN.

      CALL FUNCTION &#x27;LIST_FROM_MEMORY&#x27;
        TABLES
          listobject = list_tab
        EXCEPTIONS
          not_found  = 1
          OTHERS     = 2.

      TYPES c255 TYPE c LENGTH 255.
      DATA list_ascii TYPE TABLE OF c255 WITH EMPTY KEY.

      IF sy-subrc = 0.
        CALL FUNCTION &#x27;LIST_TO_ASCI&#x27;
          TABLES
            listasci           = list_ascii
            listobject         = list_tab
          EXCEPTIONS
            empty_list         = 1
            list_index_invalid = 2
            OTHERS             = 3.
        IF sy-subrc &lt;&gt; 0.
          WRITE / `Cannot convert list.`.
        ELSE.
          WRITE / `List converted to ASCII representation:`.
          LOOP AT list_ascii INTO DATA(wa_ascii).
            WRITE / wa_ascii.
          ENDLOOP.
        ENDIF.
      ELSE.
        WRITE / `List not found.`.
      ENDIF.

      SKIP.
      WRITE / |This text was written in the calling program { sy-repid }.|.

  ENDCASE.</code></pre>
<p>&lt;br&gt;</p>
<p><code>zdemo_abap_report_submit</code></p>
<pre><code>PROGRAM.

PARAMETERS param TYPE c LENGTH 20.

START-OF-SELECTION.

  IF param IS INITIAL.
    WRITE / `No value passed for parameter &quot;param&quot;.`.
  ELSE.
    WRITE / |param: &quot;{ param }&quot;|.
  ENDIF.
  SKIP.
  WRITE / |Some text written in the called program { sy-repid }|.
  DO 10 TIMES.
    WRITE / |Text no. { sy-index } added to list.|.
  ENDDO.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Running a program in the background&lt;br&gt;&lt;br&gt; Addition <code>VIA JOB ... NUMBER ...</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Must be used with <code>AND RETURN</code></li>
<li>Background jobs can be created programmatically using function modules. The example uses some of them, e.g. for creating, completing, and analyzing background jobs.</li>
<li>See the documentation for the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapsubmit_via_job.htm">details</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following code snippets, copyable to demo programs and executable using F8 (execute program <code>zdemo_abap_report</code>), implements the following:</p>
<ul>
<li>A program is scheduled as background task, and then completed and released.</li>
<li>The program execution is interrupted for one second. Then, function modules are used to analyze the background job.</li>
<li>Using the <code>cl_demo_output</code> class for demo output purposes, text and the content of data objects are displayed.</li>
<li>The transaction <code>SMX</code> is called to view the job overview. In the <em>Spool</em> column, you can click the icon to display the list output. On the next screen, the list of spool request is displayed. Choose the icon in the <em>Type</em> column. The <code>sy-batch</code> should be not initial denoting the run in the background.</li>
</ul>
<p><code>zdemo_abap_report</code></p>
<pre><code>PROGRAM.

PARAMETERS callbgr TYPE c AS CHECKBOX DEFAULT &#x27;X&#x27;.

DATA: number           TYPE tbtcjob-jobcount,
      name             TYPE tbtcjob-jobname VALUE &#x27;DEMO_JOB&#x27;,
      print_parameters TYPE pri_params.

START-OF-SELECTION.

  IF callbgr IS INITIAL.

    MESSAGE `You chose not to run the called program in the background.` TYPE &#x27;I&#x27;.
    SUBMIT zdemo_abap_report_submit WITH param = &#x27;not in background&#x27; AND RETURN.

  ELSE.

    MESSAGE `You chose to run the called program in the background.` TYPE &#x27;I&#x27;.

    &quot;Creating background job
    CALL FUNCTION &#x27;JOB_OPEN&#x27;
      EXPORTING
        jobname          = name
      IMPORTING
        jobcount         = number
      EXCEPTIONS
        cant_create_job  = 1
        invalid_job_data = 2
        jobname_missing  = 3
        OTHERS           = 4.

    IF sy-subrc = 0.
      &quot;Scheduling a program as a background task using the number created previously
      &quot;and the name specified
      SUBMIT zdemo_abap_report_submit WITH param = &#x27;in background&#x27;
                                      TO SAP-SPOOL
                                      SPOOL PARAMETERS print_parameters
                                      WITHOUT SPOOL DYNPRO
                                      VIA JOB name NUMBER number
                                      AND RETURN.

      IF sy-subrc = 0.
        &quot;Completing the background job
        CALL FUNCTION &#x27;JOB_CLOSE&#x27;
          EXPORTING
            jobcount             = number
            jobname              = name
            strtimmed            = &#x27;X&#x27;
          EXCEPTIONS
            cant_start_immediate = 1
            invalid_startdate    = 2
            jobname_missing      = 3
            job_close_failed     = 4
            job_nosteps          = 5
            job_notex            = 6
            lock_failed          = 7
            OTHERS               = 8.
        IF sy-subrc = 0.
          cl_demo_output=&gt;write( |The job { name } was submitted with number { number }| ).

          &quot;Interrupting the program shortly
          WAIT UP TO 1 SECONDS.

          &quot;Analyzing the background job
          DATA: BEGIN OF jobstate,
                  aborted     TYPE c LENGTH 1,
                  finished    TYPE c LENGTH 1,
                  preliminary TYPE c LENGTH 1,
                  ready       TYPE c LENGTH 1,
                  running     TYPE c LENGTH 1,
                  scheduled   TYPE c LENGTH 1,
                  suspended   TYPE c LENGTH 1,
                  other       TYPE c LENGTH 1,
                END OF jobstate.

          CALL FUNCTION &#x27;SHOW_JOBSTATE&#x27;
            EXPORTING
              jobcount         = number
              jobname          = name
            IMPORTING
              aborted          = jobstate-aborted
              finished         = jobstate-finished
              preliminary      = jobstate-preliminary
              ready            = jobstate-ready
              running          = jobstate-running
              scheduled        = jobstate-scheduled
              suspended        = jobstate-suspended
              other            = jobstate-other
            EXCEPTIONS
              jobcount_missing = 1
              jobname_missing  = 2
              job_notex        = 3
              OTHERS           = 4.
          IF sy-subrc = 0.
            cl_demo_output=&gt;write( jobstate ).
          ELSE.
            cl_demo_output=&gt;write( `Exception in SHOW_JOBSTATE` ).
          ENDIF.

          DATA joblog TYPE TABLE OF tbtc5.
          CALL FUNCTION &#x27;BP_JOBLOG_READ&#x27;
            EXPORTING
              jobcount              = number
              jobname               = name
            TABLES
              joblogtbl             = joblog
            EXCEPTIONS
              cant_read_joblog      = 1
              jobcount_missing      = 2
              joblog_does_not_exist = 3
              joblog_is_empty       = 4
              joblog_name_missing   = 5
              jobname_missing       = 6
              job_does_not_exist    = 7
              OTHERS                = 8.
          IF sy-subrc = 0.
            cl_demo_output=&gt;write( joblog ).
          ELSE.
            cl_demo_output=&gt;write( `Exception in BP_JOBLOG_READ` ).
          ENDIF.

        ELSE.
          cl_demo_output=&gt;write( `Exception in JOB_CLOSE` ).
        ENDIF.
      ELSE.
        DATA(msg) = cl_abap_submit_handling=&gt;get_error_message( ).
        cl_demo_output=&gt;write( msg ).
      ENDIF.
    ENDIF.

    cl_demo_output=&gt;display( ).
    MESSAGE `You are redirected to transaction SMX.` TYPE &#x27;I&#x27;.
    CALL TRANSACTION &#x27;SMX&#x27;.
  ENDIF.</code></pre>
<p>&lt;br&gt;</p>
<p><code>zdemo_abap_report_submit</code></p>
<pre><code>PROGRAM.

PARAMETERS param TYPE c LENGTH 20 LOWER CASE.

START-OF-SELECTION.

  IF param IS INITIAL.
    WRITE / `No value passed for parameter &quot;param&quot;.`.
  ELSE.
    WRITE / |param: &quot;{ param }&quot;|.
  ENDIF.
  SKIP.
  WRITE / |Some text written in the called program { sy-repid }|.
  DO 10 TIMES.
    WRITE / |Text no. { sy-index } added to list.|.
  ENDDO.

  SKIP.
  WRITE / |Value of sy-batch: &quot;{ sy-batch }&quot;|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>ABAP Statements for Classic Lists</h2>
<h3>Creating Lists with WRITE Statements</h3>
<ul>
<li>Lists consist of consecutive list lines that are filled one after the other using the statement <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapwrite-.htm"><code>WRITE</code></a>.</li>
<li>This way, you can output mostly flat data objects and strings/xstrings, types converted to a character-like value (i.e. no internal table content or structures directly).</li>
<li>Each time a data object is output, an output length is defined, either implicitly or explicitly (see <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenwrite_output_length.htm">here</a>).</li>
<li>The following snippets show a selection of additions to <code>WRITE</code> statements, covering positioning, creating special list elements, and formatting options.</li>
</ul>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Addition &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Writing content</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can write flat data objects (including flat structures with character-like components), data objects of type <code>string</code> and <code>xstring</code> and enumerated types.</li>
<li>Also writable: result of function calls, string expressions and method calls</li>
<li>Writing expressions in string templates: They are useful for writing embedded arithmetic or bit expressions as these cannot be specified directly. Note that control characters such as <code>\n</code> for new lines are ignored.</li>
<li>Positioning options are avaialble - as outlined below - for the <code>WRITE</code> statement. Among them is <code>/</code> that places the output in the next line. Not specifying it means the output is written in the same line.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>&quot;Writing in the same line
&quot;The example uses a character literal, i.e. an unnamed data object.
&quot;Note that there is a separating blank in the list output.
WRITE &#x27;Hello&#x27;.
WRITE &#x27;world&#x27;.

&quot;Using named data objects and string expressions
DATA some_text TYPE string VALUE `Hi`.
WRITE some_text &amp;&amp; `, ` &amp;&amp; sy-uname &amp;&amp; `.`.

&quot;Writing to a new line with /
WRITE / &#x27;New line&#x27;.

&quot;More flat data objects
&quot;Note the default positioning of the output regarding
&quot;the numeric types.
WRITE / CONV decfloat34( &#x27;1.23456789&#x27; ).
TYPES n5 TYPE n LENGTH 5.
DATA numc5 TYPE n5 VALUE &#x27;12345&#x27;.
WRITE / numc5.
DATA number TYPE i VALUE 123.
WRITE / number.

&quot;Flat structure with character-like components
&quot;Note the conversion rules.
DATA: BEGIN OF flat_struc,
        comp1 TYPE c LENGTH 4 VALUE `ABAP`,
        comp2 TYPE n LENGTH 3 VALUE &#x27;987&#x27;,
        comp3 TYPE abap_boolean VALUE &#x27;X&#x27;,
        comp4 TYPE d VALUE &#x27;20250101&#x27;,
        comp5 TYPE t VALUE &#x27;123456&#x27;,
      END OF flat_struc.

WRITE / flat_struc.

&quot;String templates
WRITE / |Today&#x27;s date is { sy-datum }.|.
&quot;Formatting options with string templates
WRITE / |{ sy-uname WIDTH = 20 ALIGN = RIGHT CASE = LOWER }|.
&quot;Arithmetic expressions cannot be specified directly. You may use
&quot;the expressions in string templates.
&quot;WRITE / 1 + 2.
WRITE / |{ 1 + 2 }|.
WRITE / |{ CONV decfloat34( 1 / 3 ) DECIMALS = 3 }|.

&quot;Concatenating using &amp;&amp;
WRITE / `conc` &amp;&amp; `atenated`.

&quot;Function calls
&quot;Note: You can use chained statements with a colon.
WRITE: / to_upper( &#x27;abap&#x27; ),
       / repeat( val = `*` occ = 50 ),
       / replace( val = `He##o wor#d` sub = `#` with = `l` occ = 0 ).

&quot;Result of method calls
&quot;The example uses the retrieval of a random integer between 1 and 10
WRITE / cl_abap_random_int=&gt;create(
              seed = cl_abap_random=&gt;seed( )
              min  = 1
              max  = 10 )-&gt;get_next( ).

&quot;Note the * specification. The returned value is of type i. Specifying *
&quot;for the length means the length required to output the current value
&quot;is used.
WRITE /(*) cl_abap_random_int=&gt;create(
              seed = cl_abap_random=&gt;seed( )
              min  = 1
              max  = 10 )-&gt;get_next( ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Positioning options (1)&lt;br&gt;&lt;br&gt; Additions <code>/</code>, <code>AT ... position(length|<em>|</em>*)</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>/</code>: Places the output in the next line. Not specifying it means the output is written in the same line.</li>
<li><code>AT</code>:</li>
<li>Explicitly specifying the output position and length</li>
<li><code>AT</code> can be omitted if the position and length are specified as numeric literals.</li>
<li>The position should not be greater than the list width; otherwise, there is not output. A value less than 1 is ignored.</li>
<li>The length value should also be between 1 and the list width.</li>
<li>The length specifications <code><em></code> and <code></em><em></code> have characteristics specific to data types that are output. They are particularly useful for ensuring that all characters are output (<code></em></code>: minimum output: <code>**</code>: maximum output). Find information in the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenarithmetic_expression_glosry.htm">documentation</a> and <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenwrite_output_length.htm">here</a> regarding the output length.</li>
<li><code>WRITE /.</code> works like <code>SKIP.</code>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; WRITE with and without /
*&amp;---------------------------------------------------------------------*

&quot;Writing output with and without specifying /
WRITE &#x27;Hello&#x27;.
WRITE &#x27;world&#x27;.
WRITE / &#x27;new line&#x27;.

*&amp;---------------------------------------------------------------------*
*&amp; Specifying the output position with(out) AT, position and length
*&amp;---------------------------------------------------------------------*

WRITE / repeat( val = `*` occ = 80 ).
&quot;WRITE statement working like SKIP.
WRITE /.

&quot;Specifying the output position, a length and new line
&quot;specification
WRITE 20 &#x27;test&#x27;.
&quot;Note the overwriting in the following example.
WRITE / &#x27;Lorem ipsum dolor sit amet&#x27;.
WRITE 10 &#x27;####&#x27;.

&quot;Specifying the length, ommitting position specification
&quot;This means that the output is written from the first column on.
WRITE /(10) &#x27;Not displayed completely&#x27;.

&quot;Specifying both length and position
WRITE /5(30) &#x27;This text displayed completely starting from position 5.&#x27;.

&quot;The AT addition can only be ommitted if position and length are specified
&quot;as numeric literals as above.
WRITE AT /3(20) &#x27;lorem ipsum&#x27;.
WRITE /3(20) &#x27;dolor sit amet&#x27;.
&quot;AT specification required
DATA position TYPE i VALUE 10.
DATA length TYPE i VALUE 50.
WRITE AT /position(length) |This text is starts from position { position }. Length specification { length }.|.

&quot;Length specifications with * or **. In that case, the output length
&quot;depends on the data type of the data object. There are special rules.
&quot;See the ABAP Keyword Documentation for the details.

&quot;Example with type c
DATA hi TYPE c LENGTH 10 VALUE &#x27;Hello     &#x27;.

WRITE: / hi,
         sy-uname.
&quot;Minimum output (trailing blanks are ignored)
WRITE: /(*) hi,
         sy-uname.
&quot;Maximum output (doubled length of data object)
WRITE: /(**) hi,
         sy-uname.

&quot;Example with type i
DATA some_number TYPE i VALUE 123.

WRITE: / some_number,
         &#x27;&lt;- some_number&#x27;.
&quot;Minimum length required to output content
WRITE: /(*) some_number,
            &#x27;&lt;- some_number&#x27;.
&quot;Maximum length to output content
WRITE: /(**) some_number,
             &#x27;&lt;- some_number&#x27;.

&quot;Example with type p
DATA some_packed_number TYPE p LENGTH 16 DECIMALS 14 VALUE &#x27;-123.4567890&#x27;.

WRITE: / some_packed_number,
         &#x27;&lt;- some_packed_number&#x27;.
&quot;Minimum length required to output content including separators
WRITE: /(*) some_packed_number,
            &#x27;&lt;- some_packed_number&#x27;.
&quot;Maximum length to output content including signs and separators
WRITE: /(**) some_packed_number,
             &#x27;&lt;- some_packed_number&#x27;.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Positioning options (2)&lt;br&gt;&lt;br&gt; Additions <code>NO-GAP</code>, <code>UNDER</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>NO-GAP</code>: Positions directly after the output</li>
<li><code>UNDER</code>: Output in the position of previous output</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; NO-GAP
*&amp;---------------------------------------------------------------------*

WRITE / repeat( val = `*` occ = 80 ).

&quot;Output: AB AP
WRITE / &#x27;AB&#x27;.
WRITE &#x27;AP&#x27;.
&quot;Output: ABAP
WRITE: / &#x27;AB&#x27; NO-GAP.
WRITE &#x27;AP&#x27;.

*&amp;---------------------------------------------------------------------*
*&amp; UNDER
*&amp;---------------------------------------------------------------------*

WRITE / repeat( val = `*` occ = 80 ).

&quot;Note: If the output is written in the same line in which the previous output
&quot;is displayed, this output is overwritten.
DATA abc TYPE c LENGTH 5 VALUE &#x27;abcde&#x27;.
WRITE /5(5) abc.
WRITE / &#x27;fghij&#x27; UNDER abc.

&quot;Output in tabular form
DATA: carrid   TYPE scarr-carrid,
      carrname TYPE scarr-carrname,
      currcode TYPE scarr-currcode,
      url      TYPE scarr-url.

WRITE: /3 &#x27;Carrier&#x27;, 12 &#x27;Name&#x27;, 35 &#x27;Currency Code&#x27;, 50 &#x27;URL&#x27;.
WRITE / repeat( val = `=` occ = 80 ).
SELECT carrid, carrname, currcode, url
       FROM scarr
       INTO @DATA(scarr_wa).
  WRITE: / scarr_wa-carrid UNDER &#x27;Carrier&#x27;,
           scarr_wa-carrname UNDER &#x27;Name&#x27;,
           scarr_wa-currcode UNDER &#x27;Currency Code&#x27;,
           scarr_wa-url UNDER &#x27;URL&#x27;.
ENDSELECT.
WRITE / .

&quot;Excursion: Writing an internal table in table-like fashion. As a prerequisite,
&quot;all values are convertible to type string. The following example does not use
&quot;UNDER, but calculates position and length values. It is an exploration and not 
&quot;meant to present a best practice example, particularly because other options 
&quot;such as ALV are available for that purpose. It may not be suitable for each 
&quot;line and component type.

&quot;Demo internal table
TYPES: BEGIN OF demo_struc,
         chars TYPE c LENGTH 5,
         num   TYPE i,
         dec   TYPE decfloat34,
         text  TYPE string,
         flag  TYPE abap_boolean,
         date  TYPE d,
       END OF demo_struc,
       ty_tab TYPE TABLE OF demo_struc WITH EMPTY KEY.

DATA(itab) = VALUE ty_tab( ( chars = &#x27;a&#x27; num = 123 dec = CONV decfloat34( &#x27;-123.456&#x27; ) text = `Hello world` flag = abap_true date = CONV d( &#x27;20250101&#x27; ) )
                           ( chars = &#x27;bcd&#x27; num = 4 dec = CONV decfloat34( &#x27;7.8945612&#x27; ) text = `Lorem ipsum` flag = abap_false date = CONV d( &#x27;20251227&#x27; ) )
                           ( chars = &#x27;efghi&#x27; num = 987654 dec = CONV decfloat34( &#x27;1.01&#x27; ) text = `ABAP` flag = abap_true date = CONV d( &#x27;20250621&#x27; ) )
                           ( chars = &#x27;jk&#x27; num = 321 dec = CONV decfloat34( &#x27;0.00000000000001&#x27; ) text = `test` flag = abap_true date = CONV d( &#x27;20251015&#x27; ) ) ).

TYPES: BEGIN OF comp_struc,
         name TYPE string,
         len  TYPE i,
         off  TYPE i,
         dref TYPE REF TO string,
       END OF comp_struc.
DATA it_comps TYPE TABLE OF comp_struc WITH EMPTY KEY.
DATA str TYPE string.

TRY.
    DATA(type_descr_obj_tab) = CAST cl_abap_tabledescr( cl_abap_typedescr=&gt;describe_by_data( itab ) ).
    DATA(tab_comps) = CAST cl_abap_structdescr( type_descr_obj_tab-&gt;get_table_line_type( ) )-&gt;get_components( ).
    LOOP AT tab_comps ASSIGNING FIELD-SYMBOL(&lt;comp&gt;).
      APPEND VALUE #( name = &lt;comp&gt;-name len = strlen( &lt;comp&gt;-name ) ) TO it_comps.
    ENDLOOP.
  CATCH cx_sy_move_cast_error INTO DATA(error).
    WRITE  / error-&gt;get_text( ).
ENDTRY.

IF error IS INITIAL.
  &quot;Implementation for properly aligning the content
  &quot;The example is implemented to check the length of the column names as well as the
  &quot;length of the values in the columns. It determines the length of the longest string
  &quot;in each column. Depending on the length values, either the length of the column name
  &quot;or the length of the longest string in a column is stored in an internal table that
  &quot;contains information for calculating the offset.
  LOOP AT tab_comps ASSIGNING FIELD-SYMBOL(&lt;len&gt;).
    ASSIGN it_comps[ name = &lt;len&gt;-name ] TO FIELD-SYMBOL(&lt;co&gt;).
    DATA(max_content) = REDUCE i( INIT len = &lt;co&gt;-len
                                  FOR &lt;line&gt; IN itab
                                  NEXT len = COND #( LET lv = |{ &lt;line&gt;-(&lt;co&gt;-name) }| IN
                                                     WHEN strlen( lv ) &gt; len
                                                     THEN strlen( lv )
                                                     ELSE len ) ).
    &quot;Extend the length value to leave some more space
    IF max_content &gt; &lt;co&gt;-len.
      &lt;co&gt;-len = max_content + 3.
    ELSE.
      &lt;co&gt;-len += 3.
    ENDIF.
  ENDLOOP.
  &quot;Calculating offset values
  DATA max_str_len TYPE i.
  LOOP AT it_comps ASSIGNING FIELD-SYMBOL(&lt;off&gt;).
    DATA(tabix) = sy-tabix.
    READ TABLE it_comps INDEX tabix - 1 ASSIGNING FIELD-SYMBOL(&lt;prev&gt;).
    &lt;off&gt;-off = COND #( WHEN tabix = 1 THEN 0 ELSE &lt;prev&gt;-len + &lt;prev&gt;-off ).
    max_str_len += &lt;off&gt;-len.
  ENDLOOP.
  &quot;Providing enough space so that table row content can be inserted based on
  &quot;the offset specification
  SHIFT str BY max_str_len PLACES RIGHT.
  &quot;Adding the column names first
  LOOP AT it_comps ASSIGNING FIELD-SYMBOL(&lt;header&gt;).
    str = insert( val = str sub = &lt;header&gt;-name off = &lt;header&gt;-off ).
  ENDLOOP.
  WRITE / str.
  &quot;Processing all lines in the internal table containing the retrieved table rows
  LOOP AT itab ASSIGNING FIELD-SYMBOL(&lt;wa&gt;).
    CLEAR str.
    SHIFT str BY max_str_len PLACES RIGHT.
    DO.
      TRY.
          str = insert( val = str sub = &lt;wa&gt;-(sy-index) off = it_comps[ sy-index ]-off ).
        CATCH cx_sy_assign_illegal_component cx_sy_range_out_of_bounds cx_sy_itab_line_not_found.
          EXIT.
      ENDTRY.
    ENDDO.
    WRITE / str.
  ENDLOOP.
ENDIF.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Adding quickinfo&lt;br&gt;&lt;br&gt; Addition <code>QUICKINFO</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Creates a tooltip for the output</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>WRITE / repeat( val = `*` occ = 80 ).
WRITE / &#x27;Place the cursor on the following output and check the tooltip:&#x27;.
WRITE: /(*) sy-uname QUICKINFO &#x27;User name&#x27;,
        &#x27;/&#x27;,
        (*) sy-datum QUICKINFO &#x27;Current date&#x27;,
        &#x27;/&#x27;,
        (*) sy-uzeit QUICKINFO &#x27;Current time&#x27;.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Adding list elements such as checkboxes, icons, symbols, and frames&lt;br&gt;&lt;br&gt; Additions <code>AS CHECKBOX</code>, <code>AS ICON</code>, <code>AS SYMBOL</code>, <code>AS LINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>AS CHECKBOX</code>: The value (blank, or <code>X</code>) is stored in the list buffer and can be evaluated during a list event.</li>
<li><code>AS ICON</code>: Outputs icons. Check the type pool <code>ICON</code> for names of predefined icons.</li>
<li><code>AS SYMBOL</code>: Outputs symbols. Check the type pool <code>SYM</code> for names of predefined symbols.</li>
<li><code>AS LINE</code>: Outputs corners, crosses, lines, and T sections. Check the type pool <code>LINE</code> for names of predefined options.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

DATA: flag          TYPE c LENGTH 1,
      checkbox_name TYPE c LENGTH 10,
      wa            TYPE c LENGTH 20.

START-OF-SELECTION.

*&amp;---------------------------------------------------------------------*
*&amp; AS CHECKBOX
*&amp;---------------------------------------------------------------------*

  DO 5 TIMES.
    checkbox_name = |Checkbox { sy-index }|.
    WRITE: / flag AS CHECKBOX, (10) checkbox_name.
  ENDDO.

*&amp;---------------------------------------------------------------------*
*&amp; AS ICON
*&amp;---------------------------------------------------------------------*

  WRITE / icon_green_light AS ICON.
  WRITE / icon_red_light AS ICON.
  WRITE / icon_yellow_light AS ICON.
  WRITE / icon_activate AS ICON.

*&amp;---------------------------------------------------------------------*
*&amp; AS SYMBOL
*&amp;---------------------------------------------------------------------*

  WRITE / sym_left_hand AS SYMBOL.
  WRITE / sym_caution AS SYMBOL.

*&amp;---------------------------------------------------------------------*
*&amp; AS LINE
*&amp;---------------------------------------------------------------------*

  WRITE / line_horizontal_line AS LINE NO-GAP.
  WRITE  line_space AS LINE NO-GAP.
  WRITE  line_vertical_line AS LINE.

*&amp;---------------------------------------------------------------------*
*&amp; Evaluating the checkbox selections
*&amp;---------------------------------------------------------------------*

AT LINE-SELECTION.

  WRITE / |You clicked line { sy-lilli }. |.
  WRITE / `Selected checkboxes:`.
    DO.
    READ LINE sy-index FIELD VALUE flag checkbox_name INTO wa.
    IF sy-subrc &lt;&gt; 0.
      EXIT.
    ELSEIF flag = &#x27;X&#x27;.
      WRITE / wa.
    ENDIF.
  CLEAR wa.
  ENDDO.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Formatting options</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Various additions are available to format output.</li>
<li>For example, they deal with aligning content, currency, unit, date and time-related formatting, among others.</li>
<li>Find more information in the ABAP Keyword Documentation <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapwrite_int_options.htm">here</a> and <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapwrite_ext_options.htm">here</a>. See also the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapformat.htm"><code>FORMAT</code></a> statement.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The example only shows a selection. Check the documentation for all additions.</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Additions for aligning content
*&amp;---------------------------------------------------------------------*

WRITE /(10) &#x27;X&#x27; RIGHT-JUSTIFIED. &quot;&#x27;         X&#x27;
WRITE /(10) &#x27;X&#x27; CENTERED.        &quot;&#x27;    X     &#x27;
WRITE /(10) &#x27;X&#x27; LEFT-JUSTIFIED.  &quot;&#x27;X         &#x27;

*&amp;---------------------------------------------------------------------*
*&amp; NO-ZERO
*&amp;---------------------------------------------------------------------*

DATA: numc10  TYPE n LENGTH 10 VALUE &#x27;123&#x27;.
WRITE /(10) numc10.
WRITE /(10) numc10 NO-ZERO.

*&amp;---------------------------------------------------------------------*
*&amp; DECIMALS
*&amp;---------------------------------------------------------------------*

DATA: packed_num TYPE p LENGTH 8 DECIMALS 4 VALUE &#x27;9876.5432&#x27;.
WRITE /(*) packed_num.
WRITE /(*) packed_num DECIMALS 2.

*&amp;---------------------------------------------------------------------*
*&amp; COLOR
*&amp;---------------------------------------------------------------------*

&quot;The commented out value stands for the value that can also
&quot;be directly specified in the syntax (except 0) or contained in
&quot;a data object. There are several additions with many options.
WRITE / &#x27;COL_BACKGROUND (0)&#x27; COLOR COL_BACKGROUND. &quot;0 (GUI dependent)
WRITE / &#x27;COL_HEADING (1)&#x27; COLOR COL_HEADING. &quot;1 (gray-blue)
WRITE / &#x27;COL_HEADING (1)&#x27; COLOR 1.
WRITE / &#x27;COL_NORMAL (2)&#x27; COLOR COL_NORMAL. &quot;2 (light gray)
WRITE / &#x27;COL_TOTAL (3)&#x27; COLOR COL_TOTAL. &quot;3 (yellow)
WRITE / &#x27;COL_KEY (4)&#x27; COLOR COL_KEY. &quot;4 (blue-green)
WRITE / &#x27;COL_POSITIVE (5)&#x27; COLOR COL_POSITIVE. &quot;5 (green)
WRITE / &#x27;COL_NEGATIVE (6)&#x27; COLOR COL_NEGATIVE. &quot;6 (red)
WRITE / &#x27;COL_GROUP (7)&#x27; COLOR COL_GROUP. &quot;7 (orange)
WRITE / &#x27;COLOR 7 OFF&#x27; COLOR 7 OFF. &quot;default color

&quot;Setting the intensity of the background color.
&quot;Note that many of the following additions can specify further
&quot;additions such as ON and OFF. See the details in the documentation.
WRITE / &#x27;COLOR 7 INTENSIFIED OFF&#x27; COLOR 7 INTENSIFIED OFF.
&quot;INVERSE: When ON, the foreground, i.e. the output is displayed
&quot;in the selected color
WRITE / &#x27;COLOR 7 INVERSE ON&#x27; COLOR 7 INVERSE ON.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Formatting Output Using FORMAT Statements</h3>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapformat.htm"><code>FORMAT</code></a> statements can be used to apply formattings to output statements.</li>
<li>The formats are valid until reverted by a new <code>FORMAT</code> statement, using the additions <code>OFF</code> or <code>RESET</code>.</li>
<li>Note that the additions for <code>FORMAT</code> are also available for <code>WRITE</code> statements and can be applied individually. See the ABAP Keyword Documentation for all details.</li>
</ul>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Addition &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Coloring output &lt;br&gt;&lt;br&gt; <code>COLOR</code> addition</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>As shown above, multiple colors are available. Instead of <code>COL_*</code>, the numbers that represent the color specifications can also be specified.</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>FORMAT COLOR COL_POSITIVE.
WRITE / &#x27;ABC&#x27;.
WRITE / &#x27;DEF&#x27;.
WRITE / &#x27;GHI&#x27;.
FORMAT COLOR OFF.

WRITE / &#x27;JKL&#x27;.
WRITE / &#x27;MNO&#x27;.

FORMAT COLOR COL_KEY.
WRITE / &#x27;PQR&#x27;.
WRITE / &#x27;STU&#x27;.
FORMAT COLOR OFF.

WRITE / &#x27;VWX&#x27;.
WRITE / &#x27;YZ&#x27;.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying a hotspot&lt;br&gt;&lt;br&gt; <code>HOTSPOT</code> addition</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Displays the mouse pointer when hovering over an element.</li>
<li>Here, a single click has then the same effect as a double click (i.e. the selection of function key <em>F2</em>).</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

START-OF-SELECTION.
  FORMAT HOTSPOT.
  DO 5 TIMES.
    WRITE / |{ sy-index } - some clickable colored text showing the mouse pointer.| COLOR 5.
  ENDDO.
  FORMAT HOTSPOT OFF.

  WRITE / `Some colored text that doesn&#x27;t show the mouse pointer.` COLOR 6.

AT LINE-SELECTION.

  WRITE |You clicked line { sy-lilli }.|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying an input field&lt;br&gt;&lt;br&gt; <code>INPUT</code> addition</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Enables user input that can be evaluated during a list event.</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

DATA: some_text   TYPE c LENGTH 100,
      line_number TYPE i.

START-OF-SELECTION.
  WRITE &#x27;Make an entry:&#x27;.
  &quot;Note: The input field is currently empty. Without the following statement,
  &quot;the input field would not be displayed.
  SET BLANK LINES ON.
  FORMAT INPUT.
  WRITE / some_text.
  FORMAT INPUT OFF.

  WRITE / &#x27;Submit&#x27; COLOR 5 HOTSPOT.

AT LINE-SELECTION.
  IF sy-lisel = &#x27;Submit&#x27;.
    line_number = sy-lilli - 1.
    READ LINE line_number FIELD VALUE some_text.
    WRITE: &#x27;Your entry:&#x27;,
           / some_text.
  ENDIF.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating frames&lt;br&gt;&lt;br&gt; <code>FRAME</code> addition</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Defines whether the - and | characters are converted to line elements, producing continuous lines.</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>
FORMAT FRAMES ON.
WRITE: / &#x27;----&#x27;,
       / &#x27;|  |&#x27;,
       / &#x27;----&#x27;.
FORMAT FRAMES OFF.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Resetting formats&lt;br&gt;&lt;br&gt; <code>RESET</code> addition</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>This addition sets all formatting settings for which the corresponding addition is not specified in the same <code>FORMAT</code> statement to the state <code>OFF</code> (exception: <code>FRAMES</code>).</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>In the following example, all settings are reset without specifying concrete additions (e.g. <code>INVERSE OFF</code>). Note the effects of such a statement (e.g. <code>INTENSIFIED</code> is <code>ON</code> by default at program start. Now it is set to <code>OFF</code>. See the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapformat.htm">ABAP Keyword Documentation</a> for more information.</p>
<pre><code>WRITE / &#x27;ABCD&#x27;.
FORMAT COLOR 7 INVERSE ON.
WRITE / &#x27;DEFG&#x27;.
WRITE / &#x27;HIJK&#x27;.
FORMAT RESET.
WRITE / &#x27;LMNO&#x27;.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>List Creation-Related ABAP Keywords and Additions</h3>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Keyword &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating a horizontal line&lt;br&gt;&lt;br&gt; <code>ULINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Additions are available such as for position and length.</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>ULINE.
ULINE AT 5(20).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Specifying if blank lines created using <code>WRITE</code> are displayed&lt;br&gt;&lt;br&gt; <code>SET BLANK LINES</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The statement must be specified with the additions <code>ON</code> and <code>OFF</code>.</li>
<li>Note that blank lines are not displayed by default.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>DATA(itab) = VALUE string_table( ( `a` ) ( `b` ) ( `` ) ( `` )  ( `c` ) ( `` ) ( `d` ) ).

&quot;Blank lines not output by default
LOOP AT itab INTO DATA(wa).
  WRITE / wa.
ENDLOOP.

WRITE / repeat( val = `*` occ = 80 ).

&quot;Explicitly specifying that blank lines should not be output
SET BLANK LINES OFF.
LOOP AT itab INTO wa.
  WRITE / wa.
ENDLOOP.

WRITE / repeat( val = `*` occ = 80 ).

&quot;Blank lines are output
SET BLANK LINES ON.
LOOP AT itab INTO wa.
  WRITE / wa.
ENDLOOP.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Setting the list cursor explicitly in another line&lt;br&gt;&lt;br&gt; <code>SKIP</code>, <code>SKIP TO LINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>You can create blank lines, but also set to the cursor to a specified line.</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Creating blank lines
*&amp;---------------------------------------------------------------------*

WRITE / &#x27;abc&#x27;.
&quot;Skips 1 line
SKIP.
WRITE / &#x27;def&#x27;.
&quot;Specifying the number of lines to be skipped
SKIP 3.
WRITE / &#x27;ghi&#x27;.
SKIP.

*&amp;---------------------------------------------------------------------*
*&amp; Positioning the list cursor in another line
*&amp;---------------------------------------------------------------------*

&quot;In the example, the list cursor is explicitly set to
&quot;a particular number. Here, B and C are not output (i.e.
&quot;overwritten with the output that follows).
WRITE / &#x27;Text A&#x27;.
WRITE / &#x27;Text B&#x27;.
&quot;The line number of the previous WRITE is retrieved.
DATA(line) = sy-linno.
WRITE / &#x27;Text C&#x27;.
SKIP TO LINE line.
WRITE / &#x27;Text D&#x27;.
WRITE / &#x27;Text E&#x27;.
WRITE / &#x27;Text F&#x27;.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Setting the list cursor to the first position of the next line&lt;br&gt;&lt;br&gt; <code>NEW-LINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Additions are available to affect the scrolling behavior</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>WRITE / &#x27;Next statement is NEW-LINE&#x27;.
NEW-LINE.
&quot;The following output is in a new line.
WRITE &#x27;WRITE statement without /&#x27;. </code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Storing the content of a flat variable together with the current list line&lt;br&gt;&lt;br&gt; <code>HIDE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can read the stored content again during a list event.</li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abaphide.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

TYPES: BEGIN OF s,
         num1 TYPE i,
         num2 TYPE i,
       END OF s,
       ty_int_tab TYPE TABLE OF s WITH EMPTY KEY.
DATA(itab) = VALUE ty_int_tab( ( num1 = 10 num2 = 5 )
                               ( num1 = 2346 num2 = 3475 )
                               ( num1 = 2 num2 = 4 )
                               ( num1 = 347 num2 = 823 )
                               ( num1 = 20 num2 = 54 ) ).

DATA: addition_result TYPE i.

START-OF-SELECTION.
  FORMAT HOTSPOT.
  LOOP AT itab INTO DATA(wa).
    addition_result = wa-num1 + wa-num2.
    WRITE / |{ wa-num1 } + { wa-num2 } = ...|.
    HIDE: addition_result.
  ENDLOOP.

AT LINE-SELECTION.
  WRITE (*) addition_result.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Creating a page break&lt;br&gt;&lt;br&gt; <code>RESERVE n LINES</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Relevant if there&#x27;s not sufficient space on the list page between the last output and page end or footer.</li>
<li>The page break creation raises the <code>END-OF-PAGE</code> event.</li>
<li>Has an impact on the behavior of <code>BACK</code> statements as shown below.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The following example creates multiple lines in a loop.</li>
<li>Additions to <code>REPORT</code> are used to cater for the example demonstration with a small page length:</li>
<li><code>NO STANDARD PAGE HEADING</code> suppresses the output of the standard page header in the basic list</li>
<li><code>LINE-COUNT</code> determines the page length. The number of lines in parentheses stands for the number of lines in the page footer. The footer can be defined in the <code>END-OF-PAGE</code> block. Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapreport_list_options.htm">here</a>.</li>
<li>Due to the <code>RESERVE</code> statement, page breaks are created as there is not enough space left on the page. The loop writes 4 sample texts to the list and can be considered a line block. They should not be separated by page breaks.</li>
<li>You can try and comment out the line with the <code>RESERVE</code> statement to check out the difference.</li>
</ul>
<pre><code>REPORT NO STANDARD PAGE HEADING LINE-COUNT 7(2).

START-OF-SELECTION.
  DO 5 TIMES.
    RESERVE 4 LINES.
    WRITE: / &#x27;text a&#x27;, / &#x27;text b&#x27;, / &#x27;text c&#x27;, / &#x27;text d&#x27;.
  ENDDO.

END-OF-PAGE.
  WRITE |--- { sy-pagno } ---|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Positioning the list cursor in the first position of the first line of a logical unit&lt;br&gt;&lt;br&gt; <code>BACK</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Such a logical unit can be a line block determined using <code>RESERVE</code></li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapback.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following example demonstrates <code>BACK</code> with <code>RESERVE</code>. Consider the <code>RESERVE 5 LINES</code> statements as defining a block of 5 lines. When using <code>BACK</code> after <code>RESERVE</code>, e.g. after a loop, you can put the suqsequent output to the first line of this block of lines.</p>
<pre><code>DATA int_for_back TYPE i.

WRITE / &#x27;RESERVE without BACK:&#x27;.
int_for_back = sy-colno.
ULINE AT /(int_for_back).

RESERVE 5 LINES.

DO 5 TIMES.
  WRITE / sy-index.
ENDDO.

int_for_back = sy-colno.

WRITE AT int_for_back &#x27;   &lt;- This is not number 1.&#x27;.
SKIP.

WRITE / &#x27;RESERVE with BACK:&#x27;.
int_for_back = sy-colno.
ULINE AT /(int_for_back).

RESERVE 5 LINES.

DO 5 TIMES.
  WRITE / sy-index.
ENDDO.

int_for_back = sy-colno.

BACK.
WRITE AT int_for_back &#x27;   &lt;- This should be number 1.&#x27;.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Processing Lists in List Buffers</h3>
<ul>
<li>ABAP statements are available that allow lists to be processed that have already been saved as <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenscreen_list_glosry.htm">screen lists</a> in the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenlist_buffer_glosry.htm">list buffer</a>.</li>
<li>The following selection covers <code>READ [CURRENT] LINE</code> and <code>MODIFY [CURRENT] LINE</code>.</li>
</ul>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Subject/Keyword &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Reading content of lines&lt;br&gt;&lt;br&gt; <code>READ [CURRENT] LINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Assigns the content of a line stored in the list buffer to the system field <code>sy-lisel</code>.</li>
<li>Allows other target fields to be specified.</li>
<li><code>sy-subrc</code> is set: 0 (line was read), other than 0 (line does not exist)</li>
<li>Specifying the line with additions:</li>
<li><code>READ LINE</code> and a concrete line</li>
<li><code>READ CURRENT LINE</code> to read the current line</li>
<li>More additions are available regarding the list level and assigning the output of individual data objects to target fields. Find more information in the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapread_line.htm">ABAP Keyword Documentation</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

DATA: flag          TYPE c LENGTH 1,
      checkbox_name TYPE c LENGTH 10,
      flag_check    TYPE c LENGTH 1,
      target        TYPE c LENGTH 20.

START-OF-SELECTION.

  WRITE / &#x27;Text A&#x27;.
  WRITE / &#x27;Text B&#x27;.
  WRITE / &#x27;Text C&#x27;.
  WRITE / &#x27;Text D&#x27;.
  WRITE / &#x27;Text E&#x27;.
  WRITE / &#x27;Text F&#x27;.
  checkbox_name = &#x27;Checkbox 1&#x27;.
  flag = &#x27;X&#x27;.
  WRITE: / flag AS CHECKBOX, (10) checkbox_name.
  checkbox_name = &#x27;Checkbox 2&#x27;.
  flag = &#x27;X&#x27;.
  WRITE: / flag AS CHECKBOX, (10) checkbox_name.

AT LINE-SELECTION.

  WRITE / |You selected line { sy-lilli } (value of sy-lilli).|.
  &quot;Accessing the clicked line using sy-lisel
  WRITE / |Value of sy-lisel: &quot;{ sy-lisel }&quot;|.

  SKIP.

*&amp;---------------------------------------------------------------------*
*&amp; READ LINE
*&amp;---------------------------------------------------------------------*

  WRITE / `---------------- READ LINE ----------------`.

  &quot;Reading the content of the clicked line using READ LINE
  READ LINE sy-lilli.
  WRITE / |Value of sy-lisel after &quot;READ LINE sy-lisel.&quot;: &quot;{ sy-lisel }&quot;|.

  SKIP.

  &quot;Reading the content of all lines
  &quot;Note the output lines including the standard page header
  DO.
    READ LINE sy-index.
    IF sy-subrc = 0.
      WRITE / |Value of sy-lisel after READ LINE: &quot;{ sy-lisel }&quot;|.
    ELSE.
      EXIT.
    ENDIF.
  ENDDO.

  SKIP.

*&amp;---------------------------------------------------------------------*
*&amp; READ CURRENT LINE
*&amp;---------------------------------------------------------------------*

  WRITE / `---------------- READ CURRENT LINE ----------------`.

  &quot;sy-lisel contains the entire line content of the current line
  &quot;Note: The line is read where the screen cursor was last positioned or
  &quot;a line was last read.
  &quot;As the previous example reads all lines, the current line is here the
  &quot;last line available.
  READ CURRENT LINE.
  WRITE / |Value of sy-lisel after &quot;READ CURRENT LINE.&quot;: &quot;{ sy-lisel }&quot;|.

  SKIP.

*&amp;---------------------------------------------------------------------*
*&amp; READ [CURRENT] LINE LINE VALUE
*&amp;---------------------------------------------------------------------*

  &quot;Assigning the output of individual data objects
  &quot;Here, it is the entire line content.

  SKIP.
  WRITE / `---------------- READ ... LINE LINE VALUE ----------------`.

  &quot;Specifying the current line
  &quot;See the notes above regarding the current line.
  DATA wa TYPE c LENGTH 50.
  READ CURRENT LINE LINE VALUE INTO wa.
  WRITE / |Content of target field after READ CURRENT LINE using the LINE VALUE addition: &quot;{ wa }&quot;|.
  WRITE / |Value of sy-lisel after statement: &quot;{ sy-lisel }&quot;|.

  &quot;Specifying a concrete line
  READ LINE 3 LINE VALUE INTO wa.
  WRITE / |Content of target field after READ LINE n using the LINE VALUE addition: &quot;{ wa }&quot;|.
  WRITE / |Value of sy-lisel after statement: &quot;{ sy-lisel }&quot;|.

  SKIP.

*&amp;---------------------------------------------------------------------*
*&amp; READ [CURRENT] LINE FIELD VALUE
*&amp;---------------------------------------------------------------------*

  &quot;Assigning the output of data objects to these data objects or other target
  &quot;data objects.
  &quot;In the example, the checkbox selections are evaluated. Dedicated target
  &quot;objects are specified for both data objects using INTO.

  WRITE / `---------------- READ ... LINE FIELD VALUE ----------------`.

  DATA(no_selection) = 0.
  DO.
    READ LINE sy-index FIELD VALUE flag INTO flag_check checkbox_name INTO target.
    IF sy-subrc &lt;&gt; 0.
      EXIT.
    ELSEIF flag_check = &#x27;X&#x27;.
      no_selection = 1.
      WRITE / |Value of  sy-lilli: &quot;{ sy-lilli }&quot;|.
      WRITE / |Value of target after READ LINE: &quot;{ target }&quot;|.
      WRITE / |Value of sy-lisel after READ LINE: &quot;{ sy-lisel }&quot;|.
      WRITE / `*****`.
    ENDIF.
    CLEAR: target, flag_check.
  ENDDO.

  IF no_selection = 0.
    WRITE / `No checkbox selected.`.
  ENDIF.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Overwriting line content and format&lt;br&gt;&lt;br&gt; <code>MODIFY [CURRENT] LINE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Overwrites a line stored in the list buffer with the content of the <code>sy-lisel</code> system field</li>
<li>Allows additional modifications such as changing the value and format.</li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapmodify_line.htm">here</a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

DATA text TYPE c LENGTH 4 VALUE &#x27;Text&#x27;.
DATA letter TYPE c LENGTH 1.
DATA linno4mod_a TYPE i.
DATA linno4mod_b TYPE i.
DATA linno4mod_c TYPE i.
DATA linno4mod_d TYPE i.
DATA linno4mod_e TYPE i.
DATA linno4mod_f TYPE i.

START-OF-SELECTION.

  linno4mod_a = sy-linno.
  letter = &#x27;A&#x27;.
  WRITE: / text, letter.
  letter = &#x27;B&#x27;.
  WRITE: / text, letter.
  linno4mod_b = sy-linno.
  letter = &#x27;C&#x27;.
  WRITE: / text, letter.
  letter = &#x27;D&#x27;.
  WRITE: / text, letter.
  linno4mod_c = sy-linno.
  letter = &#x27;E&#x27;.
  WRITE: / text, letter.
  letter = &#x27;F&#x27;.
  WRITE: / text, letter.
  linno4mod_d = sy-linno.
  letter = &#x27;G&#x27;.
  WRITE: / text, letter.
  letter = &#x27;H&#x27;.
  WRITE: / text, letter.
  linno4mod_e = sy-linno.
  letter = &#x27;I&#x27;.
  WRITE: / text, letter.
  letter = &#x27;J&#x27;.
  WRITE: / text, letter.
  linno4mod_f = sy-linno.

AT LINE-SELECTION.

  IF sy-lilli BETWEEN linno4mod_a + 2 AND linno4mod_b.
    MODIFY CURRENT LINE LINE VALUE FROM &#x27;Line overwritten&#x27;.
  ELSEIF sy-lilli BETWEEN linno4mod_b + 1 AND linno4mod_c.
    MODIFY CURRENT LINE FIELD VALUE text FROM &#x27;ABAP&#x27;.
  ELSEIF sy-lilli BETWEEN linno4mod_c + 1 AND linno4mod_d.
    MODIFY CURRENT LINE FIELD FORMAT letter COLOR 3.
  ELSEIF sy-lilli BETWEEN linno4mod_d + 1 AND linno4mod_e.
    MODIFY CURRENT LINE LINE FORMAT COLOR 4.
  ELSEIF sy-lilli BETWEEN linno4mod_e + 1 AND linno4mod_f.
    MODIFY CURRENT LINE FIELD FORMAT text COLOR 5 letter COLOR 6 LINE FORMAT COLOR 7.
  ELSE.
    MESSAGE `That won&#x27;t be modified.` TYPE &#x27;I&#x27;.
  ENDIF.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<div class="note">[!NOTE] - [Relevant sy components in the context of lists](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenlist_systemfields.htm) - You can interact with a list by double-clicking a line (or pressing F2; the default `PICK` function code raises the `AT LINE-SELECTION` event). Other function codes usually trigger the `AT USER-COMMAND` event. See the event blocks below. In your ABAP program, you can react to the user action by implementing the individual event blocks. Regarding the function code, you can use the `sy-ucomm` system field for evaluation (unlike dynpros, there's no `OK_CODE` field to be filled).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Event Blocks</h2>
<ul>
<li>Event blocks are introduced by an <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenevent_keyword_glosry.htm">event keyword</a>.</li>
<li>Ended by the next processing block (there&#x27;s no closing statement).</li>
<li>Should not contain separate declarations, i.e. you should put all data object declarations in the global declaration part (except for <code>AT SELECTION-SCREEN ...</code>)</li>
<li>It is recommended that you use ABAP Objects and methods consistently for data encapsulation</li>
<li>Should not be specified more than once in a program</li>
<li>Called depending on the user action and the program state (even if not explicitly specified)</li>
</ul>
<div class="note">[!NOTE] The main purpose of the examples is to visualize the calling of the events. Internal tables are filled during the events to log the event name and the time stamp when it is called. The tables are then output.</div>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;th&gt; Event block &lt;/th&gt; &lt;th&gt; Notes &lt;/th&gt; &lt;th&gt; Code Snippet &lt;/th&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Program constructor</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>LOAD-OF-PROGRAM</code></li>
<li>Called when a program is loaded into the internal session</li>
<li>Can be used to initialize the global data of a program. However, for executable programs, it is recommended that you use the event block <code>INITIALIZATION</code> for this task since the start values for parameters and selection criteria are set after <code>LOAD-OF-PROGRAM</code>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>See below</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Reporting events related to executable programs in particular</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li><code>INITIALIZATION</code></li>
<li>Used to initialize executable programs and provide start values (that cannot be statically expressed using <code>DEFAULT</code>)</li>
<li>Called directly after <code>LOAD-OF-PROGRAM</code> and before selection screen processing</li>
<li><code>START-OF-SELECTION</code></li>
<li>Called after selection screen processing (see the following event blocks)</li>
<li>If an executable program does not contain selection screens, this block is called automatically and implicitly.</li>
<li>All statements are implicitly assigned to this block unless they are explicitly assigned to another block.</li>
<li>Although it is optional, it is recommended that you specify the statement explicitly to improve readability.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

TYPES: BEGIN OF st,
         id          TYPE c LENGTH 3,
         event_block TYPE c LENGTH 50,
         time        TYPE utclong,
       END OF st.
DATA log TYPE TABLE OF st WITH EMPTY KEY.
DATA counter TYPE i.

SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT (10) txt1.
  PARAMETERS text1 TYPE c LENGTH 10 LOWER CASE.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT (10) txt2.
  PARAMETERS text2 TYPE c LENGTH 10 LOWER CASE.
SELECTION-SCREEN END OF LINE.

LOAD-OF-PROGRAM.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `LOAD-OF-PROGRAM` time = utclong_current( ) ) TO log.

INITIALIZATION.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `INITIALIZATION` time = utclong_current( ) ) TO log.
  text1 = &#x27;Hello&#x27;.
  text2 = &#x27;World&#x27;.
  txt1 = &#x27;Text 1&#x27;.
  txt2 = &#x27;Text 2&#x27;.

START-OF-SELECTION.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `START-OF-SELECTION` time = utclong_current( ) ) TO log.

  LOOP AT log INTO DATA(wa).
    WRITE: /(*) wa-id, (20) wa-event_block, (40) wa-time.
  ENDLOOP.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Selection screen events</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to individually evaluate selection screen elements, e.g. a specific parameter.</li>
<li>In many cases, the events can be used for input validation (this should not wait until <code>START-OF-SELECTION</code>). If your custom input validation fails on an input, you can raise a message (e.g. a message of type <code>E</code>: <code>MESSAGE &#x27;Error&#x27; TYPE &#x27;E&#x27;.</code>) and allow users to edit the input again.</li>
</ul>
<p>Event blocks:</p>
<ul>
<li><code>AT SELECTION-SCREEN OUTPUT</code>: Called by the dynpro event PBO of a selection screen after <code>INITIALIZATION</code>; can be used for modifying screen elements (e.g. using <code>MODIFY SCREEN</code>)</li>
<li><code>AT SELECTION-SCREEN ON par</code>: Called when values for a parameter or selection criteria were passed to the program; note: the syntax is also available for selection criteria; an additional variant <code>... ON END OF ...</code> is available (this event block is called when the selection table is passed completely allowing to check the entire selection table content)</li>
<li><code>AT SELECTION-SCREEN ON BLOCK bl</code>: Called when all input for a block is passed to the program</li>
<li><code>AT SELECTION-SCREEN ON RADIOBUTTON GROUP gr</code>: Called when the values for a radio button group were passed</li>
<li><code>AT SELECTION-SCREEN ON EXIT-COMMAND</code>: Called by <em>Back</em>, <em>Exit</em>, or <em>Cancel</em> (e.g. used for cleanup tasks)</li>
<li><code>AT SELECTION-SCREEN ON HELP-REQUEST | VALUE-REQUEST</code>: Relates to field (F1) and input (F4) help</li>
<li><code>AT SELECTION-SCREEN.</code>: Called as last event in the selection screen processing when all input values are passed to the program</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>PROGRAM.

TYPES: BEGIN OF st,
         id          TYPE c LENGTH 3,
         event_block TYPE c LENGTH 50,
         time        TYPE utclong,
       END OF st.
DATA log TYPE TABLE OF st WITH EMPTY KEY.
DATA counter TYPE i.

SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT (10) txt1.
  PARAMETERS text1 TYPE c LENGTH 10 LOWER CASE.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF BLOCK blk.
  PARAMETERS text2 TYPE c LENGTH 10 LOWER CASE.
  PARAMETERS text3 TYPE c LENGTH 10 LOWER CASE.
SELECTION-SCREEN END OF BLOCK blk.

PARAMETERS: rb1 RADIOBUTTON GROUP grp DEFAULT &#x27;X&#x27;,
            rb2 RADIOBUTTON GROUP grp.

LOAD-OF-PROGRAM.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `LOAD-OF-PROGRAM` time = utclong_current( ) ) TO log.

INITIALIZATION.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `INITIALIZATION` time = utclong_current( ) ) TO log.
  text1 = &#x27;Hello&#x27;.
  text2 = &#x27;World&#x27;.
  text3 = &#x27;ABAP&#x27;.
  txt1 = &#x27;Text 1&#x27;.

AT SELECTION-SCREEN OUTPUT.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `AT SELECTION-SCREEN OUTPUT` time = utclong_current( ) ) TO log.

  LOOP AT SCREEN INTO DATA(wa).
    IF wa-name = &#x27;TEXT1&#x27;.
      wa-intensified = &#x27;1&#x27;.
      MODIFY SCREEN FROM wa.
    ENDIF.
  ENDLOOP.

AT SELECTION-SCREEN ON text1.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `AT SELECTION-SCREEN ON` time = utclong_current( ) ) TO log.

AT SELECTION-SCREEN ON BLOCK blk.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `AT SELECTION-SCREEN ON BLOCK` time = utclong_current( ) ) TO log.

AT SELECTION-SCREEN ON RADIOBUTTON GROUP grp.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `AT SELECTION-SCREEN ON RADIOBUTTON GROUP` time = utclong_current( ) ) TO log.

AT SELECTION-SCREEN ON EXIT-COMMAND.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `AT SELECTION-SCREEN ON EXIT-COMMAND` time = utclong_current( ) ) TO log.

  DATA msg TYPE string VALUE `These events have already been raised:`.
  LOOP AT log INTO DATA(cmd).
    msg &amp;&amp;= | { cmd-event_block }|.
  ENDLOOP.
  MESSAGE msg TYPE &#x27;I&#x27;.

AT SELECTION-SCREEN.
  APPEND VALUE #( id = counter event_block = `AT SELECTION-SCREEN` time = utclong_current( ) ) TO log.

START-OF-SELECTION.
  counter += 1.
  APPEND VALUE #( id = counter event_block = `START-OF-SELECTION` time = utclong_current( ) ) TO log.

  LOOP AT log INTO DATA(wa).
    WRITE: /(*) wa-id, (45) wa-event_block, (30) wa-time.
  ENDLOOP.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>List events</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Are triggered during the list creation and after certain user actions on a displayed list.</li>
<li>During list creation:</li>
<li><code>TOP-OF-PAGE</code> &lt;br&gt;For definining page headers; called when a <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenbasic_list_glosry.htm">basic list</a> is created and when a new page begins (<code>... DURING LINE-SELECTION</code> is called when <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abendetails_list_glosry.htm">details lists</a> are created)</li>
<li><code>END-OF-PAGE</code> &lt;br&gt;For definining page footers; called when the end of a page is reached, for example, when the specified number of lines in the <code>LINE-COUNT</code> addition (e.g. <code>PROGRAM LINE-COUNT 20(3).</code>) has been reached</li>
<li>After user actions:</li>
<li><code>AT LINE-SELECTION</code> &lt;br&gt;Called when a screen list is displayed, the screen cursor is on a list line and a function is selected using the function code <code>PICK</code>; when specified, the function key F2 is enabled and, therefore, double click functionality; F2 and a double click are linked with the function code <code>PICK</code>.</li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapat_user-command.htm"><code>AT USER-COMMAND</code></a> &lt;br&gt;Called when a function with a user-defined function code is selected when a screen list is displayed; the function codes can be evaluated using <code>sy-ucomm</code>; you can set a list event programmatically using <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abapset_user-command.htm"><code>SET USER-COMMAND</code></a>.</li>
</ul>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The following example is an excerpt of the executable example contained in the repository. Find more information in the comments there. The example is just intended to explore and visualize various list events.</p>
<pre><code>PROGRAM LINE-COUNT 20(3).

TYPES: BEGIN OF st,
         id          TYPE c LENGTH 3,
         event_block TYPE c LENGTH 50,
         time        TYPE utclong,
       END OF st.
DATA: counter_line             TYPE i,
      reporting_and_selscr_log TYPE TABLE OF st WITH EMPTY KEY,
      list_log                 TYPE TABLE OF st WITH EMPTY KEY,
      counter                  TYPE i,
      tstmp                    TYPE utclong.

SELECTION-SCREEN COMMENT /1(70) intro.
SELECTION-SCREEN SKIP.

PARAMETERS: text TYPE c length 10.

SELECTION-SCREEN SKIP 2.
SELECTION-SCREEN COMMENT /1(70) note.

CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS:
      sel_screen_evt IMPORTING evt TYPE string,
      write_line IMPORTING evt TYPE string
                           utc TYPE utclong,
      counter,
      list_evt IMPORTING evt TYPE string,
      write_rep_sel_log,
      write_list_log.
    CLASS-DATA: li_wa  LIKE LINE OF list_log,
                sel_wa LIKE LINE OF reporting_and_selscr_log.
ENDCLASS.

CLASS demo IMPLEMENTATION.
  METHOD counter.
    IF counter = 0.
      counter = 1.
    ELSE.
      counter += 1.
    ENDIF.
  ENDMETHOD.

  METHOD sel_screen_evt.
    counter( ).
    APPEND VALUE #( id = counter event_block  = evt time = utclong_current( ) ) TO reporting_and_selscr_log.
  ENDMETHOD.

  METHOD list_evt.
    counter( ).
    APPEND VALUE #( id = counter event_block  = evt time = utclong_current( ) ) TO list_log.
  ENDMETHOD.

  METHOD write_line.
    counter( ).
    WRITE: /3(*) counter,
            15  evt,
            60  utc.
  ENDMETHOD.

  METHOD write_rep_sel_log.
    SKIP.
    WRITE / &#x27;********** Log entries for reporting and selection screen events **********&#x27; COLOR 2.

    FORMAT COLOR 2.
    WRITE: /3 &#x27;ID&#x27;, 15 &#x27;Event Block&#x27;, 60 &#x27;Time&#x27; .
    FORMAT COLOR OFF.
    LOOP AT reporting_and_selscr_log INTO sel_wa.
      WRITE: / sel_wa-id UNDER &#x27;ID&#x27;,
              sel_wa-event_block UNDER &#x27;Event Block&#x27;,
              sel_wa-time UNDER &#x27;Time&#x27;.
    ENDLOOP.
  ENDMETHOD.

  METHOD write_list_log.
    WRITE / &#x27;********** Log entries for list events **********&#x27; COLOR 2.

    FORMAT COLOR 2.
    WRITE: /3 &#x27;ID&#x27;, 15 &#x27;Event Block&#x27;, 60 &#x27;Time&#x27; .
    FORMAT COLOR OFF.
    LOOP AT list_log INTO li_wa.
      WRITE: / li_wa-id UNDER &#x27;ID&#x27;,
              li_wa-event_block UNDER &#x27;Event Block&#x27;,
              li_wa-time UNDER &#x27;Time&#x27;.
    ENDLOOP.
  ENDMETHOD.
ENDCLASS.

**********************************************************************

LOAD-OF-PROGRAM.

  demo=&gt;sel_screen_evt( `LOAD-OF-PROGRAM` ).

**********************************************************************

INITIALIZATION.

  demo=&gt;sel_screen_evt( `INITIALIZATION` ).
  intro = &#x27;Exploring list events&#x27;.
  note = &#x27;The Back/Exit/Cancel buttons raise the ON EXIT-COMMAND event.&#x27;.
  text = &#x27;Hello&#x27;.

**********************************************************************

START-OF-SELECTION.

  SET USER-COMMAND &#x27;START&#x27;.

  demo=&gt;sel_screen_evt( `START-OF-SELECTION` ).

  WRITE / &#x27;This is the basic list showing the content of a log table (mostly reporting and selection screen events). Double-click a line in this list to display more events in a details list.&#x27; COLOR 4.
  SKIP.
  WRITE / |Parameter value: &quot;{ text }&quot;| COLOR 2.


  IF reporting_and_selscr_log IS NOT INITIAL.
    demo=&gt;write_rep_sel_log( ).
  ENDIF.

**********************************************************************

AT LINE-SELECTION.

  demo=&gt;list_evt( evt = `AT LINE-SELECTION` ).

  SET USER-COMMAND &#x27;LINE&#x27;.

  IF sy-lsind &lt;= 1.
    WRITE / &#x27;This is a details list. It shows the content of another log table (mostly list events). Clicking in this list does nothing meaningful.&#x27; COLOR 4.
    WRITE / &#x27;You may want to go back to the basic list, double-click a line there to come back here, and see more log entries added for list events.&#x27; COLOR 4.
    WRITE / &#x27;When the counter reaches 40, the program is restarted :)&#x27; COLOR 4.
    SKIP.
    WRITE / `************** Some sy Components **************` COLOR 2.
    WRITE / |Content of clicked line (sy-lisel): &quot;{ sy-lisel }&quot;|.
    WRITE / |Number of clicked line (sy-lilli): &quot;{ sy-lilli }&quot;|.
    WRITE / |List level of the current list (sy-lsind): &quot;{ sy-lsind }&quot;|.
    SKIP.
    IF list_log IS NOT INITIAL.
      demo=&gt;write_list_log( ).
    ENDIF.
  ENDIF.

**********************************************************************

AT USER-COMMAND.

  CASE sy-ucomm.
    WHEN &#x27;START&#x27;.
      demo=&gt;list_evt( `AT USER-COMMAND (Function code: START)` ).
      MESSAGE  |AT USER-COMMAND event raised (Function code: START). Counter value: { counter }.| TYPE &#x27;I&#x27;.
    WHEN &#x27;LINE&#x27;.
      demo=&gt;list_evt( `AT USER-COMMAND (Function code: LINE)` ).
      MESSAGE  |AT USER-COMMAND event raised (Function code: LINE). Counter value: { counter }.| TYPE &#x27;I&#x27;.
      IF counter &gt;= 40.
        MESSAGE  |The counter has reached the threshold. The program will be started again using a SUBMIT statement.| TYPE &#x27;I&#x27;.
        SUBMIT (sy-repid) VIA SELECTION-SCREEN.
      ENDIF.
  ENDCASE.

**********************************************************************

TOP-OF-PAGE.

  tstmp = utclong_current( ).
  demo=&gt;counter( ).
  APPEND VALUE #( id = counter event_block  = |TOP-OF-PAGE| time = tstmp ) TO list_log.
  WRITE: / |********************* Title inserted at TOP-OF-PAGE event ({ counter }) *********************|.

**********************************************************************

TOP-OF-PAGE DURING LINE-SELECTION.

  tstmp = utclong_current( ).
  demo=&gt;counter( ).
  APPEND VALUE #( id = counter event_block  = |TOP-OF-PAGE DURING LINE-SELECTION| time = tstmp ) TO list_log.
  WRITE: / |********************* Title inserted at TOP-OF-PAGE DURING LINE-SELECTION event ({ counter }) *********************|.

**********************************************************************

END-OF-PAGE.

  tstmp = utclong_current( ).
  demo=&gt;counter( ).
  APPEND VALUE #( id = counter event_block  = |END-OF-PAGE| time = tstmp ) TO list_log.
  WRITE: / |********************* Footer inserted at END-OF-PAGE event ({ counter }) *********************|.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursion: SAP List Viewer (ALV)</h2>
<ul>
<li>Provides an object-oriented API for displaying and formatting lists</li>
<li>Allows you to specify layout settings and functionality</li>
<li>Classes such as <code>CL_SALV_TABLE</code> encapsulate the use of the ALV grid control and simplify the integration into ABAP programs. Note: The older class <code>CL_GUI_ALV_GRID</code> should no longer be used directly for new developments.</li>
<li>Other classes are available to handle hierarchical lists and tree structures. The focus of the code snippets is on simple, non-nested tables and the use of the <code>CL_SALV_TABLE</code> class.</li>
</ul>
<h3>Getting Started</h3>
<ul>
<li>To get started quickly, you only need to ...</li>
<li>instantiate an ALV table object using the <code>factory</code> method.</li>
<li>define the display type for the ALV output. By default, full-screen display is enabled.</li>
<li>display the ALV output using the <code>display</code> method.</li>
</ul>
<div class="note">[!NOTE] - When working with ALV, make sure that you implement appropriate error handling. - The `factory` method also has optional exporting parameters. You can use the optional `list_display` parameter to specify whether you want to display the ALV output as classic list. It is set to false by default. There are also exporting parameters to display the ALV output in containers (e.g. see the dynpro cheat sheet example). The exporting parameters are not relevant in this example. Here, the ALV output is displayed on the entire screen (however, it is also possible to display the ALV output in a dialog box).</div>
<p>The following code snippet shows a simple example to get you started quickly with the necessary method calls. It includes a demo internal table with content.</p>
<pre><code>TYPES: BEGIN OF demo_struct,
         col1 TYPE string,
         col2 TYPE i,
         col3 TYPE c LENGTH 1,
       END OF demo_struct.

DATA itab TYPE TABLE OF demo_struct WITH EMPTY KEY.
itab = VALUE #( ( col1 = `abc` col2 = 1 col3 = &#x27;X&#x27; )
                ( col1 = `def` col2 = 2 col3 = &#x27;Y&#x27; )
                ( col1 = `ghi` col2 = 3 col3 = &#x27;Z&#x27; ) ).

TRY.
    cl_salv_table=&gt;factory( IMPORTING r_salv_table = DATA(alv)
                            CHANGING  t_table      = itab ).

    alv-&gt;display( ).
  CATCH cx_salv_msg INTO DATA(err).
    MESSAGE err-&gt;get_text( ) TYPE &#x27;I&#x27; DISPLAY LIKE &#x27;E&#x27;.
ENDTRY.</code></pre>
<h3>Optional Layout Settings and Functionality</h3>
<ul>
<li>The following commented code snippet covers a selection of the many setting and functionality options.</li>
<li>For simplicity of the snippet, the code only uses the root exception class. Make sure that you implement appropriate error handling and exception classes.</li>
<li>You can copy and paste the code into your own test program to explore the ALV output and the effect of the method calls.</li>
</ul>
<div class="note">[!NOTE] Check the comments for the custom functions. If there are errors in your test program, replace the relevant code section and enable the generic ALV functions.</div>
<pre><code>TYPES: BEGIN OF demo_struct,
         col1 TYPE string,
         col2 TYPE i,
         col3 TYPE string,
         col4 TYPE icon_d,
         col5 TYPE string,
       END OF demo_struct.
DATA itab TYPE TABLE OF demo_struct WITH EMPTY KEY.
DATA alv TYPE REF TO cl_salv_table.
DATA cnt4evt TYPE i VALUE 6.

&quot;Local class to handle events
CLASS lcl_events DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS single_click
      FOR EVENT link_click OF cl_salv_events_table
      IMPORTING row column.
    CLASS-METHODS double_click
      FOR EVENT double_click OF cl_salv_events_table
      IMPORTING row column.
    CLASS-METHODS func_click
      FOR EVENT added_function OF cl_salv_events
      IMPORTING e_salv_function.
ENDCLASS.
CLASS lcl_events IMPLEMENTATION.
  METHOD single_click.
    &quot;Both single and double click events trigger messages.
    READ TABLE itab INDEX row REFERENCE INTO DATA(sc_ref).
    IF sy-subrc = 0.
      ASSIGN sc_ref-&gt;(column) TO FIELD-SYMBOL(&lt;fs_sc&gt;).
      MESSAGE `Single click event. ` &amp;&amp;
      |Row: { row } { COND #( WHEN column IS NOT INITIAL THEN `Column: ` &amp;&amp; column ) } | &amp;&amp;
      |{ COND #( WHEN &lt;fs_sc&gt; IS ASSIGNED THEN `Value: ` &amp;&amp; &lt;fs_sc&gt; ) }| TYPE &#x27;I&#x27;.
    ELSE.
      MESSAGE `Single click event` TYPE &#x27;I&#x27;.
    ENDIF.
  ENDMETHOD.
  METHOD double_click.
    READ TABLE itab INDEX row REFERENCE INTO DATA(dc_ref).
    IF sy-subrc = 0.
      ASSIGN dc_ref-&gt;(column) TO FIELD-SYMBOL(&lt;fs_dc&gt;).
      MESSAGE `Double click event. ` &amp;&amp;
      |Row: { row } { COND #( WHEN column IS NOT INITIAL THEN `Column: ` &amp;&amp; column ) } | &amp;&amp;
      |{ COND #( WHEN &lt;fs_dc&gt; IS ASSIGNED THEN `Value: ` &amp;&amp; &lt;fs_dc&gt; ) }| TYPE &#x27;I&#x27;.
    ELSE.
      MESSAGE `Double click event` TYPE &#x27;I&#x27;.
    ENDIF.
  ENDMETHOD.
  METHOD func_click.
    &quot;Handling custom functions
    CASE e_salv_function.
      WHEN &#x27;DATA&#x27;.
        MESSAGE `Custom function DATA. Do something ...` TYPE &#x27;I&#x27;.
      WHEN &#x27;TEST&#x27;.
        IF cnt4evt = 6.
          MESSAGE `A table row will be added, demonstrating the &#x27;refresh&#x27; ` &amp;&amp;
          `method that rebuilds the output table.` TYPE &#x27;I&#x27;.
        ENDIF.
        cnt4evt += 1.
        itab = VALUE #( BASE itab ( col1 = `fffff` col2 = cnt4evt
                                    col3 = `ttttt` col4 = icon_green_light ) ).
        &quot;Rebuilding the output table
        alv-&gt;refresh( ).
      WHEN &#x27;QUIT&#x27;.
        MESSAGE `Custom function QUIT. Do something ...` TYPE &#x27;I&#x27;.
      WHEN OTHERS.
        MESSAGE `Some other function` TYPE &#x27;I&#x27;.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
&quot;Populating internal table that is output
itab = VALUE #( ( col1 = `aaaaa` col2 = 1 col3 = `zzzzz` col4 = icon_green_light )
                ( col1 = `bbbbb` col2 = 2 col3 = &#x27;yyyyy&#x27; col4 = icon_green_light )
                ( col1 = `ccccc` col2 = 3 col3 = &#x27;xxxxx&#x27; col4 = icon_green_light ) ).
TRY.
    &quot;----------- Instantiating an ALV table object -----------
    cl_salv_table=&gt;factory( IMPORTING r_salv_table = alv
                            CHANGING  t_table      = itab ).
    &quot;----------- Creating a new instance with new data -----------
    &quot;Notes on the set_data method:
    &quot;- All objects that were referred to previously are removed.
    &quot;- Not to be used in event handlers.
    &quot;In the example, new lines are added to the existing internal table.
    itab = VALUE #( BASE itab ( col1 = `ddddd` col2 = 4
                                col3 = `wwwww` col4 = icon_green_light )
                              ( col1 = `ddddd` col2 = 5
                                col3 = `vvvvv` col4 = icon_green_light )
                              ( col1 = `eeeee` col2 = 6
                                col3 = `uuuuu` col4 = icon_green_light ) ).
    alv-&gt;set_data( CHANGING t_table = itab ).
    &quot;----------- Layout-related settings -----------
    &quot;Changing the list header title
    CAST cl_salv_display_settings(
      alv-&gt;get_display_settings( ) )-&gt;set_list_header( &#x27;Demo Title&#x27; ).
    &quot;There are several methods to retrieve column-specific information.
    &quot;The following examples show a selection. You can check the variable
    &quot;content in the debugger.
    DATA(col1obj) = alv-&gt;get_columns( )-&gt;get_column( &#x27;COL1&#x27; ).
    DATA(col1alignment) = col1obj-&gt;get_alignment( ).
    DATA(col1type) = col1obj-&gt;get_ddic_inttype( ).
    DATA(col1colname) = col1obj-&gt;get_columnname( ).
    DATA(is_col1_visible) = col1obj-&gt;is_visible( ).
    &quot;Settings for the column header
    &quot;This example uses the get method, which returns all column objects of
    &quot;the output table. The table is processed in a loop.
    &quot;Settings covered:
    &quot;- Specifying the column header titles (long, medium, and short
    &quot;  column header)
    &quot;- Specifying a tooltip for the column header
    LOOP AT alv-&gt;get_columns( )-&gt;get( ) REFERENCE INTO DATA(colref).
      &quot;Specifying the column header titles
      &quot;The example sets the texts based on the columname value.
      &quot;Note: The column width is optimized further down. You may want to
      &quot;manually adjust the width in the ALV output to see the column header
      &quot;name change.
      colref-&gt;r_column-&gt;set_long_text( |{ colref-&gt;columnname }| ).
      colref-&gt;r_column-&gt;set_medium_text( |{ colref-&gt;columnname+2(3) }| ).
      colref-&gt;r_column-&gt;set_short_text( |{ colref-&gt;columnname+3(3) }| ).
      &quot;Specifying a tooltip for the column headers
      colref-&gt;r_column-&gt;set_tooltip(
        |Demo tooltip { colref-&gt;columnname+3(3) }| ).
    ENDLOOP.
    &quot;Displaying/Hiding the column headers
    &quot;In the example, the value is set to abap_true. Set it to abap_false
    &quot;to hide the column header.
    &quot;Note: In this and the following examples, the settings are
    &quot;intentionally done using separate casts for each example. You can also
    &quot;create object reference variables, such as for &#x27;col1obj&#x27; to avoid having
    &quot;to specify the casts explicitly each time.
    CAST cl_salv_columns_list(
      alv-&gt;get_columns( ) )-&gt;set_headers_visible( abap_true ).
    &quot;Setting key columns
    &quot;Note: The key columns have a default color setting. There, you can only
    &quot;change the color in individual cells.
    CAST cl_salv_column_table(
        alv-&gt;get_columns( )-&gt;get_column( &#x27;COL1&#x27; ) )-&gt;set_key( abap_true ).

    &quot;Setting column color
    &quot;Note: The executable example includes examples of coloring entire rows and
    &quot;specific cells.
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL2&#x27; )
      )-&gt;set_color( VALUE lvc_s_colo( col = col_positive ) ).

    &quot;Hiding columns
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL5&#x27; )
      )-&gt;set_visible( abap_false ).

    &quot;Setting text alignment
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL2&#x27; )
      )-&gt;set_alignment( if_salv_c_alignment=&gt;left ).

    &quot;Optimizing column width, i.e. automatically adjusting the column width
    &quot;to display values completely (can also be done for individual columns)
    alv-&gt;get_columns( )-&gt;set_optimize( abap_true ).

    &quot;Setting output width explicitly (for an individual column)
    &quot;Note: Just to demonstrate the method call. This setting has no effect in
    &quot;the example due to the column width optimization above, and the column is
    &quot;hidden anyway.
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL5&#x27; )
      )-&gt;set_output_length( &#x27;100&#x27; ).

    &quot;Setting table rows to a striped pattern
    alv-&gt;get_display_settings( )-&gt;set_striped_pattern( abap_true ).

    &quot;Displaying/hiding horizontal and vertical grid lines (here, they&#x27;re hidden)
    alv-&gt;get_display_settings( )-&gt;set_horizontal_lines( abap_false ).
    alv-&gt;get_display_settings( )-&gt;set_vertical_lines( abap_false ).

    &quot;With the following method call, the ALV output is displayed in a dialog box.
    &quot;Demo coordinates are provided. You can comment it in to see the effect.
*      alv-&gt;set_screen_popup( start_column = 10
*                             end_column   = 100
*                             start_line   = 4
*                             end_line     = 15 ).

    &quot;------ Adding functionality/user interaction options to the ALV output ------
    &quot;Specifying the selection type
    &quot;The following specification allows you to select multiple rows/columns (a column
    &quot;is added on the left for selections). Check the other options in the
    &quot;if_salv_c_selection_mode interface.
    alv-&gt;get_selections( )-&gt;set_selection_mode( if_salv_c_selection_mode=&gt;row_column ).

    &quot;Setting the sorting
    &quot;In the example, a particular column is sorted in descending order.
    alv-&gt;get_sorts( )-&gt;add_sort( &#x27;COL1&#x27; )-&gt;set_sequence( if_salv_c_sort=&gt;sort_down ).

    &quot;Applying a filter
    &quot;The example is implemented to exclude one entry from the demo table.
    alv-&gt;get_filters( )-&gt;add_filter( &#x27;COL2&#x27; )-&gt;add_selopt( sign   = &#x27;E&#x27;
                                                            option = &#x27;EQ&#x27;
                                                            low    = &#x27;6&#x27; ).

    &quot;Making a calculation/aggregation
    &quot;In this example, an aggregation is added for a specific column.
    &quot;In this case, the average value is calculated. It is displayed in a row added
    &quot;at the bottom of the table.
    alv-&gt;get_aggregations( )-&gt;add_aggregation(
      columnname  = &#x27;COL2&#x27;
      aggregation = if_salv_c_aggregation=&gt;average ).

    &quot;Generic and custom ALV functions
    &quot;Note:
    &quot;- By default, ALV functions (such as sorting or filtering) are not available
    &quot;  to users. You must explicitly enable them.
    &quot;- Depending on whether the display is full-screen (as it is here) or in a
    &quot;  container, restrictions apply. In the first case, you can use your own GUI status
    &quot;  to integrate custom functions.

    &quot;This (self-contained, ready-to-use) code snippet is intended so that you can simply
    &quot;copy &amp; paste the code into a test program. For this purpose, and to be able to
    &quot;demonstrate custom functions in this full-screen ALV example using a GUI status, the
    &quot;example uses a demo GUI status that is included in another sample program (in a
    &quot;subpackage of SALV). For your own GUI status, you can check the SALV_TABLE_STANDARD
    &quot;status contained in the SALV_METADATA_STATUS function group, which you can copy and
    &quot;use as a template, for example.
    &quot;*********************************** NOTE ***********************************
    &quot;- The GUI status used here is just reused to have a copyable and self-contained example.
    &quot;- The GUI status of the sample program (specified for the report parameter below)
    &quot;  contains generic and additional functions. For the additional functions, a simple
    &quot;  implementation is included in this snippet. The GUI status should include the 
    &quot;  functions TEST, DATA, QUIT.
    &quot;- The implementations in the event handler class here do not match the implementations
    &quot;  there, the button texts do not make sense for the implementations here, and so on.
    &quot;- So even though it (the button names, icons, implementations, etc.) does not make much
    &quot;  sense for this snippet, you should get an idea about custom functions and be able to
    &quot;  explore the event handling by clicking on the buttons.
    &quot;- The TEST function demonstrates adding new rows to the table and the &#x27;refresh&#x27; method.
    &quot;- If this particular program, the GUI status and/or the custom functions are not available
    &quot;  in your system, or if you insert the code into your own test program, run it, and
    &quot;  encounter problems setting the status, remove the TRY control structure that contains
    &quot;  the set_screen_status method call, and comment in the code below to use the standard
    &quot;  ALV functions. In this case, the custom functions cannot be checked out with this
    &quot;  snippet.
    &quot;- Check out the executable example of the cheat sheet that includes a GUI status.
    TRY.
        alv-&gt;set_screen_status(
          pfstatus      = &#x27;SALV_TABLE_STANDARD&#x27;
          report        = &#x27;SALV_TEST_REFRESH&#x27;
          set_functions = alv-&gt;c_functions_all ).

        &quot;In this GUI status, the custom functions TEST, DATA, and QUIT are specified. You can 
        &quot;check the results of the following method calls in the debugger.
        DATA(getfunc) = alv-&gt;get_functions( ).
        &quot;Checking the activation status
        DATA(is_enabled) = getfunc-&gt;is_enabled( &#x27;TEST&#x27; ).
        &quot;Checking the visibility
        DATA(is_visible) = getfunc-&gt;is_visible( &#x27;TEST&#x27; ).

        &quot;Registering an event handler for the custom functions
        &quot;The added_function event is raised. In the implementation of the event handler method,
        &quot;you can then implement your code based on the value of the e_salv_function parameter
        &quot;(which contains the specified function name), for example, using a CASE statement.
        &quot;The implementation in this code snippet is different from the original program.
        SET HANDLER lcl_events=&gt;func_click FOR alv-&gt;get_event( ).

      CATCH cx_salv_object_not_found.
        MESSAGE `GUI status error. Instead, use the set_default method, for example.`
        TYPE &#x27;I&#x27;.
    ENDTRY.

    &quot;If you remove the TRY control structure above, you can comment in the following
    &quot;code to use the generic ALV functions. Use the set_all method for all generic
    &quot;functions. You can also enable generic ALV functions individually. Check the
    &quot;set_* methods.
    &quot;alv-&gt;get_functions( )-&gt;set_default( abap_true ).

    &quot;Hotspot/single click functionality
    &quot;You can define the content of a cell as a clickable area. You can do this by
    &quot;specifying the cell type hotspot. When a user clicks the cell content,
    &quot;the link_click event is raised.
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL1&#x27; )
      )-&gt;set_cell_type( if_salv_c_cell_type=&gt;hotspot ).

    &quot;You can set icon columns and make the icon cells clickable areas. To do this,
    &quot;use the set_icon method. As above, the cell type of the column is set to hotspot.
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL4&#x27; )
      )-&gt;set_icon( if_salv_c_bool_sap=&gt;true ).
    CAST cl_salv_column_table( alv-&gt;get_columns( )-&gt;get_column( &#x27;COL4&#x27; )
      )-&gt;set_cell_type( if_salv_c_cell_type=&gt;hotspot ).

    &quot;An event handler is registered for the clickable areas (the link_click event
    &quot;is raised). The get_event method returns the required event object. In the
    &quot;example, a message is displayed.
    SET HANDLER lcl_events=&gt;single_click FOR alv-&gt;get_event( ).

    &quot;Double click functionality
    &quot;Registering an event handler for the double click event. In the example, a message
    &quot;is displayed. Double-click cells in columns for which the cell type is not set to hotspot.
    SET HANDLER lcl_events=&gt;double_click FOR alv-&gt;get_event( ).

    &quot;Adding tooltips to the icon column cells
    alv-&gt;get_functional_settings( )-&gt;get_tooltips( )-&gt;add_tooltip(
      type    = cl_salv_tooltip=&gt;c_type_icon
      value   = CONV lvc_value( icon_green_light )
      tooltip = `This is a tooltip for an icon` ).

    &quot;----------- Displaying the ALV output -----------
    alv-&gt;display( ).
  CATCH cx_root INTO DATA(error).
    &quot;For simplicity, this example uses the root exception class.
    &quot;Always make sure that you use appropriate exception classes. Check the F2
    &quot;information for the methods in ADT.
    MESSAGE error-&gt;get_text( ) TYPE &#x27;I&#x27; DISPLAY LIKE &#x27;E&#x27;.
ENDTRY.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>More Information</h2>
<ul>
<li>ABAP Keyword Documentation:</li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenselection_screen.htm">Selection Screens</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_dynpro_list.htm">Classic Lists</a></li>
<li>ALV:</li>
<li><a href="https://help.sap.com/docs/SAP_NETWEAVER_731_BW_ABAP/b1c834a22d05483b8a75710743b5ff26/4ec38f8788d22b90e10000000a42189d.html?locale=en-US">SAP List Viewer (ALV)</a> on the SAP Help Portal</li>
<li>Find more demonstration examples in package <code>SALV</code> and its subpackages.</li>
</ul>
<h2>Executable Examples</h2>
<p>After the import of the repository, proceed as follows:</p>
<ul>
<li>Find the program in ADT using the search by choosing <code>CTRL + SHIFT + A</code>.</li>
<li>Enter <code>ZDEMO_ABAP_SELSCR_LISTS_INTRO</code> and open the program. This program serves as an entry point for all selection screen and list examples. Program names of the individual examples:</li>
<li><code>ZDEMO_ABAP_SELSCR_PARAMETERS</code>: Demonstrates <code>PARAMETERS</code> statements</li>
<li><code>ZDEMO_ABAP_SELSCR_SELECT_OPT</code>: Demonstrates <code>SELECT-OPTIONS</code> statements</li>
<li><code>ZDEMO_ABAP_SELSCR_STANDALONE</code>: Demonstrates the creation (<code>SELECTION-SCREEN</code> statements) and calling of standalone selection screens</li>
<li><code>ZDEMO_ABAP_SELSCR_STMTS_VAR</code>: Demonstrates variants of the <code>SELECTION-SCREEN</code> statements that do not create selection screens</li>
<li><code>ZDEMO_ABAP_LISTS</code>: Demonstrates various ABAP statements to create and handle classic lists</li>
<li><code>ZDEMO_ABAP_EVENT_BLOCKS</code>: Demonstrates event blocks</li>
<li><code>ZDEMO_ABAP_ALV</code>: Demonstrates the SAP List Viewer (ALV)</li>
<li>Run the program by choosing <code>F8</code>.</li>
</ul>
<div class="note">[!NOTE] - The examples in the *main* branch of the ABAP cheat sheet repository are designed to be imported into the SAP BTP ABAP Environment. For Standard ABAP, you can find examples (such as `ZDEMO_ABAP_SELSCR_LISTS_INTRO`) in the other branches of the repository. - The executable examples ... - do not claim to include meaningful selection screens and lists. - are not intended to be role models for proper selection screen and list design. - are not intended to solve concrete programming tasks. You should always work out your own solution for each individual case. - are only intended to demonstrate a selection of keywords and visualize related syntax in action on a high level. - include comments in the program code. - The steps to import and run the code are outlined [here](README.md#-getting-started-with-the-examples). - [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
</body>
</html>