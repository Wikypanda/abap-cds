<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>21 XML JSON - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>Working with XML and JSON in ABAP</h1>
<ul>
<li><a href="#working-with-xml-and-json-in-abap">Working with XML and JSON in ABAP</a></li>
<li><a href="#introduction">Introduction</a></li>
<li><a href="#working-with-xml">Working with XML</a></li>
<li><a href="#processing-xml-using-class-libraries">Processing XML Using Class Libraries</a></li>
<li><a href="#ixml">iXML</a></li>
<li><a href="#sxml">sXML</a></li>
<li><a href="#xml-transformations">XML Transformations</a></li>
<li><a href="#call-transformation-syntax">CALL TRANSFORMATION Syntax</a></li>
<li><a href="#exploring-call-transformation-syntax-using-the-predefined-identity-transformation-id">Exploring CALL TRANSFORMATION Syntax Using the Predefined Identity Transformation ID</a></li>
<li><a href="#working-with-json">Working with JSON</a></li>
<li><a href="#creating-and-reading-json-data-using-sxml">Creating and Reading JSON Data Using sXML</a></li>
<li><a href="#transforming-json-data-using-transformations">Transforming JSON Data Using Transformations</a></li>
<li><a href="#handling-json-data-with-abap-classes">Handling JSON Data with ABAP Classes</a></li>
<li><a href="#excursions">Excursions</a></li>
<li><a href="#serializing-and-deserializing-objects">Serializing and Deserializing Objects</a></li>
<li><a href="#converting-string---xstring">Converting string \&lt;-\&gt; xstring</a></li>
<li><a href="#compressing-and-decompressing-binary-data">Compressing and Decompressing Binary Data</a></li>
<li><a href="#exporting-and-importing-data-clusters">Exporting and Importing Data Clusters</a></li>
<li><a href="#more-information">More Information</a></li>
<li><a href="#executable-example">Executable Example</a></li>
</ul>
<h2>Introduction</h2>
<p>This cheat sheet provides a high-level overview of working with XML and JSON in ABAP. It covers the following topics:</p>
<ul>
<li>Processing XML using class libraries (iXML, sXML)</li>
<li>XML Transformations using XSLT and Simple Transformations (ST), i.e. serializations (ABAP to XML) and deserializations (XML to ABAP)</li>
<li><code>CALL TRANSFORMATION</code> syntax</li>
<li>Working with JSON data</li>
</ul>
<div class="note">[!NOTE] - The cheat sheet snippets and the executable example cover simple cases. Find more executable examples of the ABAP Keyword Documentation following the links in the [More Information](#more-information) section. - For more detailed information, such as documentation on ST syntax, what asXML is, etc., also see the links in the [More Information](#more-information) section.</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Working with XML</h2>
<h3>Processing XML Using Class Libraries</h3>
<h4>iXML</h4>
<ul>
<li><em>Integrated</em> XML Library</li>
<li>Provides methods for ...</li>
<li>validating and parsing (i.e. reading) XML data in 1.0 format into a DOM representation (Document Object Model, which is a tree-like representation of the documents in the memory)</li>
<li>processing the DOM representation (you can create or edit XML data)</li>
<li>rendering (i.e. writing) the DOM representation</li>
<li>To access XML data, you need input and output streams that are created using iXML methods.</li>
<li>Advantages: Easy access to the individual parts of an XML document possible, DTDs (Document Type Definitions) are possible</li>
<li>Disadvantages: High memory consumption of the DOM (if the complete DOM is created)</li>
</ul>
<p>The following code snippets demonstrate a selection of iXML methods for handling XML data. Note that the snippets use classes available for <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_for_cloud_dev_glosry.htm">ABAP for Cloud Development</a>. Find documentation on the classes for <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclassic_abap_glosry.htm">classic ABAP</a> <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_ixml_lib.htm">here (F1 documentation for Standard ABAP)</a>.</p>
<p>Creating XML data using iXML:</p>
<pre><code>&quot;Creating one factory object of the access class cl_ixml_core using the
&quot;create method. It is used to access the iXML library.
DATA(ixml_cr)     = cl_ixml_core=&gt;create( ).

&quot;Creating an XML document stored in DOM format in the memory
DATA(document_cr) = ixml_cr-&gt;create_document( ).

&quot;Creating DOM nodes step by step. Here, elements, attributes, and content are
&quot;and inserted into the XML. Using an appropriate attribute setting, the result
&quot;represents XML data in the asXML format (see more information further down).

&quot;Creating a root node
DATA(root) = document_cr-&gt;create_element_ns( name = &#x27;abap&#x27;
                                              prefix = &#x27;asx&#x27; ).
root-&gt;set_attribute_ns( name =  &#x27;asx&#x27;
                        prefix = &#x27;xmlns&#x27;
                        value = &#x27;http://www.sap.com/abapxml&#x27; ).
root-&gt;set_attribute_ns( name =  &#x27;version&#x27;
                        value = &#x27;1.0&#x27; ).
document_cr-&gt;append_child( root ).

DATA(xml_node1) = document_cr-&gt;create_element_ns( prefix = &#x27;asx&#x27;
                                                  name   = &#x27;values&#x27; ).
root-&gt;append_child( xml_node1 ).
DATA(xml_node2) = document_cr-&gt;create_element_ns( name = &#x27;STRING&#x27; ).
xml_node1-&gt;append_child( xml_node2 ).
xml_node2-&gt;append_child( document_cr-&gt;create_text( &#x27;Hello World&#x27; ) ).

&quot;Creating a renderer for rendering the XML document into an output stream
&quot;that is used to pass XML data from the renderer.
DATA xml_doc TYPE xstring.
ixml_cr-&gt;create_renderer( document = document_cr
                          ostream  = ixml_cr-&gt;create_stream_factory( )-&gt;create_ostream_xstring( string = xml_doc )
                        )-&gt;render( ).

&quot;Result (xstring converted to string):
&quot;&lt;?xml version=&quot;1.0&quot;?&gt;&lt;asx:abap xmlns:asx=&quot;http://www.sap.com/abapxml&quot; version=&quot;1.0&quot;&gt;
&quot;&lt;asx:values&gt;&lt;STRING&gt;Hello World&lt;/STRING&gt;&lt;/asx:values&gt;&lt;/asx:abap&gt;</code></pre>
<p>Parsing XML data using iXML:</p>
<pre><code>&quot;Creating demo XML data to be used in the example as string. Using the cl_abap_conv_codepage
&quot;class, you can convert the string to xstring which is required for the example.
DATA(some_xml) = cl_abap_conv_codepage=&gt;create_out( )-&gt;convert(
      `&lt;?xml version=&quot;1.0&quot;?&gt;` &amp;&amp;
      `&lt;node attr_a=&quot;123&quot;&gt;` &amp;&amp;
      ` &lt;subnode1&gt;` &amp;&amp;
      `  &lt;hallo&gt;hi&lt;/hallo&gt;` &amp;&amp;
      ` &lt;/subnode1&gt;` &amp;&amp;
      ` &lt;subnode2&gt;` &amp;&amp;
      `  &lt;letter&gt;a&lt;/letter&gt;` &amp;&amp;
      `  &lt;date format=&quot;mm-dd-yyyy&quot;&gt;01-01-2025&lt;/date&gt;` &amp;&amp;
      ` &lt;/subnode2&gt;` &amp;&amp;
      ` &lt;subnode3&gt;`  &amp;&amp;
      `  &lt;text attr_b=&quot;1&quot; attr_c=&quot;a&quot;&gt;abc&lt;/text&gt;` &amp;&amp;
      `  &lt;text attr_b=&quot;2&quot; attr_c=&quot;b&quot;&gt;def&lt;/text&gt;` &amp;&amp;
      `  &lt;text attr_b=&quot;3&quot; attr_c=&quot;c&quot;&gt;ghi&lt;/text&gt;` &amp;&amp;
      `  &lt;text attr_b=&quot;4&quot; attr_c=&quot;d&quot;&gt;jkl&lt;/text&gt;` &amp;&amp;
      ` &lt;/subnode3&gt;` &amp;&amp;
      `&lt;/node&gt;` ).

&quot;Creating one factory object of the access class cl_ixml_core using the
&quot;create method. It is used to access the iXML library.
DATA(ixml_pa) = cl_ixml_core=&gt;create( ).
&quot;Creaing an input stream that is used for the input of XML data
DATA(stream_factory_pa) = ixml_pa-&gt;create_stream_factory( ).
&quot;Creating an XML document stored in DOM format in the memory
DATA(document_pa) = ixml_pa-&gt;create_document( ).
&quot;Creating a parser. It requires the following input parameters: input stream to be parsed,
&quot;the XML document to which the stream is parsed, a factory required to create a stream
DATA(parser_pa) = ixml_pa-&gt;create_parser(
                    istream = stream_factory_pa-&gt;create_istream_xstring( string = some_xml )
                    document = document_pa
                    stream_factory = stream_factory_pa ).

&quot;Parsing XML data to a DOM representation in one go. It is put in the memory.
&quot;Note: You can also parse sequentially, and not in one go.
DATA(parsing_check) = parser_pa-&gt;parse( ).
IF parsing_check = 0. &quot;Parsing was successful

  &quot;--------------------- Directly reading nodes ----------------------

  &quot;Accessing the root element of the DOM. It can be used as the initial node
  &quot;for accessing subnodes.
  &quot;Note: Multiple methods are available to further process the nodes.
  DATA(root_element) = document_pa-&gt;get_root_element( ).
  &quot;First subnode
  DATA(child_element) = root_element-&gt;get_first_child( ).
  &quot;Gettng the value of that node
  DATA(child_element_value) = child_element-&gt;get_value( ).
  &quot;Next adjacent node/getting the value
  DATA(next_element) = child_element-&gt;get_next( ).
  DATA(next_element_value) = next_element-&gt;get_value( ).
  &quot;Direct access using element names
  &quot;Result: First element searched for
  DATA(element_by_name) = document_pa-&gt;find_from_name( name = `letter` )-&gt;get_value( ).

  &quot;--------------------- Reading using iterators ----------------------

  &quot;i.e. going over the XML nodes one after another

  &quot;Creating an iterator
  DATA(iterator_pa) = document_pa-&gt;create_iterator( ).


  DATA xml_nodes TYPE string_table.

  DATA(node_pa) = iterator_pa-&gt;get_next( ).
  &quot;For the iteration, you can use the get_next method to process the nodes one after another.
  &quot;Note: Here, all nodes are respected. You can also create filters to go over specific nodes.
  WHILE NOT node_pa IS INITIAL.
    &quot;DATA(indent) = node_pa-&gt;get_height( ) * 2.
    &quot;Rettrieving the node type
    DATA(node_type) = node_pa-&gt;get_type( ).
    CASE node_type.
      WHEN if_ixml_node=&gt;co_node_element.
        APPEND |Element: &quot;{ node_pa-&gt;get_name( ) }&quot;| TO xml_nodes.
        &quot;Retrieving attributes
        DATA(attributes_pa) = node_pa-&gt;get_attributes( ).
        IF NOT attributes_pa IS INITIAL.
          DO attributes_pa-&gt;get_length( ) TIMES.
            DATA(attr) = attributes_pa-&gt;get_item( sy-index - 1 ).
            APPEND |Attribute: &quot;{ attr-&gt;get_name( ) } = { attr-&gt;get_value( ) }&quot;| TO xml_nodes.
          ENDDO.
        ENDIF.
      WHEN if_ixml_node=&gt;co_node_text OR if_ixml_node=&gt;co_node_cdata_section.
        APPEND |Text: &quot;{ node_pa-&gt;get_value( ) }&quot;| TO xml_nodes.

        &quot;When iterating the XML nodes, you may want to perform certain actions, e.g.
        &quot;modifying values etc. Check out the many methods that are available.
        &quot;This example just transform the textual content to uppercase using the set_value method.
        DATA(val) = to_upper( node_pa-&gt;get_value( ) ).
        node_pa-&gt;set_value( val ).
      WHEN OTHERS.
        ...
    ENDCASE.
    &quot;Retrieving the next node
    node_pa = iterator_pa-&gt;get_next( ).
  ENDWHILE.

  &quot;Creating a renderer. It renders the XML document into an output stream
  &quot;that is used to pass XML data from the renderer.
  DATA xml_pa TYPE xstring.
  ixml_pa-&gt;create_renderer( document = document_pa
                            ostream = ixml_pa-&gt;create_stream_factory( )-&gt;create_ostream_xstring( string = xml_pa )
                          )-&gt;render( ).

ELSE.
  &quot;Parsing was not successful.
  ...
ENDIF.

DATA(conv_string) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml_pa ).

*--- xml_nodes ---
*Element: &quot;node&quot;
*Attribute: &quot;attr_a = 123&quot;
*Element: &quot;subnode1&quot;
*Element: &quot;hallo&quot;
*Text: &quot;hi&quot;
*Element: &quot;subnode2&quot;
*Element: &quot;letter&quot;
*Text: &quot;a&quot;
*Element: &quot;date&quot;
*Attribute: &quot;format = mm-dd-yyyy&quot;
*Text: &quot;01-01-2025&quot;
*Element: &quot;subnode3&quot;
*Element: &quot;text&quot;
*Attribute: &quot;attr_b = 1&quot;
*Attribute: &quot;attr_c = a&quot;
*Text: &quot;abc&quot;
*Element: &quot;text&quot;
*Attribute: &quot;attr_b = 2&quot;
*Attribute: &quot;attr_c = b&quot;
*Text: &quot;def&quot;
*Element: &quot;text&quot;
*Attribute: &quot;attr_b = 3&quot;
*Attribute: &quot;attr_c = c&quot;
*Text: &quot;ghi&quot;
*Element: &quot;text&quot;
*Attribute: &quot;attr_b = 4&quot;
*Attribute: &quot;attr_c = d&quot;
*Text: &quot;jkl&quot;

*--- conv_string ---
*&lt;?xml version=&quot;1.0&quot; encoding=&quot;utf-8&quot;?&gt;&lt;node attr_a=&quot;123&quot;&gt;&lt;subnode1&gt;&lt;hallo&gt;HI&lt;/hallo&gt;&lt;/subnode1&gt;&lt;subnode2&gt;&lt;letter&gt;A&lt;/letter&gt;
*&lt;date format=&quot;mm-dd-yyyy&quot;&gt;01-01-2025&lt;/date&gt;&lt;/subnode2&gt;&lt;subnode3&gt;&lt;text attr_b=&quot;1&quot; attr_c=&quot;a&quot;&gt;ABC&lt;/text&gt;
*&lt;text attr_b=&quot;2&quot; attr_c=&quot;b&quot;&gt;DEF&lt;/text&gt;&lt;text attr_b=&quot;3&quot; attr_c=&quot;c&quot;&gt;GHI&lt;/text&gt;&lt;text attr_b=&quot;4&quot; attr_c=&quot;d&quot;&gt;JKL&lt;/text&gt;&lt;/subnode3&gt;&lt;/node&gt;</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>sXML</h4>
<ul>
<li><em>Serial</em> XML Library</li>
<li>Provides XML readers and writers for different sources and targets</li>
<li>Processes XML data sequentially</li>
<li>Allows you to parse and render XML data in a token-based (iterating across all nodes, i.e. tokens, in the tree structure of the XML data) and object-oriented way (wrapping methods of the token-based access; providing an object-oriented way to access XML nodes)</li>
<li>In contrast to iXML, ...</li>
<li>more formats are possible (standard XML 1.0, but also XOP, binary XML, and JSON).</li>
<li>no document is created in DOM format (if you do not need a DOM representation and DTDs, sXML is a more performant alternative to iXML).</li>
</ul>
<p>Creating XML data using sXML:</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Token-based rendering
*&amp;---------------------------------------------------------------------*

&quot;For sXML, there are specialized writer classes, such as CL_SXML_STRING_WRITE.
&quot;Writers created with this class render XML data to a byte string.
&quot;The XML 1.0 format and UTF-8 are used by default in the create method.
&quot;Here, the parameters are specified explicitly.
&quot;Note: The interface IF_SXML_WRITER contains the components that are valid
&quot;for all readers (the abstract super class CL_SXML_WRITER includes this
&quot;interface as well as implementations for all readers). In the example below,
&quot;a cast is required so as to access special methods (such as open_element).

DATA(writer) = CAST if_sxml_writer( cl_sxml_string_writer=&gt;create( type     = if_sxml=&gt;co_xt_xml10
                                                                   encoding = &#x27;UTF-8&#x27; ) ).

&quot;Creating/Rendering XML data using token-based rendering
&quot;The example shows shows how XML data can be created using various available methods.
&quot;Here, token-based rendering is used, i.e. each node is added to the XML data using 
&quot;a method. There is a method for each node type.
TRY.
    &quot;Creating nodes (the order of the nodes is crucial)
    writer-&gt;open_element( name = &#x27;flights&#x27; ).
    writer-&gt;open_element( name = &#x27;flight&#x27; ).
    writer-&gt;open_element( name = &#x27;carrier&#x27; ).
    writer-&gt;write_value( &#x27;LH&#x27; ).
    writer-&gt;close_element( ).
    writer-&gt;open_element( name = &#x27;flightnumber&#x27; ).
    writer-&gt;write_value( &#x27;400&#x27; ).
    writer-&gt;close_element( ).
    writer-&gt;close_element( ).
    writer-&gt;open_element( name = &#x27;flight&#x27; ).
    writer-&gt;open_element( name = &#x27;carrier&#x27; ).
    writer-&gt;write_value( &#x27;DL&#x27; ).
    writer-&gt;close_element( ).
    writer-&gt;open_element( name = &#x27;flightnumber&#x27; ).
    writer-&gt;write_value( &#x27;1984&#x27; ).
    writer-&gt;close_element( ).
    writer-&gt;close_element( ).
    writer-&gt;close_element( ).
  CATCH cx_sxml_state_error.
    ...
ENDTRY.

&quot;The XML data can be retrieved with the GET_OUTPUT method.
&quot;Also here, a cast is required.
&quot;The result is of type xstring.
DATA(xml) = CAST cl_sxml_string_writer( writer )-&gt;get_output(  ).

&quot;The result looks like this (when converted to string and indented):
&quot;&lt;?xml version=&quot;1.0&quot; encoding=&quot;utf-8&quot;?&gt;
&quot;&lt;flights&gt;
&quot;  &lt;flight&gt;
&quot;    &lt;carrier&gt;LH&lt;/carrier&gt;
&quot;    &lt;flightnumber&gt;400&lt;/flightnumber&gt;
&quot;  &lt;/flight&gt;
&quot;  &lt;flight&gt;
&quot;    &lt;carrier&gt;DL&lt;/carrier&gt;
&quot;    &lt;flightnumber&gt;1984&lt;/flightnumber&gt;
&quot;  &lt;/flight&gt;
&quot;&lt;/flights&gt;

*&amp;---------------------------------------------------------------------*
*&amp; Object-oriented rendering
*&amp;---------------------------------------------------------------------*

DATA(writer_oo) = CAST if_sxml_writer( cl_sxml_string_writer=&gt;create( type     = if_sxml=&gt;co_xt_xml10
                                                                      encoding = &#x27;UTF-8&#x27; ) ).

TRY.
    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;flights&#x27; ) ).

    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;flight&#x27; ) ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;carrier&#x27; ) ).
    DATA(val) = writer_oo-&gt;new_value( ).
    val-&gt;set_value( &#x27;AZ&#x27; ).
    writer_oo-&gt;write_node( val ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;flightnumber&#x27; ) ).
    val = writer_oo-&gt;new_value( ).
    val-&gt;set_value( &#x27;788&#x27; ).
    writer_oo-&gt;write_node( val ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).

    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;flight&#x27; ) ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;carrier&#x27; ) ).
    val = writer_oo-&gt;new_value( ).
    val-&gt;set_value( &#x27;JL&#x27; ).
    writer_oo-&gt;write_node( val ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_open_element( name = &#x27;flightnumber&#x27; ) ).
    val = writer_oo-&gt;new_value( ).
    val-&gt;set_value( &#x27;407&#x27; ).
    writer_oo-&gt;write_node( val ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).
    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).

    writer_oo-&gt;write_node( writer_oo-&gt;new_close_element( ) ).
  CATCH cx_sxml_state_error.
    ...
ENDTRY.

DATA(xml_oo) =  CAST cl_sxml_string_writer( writer_oo )-&gt;get_output( ).

&quot;The result looks like this (when converted to string and indented):
&quot;&lt;flights&gt;
&quot; &lt;flight&gt;
&quot;  &lt;carrier&gt;AZ&lt;/carrier&gt;
&quot;  &lt;flightnumber&gt;788&lt;/flightnumber&gt;
&quot; &lt;/flight&gt;
&quot; &lt;flight&gt;
&quot;  &lt;carrier&gt;JL&lt;/carrier&gt;
&quot;  &lt;flightnumber&gt;407&lt;/flightnumber&gt;
&quot; &lt;/flight&gt;
&quot;&lt;/flights&gt;</code></pre>
<p>Parsing XML data using sXML:</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Token-based parsing
*&amp;---------------------------------------------------------------------*

&quot;Creating demo XML data to be used in the example
TRY.
    DATA(xml_to_parse) = cl_abap_conv_codepage=&gt;create_out( )-&gt;convert(
      `&lt;?xml version=&quot;1.0&quot;?&gt;` &amp;&amp;
      `&lt;node attr_a=&quot;123&quot;&gt;` &amp;&amp;
      ` &lt;subnode1&gt;` &amp;&amp;
      `  &lt;hallo&gt;hi&lt;/hallo&gt;` &amp;&amp;
      ` &lt;/subnode1&gt;` &amp;&amp;
      ` &lt;subnode2&gt;` &amp;&amp;
      `  &lt;letter&gt;a&lt;/letter&gt;` &amp;&amp;
      `  &lt;date format=&quot;mm-dd-yyyy&quot;&gt;01-01-2025&lt;/date&gt;` &amp;&amp;
      ` &lt;/subnode2&gt;` &amp;&amp;
      ` &lt;subnode3&gt;`  &amp;&amp;
      `  &lt;text attr_b=&quot;1&quot; attr_c=&quot;a&quot;&gt;abc&lt;/text&gt;` &amp;&amp;
      `  &lt;text attr_b=&quot;2&quot; attr_c=&quot;b&quot;&gt;def&lt;/text&gt;` &amp;&amp;
      `  &lt;text attr_b=&quot;3&quot; attr_c=&quot;c&quot;&gt;ghi&lt;/text&gt;` &amp;&amp;
      `  &lt;text attr_b=&quot;4&quot; attr_c=&quot;d&quot;&gt;jkl&lt;/text&gt;` &amp;&amp;
      ` &lt;/subnode3&gt;` &amp;&amp;
      `&lt;/node&gt;` ).
  CATCH cx_sy_conversion_codepage.
ENDTRY.

&quot;Creating an internal table for demonstration purposes
DATA: BEGIN OF node_info,
        node_type TYPE string,
        name      TYPE string,
        value     TYPE string,
      END OF node_info,
      nodes_tab LIKE TABLE OF node_info.

&quot;Creating reader
&quot;Note: See the comments for the writer above which is similar. For readers,
&quot;the interface IF_SXML_READER exists. In this example, no special methods
&quot;are used. Therefore, a cast is not carried out.
DATA(reader) = cl_sxml_string_reader=&gt;create( xml_to_parse ).
&quot;DATA(reader_cast) = CAST if_sxml_reader( cl_sxml_string_reader=&gt;create( xml_oo ) ).

&quot;To iterate accros all nodes, you can call the NEXT_NODE method.
TRY.
    DO.
      CLEAR node_info.
      reader-&gt;next_node( ).

      &quot;When reaching the end of the XML data, the loop is exited.
      IF reader-&gt;node_type = if_sxml_node=&gt;co_nt_final.
        EXIT.
      ENDIF.

      &quot;You can access the properties of the node directly.
      &quot;For display purposes, the property information is stored in an internal table.
      &quot;The example here just uses simple demo JSON data. Not all properties are
      &quot;retrieved and displayed.

      &quot;Node type, see the interface if_sxml_node
      node_info-node_type = SWITCH #( reader-&gt;node_type WHEN if_sxml_node=&gt;co_nt_initial THEN `CO_NT_INITIAL`
                                                        WHEN if_sxml_node=&gt;co_nt_element_open THEN `CO_NT_ELEMENT_OPEN`
                                                        WHEN if_sxml_node=&gt;co_nt_element_close THEN `CO_NT_ELEMENT_CLOSE`
                                                        WHEN if_sxml_node=&gt;co_nt_value THEN `CO_NT_VALUE`
                                                        WHEN if_sxml_node=&gt;co_nt_attribute THEN `CO_NT_ATTRIBUTE`
                                                        ELSE `Error` ).
      &quot;Name of the element
      node_info-name = reader-&gt;name.
      &quot;Character-like value (if it is textual data)
      node_info-value = COND #( WHEN reader-&gt;node_type = if_sxml_node=&gt;co_nt_value THEN reader-&gt;value ).
      APPEND node_info TO nodes_tab.

      &quot;Once the method is called, you can directly access the attributes of the reader with the required
      &quot;properties of the node. When the parser is on the node of an element opening, you can use the method
      &quot;NEXT_ATTRIBUTE to iterate across the XML element attributes.
      IF reader-&gt;node_type = if_sxml_node=&gt;co_nt_element_open.
        DO.
          reader-&gt;next_attribute( ).
          IF reader-&gt;node_type &lt;&gt; if_sxml_node=&gt;co_nt_attribute.
            EXIT.
          ENDIF.
          APPEND VALUE #( node_type  = `CO_NT_ATTRIBUTE`
                          name       = reader-&gt;name
                          value      = reader-&gt;value ) TO nodes_tab.
        ENDDO.
      ENDIF.
    ENDDO.
  CATCH cx_sxml_state_error INTO DATA(error_parse_token).
    DATA(error_text) = error_parse_token-&gt;get_text( ).
ENDTRY.

*--- nodes_tab ---
*NODE_TYPE              NAME        VALUE     
*CO_NT_ELEMENT_OPEN     node                  
*CO_NT_ATTRIBUTE        attr_a      123       
*CO_NT_ELEMENT_OPEN     subnode1              
*CO_NT_ELEMENT_OPEN     hallo                 
*CO_NT_VALUE            hallo       hi        
*CO_NT_ELEMENT_CLOSE    hallo                 
*CO_NT_ELEMENT_CLOSE    subnode1              
*CO_NT_ELEMENT_OPEN     subnode2              
*CO_NT_ELEMENT_OPEN     letter                
*CO_NT_VALUE            letter      a         
*CO_NT_ELEMENT_CLOSE    letter                
*CO_NT_ELEMENT_OPEN     date                  
*CO_NT_ATTRIBUTE        format      mm-dd-yyyy
*CO_NT_VALUE            date        01-01-2025
*CO_NT_ELEMENT_CLOSE    date                  
*CO_NT_ELEMENT_CLOSE    subnode2              
*CO_NT_ELEMENT_OPEN     subnode3              
*CO_NT_ELEMENT_OPEN     text                  
*CO_NT_ATTRIBUTE        attr_b      1         
*CO_NT_ATTRIBUTE        attr_c      a         
*CO_NT_VALUE            text        abc       
*CO_NT_ELEMENT_CLOSE    text                  
*CO_NT_ELEMENT_OPEN     text                  
*CO_NT_ATTRIBUTE        attr_b      2         
*CO_NT_ATTRIBUTE        attr_c      b         
*CO_NT_VALUE            text        def       
*CO_NT_ELEMENT_CLOSE    text                  
*CO_NT_ELEMENT_OPEN     text                  
*CO_NT_ATTRIBUTE        attr_b      3         
*CO_NT_ATTRIBUTE        attr_c      c         
*CO_NT_VALUE            text        ghi       
*CO_NT_ELEMENT_CLOSE    text                  
*CO_NT_ELEMENT_OPEN     text                  
*CO_NT_ATTRIBUTE        attr_b      4         
*CO_NT_ATTRIBUTE        attr_c      d         
*CO_NT_VALUE            text        jkl       
*CO_NT_ELEMENT_CLOSE    text                  
*CO_NT_ELEMENT_CLOSE    subnode3              
*CO_NT_ELEMENT_CLOSE    node                

*&amp;---------------------------------------------------------------------*
*&amp; Object-oriented parsing
*&amp;---------------------------------------------------------------------*

&quot;The example uses the XML from above
CLEAR nodes_tab.
DATA(reader_oo) = cl_sxml_string_reader=&gt;create( xml_to_parse ).

TRY.
    DO.
      &quot;To iterate accros all nodes, you can call the READ_NEXT_NODE method.
      &quot;When the end of the XML data is reached, the returned value is initial.
      DATA(node_oo) = reader_oo-&gt;read_next_node( ).
      IF node_oo IS INITIAL.
        EXIT.
      ENDIF.

      &quot;In object-oriented parsing, methods for token-based parsing are wrapped.
      &quot;An object-oriented access to the node is provided.
      &quot;References to objects that represent the current node are returned.

      &quot;Getting the node type
      DATA(n_type) = node_oo-&gt;type.

      &quot;If the parser is currently on the node of an element opening,
      &quot;the node object has the class CL_SXML_OPEN_ELEMENT that implements the
      &quot;interface IF_SXML_OPEN_ELEMENT. With the methods included, you can
      &quot;access the XML attributes of the element, e.g. using the GET_ATTRIBUTES
      &quot;method to put the references for all attributes into an internal table.
      &quot;To access the attributes, a downcast is required.

      CASE n_type.
        WHEN if_sxml_node=&gt;co_nt_element_open.
          DATA(open_element) = CAST if_sxml_open_element( node_oo ).

          APPEND VALUE #( node_type = `open element`
                          name      = open_element-&gt;qname-name
                        ) TO nodes_tab.

          DATA(attributes) = open_element-&gt;get_attributes( ).

          LOOP AT attributes INTO DATA(attribute).
            APPEND VALUE #( node_type = `attribute`
                            name      = attribute-&gt;qname-name
                            value  = SWITCH #( attribute-&gt;value_type WHEN if_sxml_value=&gt;co_vt_text THEN attribute-&gt;get_value( ) )
                          ) TO nodes_tab.
          ENDLOOP.

        WHEN  if_sxml_node=&gt;co_nt_element_close.
          DATA(close_element) = CAST if_sxml_close_element( node_oo ).

          APPEND VALUE #( node_type = `close element`
                          name      = close_element-&gt;qname-name
                        ) TO nodes_tab.

        WHEN  if_sxml_node=&gt;co_nt_value.
          DATA(value_node_oo) = CAST if_sxml_value_node( node_oo ).

          APPEND VALUE #( node_type = `value`
                          value     = SWITCH #( value_node_oo-&gt;value_type WHEN if_sxml_value=&gt;co_vt_text THEN value_node_oo-&gt;get_value( ) )
                        ) TO nodes_tab.

        WHEN OTHERS.
          APPEND VALUE #( node_type = `Error` ) TO nodes_tab.
      ENDCASE.
    ENDDO.
  CATCH cx_sxml_state_error INTO DATA(error_parse_oo).
    DATA(error_parse_oo_text) = error_parse_oo-&gt;get_text( ).
ENDTRY.

*--- nodes_tab ---
*NODE_TYPE        NAME        VALUE     
*open element     node                  
*attribute        attr_a      123       
*open element     subnode1              
*open element     hallo                 
*value                        hi        
*close element    hallo                 
*close element    subnode1              
*open element     subnode2              
*open element     letter                
*value                        a         
*close element    letter                
*open element     date                  
*attribute        format      mm-dd-yyyy
*value                        01-01-2025
*close element    date                  
*close element    subnode2              
*open element     subnode3              
*open element     text                  
*attribute        attr_b      1         
*attribute        attr_c      a         
*value                        abc       
*close element    text                  
*open element     text                  
*attribute        attr_b      2         
*attribute        attr_c      b         
*value                        def       
*close element    text                  
*open element     text                  
*attribute        attr_b      3         
*attribute        attr_c      c         
*value                        ghi       
*close element    text                  
*open element     text                  
*attribute        attr_b      4         
*attribute        attr_c      d         
*value                        jkl       
*close element    text                  
*close element    subnode3              
*close element    node              </code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>XML Transformations</h3>
<p>To perform transformations in ABAP, you can use:</p>
<ul>
<li>XSL Transformations (XSLT)</li>
<li>Repository objects written in XSLT</li>
<li>XSLT can process ABAP data because ABAP data is first implicitly transformed into the asXML format. The result of this implicit transformation (the asXML format) is then used as the actual source for the XSL transformation.</li>
<li>Identity transformations</li>
<li>Predefined XSL transformation with the name <code>ID</code></li>
<li>Used to read and write the asXML (and also asJSON) format.</li>
<li>When the transformation ID is called, the resulting intermediate formats (asXML, asJSON) are the direct output.</li>
<li>When used, for example, to transform ABAP data, such as a structure, to XML, the transformation does not change the structure itself. You can, however, change the structure by implementing your own XSLT.</li>
<li>Simple Transformation (ST)</li>
<li>Repository objects written in an SAP-specific programming language for transformations between XML formats and ABAP data</li>
</ul>
<p>The tranformations are called using <code>CALL TRANSFORMATION</code> statements.</p>
<p>Possible transformations, some of which are covered in the example:</p>
<table>
<thead><tr>
<th>Transformation</th>
<th>XSLT</th>
<th>ST</th>
</tr></thead>
<tbody>
<tr>
<td>XML &lt;-&gt; XML</td>
<td>X</td>
<td>-</td>
</tr>
<tr>
<td>ABAP &lt;-&gt; XML</td>
<td>X</td>
<td>X</td>
</tr>
<tr>
<td>ABAP &lt;-&gt; ABAP</td>
<td>X</td>
<td>-</td>
</tr>
</tbody>
</table>
<div class="note">[!NOTE] - asXML: - *ABAP Serialization XML* - Describes a format of XML data created when serializing ABAP data (ABAP -> XML) with the identity transformation - This format is a prerequisite for deserializations (XML -> ABAP) using identity transformations. - Used as an intermediate format that defines a mapping between ABAP data and XML - Therefore, if you want to deserialize XML data in ABAP, you must first transform it to the asXML format. - Make sure that you use appropriate exception classes for potential errors in transformations. See the section [Catchable Exceptions](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcall_transformation.htm) in the `CALL TRANSFORMATION` topic. - For serializing instances of classes, find more information [here](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenasxml_class_instances.htm) in the ABAP Keyword Documentation. The classes must implement the `IF_SERIALIZABLE_OBJECT` interface (find a demo in the executable example and further down).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>CALL TRANSFORMATION Syntax</h3>
<p>The following code snippets demonstrate a selection of possible syntax options when using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcall_transformation.htm"><code>CALL TRANSFORMATION</code></a> statements.</p>
<div class="note">[!NOTE] You can also transform ABAP to and from JSON data using transformations. Find examples in the [Transforming JSON Data Using Transformations](#transforming-json-data-using-transformations) section.</div>
<p><strong>Specifying transformations</strong></p>
<pre><code>&quot;Specifying the name of an XSL or Simple Transformation statically
CALL TRANSFORMATION zsome_transformation SOURCE ...
                                         RESULT ...

&quot;Specifying the identity transformation with the predefined name ID
CALL TRANSFORMATION id SOURCE ...
                       RESULT ...

&quot;Dynamic specification of the transformation
&quot;If the transformation does not exist, an exception is raised.
TRY.
  CALL TRANSFORMATION (&#x27;ID&#x27;) SOURCE ...
                             RESULT ...
CATCH cx_invalid_transformation.
  ...
ENDTRY.</code></pre>
<p><strong>Specifying the source of the transformation</strong></p>
<p>There are multiple options. Check the executable example to explore them.</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Transforming XML data
*&amp;---------------------------------------------------------------------*

&quot;Options for src: 
&quot;- Data object of type string or xstring containing XML data in XML 1.0 format
&quot;- Standard table with flat character-like or byte-like line type
&quot;- Some references to iXML and sXML libraries are possible. For example, an 
&quot;  interface reference variable of type if_sxml_reader.
CALL TRANSFORMATION ... SOURCE XML src
                        RESULT ...

*&amp;---------------------------------------------------------------------*
*&amp; Transforming ABAP data
*&amp;---------------------------------------------------------------------*

&quot;No XML specified after SOURCE
&quot;Options after SOURCE: 
&quot;- One or multiple ABAP data objects (abap1 in the snippet) can be specified as 
&quot;  static parameter list, e.g. z1 = abap1 z2 = abap2 ...
&quot;- When XSLT is used (such as ID), the data is serialized to asXML depending on 
&quot;  the target
&quot;- z1 etc. stands for names of XML elements
CALL TRANSFORMATION ... SOURCE z1 = abap1 ...
                        RESULT ...

&quot;Dynamic specification of ABAP data objects in an internal table of type 
&quot;abap_trans_srcbind_tab
DATA(srctab) = VALUE abap_trans_srcbind_tab( ( name = &#x27;A&#x27; value = REF #( b ) ) ( ... ) ).
CALL TRANSFORMATION ... SOURCE (srctab)
                        RESULT ...</code></pre>
<p><strong>Specifying the result of the transformation</strong></p>
<p>There are multiple options. Check the executable example to explore them in action.</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Transforming to XML data
*&amp;---------------------------------------------------------------------*

&quot;Options for res: 
&quot;- Data object of type string or xstring
&quot;- Data object declared inline (e.g. DATA(a) or FINAL(b)), which has then the type xstring
&quot;- Standard table with flat character-like or byte-like line type
&quot;- References for objects in iXML and sXML (e.g. sXML writers)
CALL TRANSFORMATION ... SOURCE ...
                        RESULT XML res.

*&amp;---------------------------------------------------------------------*
*&amp; Transforming to ABAP data
*&amp;---------------------------------------------------------------------*

&quot;No XML specified after RESULT
&quot;Similar to above, multiple ABAP data objects can be specified as a static parameter list.
CALL TRANSFORMATION ... SOURCE ...
                        RESULT y1 = abap1 ...

&quot;An internal table of type abap_trans_resbind_tab can be specified.
DATA(restab) = VALUE abap_trans_resbind_tab( ( name = &#x27;A&#x27; value = REF #( b ) ) ( ... ) ).       
CALL TRANSFORMATION ... SOURCE ...
                        RESULT (restab).</code></pre>
<div class="note">[!NOTE] More additions are available such as `PARAMETERS` (for parameter binding) and `OPTIONS` (for predefined transformation options). See the details in the [ABAP Keyword Documentation](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcall_transformation.htm).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Exploring CALL TRANSFORMATION Syntax Using the Predefined Identity Transformation ID</h4>
<p>The following example explores various syntax options for <code>CALL TRANSFORMATION</code> statements, using the predefined identity transformation <code>ID</code>.</p>
<p>To try the example out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    TYPES: BEGIN OF line,
             char TYPE c LENGTH 3,
             int  TYPE i,
           END OF line,
           t_type TYPE TABLE OF line WITH EMPTY KEY.
    DATA(tab) = VALUE t_type( ( char = &#x27;aaa&#x27; int = 1 )
                              ( char = &#x27;bbb&#x27; int = 2 )
                              ( char = &#x27;ccc&#x27; int = 3 ) ).

    out-&gt;write( `ABAP &lt;-&gt; XML using XSLT (Using the Predefined Identity Transformation ID)` ).

**********************************************************************

    out-&gt;write( `-------- Transforming ABAP data: ... SOURCE ... --------` ).
    &quot;This section covers syntax options after SOURCE without XML.

    &quot;ABAP (specifying ABAP data object) -&gt; asXML/xstring
    &quot;itab stands for the name of the XML element (check ... &lt;ITAB&gt; ... in the output)
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML DATA(xml_tab_a).

    DATA(xml_a) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml_tab_a ).
    out-&gt;write( data = xml_a name = `xml_a` ).

    &quot;ABAP (specifying ABAP data objects dynamically using an internal table
    &quot;of type abap_trans_srcbind_tab) -&gt; asXML/xstring

    DATA(srctab) = VALUE abap_trans_srcbind_tab( ( name = &#x27;ITAB&#x27; value = REF #( tab ) ) ).
    CALL TRANSFORMATION id SOURCE (srctab)
                           RESULT XML DATA(xml_tab_b).

    DATA(xml_b) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml_tab_b ).
    out-&gt;write( data = xml_b name = `xml_b` ).

**********************************************************************

    out-&gt;write( `-------- Transforming XML data: ... SOURCE XML ... --------` ).
    &quot;This section covers syntax options after SOURCE XML.
    &quot;Apart from data objects of type string or xstring containing XML data
    &quot;in XML 1.0 format, standard tables with flat character-like or byte-like
    &quot;line type, you can also specify certain references to iXML and sXML libraries

    &quot;sxml/ixml references as source
    &quot;The examples use XML data from above
    DATA(sxml_reader) = cl_sxml_string_reader=&gt;create( xml_tab_a ).

    CALL TRANSFORMATION id SOURCE XML sxml_reader
                           RESULT XML DATA(xml_tab_c).

    DATA(xml_c) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml_tab_c ).
    out-&gt;write( data = xml_c name = `xml_c` ).

    DATA(ixmlr) = cl_ixml_core=&gt;create( ).
    DATA(ixml_doc1) = ixmlr-&gt;create_document( ).
    DATA(ixml_cr_streamr) = ixmlr-&gt;create_stream_factory( ).
    DATA(ixml_i_stream) = ixml_cr_streamr-&gt;create_istream_xstring( xml_tab_a ).

    CALL TRANSFORMATION id SOURCE XML ixml_i_stream
                           RESULT XML DATA(xml_tab_d).

    DATA(xml_d) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml_tab_d ).
    out-&gt;write( data = xml_d name = `xml_d` ).

**********************************************************************

    out-&gt;write( `-------- Transforming to XML data: ... RESULT XML ... --------` ).
    &quot;This section covers syntax options after RESULT XML.
    &quot;Note: The following examples always use ABAP data objects as source
    &quot;(... SOURCE ...).

    &quot;ABAP -&gt; asXML/xstring
    &quot;This syntax option has already been used above.
    &quot;When declared inline, the target data object has the type xstring.
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML DATA(xml_tab_e).

    &quot;Specifying a data object of type xstring
    DATA xml_tab_f TYPE xstring.
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML xml_tab_f.

    &quot;ABAP -&gt; asXML/string
    DATA str TYPE string.
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML str.

    &quot;ABAP -&gt; asXML/table with a flat character-like line type
    TYPES c50 TYPE c LENGTH 50.
    TYPES c50_tab_type TYPE TABLE OF c50 WITH EMPTY KEY.
    DATA chartab TYPE c50_tab_type.

    &quot;The following statement inlcudes the OPTIONS addition.
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML chartab
                           OPTIONS xml_header = &#x27;NO&#x27;.

    out-&gt;write( data = chartab name = `chartab` ).

    &quot;ABAP -&gt; asXML/table with a flat byte-like line type
    TYPES x50 TYPE x LENGTH 50.
    TYPES x50_tab_type TYPE TABLE OF x50 WITH EMPTY KEY.
    DATA xtab TYPE x50_tab_type.
    &quot;The following statement inlcudes the OPTIONS addition
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML xtab
                           OPTIONS xml_header = &#x27;NO&#x27;.

    out-&gt;write( data = xtab name = `xtab` ).

    &quot;iXML/sXML references
    &quot;Using an iXML output stream
    DATA xstrg TYPE xstring.
    DATA(ixml) = cl_ixml_core=&gt;create( ).
    DATA(ixml_doc) = ixml-&gt;create_document( ).
    DATA(ixml_cr_stream) = ixml-&gt;create_stream_factory( ).
    DATA(ixml_o_stream) = ixml_cr_stream-&gt;create_ostream_xstring( string =  xstrg ).

    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML ixml_o_stream.

    ixml-&gt;create_renderer( document = ixml_doc
                           ostream  = ixml_o_stream )-&gt;render( ).

    DATA(xml_from_ixml) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xstrg ).
    out-&gt;write( data = xml_from_ixml name = `xml_from_ixml` ).

    &quot;Using an sXML writer
    DATA(sxml) = CAST if_sxml_writer( cl_sxml_string_writer=&gt;create( type     = if_sxml=&gt;co_xt_xml10
                                                                     encoding = &#x27;UTF-8&#x27; ) ).
    CALL TRANSFORMATION id SOURCE itab = tab
                           RESULT XML sxml.

    DATA(xml_from_sxml) = CAST cl_sxml_string_writer( sxml )-&gt;get_output( ).
    DATA(conv_xml_from_sxml) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xml_from_sxml ).
    out-&gt;write( data = conv_xml_from_sxml name = `conv_xml_from_sxml` ).

**********************************************************************

    out-&gt;write( `-------- Transforming to ABAP data: ... RESULT ... --------` ).

    &quot;This section covers syntax options after RESULT without XML.
    &quot;XML from above is used as source.
    DATA tab_b LIKE tab.
    CALL TRANSFORMATION id SOURCE XML xml_tab_a
                           RESULT itab = tab_b.
    out-&gt;write( data = tab_b name = `tab_b` ).

    &quot;An internal table of type abap_trans_resbind_tab can be specified.
    DATA tab_c LIKE tab.
    DATA(restab) = VALUE abap_trans_resbind_tab( ( name = &#x27;ITAB&#x27; value = REF #( tab_c ) ) ).

    CALL TRANSFORMATION id SOURCE XML xml_tab_a
                           RESULT (restab).

    out-&gt;write( data = tab_c name = `tab_c` ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Working with JSON</h2>
<ul>
<li>You can ..</li>
<li>create/render and read/parse JSON data in ABAP using the readers and writers in the sXML library. See the processing of XML data in the sXML section above. Parsing and rendering JSON data works in a similar way. However, instead of using XML readers/writers, you use JSON readers/writers.</li>
<li>transform ABAP to and from JSON data using transformations. You can directly transform ABAP &lt;-&gt; JSON using identity transformation (<code>ID</code>). In this context, note the intermediate format asJSON (see the notes on asXML above).</li>
<li>create and handle JSON data using the <a href="https://help.sap.com/docs/btp/sap-business-technology-platform/xco-library?version=Cloud">XCO library</a> or <code>/ui2/cl_json</code>.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Creating and Reading JSON Data Using sXML</h3>
<div class="note">[!NOTE] - In ABAP, the sXML library processes JSON data using JSON-XML, an SAP-specific JSON data representation in XML format. This intermediate step is used for both reading and creating JSON data. Find more information [here](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_json_xml.htm). - The following examples provide basic implementations to give you an idea. You should always work out your own solutions. - Both token-based and object-oriented rendering/parsing methods are available. These examples use the token-based approach. - For additional information and examples, see the [ABAP and JSON](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_json.htm) section of the ABAP Keyword Documentation.</div>
<p>Expand the following collapsible section for example classes. To try them out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. This example is set up to display output in the console.</p>
<p><strong>Creating JSON Data using the sXML library</strong></p>
<p>&lt;details&gt; &lt;summary&gt;🟢 Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>The <code>create</code> method from the <code>cl_sxml_string_writer</code> class is used to create a JSON writer by setting the type to <code>if_sxml=&gt;co_xt_json</code>.</li>
<li>Note the comments about the cast in the XML section.</li>
<li>Several elements and attributes are created using token-based rendering methods.</li>
<li>Finally,  the created JSON data is displayed.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;In this example, the following JSON data should be created.
    &quot;[{&quot;carrier_id&quot;:&quot;LH&quot;,&quot;connection_id&quot;:&quot;400&quot;,&quot;city_from&quot;:&quot;Frankfurt&quot;,&quot;city_to&quot;:&quot;Berlin&quot;},
    &quot; {&quot;carrier_id&quot;:&quot;AZ&quot;,&quot;connection_id&quot;:&quot;790&quot;,&quot;city_from&quot;:&quot;Rome&quot;,&quot;city_to&quot;:&quot;Osaka&quot;}]

    &quot;Creating a JSON writer by specifying the type in the &#x27;create&#x27; method appropriately
    &quot;The example uses token-based rendering.
    DATA(json_writer) = CAST if_sxml_writer( cl_sxml_string_writer=&gt;create( type = if_sxml=&gt;co_xt_json ) ).
    TRY.
        json_writer-&gt;open_element( name = &#x27;array&#x27; ).
        json_writer-&gt;open_element( name = &#x27;object&#x27; ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;carrier_id&#x27; ).
        json_writer-&gt;write_value( value = &#x27;LH&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;connection_id&#x27; ).
        json_writer-&gt;write_value( value = &#x27;400&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;city_from&#x27; ).
        json_writer-&gt;write_value( value = &#x27;Frankfurt&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;city_to&#x27; ).
        json_writer-&gt;write_value( value = &#x27;Berlin&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;object&#x27; ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;carrier_id&#x27; ).
        json_writer-&gt;write_value( value = &#x27;AZ&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;connection_id&#x27; ).
        json_writer-&gt;write_value( value = &#x27;790&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;city_from&#x27; ).
        json_writer-&gt;write_value( value = &#x27;Rome&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;open_element( name = &#x27;str&#x27; ).
        json_writer-&gt;write_attribute( name = &#x27;name&#x27; value = &#x27;city_to&#x27; ).
        json_writer-&gt;write_value( value = &#x27;Osaka&#x27; ).
        json_writer-&gt;close_element( ).

        json_writer-&gt;close_element( ).
        json_writer-&gt;close_element( ).
      CATCH cx_sxml_state_error INTO DATA(error).
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    DATA(json) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( CAST cl_sxml_string_writer( json_writer )-&gt;get_output( ) ).
    out-&gt;write( json ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;br&gt;</p>
<p><strong>Reading JSON Data using the sXML library</strong></p>
<p>&lt;details&gt; &lt;summary&gt;🟢 Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>An internal table is created for display purposes. This table is populated when iterating over all nodes. It includes information such as the node&#x27;s value.</li>
<li>For more details on the <em>name</em> component and others, refer to the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_json_xml.htm">ABAP Keyword Documentation</a>.</li>
<li>The <code>create</code> method from the <code>cl_sxml_string_reader</code> class is used to create a JSON reader.</li>
<li>After iterating through all the nodes, the retrieved information that has been added to the internal table is displayed.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;Creating demo JSON data
    DATA(json) = cl_abap_conv_codepage=&gt;create_out( )-&gt;convert(
    `[` &amp;&amp;
    `    {` &amp;&amp;
    `        &quot;carrier_id&quot;: &quot;LH&quot;,` &amp;&amp;
    `        &quot;connection_id&quot;: &quot;400&quot;,` &amp;&amp;
    `        &quot;city_from&quot;: &quot;Frankfurt&quot;,` &amp;&amp;
    `        &quot;city_to&quot;: &quot;Berlin&quot;` &amp;&amp;
    `    },` &amp;&amp;
    `    {` &amp;&amp;
    `        &quot;carrier_id&quot;: &quot;DL&quot;,` &amp;&amp;
    `        &quot;connection_id&quot;: &quot;1984&quot;,` &amp;&amp;
    `        &quot;city_from&quot;: &quot;San Francisco&quot;,` &amp;&amp;
    `        &quot;city_to&quot;: &quot;New York&quot;` &amp;&amp;
    `    },` &amp;&amp;
    `    {` &amp;&amp;
    `        &quot;carrier_id&quot;: &quot;AZ&quot;,` &amp;&amp;
    `        &quot;connection_id&quot;: &quot;790&quot;,` &amp;&amp;
    `        &quot;city_from&quot;: &quot;Rome&quot;,` &amp;&amp;
    `        &quot;city_to&quot;: &quot;Osaka&quot;` &amp;&amp;
    `    }` &amp;&amp;
    `]` ).

    &quot;Creating an internal table for display purposes
    DATA: BEGIN OF node_info,
            node_type TYPE string,
            name      TYPE string,
            value     TYPE string,
          END OF node_info,
          nodes_tab LIKE TABLE OF node_info.

    &quot;Creating reader
    &quot;In this example, no special methods are used. Therefore, a cast is not carried out.
    DATA(reader) = cl_sxml_string_reader=&gt;create( json ).

    &quot;To iterate accros all nodes, you can call the NEXT_NODE method.
    TRY.
        DO.
          CLEAR node_info.
          reader-&gt;next_node( ).

          &quot;When reaching the end of the XML data, the loop is exited.
          IF reader-&gt;node_type = if_sxml_node=&gt;co_nt_final.
            EXIT.
          ENDIF.

          &quot;You can access the properties of the node directly.
          &quot;For display purposes, the property information is stored in an internal table.
          &quot;The example here just uses simple demo JSON data. Not all properties are
          &quot;retrieved and displayed.

          &quot;Node type, see the interface if_sxml_node
          node_info-node_type = SWITCH #( reader-&gt;node_type WHEN if_sxml_node=&gt;co_nt_initial THEN `CO_NT_INITIAL`
                                                            WHEN if_sxml_node=&gt;co_nt_element_open THEN `CO_NT_ELEMENT_OPEN`
                                                            WHEN if_sxml_node=&gt;co_nt_element_close THEN `CO_NT_ELEMENT_CLOSE`
                                                            WHEN if_sxml_node=&gt;co_nt_value THEN `CO_NT_VALUE`
                                                            WHEN if_sxml_node=&gt;co_nt_attribute THEN `CO_NT_ATTRIBUTE`
                                                            ELSE `Error` ).
          &quot;Name of the element
          node_info-name = reader-&gt;name.
          &quot;Character-like value (if it is textual data)
          node_info-value = COND #( WHEN reader-&gt;node_type = if_sxml_node=&gt;co_nt_value THEN reader-&gt;value ).
          APPEND node_info TO nodes_tab.

          &quot;Once the method is called, you can directly access the attributes of the reader with the required
          &quot;properties of the node. When the parser is on the node of an element opening, you can use the method
          &quot;NEXT_ATTRIBUTE to iterate across the JSON attributes.
          IF reader-&gt;node_type = if_sxml_node=&gt;co_nt_element_open.
            DO.
              reader-&gt;next_attribute( ).
              IF reader-&gt;node_type &lt;&gt; if_sxml_node=&gt;co_nt_attribute.
                EXIT.
              ENDIF.
              APPEND VALUE #( node_type  = `CO_NT_ATTRIBUTE`
                              name       = reader-&gt;name
                              value      = reader-&gt;value ) TO nodes_tab.
            ENDDO.
          ENDIF.
        ENDDO.
      CATCH cx_sxml_state_error INTO DATA(error_parse_token).
        out-&gt;write( error_parse_token-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( nodes_tab ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Transforming JSON Data Using Transformations</h3>
<ul>
<li><code>CALL TRANSFORMATION</code> statements support JSON transformation, allowing conversion between JSON and ABAP using these options:</li>
<li>JSON readers</li>
<li>Additions <code>SOURCE JSON</code> and <code>RESULT JSON</code></li>
<li>Here, JSON data is processed in JSON-XML format, mapping JSON data to XML. This format enables all transformation categories for JSON that are also available for XML. For more information, see <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENABAP_JSON_TRAFOS.html">here</a>.</li>
<li>Both custom transformations (using XSLT and ST) and predefined identity transformations (<code>ID</code>) are supported. For identity transformations, asJSON, the canonical JSON representation of ABAP data, is used. For more details, see <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENABAP_ASJSON.html">here</a>.</li>
</ul>
<p>The following code snippets show a selection of transformation options using the predefined identity transformation. Here, a JSON writer is specified as the target.</p>
<pre><code>&quot;ABAP (elementary type) -&gt; JSON
DATA str_a TYPE string VALUE `Hello`.
&quot;Creating a JSON writer (see the type specification; you can also specify other
&quot;types, e.g. if_sxml=&gt;co_xt_xml10 for an XML writer)
DATA(writer) = cl_sxml_string_writer=&gt;create( type = if_sxml=&gt;co_xt_json ).

CALL TRANSFORMATION id SOURCE hi = str_a
                       RESULT XML writer.

&quot;Getting the output and converting to string
DATA(json) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( writer-&gt;get_output( ) ).
&quot;json: {&quot;HI&quot;:&quot;Hello&quot;}

&quot;JSON -&gt; ABAP
DATA str_b TYPE string.
&quot;Note: CALL TRANSFORMATION handles JSON sources implicitly.
CALL TRANSFORMATION id SOURCE XML json
                       RESULT hi = str_b.
&quot;str_b: Hello

&quot;ABAP -&gt; (un)formatted JSON
&quot;In this example, an internal table is transformed to JSON.
TYPES: BEGIN OF demo_struc,
          comp1 TYPE i,
          comp2 TYPE string,
          comp3 TYPE abap_boolean,
        END OF demo_struc,
        tab_type TYPE TABLE OF demo_struc WITH EMPTY KEY.
DATA(it) = VALUE tab_type( ( comp1 = 1 comp2 = `abc` comp3 = abap_true )
                            ( comp1 = 2 comp2 = `def` comp3 = abap_false )
                            ( comp1 = 3 comp2 = `ghi` comp3 = abap_true ) ).

DATA(json_wr) = cl_sxml_string_writer=&gt;create( type = if_sxml=&gt;co_xt_json ).

CALL TRANSFORMATION id SOURCE itab = it
                       RESULT XML json_wr.

DATA(json_output_xstr) = json_wr-&gt;get_output( ).
DATA(json_unformatted) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( json_output_xstr ).

*{&quot;ITAB&quot;:[{&quot;COMP1&quot;:1,&quot;COMP2&quot;:&quot;abc&quot;,&quot;COMP3&quot;:&quot;X&quot;},{&quot;COMP1&quot;:2,&quot;COMP2&quot;:&quot;def&quot;,&quot;COMP3&quot;:&quot;&quot;},{&quot;COMP1&quot;:3,&quot;COMP2&quot;:&quot;ghi&quot;,&quot;COMP3&quot;:&quot;X&quot;}]}

&quot;A cast is included for the writer (if_sxml_writer) to access special methods.
&quot;See comments in the sXML section.
DATA(json_wr_formatting) = CAST if_sxml_writer( cl_sxml_string_writer=&gt;create( type = if_sxml=&gt;co_xt_json ) ).

&quot;With the following method calls, the result is formatted.
json_wr_formatting-&gt;set_option( option = if_sxml_writer=&gt;co_opt_linebreaks ).
json_wr_formatting-&gt;set_option( option = if_sxml_writer=&gt;co_opt_indent ).

CALL TRANSFORMATION id SOURCE itab = it
                       RESULT XML json_wr_formatting.

DATA(json_formatted) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( CAST cl_sxml_string_writer( json_wr_formatting )-&gt;get_output( ) ).

*{
* &quot;ITAB&quot;:
* [
*  {
*   &quot;COMP1&quot;:1,
*   &quot;COMP2&quot;:&quot;abc&quot;,
*   &quot;COMP3&quot;:&quot;X&quot;
*  },
*  {
*   &quot;COMP1&quot;:2,
*   &quot;COMP2&quot;:&quot;def&quot;,
*   &quot;COMP3&quot;:&quot;&quot;
*  },
*  {
*   &quot;COMP1&quot;:3,
*   &quot;COMP2&quot;:&quot;ghi&quot;,
*   &quot;COMP3&quot;:&quot;X&quot;
*  }
* ]
*}</code></pre>
<p>The following code snippets show the use of the <code>SOURCE JSON</code> and <code>RESULT JSON</code> additions in <code>CALL TRANSFORMATION</code> statements. The examples use the identity transformation (<code>ID</code>).</p>
<pre><code>&quot;ABAP (elementary data object) -&gt; JSON
DATA num TYPE i VALUE 123.

CALL TRANSFORMATION id
  SOURCE var = num
  RESULT JSON DATA(json).

DATA(res_json_elem) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( json ).

&quot;JSON -&gt; ABAP (elementary data object)
DATA int TYPE i.
CALL TRANSFORMATION id
  SOURCE JSON json
  RESULT var = int.

&quot;ABAP (structure) -&gt; JSON
DATA: BEGIN OF struct,
        num  TYPE i VALUE 987,
        text TYPE string VALUE `hello world`,
        date TYPE d VALUE &#x27;20260102&#x27;,
        time TYPE t VALUE &#x27;123456&#x27;,
      END OF struct.

CALL TRANSFORMATION id
  SOURCE structure = struct
  RESULT JSON json.

DATA(res_json_struct) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( json ).

&quot;JSON -&gt; ABAP (structure)
DATA struct2 LIKE struct.
CALL TRANSFORMATION id
  SOURCE JSON json
  RESULT structure = struct2.

&quot;ABAP (internal table) -&gt; JSON
DATA itab LIKE TABLE OF struct WITH EMPTY KEY.
APPEND struct TO itab.
APPEND struct2 TO itab.

CALL TRANSFORMATION id
  SOURCE table = itab
  RESULT JSON json.

DATA(res_json_tab) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( json ).

&quot;JSON -&gt; ABAP (internal table)
DATA itab2 LIKE itab.
CALL TRANSFORMATION id
  SOURCE JSON json
  RESULT table = itab2.

&quot;ABAP (data reference) -&gt; JSON
DATA dref TYPE REF TO string.
dref = NEW #( `ABAP` ).

CALL TRANSFORMATION id
  SOURCE ref = dref
  RESULT JSON json.

DATA(res_json_ref) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( json ).

&quot;JSON -&gt; ABAP (data reference)
DATA dref2 LIKE dref.
CALL TRANSFORMATION id
  SOURCE JSON json
  RESULT ref = dref2.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Handling JSON Data with ABAP Classes</h3>
<p><strong>Using the XCO library</strong></p>
<p>The following snippets demonstrate creating and handling JSON data using the XCO library.</p>
<pre><code>&quot;Creating and populating a demo structure and internal table
DATA: BEGIN OF carrier_struc,
        carrier_id    TYPE c length 3,
        connection_id TYPE n length 4,
        city_from TYPE c length 20,
        city_to TYPE c length 20,
      END OF carrier_struc.

DATA carriers_tab LIKE TABLE OF carrier_struc WITH EMPTY KEY.

carrier_struc = VALUE #( carrier_id = &#x27;AA&#x27; connection_id = &#x27;17&#x27; city_from = &#x27;New York&#x27; city_to = &#x27;San Francisco&#x27; ).
carriers_tab = VALUE #( ( carrier_id = &#x27;AZ&#x27; connection_id = &#x27;788&#x27; city_from = &#x27;Rome&#x27; city_to = &#x27;Tokyo&#x27; )
                        ( carrier_id = &#x27;JL&#x27; connection_id = &#x27;408&#x27; city_from = &#x27;Frankfurt&#x27; city_to = &#x27;Tokyo&#x27; ) ).

&quot;ABAP (structure) -&gt; JSON using XCO
DATA(struc2json_xco) = xco_cp_json=&gt;data-&gt;from_abap( carrier_struc )-&gt;to_string( ).
&quot;Result: {&quot;CARRIER_ID&quot;:&quot;AA&quot;,&quot;CONNECTION_ID&quot;:&quot;0017&quot;,&quot;CITY_FROM&quot;:&quot;New York&quot;,&quot;CITY_TO&quot;:&quot;San Francisco&quot;}

&quot;ABAP (internal table) -&gt; JSON using XCO
DATA(itab2json_xco) = xco_cp_json=&gt;data-&gt;from_abap( carriers_tab )-&gt;to_string( ).
&quot;Result: [{&quot;CARRIER_ID&quot;:&quot;AZ&quot;,&quot;CONNECTION_ID&quot;:&quot;0788&quot;,&quot;CITY_FROM&quot;:&quot;Rome&quot;,&quot;CITY_TO&quot;:&quot;Tokyo&quot;},
&quot;         {&quot;CARRIER_ID&quot;:&quot;JL&quot;,&quot;CONNECTION_ID&quot;:&quot;0408&quot;,&quot;CITY_FROM&quot;:&quot;Frankfurt&quot;,&quot;CITY_TO&quot;:&quot;Tokyo&quot;}]

&quot;JSON -&gt; ABAP (structure) using XCO
DATA json2struc_xco LIKE carrier_struc.
xco_cp_json=&gt;data-&gt;from_string( struc2json_xco )-&gt;write_to( REF #( json2struc_xco ) ).
&quot;Result:
&quot;CARRIER_ID    CONNECTION_ID    CITY_FROM    CITY_TO
&quot;AA            0017             New York     San Francisco

&quot;JSON -&gt; ABAP (internal table) using XCO
DATA json2itab_xco LIKE carriers_tab.
xco_cp_json=&gt;data-&gt;from_string( itab2json_xco )-&gt;write_to( REF #( json2itab_xco ) ).
&quot;Result:
&quot;CARRIER_ID    CONNECTION_ID    CITY_FROM    CITY_TO
&quot;AZ            0788             Rome         Tokyo
&quot;JL            0408             Frankfurt    Tokyo

&quot;Creating JSON using XCO
&quot;You can check out more methods that offer various options to build
&quot;the JSON by clicking CTRL + Space after &#x27;-&gt;&#x27; in ADT.
&quot;In the following example, JSON data similar to above is created.
&quot;First, a JSON data builder is created. Then, using different methods,
&quot;JSON data is created.
DATA(json_builder_xco) = xco_cp_json=&gt;data-&gt;builder( ).
json_builder_xco-&gt;begin_object(
  )-&gt;add_member( &#x27;CarrierId&#x27; )-&gt;add_string( &#x27;DL&#x27;
  )-&gt;add_member( &#x27;ConnectionId&#x27; )-&gt;add_string( &#x27;1984&#x27;
  )-&gt;add_member( &#x27;CityFrom&#x27; )-&gt;add_string( &#x27;San Francisco&#x27;
  )-&gt;add_member( &#x27;CityTo&#x27; )-&gt;add_string( &#x27;New York&#x27;
  )-&gt;end_object( ).

&quot;Getting JSON data
DATA(json_created_xco) = json_builder_xco-&gt;get_data( )-&gt;to_string( ).
&quot;Result: {&quot;CarrierId&quot;:&quot;DL&quot;,&quot;ConnectionId&quot;:&quot;1984&quot;,&quot;CityFrom&quot;:&quot;San Francisco&quot;,&quot;CityTo&quot;:&quot;New York&quot;}

&quot;Transforming the created JSON to ABAP (structure)
&quot;Note: The JSON was intentionally created without the underscores in the
&quot;name to demonstrate the &#x27;apply&#x27; method. The following example demonstrates
&quot;a transformation of camel case and underscore notation. As above, check out
&quot;more options by using CTRL + Space after &#x27;...transformation-&gt;&#x27;.
CLEAR json2struc_xco.
xco_cp_json=&gt;data-&gt;from_string( json_created_xco )-&gt;apply( VALUE #(
  ( xco_cp_json=&gt;transformation-&gt;pascal_case_to_underscore ) ) )-&gt;write_to( REF #( json2struc_xco ) ).
&quot;Result
&quot;CARRIER_ID    CONNECTION_ID    CITY_FROM        CITY_TO
&quot;DL            1984             San Francisco    New York</code></pre>
<p><strong>Using the /ui2/cl_json Class</strong></p>
<p>For more information, see the class documentation. Note that there are many additional and optional parameters available, some of which are explored in the example in the collapsible section below.</p>
<pre><code>DATA(some_table) = VALUE string_table( ( `aaa` ) ( `bbb` ) ( `ccc` ) ).

*&amp;---------------------------------------------------------------------*
*&amp; ABAP -&gt; JSON
*&amp;---------------------------------------------------------------------*

DATA(abap_to_json) = /ui2/cl_json=&gt;serialize( data = some_table ).

*&amp;---------------------------------------------------------------------*
*&amp; JSON -&gt; ABAP
*&amp;---------------------------------------------------------------------*

DATA json_to_abap_table TYPE string_table.
/ui2/cl_json=&gt;deserialize( EXPORTING json = abap_to_json
                           CHANGING data  = json_to_abap_table ).</code></pre>
<p>Expand the following collapsible section for example code. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. This example is set up to display output in the console.</p>
<p>&lt;details&gt; &lt;summary&gt;🟢 Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    TYPES: BEGIN OF demo_struc,
             carrier_id    TYPE c LENGTH 3,
             connection_id TYPE n LENGTH 4,
             city_from     TYPE c LENGTH 20,
             city_to       TYPE c LENGTH 20,
           END OF demo_struc.
    DATA itab TYPE TABLE OF demo_struc WITH EMPTY KEY.
    itab = VALUE #( ( carrier_id = &#x27;AA&#x27; connection_id = &#x27;0017&#x27; city_from = &#x27;New York&#x27; city_to = &#x27;San Francisco&#x27; )
                    ( carrier_id = &#x27;AZ&#x27; connection_id = &#x27;0789&#x27; city_from = &#x27;Tokyo&#x27; city_to = &#x27;Rome&#x27; ) ).

    &quot;---------------- Serializing ----------------

    DATA(abap_to_json) = /ui2/cl_json=&gt;serialize( data = itab ).
    &quot;Note the many additional, optional parameters such as for formatting the
    &quot;serialized JSON. For more information, see the class documentation.
    DATA(abap_to_json_pretty) = /ui2/cl_json=&gt;serialize( data = itab
                                                         format_output = abap_true ).
    DATA(abap_to_json_pretty_name) = /ui2/cl_json=&gt;serialize( data = itab
                                                              format_output = abap_true
                                                              pretty_name = /ui2/cl_json=&gt;pretty_mode-camel_case ).

    out-&gt;write( `---------- ABAP -&gt; JSON ----------` ).
    out-&gt;write( abap_to_json ).
    out-&gt;write( `---------- ABAP -&gt; JSON (pretty printed) ----------` ).
    out-&gt;write( abap_to_json_pretty ).
    out-&gt;write( `---------- ABAP -&gt; JSON (camel case) ----------` ).
    out-&gt;write( abap_to_json_pretty_name ).

    &quot;---------------- Deserializing ----------------

    DATA(json_to_abap) = abap_to_json.
    DATA itab_json_to_abap LIKE itab.

    /ui2/cl_json=&gt;deserialize( EXPORTING json = json_to_abap
                               CHANGING data  = itab_json_to_abap ).

    out-&gt;write( `---------- JSON -&gt; ABAP ----------` ).
    out-&gt;write( itab_json_to_abap ).

    &quot;---------------- Deserializing: Applying name mapping ----------------
    &quot;Creating an internal table with different field names
    TYPES: BEGIN OF demo_struc4map,
             carr TYPE c LENGTH 3,
             conn TYPE n LENGTH 4,
             from TYPE c LENGTH 20,
             to   TYPE c LENGTH 20,
           END OF demo_struc4map.
    DATA itab4map TYPE TABLE OF demo_struc4map WITH EMPTY KEY.

    /ui2/cl_json=&gt;deserialize( EXPORTING json = json_to_abap
                               name_mappings = VALUE #( ( abap = &#x27;CARR&#x27; json = `CARRIER_ID` )
                                                        ( abap = &#x27;CONN&#x27; json = `CONNECTION_ID` )
                                                        ( abap = &#x27;FROM&#x27; json = `CITY_FROM` )
                                                        ( abap = &#x27;TO&#x27; json = `CITY_TO` ) )
                               CHANGING data  = itab4map ).

    out-&gt;write( `---------- JSON -&gt; ABAP (Name mapping) ----------` ).
    out-&gt;write( itab4map ).

    &quot;---------------- Deserializing: Using JSON as xstring ----------------

    DATA(json_xstring) = cl_abap_conv_codepage=&gt;create_out( )-&gt;convert(
   `[` &amp;&amp;
   `    {` &amp;&amp;
   `        &quot;carrier_id&quot;: &quot;LH&quot;,` &amp;&amp;
   `        &quot;connection_id&quot;: &quot;400&quot;,` &amp;&amp;
   `        &quot;city_from&quot;: &quot;Frankfurt&quot;,` &amp;&amp;
   `        &quot;city_to&quot;: &quot;Berlin&quot;` &amp;&amp;
   `    },` &amp;&amp;
   `    {` &amp;&amp;
   `        &quot;carrier_id&quot;: &quot;DL&quot;,` &amp;&amp;
   `        &quot;connection_id&quot;: &quot;1984&quot;,` &amp;&amp;
   `        &quot;city_from&quot;: &quot;San Francisco&quot;,` &amp;&amp;
   `        &quot;city_to&quot;: &quot;New York&quot;` &amp;&amp;
   `    },` &amp;&amp;
   `    {` &amp;&amp;
   `        &quot;carrier_id&quot;: &quot;AZ&quot;,` &amp;&amp;
   `        &quot;connection_id&quot;: &quot;790&quot;,` &amp;&amp;
   `        &quot;city_from&quot;: &quot;Rome&quot;,` &amp;&amp;
   `        &quot;city_to&quot;: &quot;Osaka&quot;` &amp;&amp;
   `    }` &amp;&amp;
   `]` ).

    DATA itab_json_xstr_to_abap LIKE itab.
    /ui2/cl_json=&gt;deserialize( EXPORTING jsonx = json_xstring
                               CHANGING data  = itab_json_xstr_to_abap ).

    out-&gt;write( `---------- JSON (xstring) -&gt; ABAP ----------` ).
    out-&gt;write( itab_json_xstr_to_abap ).

    &quot;---------------- Deserializing: No equivalent ABAP type available ----------------

    &quot;The example assumes that there is no equivalent ABAP type available for JSON data
    &quot;that is to be deserialized. You can use the &#x27;generate&#x27; method that has a
    &quot;returning parameter of the generic type &#x27;ref to data&#x27;.
    DATA(json) = `[{&quot;CARRIER_ID&quot;:&quot;AA&quot;,&quot;CONNECTION_ID&quot;:17,&quot;CITY_FROM&quot;:&quot;New York&quot;,&quot;CITY_TO&quot;:&quot;San Francisco&quot;},` &amp;&amp;
                 `{&quot;CARRIER_ID&quot;:&quot;AZ&quot;,&quot;CONNECTION_ID&quot;:789,&quot;CITY_FROM&quot;:&quot;Tokyo&quot;,&quot;CITY_TO&quot;:&quot;Rome&quot;}]`.

    DATA(dref) = /ui2/cl_json=&gt;generate( json = json ).
    DATA(dref_xstr) = /ui2/cl_json=&gt;generate( jsonx = json_xstring ).

    &quot;You can further process the content, for example, with RTTS as outlined in the
    &quot;Dynamic Programming cheat sheet.
    IF dref IS BOUND.
      out-&gt;write( `---------- JSON -&gt; ABAP (unknown type) ----------` ).
      out-&gt;write( dref-&gt;* ).
    ENDIF.

    IF dref_xstr IS BOUND.
      out-&gt;write( `---------- JSON (xstring) -&gt; ABAP (unknown type) ----------` ).
      out-&gt;write( dref_xstr-&gt;* ).
    ENDIF.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursions</h2>
<h3>Serializing and Deserializing Objects</h3>
<ul>
<li>To serialize and deserialize objects (i.e. instances of classes), you can use <code>CALL TRANSFORMATION</code> statements. As a prerequisite, the classes must implement the <code>IF_SERIALIZABLE_OBJECT</code> interface. The example uses the predefined identity transformation <code>ID</code>.</li>
<li>Find more information and examples <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenasxml_class_instances.htm">here</a> in the ABAP Keyword Documentation.</li>
</ul>
<p>Expand the following collapsible section for example classes. To try them out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The examples are set up to display output in the console.</p>
<p>&lt;details&gt; &lt;summary&gt;🟢 Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>When the class runs, it creates three instances, and three instance attributes are assigned values for each instance: the current UTC timestamp, a random number, and a UUID.</li>
<li>These instances are then serialized and subsequently deserialized.</li>
<li>The instance attributes are accessed, and their values are stored in an internal table and displayed.</li>
</ul>
<p><strong>Example 1:</strong></p>
<ul>
<li>The example class implements the <code>IF_SERIALIZABLE_OBJECT</code> interface, using the standard behavior to serialize and deserialize all instance attributes (i.e. the helper methods mentioned below are not implemented).</li>
<li>The values of all deserialized instance attributes are displayed.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES: if_oo_adt_classrun,
      if_serializable_object.
  PROTECTED SECTION.
  PRIVATE SECTION.
    DATA timestamp TYPE utclong.
    DATA random_number TYPE i.
    DATA uuid TYPE sysuuid_x16.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    DATA serialized_obj_tab TYPE string_table.
    TYPES: BEGIN OF deserialized_obj_struc,
             timestamp     TYPE utclong,
             random_number TYPE i,
             uuid          TYPE sysuuid_x16,
           END OF deserialized_obj_struc.
    DATA deserialized_obj_tab TYPE TABLE OF deserialized_obj_struc WITH EMPTY KEY.

    &quot;Creating objects, assigning values to instance attributes, and serializing objects
    DO 3 TIMES.
      DATA(oref) = NEW zcl_demo_abap( ).
      oref-&gt;timestamp = utclong_current( ).
      oref-&gt;random_number = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                                        min  = 1
                                                        max  = 100 )-&gt;get_next( ).

      TRY.
          oref-&gt;uuid = cl_system_uuid=&gt;create_uuid_x16_static( ) .
        CATCH cx_uuid_error.
      ENDTRY.

      DATA serialized_obj TYPE string.
      CALL TRANSFORMATION id SOURCE obj = oref
                             RESULT XML serialized_obj.

      APPEND serialized_obj TO serialized_obj_tab.
    ENDDO.

    &quot;Deserializing objects
    LOOP AT serialized_obj_tab INTO DATA(wa).
      DATA deserialized_obj TYPE REF TO zcl_demo_abap.
      CALL TRANSFORMATION id SOURCE XML wa
                             RESULT obj = deserialized_obj.

      &quot;Addressing instance attributes after deserialization
      DATA(deserialized_timestamp) = deserialized_obj-&gt;timestamp.
      DATA(deserialized_random_number) = deserialized_obj-&gt;random_number.
      DATA(deserialized_uuid) = deserialized_obj-&gt;uuid.

      &quot;Adding deserialized instance attribute values to an internal table
      APPEND VALUE #( timestamp = deserialized_timestamp
                      random_number = deserialized_random_number
                      uuid = deserialized_uuid
                    ) TO deserialized_obj_tab.
    ENDLOOP.

    out-&gt;write( deserialized_obj_tab ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p><strong>Example 2:</strong></p>
<ul>
<li>The example class implements the <code>IF_SERIALIZABLE_OBJECT</code> interface, along with the <code>SERIALIZE_HELPER</code> and <code>DESERIALIZE_HELPER</code> methods.</li>
<li>These methods allow you to modify the standard behavior. Both methods must be implemented together, or not at all. If neither is implemented, the standard behavior applies.</li>
<li>The methods must be declared as private instance methods. The <code>SERIALIZE_HELPER</code> method only has output parameters, while <code>DESERIALIZE_HELPER</code> only has input parameters. Any parameter specified for the <code>SERIALIZE_HELPER</code> method must have an identically named and typed parameter in the <code>DESERIALIZE_HELPER</code> method.</li>
<li>For example, you can implement these methods if you want to exclude certain attributes from (de)serialization.</li>
<li>In the given example, one of the instance attributes is excluded from (de)serialization. As a result, the output only includes initial values for one of the attributes.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES: if_oo_adt_classrun,
      if_serializable_object.
  PROTECTED SECTION.
  PRIVATE SECTION.
    DATA timestamp TYPE utclong.
    DATA random_number TYPE i.
    DATA uuid TYPE sysuuid_x16.

    METHODS:
      serialize_helper EXPORTING timestamp     TYPE utclong
                                 random_number TYPE i,
      deserialize_helper IMPORTING timestamp     TYPE utclong
                                   random_number TYPE i.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    DATA serialized_obj_tab TYPE string_table.
    TYPES: BEGIN OF deserialized_obj_struc,
             timestamp     TYPE utclong,
             random_number TYPE i,
             uuid          TYPE sysuuid_x16,
           END OF deserialized_obj_struc.
    DATA deserialized_obj_tab TYPE TABLE OF deserialized_obj_struc WITH EMPTY KEY.

    &quot;Creating objects, assigning values to instance attributes, and serializing objects
    DO 3 TIMES.
      DATA(oref) = NEW zcl_demo_abap( ).
      oref-&gt;timestamp = utclong_current( ).
      oref-&gt;random_number = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                                        min  = 1
                                                        max  = 100 )-&gt;get_next( ).

      TRY.
          oref-&gt;uuid = cl_system_uuid=&gt;create_uuid_x16_static( ) .
        CATCH cx_uuid_error.
      ENDTRY.

      DATA serialized_obj TYPE string.
      CALL TRANSFORMATION id SOURCE obj = oref
                             RESULT XML serialized_obj.

      APPEND serialized_obj TO serialized_obj_tab.
    ENDDO.

    &quot;Deserializing objects
    LOOP AT serialized_obj_tab INTO DATA(wa).
      DATA deserialized_obj TYPE REF TO zcl_demo_abap.
      CALL TRANSFORMATION id SOURCE XML wa
                             RESULT obj = deserialized_obj.

      &quot;Addressing instance attributes after deserialization
      DATA(deserialized_timestamp) = deserialized_obj-&gt;timestamp.
      DATA(deserialized_random_number) = deserialized_obj-&gt;random_number.
      DATA(deserialized_uuid) = deserialized_obj-&gt;uuid.

      &quot;Adding deserialized instance attribute values to an internal table
      APPEND VALUE #( timestamp = deserialized_timestamp
                      random_number = deserialized_random_number
                      uuid = deserialized_uuid
                    ) TO deserialized_obj_tab.
    ENDLOOP.

    out-&gt;write( deserialized_obj_tab ).
  ENDMETHOD.

  METHOD serialize_helper.
    timestamp = me-&gt;timestamp.
    random_number = me-&gt;random_number.
  ENDMETHOD.

  METHOD deserialize_helper.
    me-&gt;timestamp = timestamp.
    me-&gt;random_number = random_number.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<h3>Converting string &lt;-&gt; xstring</h3>
<p>In the code snippets above and in the executable example, many operations are performed using binary data. This excursion shows the conversion of string &lt;-&gt; xstring using a codepage. The examples use UTF-8. For example, you can use the <code>cl_abap_conv_codepage</code> class and the <a href="https://help.sap.com/docs/btp/sap-business-technology-platform/xco-library?version=Cloud">XCO library</a>. Using the <code>xco_cp</code> class of the XCO library, you can also process Base64 representations of raw binary data (see the snippet for <code>xco_cp</code> in the <a href="/22_Released_ABAP_Classes.md#string-processing">String Processing</a> section of the <em>Released ABAP Classes</em> cheat sheet).</p>
<pre><code>DATA(xml_string) = `&lt;TXT&gt;ABAP&lt;/TXT&gt;`.

&quot;string -&gt; xstring
&quot;Note: UTF-8 is used by default. Here, it is specified explicitly.
&quot;Exception class that can be used: cx_sy_conversion_codepage
TRY.
    DATA(conv_xstring) = cl_abap_conv_codepage=&gt;create_out( codepage = `UTF-8` )-&gt;convert( xml_string ).
  CATCH cx_sy_conversion_codepage.
ENDTRY.
&quot;conv_xstring: 3C5458543E414241503C2F5458543E

&quot;xstring -&gt; string
DATA(conv_string) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( conv_xstring ).
&quot;conv_string: &lt;TXT&gt;ABAP&lt;/TXT&gt;

&quot;As an alternative, you can use methods of the XCO library. More methods are available
&quot;in this context. Check the options when choosing CTRL + Space after &#x27;-&gt;&#x27;
&quot;string -&gt; xstring
DATA(conv_xstring_xco) = xco_cp=&gt;string( xml_string
  )-&gt;as_xstring( xco_cp_character=&gt;code_page-&gt;utf_8
  )-&gt;value.

&quot;xstring -&gt; string
DATA(conv_string_xco) = xco_cp=&gt;xstring( conv_xstring_xco
  )-&gt;as_string( xco_cp_character=&gt;code_page-&gt;utf_8
  )-&gt;value.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Compressing and Decompressing Binary Data</h3>
<p>You may want to process or store binary data. The data may be very large. You can compress the data in gzip format and decompress it for further processing using the <code>cl_abap_gzip</code> class.</p>
<pre><code>DATA(str) = `This is a data object of type string. It should be converted to xstring, compressed and decompressed.`.
DATA(xstr) = cl_abap_conv_codepage=&gt;create_out( )-&gt;convert( str ).
DATA xstr_comp TYPE xstring.

&quot;Compressing binary data
TRY.
    cl_abap_gzip=&gt;compress_binary( EXPORTING raw_in   = xstr
                                   IMPORTING gzip_out = xstr_comp ).
  CATCH cx_parameter_invalid_range cx_sy_buffer_overflow cx_sy_compression_error.
ENDTRY.

&quot;Comparing the length of the data objects
DATA(len_xstr) = xstrlen( xstr ). &quot;101
DATA(len_xstr_comp) = xstrlen( xstr_comp ). &quot;81

&quot;Decompressing binary data
DATA xstr_decomp TYPE xstring.
TRY.
    cl_abap_gzip=&gt;decompress_binary( EXPORTING gzip_in = xstr_comp
                                     IMPORTING raw_out = xstr_decomp ).
  CATCH cx_parameter_invalid_range cx_sy_buffer_overflow cx_sy_compression_error.
ENDTRY.

DATA(len_xstr_decomp) = xstrlen( xstr_decomp ). &quot;101
DATA(conv_str) = cl_abap_conv_codepage=&gt;create_in( )-&gt;convert( xstr_decomp ).

&quot;abap_true
DATA(is_equal) = xsdbool( len_xstr = len_xstr_decomp AND str = conv_str ). </code></pre>
<h3>Exporting and Importing Data Clusters</h3>
<ul>
<li>A data cluster groups data objects for temporary and persistent storage in a medium, such as an elementary data object of type <code>xstring</code>, or an internal table with a specific table type.</li>
<li>Potential uses include:</li>
<li>Fast serialization and deserialization of data to and from <code>xstring</code></li>
<li>Storing the data cluster (in a compressed form) in a DDIC database table field</li>
<li>Passing data clusters through parameters in procedures for further evaluation, especially with large, complex data</li>
<li>Related ABAP statements:</li>
<li><code>EXPORT</code> to write data objects to the memory medium</li>
<li><code>IMPORT</code> to read from the memory medium and extract the data objects</li>
</ul>
<div class="note">[!NOTE] - Regarding data clusters, the focus in this section is on the fast serialization and deserialization of data to and from `xstring`. - Find more information [here](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENDATA_CLUSTER.html). - More syntax options are available in [Standard ABAP](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstandard_abap_glosry.htm). - Various exceptions can be raised when using these statements; see the related subtopics in the [documentation](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENDATA_CLUSTER.html). - Additions to `IMPORT` statements offer [conversion options](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABAPIMPORT_CONVERSION.html).</div>
<p>The following example covers:</p>
<ul>
<li>Exporting a data cluster, storing it in a byte string, and importing it back to a data object</li>
<li>Activating compression using the <code>COMPRESSION ON</code> addition</li>
<li>Specifying multiple data objects in the parameter list</li>
<li>Alternative syntax for specifying parameters</li>
<li>Dynamic specification of the parameter list</li>
<li>Exporting and importing class objects</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    INTERFACES if_serializable_object.
    METHODS constructor IMPORTING text TYPE string OPTIONAL.
  PROTECTED SECTION.
  PRIVATE SECTION.
    DATA timestamp TYPE utclong.
    DATA text TYPE string.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA buffer TYPE xstring.


    &quot;---- Exporting data cluster, storing it in byte string, -------
    &quot;---- and importing it to a data object again ------------------

    &quot;The following example exports an internal table to a byte string buffer, and
    &quot;imports the content to another internal table.
    &quot;The parameter list includes one data object. Compression is deactivated
    &quot;by default.

    SELECT *
     FROM zdemo_abap_fli
     INTO TABLE @DATA(itab1).

    EXPORT flights = itab1 TO DATA BUFFER buffer.

    &quot;Determining the length of the xstring (to compare it with the
    &quot;same example that activates compression).
    DATA(strlen_export) = xstrlen( buffer ).

    out-&gt;write( data = strlen_export name = `strlen_export` ).
    out-&gt;write( |\n| ).

    DATA itab2 LIKE itab1.
    IMPORT flights = itab2 FROM DATA BUFFER buffer.

    ASSERT itab2 = itab1.

    out-&gt;write( data = itab2 name = `itab2` ).
    out-&gt;write( |\n| ).

    &quot;---------------- Activating compression ----------------
    EXPORT flights = itab1 TO DATA BUFFER buffer COMPRESSION ON.

    DATA(strlen_export_compr) = xstrlen( buffer ).

    ASSERT strlen_export_compr &lt; strlen_export.
    out-&gt;write( data = strlen_export_compr name = `strlen_export_compr` ).
    out-&gt;write( |\n| ).

    &quot;------ Multiple data objects specified in the parameter list -----
    &quot;The following example writes and reads a byte string of two numbers.

    DATA num1 TYPE i.
    DATA num2 TYPE i.
    EXPORT int1 = 100 int2 = 200 TO DATA BUFFER buffer.
    IMPORT int1 = num1 int2 = num2 FROM DATA BUFFER buffer.

    &quot;----------------- Exceptions -----------------
    &quot;The following example includes a non-compatible data object to read
    &quot;to. The example emphasizes that various exceptions can be
    &quot;raised when using the statements. Find more details in the ABAP
    &quot;Keyword Documentation.
    DATA str_table TYPE string_table.
    TRY.
        IMPORT int1 = num1 int2 = str_table FROM DATA BUFFER buffer.
      CATCH cx_sy_import_mismatch_error.
    ENDTRY.

    &quot;--------- Alternative syntax for specifying the parameters ---------
    &quot;EXPORT: Using ... FROM ... instead of ... = ...
    &quot;IMPORT: Using ... TO ... instead of ... = ...
    EXPORT flights FROM itab1 TO DATA BUFFER buffer.
    IMPORT flights TO itab2 FROM DATA BUFFER buffer.
    EXPORT int1 = 100 int2 = 200 TO DATA BUFFER buffer.
    IMPORT int1 TO num1 int2 TO num2 FROM DATA BUFFER buffer.

    &quot;--------- Dynamic specification of the parameter list ---------
    &quot;Note:
    &quot;- The parameter list is specified in an index table with two columns.
    &quot;- The column names can have random names, but the type must be character-like.
    &quot;- The first column represents the parameter name, the second column represents
    &quot;  the name of the data object.
    &quot;- Note that special behavior applies. See the documentation.

    TYPES: BEGIN OF param,
             name TYPE string,
             dobj TYPE string,
           END OF param,
           param_tab_type TYPE TABLE OF param WITH EMPTY KEY.

    DATA: txt1 TYPE string VALUE `hello`,
          txt2 TYPE string VALUE `world`,
          txt3 TYPE string VALUE `ABAP`.

    DATA(param_table) = VALUE param_tab_type(
      ( name = `txt1` dobj = `txt1` )
      ( name = `txt2` dobj = `txt2` )
      ( name = `txt3` dobj = `txt3` ) ).

    EXPORT (param_table) TO DATA BUFFER buffer.

    &quot;The example reads the content into structure components.
    DATA: BEGIN OF values,
            txt1 TYPE string,
            txt2 TYPE string,
            txt3 TYPE string,
          END OF values.

    param_table = VALUE param_tab_type(
      ( name = `txt1` dobj = `values-txt1` )
      ( name = `txt2` dobj = `values-txt2` )
      ( name = `txt3` dobj = `values-txt3` ) ).

    IMPORT (param_table) FROM DATA BUFFER buffer.

    out-&gt;write( data = values name = `values` ).
    out-&gt;write( |\n| ).

    &quot;--------- Exporting and importing objects of classes ----------
    &quot;The following example explores:
    &quot;- Objects of the example class are created
    &quot;- The objects are serialized using a CALL TRANSFORMATION statement
    &quot;- As a prerequisite, the class includes the IF_SERIALIZABLE_OBJECT interface
    &quot;- In a DO loop, 3 objects of the class are created.
    &quot;- After serializing the objects, the data is exported with compression activated.
    &quot;- Then, the data is read and deserialized again.
    &quot;- Instance attributes are accessed and added to an internal table for output
    &quot;  purposes. The class is setup that instance attributes receive different values,
    &quot;  for example, the current UTC timestamp is set.

    TYPES: BEGIN OF info,
             text      TYPE string,
             timestamp TYPE utclong,
           END OF info.

    DATA info_tab TYPE TABLE OF info WITH EMPTY KEY.

    DO 3 TIMES.
      DATA(obj_ref) = NEW zcl_demo_abap( |Instance { sy-index }| ).
      CALL TRANSFORMATION id SOURCE oref = obj_ref RESULT XML DATA(xml).

      EXPORT xml_data = xml TO DATA BUFFER buffer COMPRESSION ON.

      CLEAR obj_ref.

      IMPORT xml_data = xml FROM DATA BUFFER buffer.

      CALL TRANSFORMATION id SOURCE XML xml RESULT oref = obj_ref.
      DATA(text_deserialized) = obj_ref-&gt;text.
      DATA(timestamp_deserialized) = obj_ref-&gt;timestamp.
      APPEND VALUE #( text = text_deserialized timestamp = timestamp_deserialized ) TO info_tab.
    ENDDO.

    out-&gt;write( info_tab ).
    out-&gt;write( |\n| ).

    &quot;--------- Exporting and importing data objects to an internal table as storage medium ----------
    &quot;The following example exports an internal table to a data cluster in an internal table,
    &quot;and imports it into another internal table.
    &quot;Note that there are prerequisites for the internal table as storage medium. See the documentation.
    &quot;As the width of the second column is restricted, the data is stored across multiple table lines.

    TYPES: BEGIN OF buffer_line,
             id    TYPE i,
             clstr TYPE x LENGTH 100,
           END OF  buffer_line.

    DATA tab_buffer TYPE TABLE OF buffer_line WITH EMPTY KEY.
    EXPORT itab = itab1 TO INTERNAL TABLE tab_buffer.

    out-&gt;write( |Lines in buffer table: { lines( tab_buffer ) }| ).
    out-&gt;write( |\n| ).

    IMPORT itab = itab2 FROM INTERNAL TABLE tab_buffer.

    ASSERT itab2 = itab1.

  ENDMETHOD.

  METHOD constructor.
    me-&gt;timestamp = utclong_current( ).

    IF text IS SUPPLIED AND text IS NOT INITIAL.
      me-&gt;text = text.
    ENDIF.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>More Information</h2>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_xml.htm">ABAP and XML (main topic in the ABAP Keyword Documentation)</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_sxml_lib.htm">sXML</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_ixml_lib_cloud.htm">iXML Library for ABAP Cloud</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenabap_ixml_lib.htm">iXML Library Classic (F1 documentation for Standard ABAP)</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_xslt.htm">XSLT</a></li>
<li><a href="https://help.sap.com/docs/ABAP_PLATFORM_NEW/31bfc625c2674acdb9aa7547b62db9cc/3cb7463c32a3fe13e10000000a114084.html">SAP XSLT Processor Reference</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_st.htm">ST</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenst_abexas.htm">ST Examples</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_xslt_asxml.htm">asXML</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_asjson.htm">asJSON</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_json_abexas.htm">JSON Examples</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcall_transformation.htm"><code>CALL TRANSFORMATION</code></a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencall_transformation_abexas.htm"><code>CALL TRANSFORMATION</code> Examples</a></li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Executable Example</h2>
<p><a href="./src/zcl_demo_abap_xml_json.clas.abap">zcl_demo_abap_xml_json</a></p>
<div class="note">[!NOTE] - The executable example ... - covers the following topics: - Creating/Parsing XML Data Using iXML - Creating/Parsing XML Data Using sXML - XML Transformations using XSLT and Simple Transformations - Serializations (ABAP -> XML) and Deserialization (XML -> ABAP) using the identity transformation ID (elementary types, structures, internal tables, data and object references) - `CALL TRANSFORMATION` syntax options, sources and targets of transformations - Working with JSON data, XCO classes for JSON - Excursions: Converting string <-> xstring, compressing and decompressing binary data - uses, apart from the predefined identity transformation (ID), demo XSLT and ST programs. They are not intended to be role models for proper XSLT/ST design. - The steps to import and run the code are outlined [here](README.md#-getting-started-with-the-examples). - [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
</body>
</html>