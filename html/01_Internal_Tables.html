<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>01 Internal Tables - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>Internal Tables</h1>
<ul>
<li><a href="#internal-tables">Internal Tables</a></li>
<li><a href="#introduction">Introduction</a></li>
<li><a href="#basic-properties-of-internal-tables">Basic Properties of Internal Tables</a></li>
<li><a href="#table-keys-primary-secondary-standard-empty-and-table-indexes">Table Keys (Primary, Secondary, Standard, Empty) and Table Indexes</a></li>
<li><a href="#creating-internal-tables-and-types">Creating Internal Tables and Types</a></li>
<li><a href="#specifying-keys-in-internal-table-declarations">Specifying Keys in Internal Table Declarations</a></li>
<li><a href="#internal-tables-based-on-locally-created-linetable-types">Internal Tables Based on Locally Created Line/Table Types</a></li>
<li><a href="#overview-of-line-and-table-type-options-with-internal-tables">Overview of Line and Table Type Options with Internal Tables</a></li>
<li><a href="#creating-internal-tables-by-inline-declaration">Creating Internal Tables By Inline Declaration</a></li>
<li><a href="#populating-internal-tables">Populating Internal Tables</a></li>
<li><a href="#copying-internal-tables">Copying Internal Tables</a></li>
<li><a href="#using-insert-and-append-statements-to-populate-internal-tables">Using INSERT and APPEND Statements to Populate Internal Tables</a></li>
<li><a href="#creating-and-populating-internal-tables-using-constructor-expressions">Creating and Populating Internal Tables Using Constructor Expressions</a></li>
<li><a href="#value-operator">VALUE Operator</a></li>
<li><a href="#corresponding-operator-and-move-corresponding-statements">CORRESPONDING Operator and MOVE-CORRESPONDING Statements</a></li>
<li><a href="#filter-operator">FILTER Operator</a></li>
<li><a href="#new-operator">NEW Operator</a></li>
<li><a href="#example-exploring-populating-internal-tables">Example: Exploring Populating Internal Tables</a></li>
<li><a href="#reading-single-lines-from-internal-tables">Reading Single Lines from Internal Tables</a></li>
<li><a href="#determining-the-target-area-when-reading-single-lines-in-read-table-statements">Determining the Target Area when Reading Single Lines in READ TABLE Statements</a></li>
<li><a href="#reading-single-lines-by-index">Reading Single Lines by Index</a></li>
<li><a href="#reading-single-lines-using-table-keys">Reading Single Lines Using Table Keys</a></li>
<li><a href="#reading-single-lines-using-a-free-key">Reading Single Lines Using a Free Key</a></li>
<li><a href="#examples-of-addressing-individual-components-of-read-lines">Examples of Addressing Individual Components of Read Lines</a></li>
<li><a href="#excursions-with-read-table-statements">Excursions with READ TABLE Statements</a></li>
<li><a href="#system-field-setting-in-read-table-statements">System Field Setting in READ TABLE Statements</a></li>
<li><a href="#specifying-a-where-condition-in-read-table-statements">Specifying a WHERE Condition in READ TABLE Statements</a></li>
<li><a href="#comparing-and-transporting-additions-comparing-fields-and-specifying-fields-for-transport">COMPARING and TRANSPORTING Additions: Comparing Fields and Specifying Fields for Transport</a></li>
<li><a href="#casting-and-else-unassign-additions-when-specifying-field-symbols-as-target-areas">CASTING and ELSE UNASSIGN Additions when Specifying Field Symbols as Target Areas</a></li>
<li><a href="#binary-search-addition-optimized-read-access-when-specifying-free-keys">BINARY SEARCH Addition: Optimized Read Access When Specifying Free Keys</a></li>
<li><a href="#exploring-read-table-statements-and-table-expressions">Exploring READ TABLE Statements and Table Expressions</a></li>
<li><a href="#accessing-single-table-lines-via-table-expressions">Accessing Single Table Lines via Table Expressions</a></li>
<li><a href="#processing-multiple-internal-table-lines-sequentially">Processing Multiple Internal Table Lines Sequentially</a></li>
<li><a href="#restricting-the-area-of-a-table-to-be-looped-over">Restricting the Area of a Table to Be Looped Over</a></li>
<li><a href="#defining-the-step-size-and-the-direction-of-loop-passes">Defining the Step Size and the Direction of Loop Passes</a></li>
<li><a href="#iteration-expressions">Iteration Expressions</a></li>
<li><a href="#interrupting-and-exiting-loops">Interrupting and Exiting Loops</a></li>
<li><a href="#inserting-and-deleting-lines-in-internal-tables-in-loops">Inserting and Deleting Lines in Internal Tables in Loops</a></li>
<li><a href="#modifying-internal-table-content">Modifying Internal Table Content</a></li>
<li><a href="#modifying-read-table-lines">Modifying Read Table Lines</a></li>
<li><a href="#modifying-table-lines-using-abap-modify-statements">Modifying Table Lines Using ABAP MODIFY Statements</a></li>
<li><a href="#deleting-internal-table-content">Deleting Internal Table Content</a></li>
<li><a href="#deleting-adjacent-duplicate-lines">Deleting Adjacent Duplicate Lines</a></li>
<li><a href="#deleting-the-entire-internal-table-content">Deleting the Entire Internal Table Content</a></li>
<li><a href="#sorting-internal-tables">Sorting Internal Tables</a></li>
<li><a href="#grouping-internal-tables">Grouping Internal Tables</a></li>
<li><a href="#collecting-values">Collecting Values</a></li>
<li><a href="#getting-information-about-internal-tables-table-lines-table-types">Getting Information about Internal Tables, Table Lines, Table Types</a></li>
<li><a href="#checking-the-existence-of-a-line-in-an-internal-table">Checking the Existence of a Line in an Internal Table</a></li>
<li><a href="#checking-the-index-of-a-line-in-an-internal-table">Checking the Index of a Line in an Internal Table</a></li>
<li><a href="#checking-how-many-lines-exist-in-an-internal-table">Checking How Many Lines Exist in an Internal Table</a></li>
<li><a href="#getting-table-type-information-at-runtime">Getting Table (Type) Information at Runtime</a></li>
<li><a href="#operations-with-internal-tables-using-abap-sql-select-statements">Operations with Internal Tables Using ABAP SQL SELECT Statements</a></li>
<li><a href="#internal-tables-as-target-data-objects-in-select-queries">Internal Tables as Target Data Objects in SELECT Queries</a></li>
<li><a href="#select-queries-with-internal-tables-as-data-sources">SELECT Queries with Internal Tables as Data Sources</a></li>
<li><a href="#restrictions-regarding-internal-tables-as-data-sources-in-abap-sql-select-statements">Restrictions Regarding Internal Tables as Data Sources in ABAP SQL SELECT Statements</a></li>
<li><a href="#excursion-joiningmerging-internal-tables-into-internal-tables">Excursion: Joining/Merging Internal Tables into Internal Tables</a></li>
<li><a href="#excursions">Excursions</a></li>
<li><a href="#improving-read-performance-with-secondary-table-keys">Improving Read Performance with Secondary Table Keys</a></li>
<li><a href="#example-exploring-read-access-performance-with-internal-tables">Example: Exploring Read Access Performance with Internal Tables</a></li>
<li><a href="#generic-table-types-with-formal-parameters-of-methods-and-field-symbols">Generic Table Types with Formal Parameters of Methods and Field Symbols</a></li>
<li><a href="#searching-and-replacing-substrings-in-internal-tables-with-character-like-data-types">Searching and Replacing Substrings in Internal Tables with Character-Like Data Types</a></li>
<li><a href="#ranges-tables">Ranges Tables</a></li>
<li><a href="#comparing-content-of-compatible-internal-tables">Comparing Content of Compatible Internal Tables</a></li>
<li><a href="#bdef-derived-types-abap-eml">BDEF Derived Types (ABAP EML)</a></li>
<li><a href="#creating-internal-tables-dynamically">Creating Internal Tables Dynamically</a></li>
<li><a href="#more-information">More Information</a></li>
<li><a href="#executable-example">Executable Example</a></li>
</ul>
<h2>Introduction</h2>
<p>Internal Tables ...</p>
<ul>
<li>are tables that temporarily store variable data (i.e. any number of table lines of a fixed structure) in the working memory in ABAP.</li>
<li>are <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendynamic_data_object_glosry.htm">dynamic data objects</a>, i.e. all properties apart from the memory consumption are determined statically by the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendata_type_glosry.htm">data type</a>.</li>
<li>consist of a variable sequence of lines of the same data type.</li>
<li>have a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_type_glosry.htm">table type</a> as its data type (it is a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencomplex_data_type_glosry.htm">complex data type</a>), which defines the following properties:</li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenrow_type_glosry.htm">line type</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_category_glosry.htm">table category</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_key_glosry.htm">table key</a></li>
<li>are used when a variable data set of a random data type needs to be processed in a structured way, for example, storing and processing <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendatabase_table_glosry.htm">database table</a> content within an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_program_glosry.htm">ABAP program</a>.</li>
<li>allow access to individual table lines via a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_index_glosry.htm">table index</a> or a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_key_glosry.htm">table key</a>.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Basic Properties of Internal Tables</h3>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more details&lt;/summary&gt; &lt;!-- --&gt;</p>
<p><strong>Line Type</strong></p>
<ul>
<li>Defines how each line of the internal table is set up, i. e. it describes what columns the table has.</li>
<li>It can be any ABAP data type, e.g. an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenelementary_data_type_glosry.htm">elementary</a> or complex data type as well as a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenreference_type_glosry.htm">reference type</a>.</li>
<li>In most cases, the line type is <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstructured_type_glosry.htm">structured</a>. In this case, the individual components of a line are also referred to as the columns of the internal table.</li>
<li>In a simple case, the line consists of a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenflat_structure_glosry.htm">flat structure</a> with elementary data objects; however, it can also be a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendeep_structure_glosry.htm">deep structure</a> whose components can be structures themselves or even internal tables.</li>
</ul>
<p><strong>Table Category</strong></p>
<ul>
<li>Defines how internal tables are managed and stored internally, and how to access individual table entries.</li>
<li>Why is it relevant? The use of a suitable table category should meet your requirements, i.e. if the internal tables are large, the different categories can have significant performance differences when accessing table content.</li>
<li>Note: There are two ways to access internal tables:</li>
<li>Access by table index: A line of an internal table is accessed by its line number. This kind of access is the fastest way to access table lines.</li>
<li>Access by table key: A line of an internal table is accessed by searching for specific values in specific columns. Note: The columns in which you search can be key columns, but they can also be non-key columns.</li>
</ul>
<table>
<thead><tr>
<th>Category</th>
<th>Internally managed by</th>
<th>Access</th>
<th>Primary table key</th>
<th>When to use</th>
<th>Hints</th>
</tr></thead>
<tbody>
<tr>
<td><code>STANDARD</code></td>
<td>Primary table index (that&#x27;s why these tables are called <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenindex_table_glosry.htm">index tables</a>)</td>
<td>&lt;ul&gt;&lt;li&gt;Table index&lt;/li&gt;&lt;li&gt;Table key&lt;/li&gt;&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;Always non-unique, i.e. duplicate entries are always allowed&lt;/li&gt;&lt;li&gt;Definition of an empty key is possible if the key is not relevant(<code>WITH EMPTY KEY</code>)&lt;/li&gt;&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;If you primarily access the table content for sequential processing or via the table index.&lt;/li&gt;&lt;li&gt;Response time for accessing the table using the primary key: This kind of table access is optimized only for sorted and hashed tables. For standard tables, primary key access uses a linear search across all lines. That means that large standard tables (more than 100 lines) are not ideal if the you primarily access the table using the table key.&lt;/li&gt;&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;There is no particular sort order, but the tables can be sorted using <code>SORT</code>.&lt;/li&gt;&lt;li&gt;Populating this kind of table: Lines are either appended at the end of the table or inserted at a specific position.&lt;/li&gt;&lt;li&gt;<a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensecondary_table_key_glosry.htm">Secondary table keys</a> can be defined to make key access to standard tables more efficient.&lt;/li&gt;&lt;li&gt;Standard and sorted tables have the least <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenadmin_costs_dyn_mem_obj_guidl.htm">administration costs (F1 docu for standard ABAP)</a>.&lt;/li&gt;&lt;/ul&gt;</td>
</tr>
<tr>
<td><code>SORTED</code></td>
<td>Primary table index (that&#x27;s why these tables are called <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenindex_table_glosry.htm">index tables</a>)</td>
<td>&lt;ul&gt;&lt;li&gt;Table index&lt;/li&gt;&lt;li&gt;Table key&lt;/li&gt;&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;Non-unique&lt;/li&gt;&lt;li&gt;Unique&lt;/li&gt;&lt;br&gt;... used to sort the table in ascending order.&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;Enables an optimized access to table content using table key and index.&lt;/li&gt;&lt;li&gt;If access via table key is the main access method, but no unique key can be defined.&lt;/li&gt;&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;Sorting is done automatically when lines are inserted or deleted. As a consequence, the table index must usually be reorganized. &lt;/li&gt;&lt;li&gt;The response time for accessing the table using the primary key depends logarithmically on the number of table entries, since a binary search is used.&lt;/li&gt;&lt;li&gt;Standard and sorted tables have the least administration costs.&lt;/li&gt;&lt;/ul&gt;</td>
</tr>
<tr>
<td><code>HASHED</code></td>
<td>Hash algorithm</td>
<td>&lt;ul&gt;&lt;li&gt;Table key&lt;/li&gt;&lt;li&gt;<a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensecondary_table_index_glosry.htm">Secondary table index</a>&lt;/li&gt;&lt;/ul&gt;</td>
<td>Always unique</td>
<td>&lt;ul&gt;&lt;li&gt;For large internal tables.&lt;/li&gt;&lt;li&gt;Optimized for key access. Access to table content via table key is the main access method and a unique key can be defined.&lt;/li&gt;&lt;/ul&gt;</td>
<td>&lt;ul&gt;&lt;li&gt;The response time for primary key access is constant and independent of the number of entries in the table.&lt;/li&gt;&lt;li&gt;Hashed tables have the highest administration costs.&lt;/li&gt;&lt;/ul&gt;</td>
</tr>
</tbody>
</table>
<p><strong>Key Attributes</strong></p>
<ul>
<li>There are two types of table keys: a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenprimary_table_key_glosry.htm">primary table key</a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensecondary_table_key_glosry.htm">secondary table keys</a>.</li>
<li>Table keys ...</li>
<li>are intended to provide an optimized access to the content of internal tables.</li>
<li>are either unique or non-unique, i.e. more than one line with the same key (duplicates) can exist in the internal table or not. Regarding the primary table key, the definition depends on the table category. For the secondary table key, the definition depends on the key type. For standard tables, the primary table key can also be defined as empty, i.e. it does not contain any key columns. Note that for standard tables, an optimized access is only possible with secondary table keys.</li>
<li>Type of keys:</li>
<li>Sorted keys:</li>
<li>Are either the primary table keys of sorted tables or the secondary table keys of any table.</li>
<li>Are managed internally by a table index. In the case of sorted tables, this is the primary table index. In the case of secondary table keys, a secondary table index is added.</li>
<li>Access via sorted keys means an optimized binary search.</li>
<li>Hashed keys:</li>
<li>Are either the primary table key of hashed tables or secondary table keys of any table.</li>
<li>Internally managed by a hash algorithm.</li>
<li>There is no table index for a hashed key.</li>
</ul>
<p><strong>Further information</strong></p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenitab_oview.htm">Internal Tables - Overview</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenadmin_costs_dyn_mem_obj_guidl.htm">Programming guidelines: Internal Tables (F1 docu for standard ABAP)</a></li>
</ul>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Table Keys (Primary, Secondary, Standard, Empty) and Table Indexes</h3>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more details&lt;/summary&gt; &lt;!-- --&gt;</p>
<p><strong>Primary table key</strong></p>
<ul>
<li>Each internal table has a primary table key.</li>
<li>Can be either a self-defined key or the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstandard_key_glosry.htm">standard key</a>.</li>
<li>The primary table key is ...</li>
<li>sorted for sorted tables.</li>
<li>hashed for hashed tables.</li>
<li>Note that the key fields in sorted and hashed tables are read-only. This is not valid for standard tables.</li>
<li>The specification of the primary key can be omitted only for standard tables. The primary table key is then automatically defined as a non-unique standard key.</li>
<li>The primary table key has the predefined name <code>primary_key</code>, by which it can also be addressed explicitly. However, its use is optional, and it is usually not necessary to specify it explicitly. You can also specify an alias name for the primary key.</li>
<li>When accessing internal tables using the table key, the primary key is always used implicitly in processing statements if no secondary key is specified. Note that the primary table key must be specified in table expressions if the primary key is to be used explicitly.</li>
</ul>
<div class="note">[!NOTE] The key can consist of individual key fields or the entire line of the internal table. In this case, the pseudo component `table_line` can be used to denote the primary table key. For non-structured line types, this is the only way to define the key.</div>
<p><strong>Standard key</strong></p>
<ul>
<li>The <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstandard_key_glosry.htm">standard key</a> is a special primary table key.</li>
<li>It can be declared either explicitly or implicitly.</li>
<li>Standard key of an internal table with a ...</li>
<li>structured line type: The primary table key consists of all fields with character-like and byte-like data types.</li>
<li>non-structured/elementary line type: The entire table line is the key (<code>table_line</code>).</li>
<li>An internal table with no explicit key specification implicitly has the standard table key as the primary table key.</li>
<li>Why respecting standard keys is important:</li>
<li>Sorting of a table can produce unexpected results.</li>
<li>Since the standard key can consist of many fields, it affects the performance when accessing the internal table via the keys.</li>
<li>The key fields of the primary table key of sorted and hashed tables are always read-only, i.e. using the standard key with these table categories and then (unintentionally) modifying fields can cause unexpected runtime errors.</li>
<li>Specifying keys explicitly has the advantage of making the code more readable and preventing the standard key from being set by mistake.</li>
</ul>
<p><strong>Empty key</strong></p>
<ul>
<li>The primary table key of a standard table can be empty, i.e. it does not contain any key fields.</li>
<li>An empty key is not possible for sorted and hashed tables.</li>
<li>When used:</li>
<li>When the definition of a table key is not important.</li>
<li>To explicitly state that a table key is not required, instead of specifying no key definition. Otherwise, the standard key is used, which can lead to unexpected results as mentioned above.</li>
<li>Declaration:</li>
<li>Explicit declaration using the addition <code>EMPTY KEY</code>.</li>
<li>Implicit declaration when using the standard key if a structured</li>
</ul>
<p>line type does not contain non-numeric elementary components or if an unstructured line type is tabular.</p>
<ul>
<li>Note: When using an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninline_declaration_glosry.htm">inline declaration</a> such as <code>... INTO TABLE @DATA(itab) ...</code> in <code>SELECT</code> statements, the resulting table is a standard table and has an empty key.</li>
</ul>
<div class="note">[!NOTE] Primary table index - Index tables, such as sorted and standard tables, have a primary table index. - Each table line receives a unique line number from the primary table index. - The index updates each time a line is added or removed.</div>
<p><strong>Secondary table keys</strong></p>
<ul>
<li>Secondary table keys ...</li>
<li>are optional for all table categories.</li>
<li>can be unique/non-unique sorted keys or unique hash keys.</li>
<li>have a self-defined name. An alias name can also be specified.</li>
<li>A <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensecondary_table_index_glosry.htm">secondary table index</a> is created internally for each sorted secondary key. This allows index access to hashed tables via the secondary table key. In this case, <code>sy-tabix</code> is set.</li>
<li>When accessing internal tables using the secondary table key, the key name (or the alias if specified) must be specified. They are not selected automatically. If no secondary key is specified in a processing statement, the primary key or primary table index is always used. If you want to make use of this key in ABAP statements, for example, <code>READ</code>, <code>LOOP AT</code> or <code>MODIFY</code> statements, you must specify the key explicitly using the appropriate additions, for example, <code>WITH ... KEY ... COMPONENTS</code> or <code>USING KEY</code>.</li>
<li>The table access using secondary table keys is always optimized.</li>
<li>Use cases:</li>
<li>To enable different key accesses for an internal table</li>
<li>To enable an optimized key access to internal tables (i.e. to improve read performance), even for standard tables</li>
<li>To enable index access for hashed tables</li>
<li>Standard tables, whose primary table keys cannot be unique, can be provided with a means of ensuring that unique table entries are read.</li>
<li>Typically used ...</li>
<li>for large internal tables (that are populated once and infrequently changed)</li>
<li>for smaller tables, especially standard tables, for ensuring uniqueness of entries (in standard tables, the primary key cannot be unique)</li>
<li>if predominantly fast read accesses are required. Then non-unique sorted secondary table keys can be used, especially for existing internal tables because a later change of the table declaration does not affect exsisting statements.</li>
<li>if predominantly uniqueness is required. Then unique hash secondary table keys can be used. This is also true for smaller internal tables.</li>
<li>Note that defining secondary table keys involves additional administration costs such as additional memory consumption and regarding the key update</li>
<li>Unique secondary table keys are immediately updated, whereas non-unique secondary table keys are not (only when the table is accessed using the key).</li>
<li>The use of secondary table keys should be reserved for cases where the benefits outweigh the extra costs. So, it may not be advisable to use secondary table keys for very small tables or if you very often change the content of the table.</li>
<li>For more details, see the programming guidelines for secondary keys: [Secondary</li>
</ul>
<p>Key (F1 docu for standard ABAP)](https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abensecondary_key_guidl.htm &quot;Guideline&quot;).</p>
<div class="note">[!NOTE] - See examples of internal table declarations using the table keys mentioned above in the following section. - See more information and examples that focus on table access using keys and index below.</div>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Creating Internal Tables and Types</h2>
<p>You can declare internal tables and internal table types in <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_program_glosry.htm">ABAP programs</a> using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abaptypes.htm"><code>TYPES</code></a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapdata.htm"><code>DATA</code></a> statements. The relevant syntax elements for internal tables are <code>TABLE OF</code> in combination with the additions <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapdata_simple.htm"><code>TYPE</code></a> or <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapdata_referring.htm"><code>LIKE</code></a>.</p>
<pre><code>TYPES itab_type1 TYPE STANDARD TABLE OF data_type ...   &quot;Standard table type
TYPES itab_type2 LIKE SORTED   TABLE OF data_object ... &quot;Sorted table type

DATA  itab1      TYPE          TABLE OF data_type ...   &quot;Standard table by default
DATA  itab2      TYPE HASHED   TABLE OF data_type ...   &quot;Hashed table
DATA  itab3      TYPE                   itab_type1 ...  &quot;Based on an existing internal table type 
DATA  itab4      LIKE                   itab1 ...       &quot;Based on an existing internal table       </code></pre>
<div class="note">[!NOTE] - If the table category is not specified (`... TYPE TABLE OF ...`), it is automatically `... TYPE STANDARD TABLE OF ...`. - Using [Runtime Type Creation (RTTC)](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenrun_time_type_creation_glosry.htm "Glossary Entry"), you can define and  create new internal tables and table types as [type description objects](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentype_object_glosry.htm) at runtime. For more information, see the [Dynamic Programming](06_Dynamic_Programming.md) ABAP cheat sheet. Also find a snippet on creating internal tables dynamically by specifying the type dynamically [below](#creating-internal-tables-dynamically).</div>
<p>The following code snippets contain various internal table declarations. It is intended to demonstrate a selection of the rich variety of possible internal tables mentioned in the previous sections, e.g. in <em>Table Keys in Internal Tables</em>. In the examples, many of the internal tables are created using the structured type of a demo database table in the DDIC. The line type of the database table is automatically used when defining an internal table.</p>
<h3>Specifying Keys in Internal Table Declarations</h3>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Creating internal tables with ... &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Standard table keys &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Standard table without explicit primary table key specification.
&quot;Note that STANDARD is not explicitly specified.
&quot;Implicitly, the standard key is used; all non-numeric table fields
&quot;make up the primary table key.
DATA it1 TYPE TABLE OF zdemo_abap_flsch.

&quot;Explicitly specifying STANDARD for a standard table.
&quot;Explicitly specifying the standard table key. The declaration
&quot;corresponds to it1.
DATA it2 TYPE STANDARD TABLE OF zdemo_abap_flsch WITH DEFAULT KEY.

&quot;Hashed table with unique standard table key
DATA it3 TYPE HASHED TABLE OF zdemo_abap_flsch WITH UNIQUE DEFAULT KEY.

&quot;Sorted table with non-unique standard table key
DATA it4 TYPE SORTED TABLE OF zdemo_abap_flsch WITH NON-UNIQUE DEFAULT KEY.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Primary table keys &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Specifying the primary table key
&quot;In standard tables, only a non-unique key is possible.
&quot;The following two examples are the same. NON-UNIQUE can be ommitted but
&quot;is implicitly added.
DATA it5 TYPE TABLE OF zdemo_abap_flsch WITH NON-UNIQUE KEY carrid.
DATA it6 TYPE TABLE OF zdemo_abap_flsch WITH KEY carrid.

&quot;Sorted tables: both UNIQUE and NON-UNIQUE possible
DATA it7 TYPE SORTED TABLE OF zdemo_abap_flsch WITH UNIQUE KEY carrid connid.
DATA it8 TYPE SORTED TABLE OF zdemo_abap_flsch WITH NON-UNIQUE KEY carrid connid cityfrom.

&quot;Hashed tables: UNIQUE KEY must be specified
DATA it9 TYPE HASHED TABLE OF zdemo_abap_flsch WITH UNIQUE KEY carrid.

&quot;Explicitly specifying the predefined name primary_key and listing the components.
&quot;The example is the same as it5 and it6.
DATA it10 TYPE TABLE OF zdemo_abap_flsch WITH KEY primary_key COMPONENTS carrid.

&quot;The following example is the same as it8.
DATA it11 TYPE SORTED TABLE OF zdemo_abap_flsch
  WITH NON-UNIQUE KEY primary_key COMPONENTS carrid connid cityfrom.

&quot;Specifying an alias name for a primary table key.
&quot;Only possible for sorted/hashed tables.
DATA it12 TYPE SORTED TABLE OF zdemo_abap_flsch
  WITH NON-UNIQUE KEY primary_key
  ALIAS p1 COMPONENTS carrid connid cityfrom.

&quot;Specifying a key that is composed of the entire line using the
&quot;predefined table_line.
&quot;In the example, an alias name is defined for a primary table key.
DATA it13 TYPE HASHED TABLE OF zdemo_abap_flsch
  WITH UNIQUE KEY primary_key
  ALIAS p2 COMPONENTS table_line.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Empty keys &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Empty keys are only possible for standard tables
DATA it14 TYPE TABLE OF zdemo_abap_flsch WITH EMPTY KEY.

&quot;Empty primary table key, secondary table key specified
DATA it15 TYPE TABLE OF zdemo_abap_flsch WITH EMPTY KEY
  WITH UNIQUE SORTED KEY cities COMPONENTS cityfrom cityto.

&quot;Excursion: The inline declaration in a SELECT statement
&quot;produces a standard table with empty key.
SELECT * FROM zdemo_abap_flsch INTO TABLE @DATA(it16).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Secondary table keys &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;The following examples demonstrate secondary table keys that are
&quot;possible for all table categories.
DATA it17 TYPE TABLE OF zdemo_abap_flsch                    &quot;standard table
  WITH NON-UNIQUE KEY carrid connid                         &quot;primary table key
  WITH UNIQUE SORTED KEY cities COMPONENTS cityfrom cityto. &quot;secondary table key

DATA it18 TYPE HASHED TABLE OF zdemo_abap_flsch               &quot;hashed table
  WITH UNIQUE KEY carrid connid
  WITH NON-UNIQUE SORTED KEY airports COMPONENTS airpfrom airpto.

DATA it19 TYPE SORTED TABLE OF zdemo_abap_flsch              &quot;sorted table
  WITH UNIQUE KEY carrid connid
  WITH UNIQUE HASHED KEY countries COMPONENTS countryfr countryto.

&quot;Multiple secondary keys are possible
DATA it20 TYPE TABLE OF zdemo_abap_flsch
  WITH NON-UNIQUE KEY primary_key COMPONENTS carrid connid
  WITH NON-UNIQUE SORTED KEY cities COMPONENTS cityfrom cityto
  WITH UNIQUE HASHED KEY airports COMPONENTS airpfrom airpto.

&quot;Alias names for secondary table keys (and also for the primary
&quot;table key in the example)
DATA it21 TYPE SORTED TABLE OF zdemo_abap_flsch
  WITH NON-UNIQUE KEY primary_key ALIAS k1 COMPONENTS carrid connid
  WITH NON-UNIQUE SORTED KEY cities ALIAS s1 COMPONENTS cityfrom cityto
  WITH UNIQUE HASHED KEY airports ALIAS s2 COMPONENTS airpfrom airpto.

&quot;Excursion: Example of using table keys and alias names using a LOOP AT statement.
&quot;All of the statements below are possible.
&quot;Note that if the secondary table key is not specified (and if the USING KEY addition is not
&quot;used in the example), the primary table key is respected by default. So, the first statement
&quot;specifying the primary table key explicitly is the same as not specifying it.
&quot;Further ABAP statements, such as READ or MODIFY, are available in which the key can be
&quot;explicitly specified to process internal tables.
LOOP AT it21 INTO DATA(wa) USING KEY primary_key. 
  &quot;LOOP AT it21 INTO DATA(wa) USING KEY k1.
  &quot;LOOP AT it21 INTO DATA(wa) USING KEY cities.
  &quot;LOOP AT it21 INTO DATA(wa) USING KEY s1.
  &quot;LOOP AT it21 INTO DATA(wa) USING KEY airports.
  &quot;LOOP AT it21 INTO DATA(wa) USING KEY s2.
  ...
ENDLOOP.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Internal Tables Based on Locally Created Line/Table Types</h3>
<p>The examples above demonstrate internal tables that are created using the structured type of a database table in the DDIC. The following example shows the pattern and various examples of declaring internal tables and types by including the local definition of structured data and internal table types for demonstration purposes.</p>
<p>Steps:</p>
<ol>
<li>Define a structured data type (locally or globally).</li>
</ol>
<p>This is not necessary if you use, for example, the name of a database table or <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencds_view_glosry.htm">CDS view</a> in the internal table declaration. In these cases their line type is used automatically.</p>
<ol>
<li>Define an internal table type.</li>
<li>Create the internal table, i.e. a data object that uses this type.</li>
</ol>
<p>You can also create an internal table by ...</p>
<ul>
<li>combining the data object creation and table type definition in one step.</li>
<li>using an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninline_declaration_glosry.htm">inline declaration</a>. Such inline declarations are possible at suitable [declaration</li>
</ul>
<p>positions](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendeclaration_positions.htm) if the operand type can be fully determined, for example, using a <code>DATA</code> statement (or <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfinal_inline.htm"><code>FINAL</code></a> for immutable variables).</p>
<pre><code>&quot;Internal table creation steps based on a locally declared structure
&quot;1. Defining line type locally
TYPES: BEGIN OF ls_loc,
          key_field TYPE i,
          char1     TYPE c LENGTH 10,
          char2     TYPE c LENGTH 10,
          num1      TYPE i,
          num2      TYPE i,
        END OF ls_loc.

&quot;2. Defining internal table types
TYPES: tt_std    TYPE TABLE OF ls_loc WITH EMPTY KEY,
        tt_sorted TYPE SORTED TABLE OF ls_loc WITH NON-UNIQUE KEY key_field,
        tt_hashed TYPE HASHED TABLE OF ls_loc WITH UNIQUE KEY key_field.

&quot;3. Creating internal tables from locally defined table types
DATA: it22 TYPE tt_std,
      it23 TYPE tt_sorted,
      it24 TYPE tt_hashed.

&quot;Combining data object and table type definition
DATA it25 TYPE TABLE OF ls_loc WITH NON-UNIQUE KEY key_field.

&quot;LIKE addition
&quot;Internal table based on an already existing internal table using LIKE.
DATA it26 LIKE it25.
&quot;In the following example, an internal table is created containing internal tables
&quot;of the type of itab_a6.
DATA it27 LIKE TABLE OF it25.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Overview of Line and Table Type Options with Internal Tables</h3>
<p>This section explores various line and table type options when declaring internal tables. Examples have already been covered in the previous sections. Among the options are, for example:</p>
<ul>
<li>Elementary line types based on elementary built-in ABAP types, locally declared elementary types, globally available elementary types such as DDIC data elements</li>
<li>Line types based on both locally declared and globally available structured types</li>
<li>Among the globally available line types are, for example, DDIC structures, database tables and CDS objects such as CDS view entities</li>
<li>Note that internal tables can be created as <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendeep_table_glosry.htm">deep tables</a>. That is, internal tables can contain <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendeep_glosry.htm">deep</a> components (references, internal tables or strings). Structures can also be components of internal tables. For more information, see the <a href="02_Structures.md#variants-of-structures">Variants of Structures</a> in the <em>Structures</em> cheat sheet.</li>
<li>Table types based on both locally declared and globally available table types</li>
<li>References</li>
</ul>
<div class="note">[!NOTE] Types declared in the public visibility section of classes/interfaces are also globally visible and can be used for the creation.</div>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Subject &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Elementary line types &lt;/td&gt; &lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Internal tables declared using elementary line types
*&amp;---------------------------------------------------------------------*
&quot;Note: In this case, the whole table line is the standard table key.

&quot;Elementary built-in ABAP types
DATA it_elem_1 TYPE TABLE OF i.
DATA it_elem_2 TYPE TABLE OF string WITH EMPTY KEY.
DATA it_elem_3 TYPE TABLE OF xstring WITH EMPTY KEY.
DATA it_elem_4 TYPE TABLE OF utclong WITH EMPTY KEY.

&quot;Note: As described above, the table key can consist of components of the
&quot;line type or of the entire line using the pseudo component table_line. In
&quot;case of elementary line types, table_line is the only option to refer to the
&quot;component/entire line.
DATA it_elem_5 TYPE SORTED TABLE OF string WITH NON-UNIQUE KEY table_line.

&quot;Locally declared elementary types
TYPES ty_c3 TYPE c LENGTH 3.
DATA it_elem_6 TYPE TABLE OF ty_c3 WITH EMPTY KEY.

&quot;Apart from the globally available elementary built-in ABAP types, you can
&quot;also use elementary types from the DDIC such as data elements that are
&quot;visible anywhere. You can also refer to CDS simple types that
&quot;constitute elementary data types.
DATA it_elem_7 TYPE TABLE OF timestamp WITH EMPTY KEY.
DATA it_elem_8 TYPE TABLE OF timestampl WITH EMPTY KEY.

&quot;Globally available elementary types in the public visibility section of
&quot;classes/interfaces
&quot;Elementary type declared in an interface
DATA it_elem_9 TYPE TABLE OF zdemo_abap_get_data_itf=&gt;occ_rate WITH EMPTY KEY.

&quot;Note the syntax options when referring to components. In the following
&quot;example, a database table field is referred to using the database table name,
&quot;the component selector and the name of a field having an elementary data type.
DATA it_elem_10 TYPE TABLE OF zdemo_abap_fli-carrid WITH EMPTY KEY.
&quot;CDS view entity component
DATA it_elem_11 TYPE TABLE OF zdemo_abap_carr_ve-url WITH EMPTY KEY.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Structured types &lt;/td&gt; &lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Internal tables declared using structured types
*&amp;---------------------------------------------------------------------*

&quot;Locally declared structured type
TYPES: BEGIN OF local_struct,
          comp1 TYPE i,
          comp2 TYPE string,
          comp3 TYPE c LENGTH 3,
        END OF local_struct.

DATA it_struc_1 TYPE SORTED TABLE OF local_struct WITH UNIQUE KEY comp1.

&quot;Globally available structured types
&quot;E.g. DDIC database tables
DATA it_struc_2 TYPE TABLE OF zdemo_abap_fli WITH EMPTY KEY.

&quot;CDS objects such as ...
&quot;CDS view entity
DATA it_struc_3 TYPE TABLE OF zdemo_abap_carr_ve WITH EMPTY KEY.
&quot;CDS abstract entity
DATA it_struc_4 TYPE TABLE OF zdemo_abap_abstract_ent WITH EMPTY KEY.
&quot;CDS table function
DATA it_struc_5 TYPE TABLE OF zdemo_abap_table_function WITH EMPTY KEY.

&quot;Globally available structured types in the public visibility section of
&quot;classes/interfaces
DATA it_struc_6 TYPE TABLE OF zcl_demo_abap_amdp=&gt;fli_struc WITH EMPTY KEY.

&quot;Many of the previous examples demonstrate flat line types.
&quot;Deep line types are also possible, that is, the line type
&quot;can include components such as strings, references, and internal
&quot;tables.
TYPES: BEGIN OF loc_deep_struct,
          key_field TYPE i,
          char      TYPE c LENGTH 10,
          str       TYPE string,
          dref      TYPE REF TO i,
          struct    TYPE zdemo_abap_flsch,
          str_tab   TYPE string_table,
          tab       TYPE TABLE OF zdemo_abap_flsch WITH EMPTY KEY,
        END OF loc_deep_struct.

DATA it_struc_7 TYPE TABLE OF loc_deep_struct WITH EMPTY KEY.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Table types &lt;/td&gt; &lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Internal tables declared using table types
*&amp;---------------------------------------------------------------------*

&quot;Locally declared table type (based on a locally declared structured type)
TYPES: BEGIN OF loc_struct,
          comp1 TYPE i,
          comp2 TYPE string,
          comp3 TYPE c LENGTH 3,
        END OF loc_struct,
        ty_tab_1 TYPE TABLE OF loc_struct WITH EMPTY KEY.

DATA it_tab_1 TYPE ty_tab_1.

&quot;Locally declared table type (based on a globally available structured type)
TYPES ty_tab_2 TYPE TABLE OF zdemo_abap_fli WITH EMPTY KEY.
DATA it_tab_2 TYPE ty_tab_2.

&quot;Globally available table types
&quot;The following examples show released table types. They have an
&quot;elementary line type.
DATA it_tab_3 TYPE string_table.
DATA it_tab_4 TYPE string_hashed_table.
DATA it_tab_5 TYPE xstring_table.

&quot;Globally available table types in the public visibility section of
&quot;classes/interfaces
DATA it_tab_6 TYPE zdemo_abap_get_data_itf=&gt;carr_tab.
DATA it_tab_7 TYPE zcl_demo_abap_amdp=&gt;flsch_tab.

&quot;Creating an internal table based on the table type of an existing
&quot;internal table
DATA it_tab_8 LIKE it_tab_7.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; References as line types &lt;/td&gt; &lt;td&gt;</p>
<pre><code>DATA it_ref_1 TYPE TABLE OF REF TO i.
DATA it_ref_2 TYPE TABLE OF REF TO string.
DATA it_ref_3 TYPE TABLE OF REF TO zdemo_abap_carr.

DATA it_ref_4 TYPE TABLE OF REF TO data.

&quot;Such a table can hold any data type
it_ref_4 = VALUE #( ( NEW i( 3 ) ) &quot;Elementary type
                    ( NEW string( `hello` ) ) &quot;Elementary type
                    ( NEW zdemo_abap_flsch( carrid = &#x27;XY&#x27; connid = &#x27;1234&#x27; ) ) &quot;Structured type
                    ( NEW string_table( ( `a` ) ( `b` ) ( `c` ) ) ) &quot;Table type
                  ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Creating Internal Tables By Inline Declaration</h3>
<pre><code>&quot;Table declared inline in the context of an assignment
&quot;The examples show the copying of a table including the content
&quot;on the fly and creating the table in one step. The data type of the
&quot;declared variable is determined by the right side.
DATA string_tab TYPE string_table.

DATA(it_a) = string_tab.
DATA(it_b) = it_a.

&quot;Using FINAL for creating immutable variables
FINAL(it_c) = it_a.
&quot;For example, it is not possible to modify such a table in the following position.
&quot;Find more information in the Data Types and Data Objects cheat sheet.
&quot;APPEND INITIAL LINE TO it_c.

&quot;As shown below and in other cheat sheets, constructor operators
&quot;are handy when creating internal tables in place. The following
&quot;examples uses the VALUE operator and an internal table type. You
&quot;may also use the NEW operator to create anonymous data objects.
DATA(it_d) = VALUE string_table( ( `aaa` )
                                 ( `bbb` ) ).

TYPES it_type TYPE TABLE OF zdemo_abap_carr with empty key.
DATA(it_e) = VALUE it_type( ( carrid = &#x27;XY&#x27; carrname = &#x27;XY Airlines&#x27; )
                            ( carrid = &#x27;YZ&#x27; carrname = &#x27;Air YZ&#x27; ) ).

&quot;Not providing any table lines means the table is initial
&quot;and has the same effect as the declaration of it_g.
DATA(it_f) = VALUE string_table( ).
DATA it_g TYPE string_table.

DATA(it_h) = VALUE it_type( ).
DATA it_i TYPE it_type.

&quot;Excursion
&quot;Table declared inline in the context of a SELECT statement;
&quot;a prior extra declaration of an internal table is not needed.
SELECT * FROM zdemo_abap_fli INTO TABLE @DATA(it_j).

&quot;Instead of
DATA it_k TYPE TABLE OF zdemo_abap_fli WITH EMPTY KEY.
SELECT * FROM zdemo_abap_fli INTO TABLE @it_k.

&quot;Using FINAL
SELECT * FROM zdemo_abap_fli INTO TABLE @FINAL(it_l).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Populating Internal Tables</h2>
<div class="note">[!NOTE] Various ABAP statements populate internal tables. The following sections cover a selection. ABAP SQL `SELECT`  statements are covered further down and in the [ABAP SQL](03_ABAP_SQL.md) cheat sheet.</div>
<h3>Copying Internal Tables</h3>
<p>To copy internal table content from one table to another, you can use the assignment operator. Such an assignment (without a constructor expression) deletes the existing content in the target internal table. The example below assumes that the source and target table have compatible line types. Using inline declaration is helpful to avoid an additional internal table declaration with an appropriate type.</p>
<pre><code>itab = itab2.

DATA(itab3) = itab.
FINAL(itab4) = itab.</code></pre>
<div class="note">[!NOTE] - Internal tables can only be assigned to internal tables. - Internal tables can be assigned to each other if their line types are [compatible](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencompatible_glosry.htm) or [convertible](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconvertible_glosry.htm). - An assignment can trigger an uncatchable exception if, for example, the target table is assigned a duplicate of a unique primary table key or secondary table key. - More information: [Conversion Rules for Internal Tables](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconversion_itab.htm)</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Using INSERT and APPEND Statements to Populate Internal Tables</h3>
<p>You can use the ABAP keywords <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapinsert_itab.htm"><code>INSERT</code></a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapappend.htm"><code>APPEND</code></a> to add lines to internal tables.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for notes on using &lt;code&gt;APPEND&lt;/code&gt; and &lt;code&gt;INSERT&lt;/code&gt;&lt;/summary&gt; &lt;!-- --&gt;</p>
<ul>
<li><code>APPEND</code> ...</li>
<li>always adds lines at the bottom of the internal table.</li>
<li>is particularly suitable for standard tables where lines are managed</li>
</ul>
<p>by an index. When the statement is used, the system field <code>sy-tabix</code> is set to the index of the recently added line. <code>sy-tabix</code> is always set to the index with respect to the primary table index.</p>
<ul>
<li>cannot be used for hashed tables. For sorted tables,</li>
</ul>
<p>lines are appended only if they match the sort order, and no duplicate entries are created if the primary table key is unique. Therefore, <code>INSERT</code> should be used when adding lines to sorted tables.</p>
<ul>
<li><code>INSERT</code> ...</li>
<li>can be used to add lines at a specific position in tables (by</li>
</ul>
<p>specifying the target index). In doing so, all the following lines are moved down one position.</p>
<ul>
<li>without specifying the position adds the lines at the bottom of</li>
</ul>
<p>the table in case of standard tables. However, when using <code>INSERT</code>, <code>sy-tabix</code> is not set unlike <code>APPEND</code>. In case of sorted tables, the line is automatically inserted at the correct position.</p>
<ul>
<li>With <code>INSERT</code>, you can specify the position in the internal table at which lines are inserted after <code>INTO</code>.</li>
<li><code>... INTO TABLE itab ...</code>: Line is inserted in ...</li>
<li>standard tables as last line (i.e. appended)</li>
<li>sorted tables in the sort order in accordance with primary key values</li>
<li>hashed table by the hash administration in accordance with primary key values</li>
<li><code>... INTO itab INDEX n</code>: Possible for index tables. The line is inserted before the line with the line number <code>n</code> in the primary table index.</li>
<li>Note: In the case of unique primary table keys, the table cannot have entries with duplicate</li>
</ul>
<p>keys. If a duplicate is inserted, the insertion fails and the system field <code>sy-subrc</code> is set to 4.</p>
<ul>
<li>What to use (for standard tables)? With the <code>INSERT</code> statement, you are prepared for a potential change of internal table definitions. It covers all table and key types. Consider potential issues when you change table/key types, and you use <code>APPEND</code> in your code. The ABAP cheat sheet examples often use <code>APPEND</code> statements, as standard tables are frequently used and those considerations are not relevant here. Additionally, explore constructor expressions for adding lines to internal tables.</li>
</ul>
<p>&lt;/details&gt; &lt;br&gt;</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Subject &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Adding a line to an internal table &lt;/td&gt; &lt;td&gt;</p>
<p>The example shows both a structure that is created using the <code>VALUE</code> operator as well as an existing structure that is added.</p>
<p>&lt;br&gt;</p>
<pre><code>APPEND VALUE #( comp1 = a comp2 = b ... ) TO itab.
APPEND struc TO itab.

INSERT VALUE #( comp1 = a comp2 = b ... ) INTO TABLE itab.
INSERT struc INTO TABLE itab.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Adding an initial line &lt;/td&gt; &lt;td&gt;</p>
<p>... to an internal table without providing any field values. &lt;br&gt;</p>
<pre><code>APPEND INITIAL LINE TO itab.

INSERT INITIAL LINE INTO TABLE itab.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Adding a line and assigning the added line to a field symbol or data reference variable &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;When inserting single lines, you can specify the optional additions 
&quot;ASSIGNING and REFERENCE INTO. If the insertion is successful, the 
&quot;line is assigned to a field symbol or a data reference variable.
&quot;The targets can also be created inline. 
APPEND VALUE #( comp1 = a comp2 = b ... ) TO itab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
APPEND INITIAL LINE TO itab ASSIGNING &lt;fs&gt;.
INSERT INITIAL LINE INTO TABLE itab REFERENCE INTO DATA(dref).

&quot;Note: Once assigned, you can then, for example, address individual components
&quot;of the (initial) line and assign values, e.g. &lt;fs&gt;-comp = ... or dref-&gt;comp = ....</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Adding all lines from another internal table &lt;/td&gt; &lt;td&gt;</p>
<pre><code>APPEND LINES OF itab2 TO itab.

INSERT LINES OF itab2 INTO TABLE itab.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Adding multiple lines from another internal table with a specified index range &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>Both <code>FROM</code> and <code>TO</code> are not mandatory in one statement. it is possible to use only one of them.</li>
<li>If you use only ...</li>
<li><code>FROM</code>, all lines up to the last table entry are respected.</li>
<li><code>TO</code>, all lines starting with the first table entry are respected.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>APPEND LINES OF itab2 FROM 3 TO 5 TO itab.

APPEND LINES OF itab2 FROM 3 TO itab.

APPEND LINES OF itab2 TO 7 TO itab.

INSERT LINES OF itab2 FROM 5 TO 9 INTO TABLE itab.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Inserting one line represented by a structure or multiple lines from another internal table at a specific position &lt;/td&gt; &lt;td&gt;</p>
<p><code>FROM</code> and <code>TO</code> can be used here, too.</p>
<p>&lt;br&gt;</p>
<pre><code>INSERT struc INTO itab2 INDEX i.

INSERT LINES OF itab2 INTO itab INDEX i.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Creating and Populating Internal Tables Using Constructor Expressions</h3>
<p>The constructor expressions can be specified in/with various positions/statements in ABAP. In most of the following snippets, simple assignments are demonstrated.</p>
<div class="note">[!NOTE] The following sections cover a selection. There are more constructor expressions used in the context of internal tables (e.g. for creating internal tables). Find more details in the [Constructor Expressions](05_Constructor_Expressions.md) cheat sheet.</div>
<h4>VALUE Operator</h4>
<p>As mentioned above, table lines that are constructed inline as arguments to the <code>VALUE</code> operator, for example, can be added to internal tables. In the following cases, internal tables are populated using constructor expressions in the context of <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenassignment_glosry.htm">assignments</a>.</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Subject &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Populating an existing internal table by assigning an internal table that is constructed inline &lt;/td&gt; &lt;td&gt;</p>
<p>In the example below, the internal table is populated by assigning an internal table that is constructed inline with the <code>VALUE</code> operator. The inline constructed table has two lines. <code>line</code> represents an existing structure with a compatible line type. The other line is constructed inline.</p>
<div class="note">[!NOTE] - The extra pair of parentheses represents a table line. The `#` character indicates that the line type can be derived from the context. The assignment deletes the existing content of the internal table on the left side. - The existing content of the internal table is deleted, and the new content, which is created in place, is added.</div>
<p>&lt;br&gt;</p>
<pre><code>itab = VALUE #( ( line )
¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†( comp1 = a comp2 = b ...¬†¬†) ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Creating an internal table by inline declaration and adding lines with a constructor expression &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Internal table type
TYPES it_type LIKE itab.

&quot;Inline declaration
&quot;The # character would not be possible here since the line type
&quot;cannot be derived from the context.
DATA(it_in) = VALUE it_type( ( comp1 = a comp2 = b ... )
                             ( comp1 = c comp2 = d ...  ) ).

&quot;Creating string tables
DATA(str_tab_a) = VALUE string_table( ( `Hallo` ) ( `World` ) ).                            
DATA(str_tab_b) = VALUE string_table( ).                            
&quot;In the previous statement, the internal table is declared
&quot;inline, however, no content, no table lines are provided. 
&quot;This means that an initial string table was created. This 
&quot;way, the statement has the same effect as the following 
&quot;statement.
DATA str_tab_c TYPE string_table.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;BASE&lt;/code&gt; addition: Adding new lines without deleting existing content &lt;/td&gt; &lt;td&gt;</p>
<p>When you use the above assignments to an existing internal table (<code>itab = ...</code>), the internal table is initialized and the existing content is deleted. To add new lines without deleting existing content, use the  <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenvalue_constructor_params_itab.htm"><code>BASE</code></a> addition.</p>
<p>&lt;br&gt;</p>
<pre><code>itab = VALUE #( BASE itab ( comp1 = a comp2 = b ... )
¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†( comp1 = c comp2 = d ... ) ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;LINES OF&lt;/code&gt; addition: Adding lines of other tables &lt;/td&gt; &lt;td&gt;</p>
<p>... using the <code>LINES OF</code> addition to the <code>VALUE</code> operator. Without the <code>BASE</code> addition, the existing content is deleted. It is assumed that the line type of the source table is compatible with that of the target table. You have multiple syntax options following the <code>LINES OF</code> addition, e.g. you can further determine and restrict the lines to be added using <code>FROM</code> and <code>TO</code>. &lt;br&gt;</p>
<pre><code>itab = VALUE #( ( comp1 = a comp2 = b ...)
¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†( comp1 = a comp2 = b ...)
                &quot;All lines
¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†( LINES OF itab2 )
                &quot;More syntax options
                ( LINES OF itab3 FROM 2 TO 5 )
                ( LINES OF itab4 FROM 3 )
                ( LINES OF itab5 TO 7 )
                ( LINES OF itab6 STEP 2 )
                ( LINES OF itab7 USING KEY primary_key FROM 3 TO 6 )
¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†¬†... ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Iteration expressions with &lt;code&gt;FOR&lt;/code&gt; &lt;/td&gt; &lt;td&gt;</p>
<p>Using the <code>VALUE</code> operator and iteration expressions with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfor.htm"><code>FOR</code></a>, you can create content of an internal table by evaluating one or more source tables. The expressions are covered in the <a href="05_Constructor_Expressions.md">Constructor Expressions</a> cheat sheet. &lt;br&gt;</p>
<pre><code>TYPES ty_int_tab TYPE TABLE OF i WITH EMPTY KEY.
DATA(int_table_a) = VALUE ty_int_tab( ( 1 ) ( 2 ) ( 3 ) ( 4 ) ( 5 ) ).
DATA int_table_b TYPE ty_int_tab.
int_table_b = VALUE #( FOR wa_b IN int_table_a ( wa_b * 2 ) ).
&quot;Table Content: 2 / 4 / 6 / 8 / 10</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>CORRESPONDING Operator and MOVE-CORRESPONDING Statements</h4>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Subject &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Copying the content of another (incompatible) internal table &lt;/td&gt; &lt;td&gt;</p>
<p>... using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_expr_corresponding.htm"><code>CORRESPONDING</code></a> operator.</p>
<ul>
<li>Note that the existing content is deleted.</li>
<li>As an alternative to the <code>CORRESPONDING</code> operator, you can use <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmove-corresponding.htm"><code>MOVE-CORRESPONDING</code></a> statements.</li>
<li>The operator is particularly useful for incompatible source and target types. Value assignments are made based on identical components in the source and target. As shown further down, you can also specify mapping rules using the <code>MAPPING</code> addition.</li>
<li>The example assumes that the line types of the source and target table are not compatible. However, if the line types are compatible, the syntax will also work.</li>
<li>Several additions are possible. They can also be combined. Check the ABAP Keyword Documentation.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>itab = CORRESPONDING #( itab3 ).

MOVE-CORRESPONDING itab3 TO itab.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; &lt;code&gt;BASE&lt;/code&gt;/&lt;code&gt;KEEPING TARGET LINES&lt;/code&gt; additions: Copying content and retaining existing content &lt;/td&gt; &lt;td&gt;</p>
<p>... using the <code>CORRESPONDING</code> operator. The <code>KEEPING TARGET LINES</code> addition of the <code>MOVE-CORRESPONDING</code> statement preserves the table content. &lt;br&gt;</p>
<pre><code>itab = CORRESPONDING #( BASE ( itab ) itab3 ).

MOVE-CORRESPONDING itab3 TO itab KEEPING TARGET LINES.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;MAPPING&lt;/code&gt; addition: Assigning components using mapping relationships &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>You can use the <code>MAPPING</code> addition of the <code>CORRESPONDING</code> operator to specify components of a source table that are assigned to the components of a target table in mapping relationships.</li>
<li>For elementary components, the assignment is made according to the associated assignment rules.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>itab = CORRESPONDING #( itab3 MAPPING a = c b = d ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;EXCEPT&lt;/code&gt; addition: Excluding components from the assignment &lt;/td&gt; &lt;td&gt;</p>
<p>... using the <code>EXCEPT</code> addition to the <code>CORRESPONDING</code> operator.</p>
<ul>
<li>This is particularly useful if there are identically named components in the source and target tables that are not compatible or convertible. You can avoid syntax errors or runtime errors.</li>
<li>Instead of a component list, <code>EXCEPT</code> can also be followed by <code>*</code> to exclude all components that are not mentioned in a previous mapping of components.</li>
<li>If <code>EXCEPT *</code> is used without the <code>MAPPING</code> addition, all components remain initial.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>itab = CORRESPONDING #( itab3 EXCEPT e ).

itab = CORRESPONDING #( itab3 EXCEPT * ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; &lt;code&gt;DISCARDING DUPLICATES&lt;/code&gt; addition: Preventing runtime errors when duplicate lines are assigned &lt;/td&gt; &lt;td&gt;</p>
<p>... to the target table that is defined to accept only unique keys using the <code>DISCARDING DUPLICATES</code> addition of the <code>CORRESPONDING</code> operator.</p>
<ul>
<li>In this case, the duplicate line is ignored in the source table.</li>
<li>The addition can also be specified with <code>MAPPING ...</code>.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>itab = CORRESPONDING #( itab2 DISCARDING DUPLICATES ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; &lt;code&gt;DEEP&lt;/code&gt;/&lt;code&gt;EXPANDING NESTED TABLES&lt;/code&gt; additions: Copying data from deep internal tables &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>A deep internal table is a table with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendeep_glosry.htm">deep</a> line type, which means the table can itself contain internal tables as components, among others.</li>
<li>The <code>BASE</code> addition does not delete the existing content.</li>
<li>See also the alternative <code>MOVE-CORRESPONDING</code> statements that use the <code>EXPANDING NESTED TABLES</code> addition.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>itab_nested2 = CORRESPONDING #( DEEP itab_nested1 ).

itab_nested2 = CORRESPONDING #( DEEP BASE ( itab_nested2 ) itab_nested1 ).

MOVE-CORRESPONDING itab_nested1 TO itab_nested2 EXPANDING NESTED TABLES.

MOVE-CORRESPONDING itab_nested1 TO itab_nested2 EXPANDING NESTED TABLES KEEPING TARGET LINES.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; &lt;code&gt;CORRESPONDING&lt;/code&gt; with lookup table &lt;/td&gt; &lt;td&gt;</p>
<pre><code>    ... CORRESPONDING type|#( itab FROM lookup_tab 
                              USING [KEY key_name] a1 = b1 a2 = b2 ... 
                              [MAPPING ...] ) ...</code></pre>
<ul>
<li>You can construct an internal table by joining an internal table and a lookup table and comparing their components.</li>
<li>Syntax pattern of the variant:</li>
<li>Find more information and code snippets in the <a href="05_Constructor_Expressions.md#corresponding-with-lookup-table">Constructor Expressions</a> cheat sheet.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<div class="note">[!NOTE] The `CL_ABAP_CORRESPONDING` class can be used for assignments. Find an example in the [Released ABAP Classes](22_Released_ABAP_Classes.md) cheat sheet.</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>FILTER Operator</h4>
<p>To create an internal table by copying data from another internal table and filtering out lines that do not meet the <code>WHERE</code> condition, you can use the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_expression_filter.htm"><code>FILTER</code> operator</a>.</p>
<ul>
<li>The</li>
</ul>
<p><code>FILTER</code> operator constructs an internal table according to a specified type (which can be an explicitly specified, non-generic table type or the <code>#</code> character as a symbol for the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenoperand_type_glosry.htm">operand type</a> before the first parenthesis).</p>
<ul>
<li>The lines for the new internal table are taken from an</li>
</ul>
<p>existing internal table based on conditions specified in a <code>WHERE</code> clause. Note that the table type of the existing internal table must be convertible into the specified target type.</p>
<ul>
<li>The conditions can be based on either single values or a [filter</li>
</ul>
<p>table](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_expr_filter_table.htm).</p>
<ul>
<li>Additions:</li>
</ul>
<table>
<thead><tr>
<th>Addition</th>
<th>Details</th>
</tr></thead>
<tbody>
<tr>
<td><code>USING KEY</code></td>
<td>Specifies the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_key_glosry.htm">table key</a> used to evaluate the <code>WHERE</code> condition: either a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensorted_key_glosry.htm">sorted key</a> or a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenhash_key_glosry.htm">hash key</a>. If the internal table does not have either of these, it must have a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensecondary_table_key_glosry.htm">secondary table key</a>, which must be specified after <code>USING KEY</code>.</td>
</tr>
<tr>
<td><code>EXCEPT</code></td>
<td>Specifying <code>EXCEPT</code> means that those lines of the existing table are used that do not meet the condition specified in the <code>WHERE</code> clause. If <code>EXCEPT</code> is not specified, those lines of the existing table that meet the condition are used.</td>
</tr>
</tbody>
</table>
<pre><code>&quot;FILTER on conditions based on single values
&quot;Assumption: The component num is of type i.
DATA itab1 TYPE SORTED TABLE OF struc WITH NON-UNIQUE KEY num.
DATA itab2 TYPE STANDARD TABLE OF struc WITH NON-UNIQUE SORTED KEY sec_key COMPONENTS num.
DATA itab3 TYPE HASHED TABLE OF struc WITH UNIQUE KEY num.

&quot;The lines meeting the condition are respected.
&quot;Note: The source table must have at least one sorted or hashed key.
&quot;Here, the primary key is used
DATA(f1) = FILTER #( itab1 WHERE num &gt;= 3 ).

&quot;USING KEY primary_key explicitly specified; same as above
DATA(f2) = FILTER #( itab1 USING KEY primary_key WHERE num &gt;= 3 ).

&quot;EXCEPT addition
DATA(f3) = FILTER #( itab1 EXCEPT WHERE num &gt;= 3 ).
DATA(f4) = FILTER #( itab1 EXCEPT USING KEY primary_key WHERE num &gt;= 3 ).

&quot;Secondary table key specified after USING KEY
DATA(f5) = FILTER #( itab2 USING KEY sec_key WHERE num &gt;= 4 ).
DATA(f6) = FILTER #( itab2 EXCEPT USING KEY sec_key WHERE num &gt;= 3 ).

&quot;Note: In case of a hash key, exactly one comparison expression for each key component is allowed;
&quot;only = as comparison operator possible.
DATA(f7) = FILTER #( itab3 WHERE num = 3 ).

&quot;Using a filter table
&quot;In the WHERE condition, the columns of source and filter table are compared. 
&quot;Those lines in the source table are used for which at least one line in the filter 
&quot;table meets the condition. EXCEPT and USING KEY are also possible.

DATA filter_tab1 TYPE SORTED TABLE OF i
  WITH NON-UNIQUE KEY table_line.

DATA filter_tab2 TYPE STANDARD TABLE OF i
  WITH EMPTY KEY
  WITH UNIQUE SORTED KEY line COMPONENTS table_line.

DATA(f8) = FILTER #( itab1 IN filter_tab1 WHERE num = table_line ).

&quot;EXCEPT addition
DATA(f9) = FILTER #( itab1 EXCEPT IN filter_tab1 WHERE num = table_line ).

&quot;USING KEY is specified for the filter table
DATA(f10) = FILTER #( itab2 IN filter_tab2 USING KEY line WHERE num = table_line ).

&quot;USING KEY is specified for the source table, including EXCEPT
DATA(f11) = FILTER #( itab2 USING KEY sec_key EXCEPT IN filter_tab2 WHERE num = table_line ).</code></pre>
<p>Examples:</p>
<div class="note">[!NOTE] More constructor expressions are available to deal with internal tables, for example the `REDUCE` operator. Find more information and examples in the [Constructor Expression cheat sheet](05_Constructor_Expressions.md).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>NEW Operator</h4>
<p>Using the instance operator <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_expression_new.htm"><code>NEW</code></a>, you can create <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenanonymous_data_object_glosry.htm">anonymous data objects</a>, such as anonymous internal tables. You can access the lines, components or the entire data objects by <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendereferencing_operat_glosry.htm">dereferencing</a>. For more information, refer to the  <a href="06_Dynamic_Programming.md">Dynamic Programming</a> and <a href="05_Constructor_Expressions.md">Constructor Expressions</a> cheat sheets.</p>
<pre><code>TYPES: BEGIN OF s,
          a TYPE c LENGTH 3,
          b TYPE i,
        END OF s,
        tab_type TYPE TABLE OF s WITH EMPTY KEY.

&quot;Creating and populating an anonymous data object
DATA(dref_tab) = NEW tab_type( ( a = &#x27;aaa&#x27; b = 1 )
                               ( a = &#x27;bbb&#x27; b = 2 ) ).

&quot;Access by dereferencing
DATA(copy_deref_itab) = dref_tab-&gt;*.
DATA(read_line) = dref_tab-&gt;*[ 2 ].
DATA(read_comp) = dref_tab-&gt;*[ 1 ]-a.
dref_tab-&gt;*[ 1 ]-a = &#x27;zzz&#x27;.
ASSERT dref_tab-&gt;*[ 1 ]-a = &#x27;zzz&#x27;.
INSERT VALUE s( a = &#x27;yyy&#x27; b = 3 ) INTO TABLE dref_tab-&gt;*.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Example: Exploring Populating Internal Tables</h3>
<p>Expand the following collapsible section for example code. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console, but only for few data objects. You may want to set a break point at the earliest possible position and walk through the example in the debugger. This will allow you to double-click on data objects and observe how the different statements affect their contents.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for a code example&lt;/summary&gt; &lt;!-- --&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;Populating internal tables by adding a line (structure) using
    &quot;APPEND ... TO/INSERT ... INTO

    TYPES: BEGIN OF st_a,
             num  TYPE i,
             str  TYPE string,
             char TYPE c LENGTH 2,
           END OF st_a,
           ty_tab_a TYPE TABLE OF st_a WITH EMPTY KEY.
    DATA it_a TYPE ty_tab_a.

    &quot;Adding a line created inline
    APPEND VALUE #( num = 1 str = `A` char = &#x27;bb&#x27; ) TO it_a.
    INSERT VALUE #( num = 2 str = `C` char = &#x27;dd&#x27; ) INTO TABLE it_a.

    &quot;Adding an existing line
    DATA(struc_a) = VALUE st_a( num = 3 str =  `E` char = &#x27;ff&#x27; ).
    &quot;Structure whose components are assigned individually using the
    &quot;structure component selector
    DATA struc_b TYPE st_a.
    struc_b-num = 4.
    struc_b-str =  `G`.
    struc_b-char = &#x27;hh&#x27;.

    APPEND struc_a TO it_a.
    INSERT struc_b INTO TABLE it_a.

**********************************************************************
    &quot;INITIAL LINE addition
    &quot;Adding an initial line

    APPEND INITIAL LINE TO it_a.
    INSERT INITIAL LINE INTO TABLE it_a.

**********************************************************************
    &quot;ASSIGNING/REFERENCE INTO additions

    &quot;Adding a line and assigning the added line to a field symbol or
    &quot;data reference variable

    &quot;Creating field symbol inline
    APPEND VALUE st_a( num = 5 str =  `I` char = &#x27;jj&#x27; ) TO it_a ASSIGNING FIELD-SYMBOL(&lt;fs_a&gt;).
    &quot;Addressing individual components
    ASSERT &lt;fs_a&gt;-num = 5.
    &lt;fs_a&gt;-num = 123.

    FIELD-SYMBOLS &lt;fs_b&gt; TYPE st_a.
    DATA(struc_c) = VALUE st_a( num = 6 ).
    INSERT struc_c INTO TABLE it_a ASSIGNING &lt;fs_b&gt;.
    &lt;fs_b&gt;-str =  `K`.

    &quot;Adding an initial line
    &quot;The examples use data reference variables.
    &quot;Using inline declaration
    APPEND INITIAL LINE TO it_a REFERENCE INTO DATA(dref_a).
    dref_a-&gt;num = 7.
    DATA dref_b TYPE REF TO st_a.
    INSERT INITIAL LINE INTO TABLE it_a REFERENCE INTO dref_b.
    dref_b-&gt;num = 8.

    DO 3 TIMES.
      APPEND INITIAL LINE TO it_a REFERENCE INTO dref_b.
      dref_b-&gt;* = VALUE #( num = sy-index str = sy-index char = sy-index ).
    ENDDO.

**********************************************************************
    &quot;LINES OF addition
    &quot;Adding all lines from another internal table

    &quot;Adding lines to one internal table that are all added to
    &quot;another one
    DATA it_b TYPE ty_tab_a.
    INSERT VALUE #( num = 99 str =  `L` char = &#x27;mm&#x27; ) INTO TABLE it_b.
    INSERT VALUE #( num = 100 str =  `N` char = &#x27;oo&#x27; ) INTO TABLE it_b.

    APPEND LINES OF it_b TO it_a.
    INSERT LINES OF it_b INTO TABLE it_a.

**********************************************************************
    &quot;Adding multiple lines from another internal table with
    &quot;a specified index range

    APPEND LINES OF it_a FROM 5 TO 7 TO it_b.
    APPEND LINES OF it_a FROM 12 TO it_b. &quot;further lines up to the last line
    APPEND LINES OF it_a TO 3 TO it_b.  &quot;all lines from the start up to the specified index
    INSERT LINES OF it_a FROM 8 TO 10 INTO TABLE it_b.

**********************************************************************
    &quot;INDEX addition
    &quot;Inserting one line or multiple lines from another internal table at
    &quot;a specific position. To be used for index tables.

    INSERT VALUE #( num = 9 str =  `P` char = &#x27;qq&#x27; ) INTO it_b INDEX 2.
    INSERT LINES OF VALUE ty_tab_a( ( num = 10 str =  `R` ) ( num = 11 str =  `S` ) ) INTO it_b INDEX 5.
    &quot;FROM and TO can also be used
    INSERT LINES OF it_a FROM 1 TO 3 INTO it_b INDEX 1.

**********************************************************************
    &quot;Using the VALUE operator

    DATA(struc_d) = VALUE st_a( num = 11 str =  `T` char = &#x27;uu&#x27; ).
    DATA it_c TYPE ty_tab_a.

    &quot;Adding an existing line and a line created inline
    it_c = VALUE #( ( struc_d )
                    ( num = 11 str =  `V` char = &#x27;ww&#x27; ) ).

    &quot;Creating an internal table by inline declaration and adding lines with VALUE
    DATA(it_d) = VALUE ty_tab_a( ( num = 12 str =  `X` char = &#x27;yy&#x27; )
                                 ( num = 13 str =  `Z` char = &#x27;aa&#x27; )
                                 ( struc_d ) ).

    &quot;******* BASE addition *******
    &quot;Adding new lines without deleting existing content
    it_d =  VALUE #( BASE it_d ( num = 14 str =  `B` char = &#x27;cc&#x27; )
                               ( num = 15 str =  `D` char = &#x27;ee&#x27; ) ).

    &quot;******* LINES OF addition *******
    &quot;Adding lines of other tables
    it_d = VALUE #( ( LINES OF it_c ) ). &quot;No BASE addition, existing content is deleted

    it_c = VALUE #( BASE it_c ( num = 16 str =  `F` char = &#x27;gg&#x27; )
                              ( LINES OF it_d ) ).

**********************************************************************
    &quot;CORRESPONDING operator / MOVE-CORRESPONDING statements

    &quot;Creating and populating demo internal tables
    TYPES: BEGIN OF st_b,
             num    TYPE i,
             char   TYPE c LENGTH 2,
             comp_a TYPE string,
           END OF st_b,
           ty_tab_b TYPE TABLE OF st_b WITH EMPTY KEY,
           BEGIN OF st_c,
             num    TYPE i,
             char   TYPE c LENGTH 2,
             comp_b TYPE string,
           END OF st_c,
           ty_tab_c TYPE TABLE OF st_c WITH EMPTY KEY.

    DATA(it_e_original) = VALUE ty_tab_b( ( num = 1 char = &#x27;aa&#x27; comp_a = `B` )
                                          ( num = 2 char = &#x27;cc&#x27; comp_a = `D` ) ).
    DATA(it_f_original) = VALUE ty_tab_c( ( num = 3 char = &#x27;ee&#x27; comp_b = `F` )
                                          ( num = 4 char = &#x27;gg&#x27; comp_b = `H` ) ).

    DATA(it_e) = it_e_original.
    DATA(it_f) = it_f_original.

    &quot;Copying the content of another internal table respecting identically
    &quot;named components
    &quot;it_f -&gt; it_e
    it_e = CORRESPONDING #( it_f ).

    &quot;it_e -&gt; it_f
    it_e = it_e_original.
    MOVE-CORRESPONDING it_e TO it_f.

    &quot;******* BASE addition / /KEEPING TARGET LINES addition *******
    &quot;Copying content and retaining existing content
    it_e = it_e_original.
    it_f = it_f_original.

    it_e = CORRESPONDING #( BASE ( it_e ) it_f ).

    it_e = it_e_original.
    it_f = it_f_original.
    MOVE-CORRESPONDING it_e TO it_f KEEPING TARGET LINES.

    &quot;******* MAPPING addition *******
    &quot;Assigning components using mapping relationships
    it_e = it_e_original.
    it_f = it_f_original.

    it_e = CORRESPONDING #( it_f MAPPING comp_a = comp_b ).
    it_e = it_e_original.
    it_f = CORRESPONDING #( BASE ( it_f ) it_e MAPPING comp_b = comp_a ). &quot;Retaining content with BASE

    &quot;******* EXCEPT addition *******
    &quot;Excluding components from the assignment
    it_e = it_e_original.
    it_f = it_f_original.

    it_e = CORRESPONDING #( it_f EXCEPT char ).

    it_e = it_e_original.
    it_f = CORRESPONDING #( it_e MAPPING comp_b = comp_a EXCEPT * ). &quot;Mapping components

    &quot;******* DISCARDING DUPLICATES addition *******
    &quot;Preventing runtime errors when duplicate lines are assigned
    it_e = VALUE #( ( num = 1 char = &#x27;aa&#x27; comp_a = `B` )
                    ( num = 1 char = &#x27;cc&#x27; comp_a = `D` ) ).

    DATA it_g TYPE SORTED TABLE OF st_b WITH UNIQUE KEY num.

    &quot;The statement commented out raises the runtime error ITAB_DUPLICATE_KEY.
    &quot;it_g = CORRESPONDING #( it_e ).
    it_g = CORRESPONDING #( it_e DISCARDING DUPLICATES ).

    &quot;******* DEEP addition / EXPANDING NESTED TABLES addition *******
    &quot;Handling deep components such as nested internal tables

    &quot;Creating and populating demo internal tables
    TYPES: BEGIN OF st_d,
             char_a TYPE c LENGTH 2,
             char_b TYPE c LENGTH 2,
           END OF st_d,
           BEGIN OF st_e,
             char_b TYPE c LENGTH 2,
             char_c TYPE c LENGTH 2,
           END OF st_e,
           BEGIN OF st_f,
             comp1 TYPE c LENGTH 2,
             comp2 TYPE c LENGTH 2,
             comp3 TYPE TABLE OF st_d WITH EMPTY KEY,
           END OF st_f,
           BEGIN OF st_g,
             comp2 TYPE c LENGTH 2,
             comp3 TYPE TABLE OF st_e WITH EMPTY KEY,
             comp4 TYPE c LENGTH 2,
           END OF st_g,
           ty_tab_d TYPE TABLE OF st_f WITH EMPTY KEY,
           ty_tab_e TYPE TABLE OF st_g WITH EMPTY KEY.

    DATA(it_h_original) = VALUE ty_tab_d(
      ( comp1 = &#x27;a1&#x27; comp2 = &#x27;a2&#x27; comp3 = VALUE #( ( char_a = &#x27;a3&#x27; char_b = &#x27;a4&#x27; ) ( char_a = &#x27;a5&#x27; char_b = &#x27;a6&#x27; ) ) )
      ( comp1 = &#x27;b1&#x27; comp2 = &#x27;b2&#x27; comp3 = VALUE #( ( char_a = &#x27;b3&#x27; char_b = &#x27;b4&#x27; ) ( char_a = &#x27;b5&#x27; char_b = &#x27;b6&#x27; ) ) ) ).

    DATA(it_i_original) = VALUE ty_tab_e(
      ( comp2 = &#x27;c1&#x27; comp3 = VALUE #( ( char_b = &#x27;c2&#x27; char_c = &#x27;c3&#x27; ) ( char_b = &#x27;c4&#x27; char_c = &#x27;c5&#x27; ) ) comp4 = &#x27;c6&#x27; )
      ( comp2 = &#x27;d1&#x27; comp3 = VALUE #( ( char_b = &#x27;d2&#x27; char_c = &#x27;d3&#x27; ) ( char_b = &#x27;d4&#x27; char_c = &#x27;d5&#x27; ) ) comp4 = &#x27;d6&#x27; ) ).

    DATA(it_h) = it_h_original.
    DATA(it_i) = it_i_original.

    &quot;Compare the output of the examples
    out-&gt;write( `******* CORRESPONDING *******` ).
    &quot;Note: The following example uses just CORRESPONDING. The outcome of the assignment
    &quot;is a different one compared to using DEEP. Refer to the ABAP Keyword Documentation
    &quot;for more details.
    it_h = CORRESPONDING #( it_i ).
    out-&gt;write( it_h ).

    out-&gt;write( `******* CORRESPONDING ... DEEP *******` ).
    it_h = CORRESPONDING #( DEEP it_i ).
    out-&gt;write( it_h ).

    out-&gt;write( `******* CORRESPONDING ... DEEP BASE *******` ).
    it_h = it_h_original.
    it_h = CORRESPONDING #( DEEP BASE ( it_h ) it_i ).
    out-&gt;write( it_h ).

    out-&gt;write( `******* MOVE-CORRESPONDING ... EXPANDING NESTED TABLES *******` ).
    it_h = it_h_original.
    MOVE-CORRESPONDING it_i TO it_h EXPANDING NESTED TABLES.
    out-&gt;write( it_h ).

    out-&gt;write( `******* MOVE-CORRESPONDING ... EXPANDING NESTED TABLES KEEPING TARGET LINES *******` ).
    it_h = it_h_original.
    MOVE-CORRESPONDING it_i TO it_h EXPANDING NESTED TABLES KEEPING TARGET LINES.
    out-&gt;write( it_h ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Reading Single Lines from Internal Tables</h2>
<p>There are three different ways to specify the line to read:</p>
<ul>
<li>by index (only index tables)</li>
<li>by table keys (only tables for which a key is defined)</li>
<li>by free key</li>
</ul>
<p>The following code snippets include <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapread_table.htm"><code>READ TABLE</code></a> statements and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_expressions.htm">table expressions</a> to read from internal tables. Note that you can also use ABAP SQL <code>SELECT</code> statements to read from internal tables. This is covered further down.</p>
<h3>Determining the Target Area when Reading Single Lines in READ TABLE Statements</h3>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Target Area &lt;/td&gt; &lt;td&gt; Notes &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Data object</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to copy a line to a data object using the addition <code>INTO</code>.</li>
<li>After the copying, the line found exists separately in the internal table and in the data object.</li>
<li>If you change the data object or the table line, the change does not affect the other.</li>
<li>However, you can modify the copied table line and use a <code>MODIFY</code> statement to modify the table based on the modified table line (see below).</li>
<li>The <code>TRANSPORTING</code> addition specifies which components to copy. If it is not specified, all components are respected.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;dobj must have the table&#x27;s line type
READ TABLE itab INTO dobj ...   

READ TABLE itab INTO DATA(dobj_inl) ...

READ TABLE itab INTO ... TRANSPORTING comp1 [comp2 ... ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Field symbol</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Assigning a line to a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfield_symbol_glosry.htm">field symbol</a>,</li>
</ul>
<p>for example, using an inline declaration (<code>... ASSIGNING FIELD-SYMBOL(&lt;fs&gt;) ...</code>).</p>
<ul>
<li>When you then access the field symbol, it means that you access the found table line. There is no actual copying of content. Therefore, modifying the field symbol means</li>
</ul>
<p>modifying the table line directly.</p>
<ul>
<li>Note that you cannot use the <code>TRANSPORTING</code> addition since the entire table is assigned to the field symbol.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;The field symbol must have an appropriate type.
READ TABLE itab ASSIGNING &lt;fs1&gt; ...                 

&quot;Inline declaration
READ TABLE itab ASSIGNING FIELD-SYMBOL(&lt;fs2&gt;) ...   </code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Data reference variable</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Reading a line into a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendata_reference_variable_glosry.htm">data reference variable</a> using <code>REFERENCE INTO</code>.</li>
<li>In this case, no copying takes place.</li>
<li>If you want to address the line, you must first <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendereferencing_operat_glosry.htm">dereference</a> the data reference.</li>
<li>You cannot use the addition <code>TRANSPORTING</code>.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;The data reference variable must have an appropriate type.
READ TABLE itab REFERENCE INTO dref ...

&quot;Inline declaration
READ TABLE itab REFERENCE INTO DATA(dref_inl) ...</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<div class="note">[!TIP] Which to use then? Since all syntax options basically provide similar</div>
<p>functionality, your use case, the performance or readability of the code may play a role. For more information, see the programming guidelines for the <a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abentable_output_guidl.htm">target area (F1 docu for standard ABAP)</a>. A use case for <code>INTO dobj</code> is when the table should not be changed using the copied table line. However, copying comes at a performance cost. Imagine that your table contains many columns or nested components. In this case, it is advisable not to copy at all (although you can use the <code>TRANSPORTING</code> addition to restrict the fields to be copied).</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Reading Single Lines by Index</h3>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Statement &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;READ TABLE&lt;/code&gt; &lt;/td&gt; &lt;td&gt;</p>
<p>The following example shows <code>READ TABLE</code> statements to read a single line from an internal table by specifying the index. You can use the addition <code>USING KEY</code> to specify a table key and thus explicitly determine the table index to use. If the table has a sorted secondary key, the addition can be specified and the line to be read is then determined from its secondary table index. If the primary table key is specified by its name <code>primary_key</code>, the table must be an index table, and the behavior is the same as if <code>USING KEY</code> was not specified. Note that the examples only show reading into a work area. Other targets are possible as shown above.</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;Primary table index is used by default
READ TABLE itab INTO wa INDEX i.

&quot;Primary table index is used, primary key&#x27;s default name specified explicitly 
READ TABLE itab INTO wa INDEX i USING KEY primary_key.

&quot;Secondary table index is used (assuming a secondary table key sec_key exists) 
READ TABLE itab INTO wa INDEX i USING KEY sec_key.

&quot;Note: You can also use alias names for the keys if specified.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Table expressions &lt;/td&gt; &lt;td&gt;</p>
<p>Using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_expressions.htm">table expressions</a>, the read result is stored in a variable that can be declared inline. The number in the square brackets represents the index. A line that is not found results in an runtime error. To avoid an error, you can use a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abaptry.htm"><code>TRY ... CATCH ... ENDTRY.</code></a> block.</p>
<p>Find more information about syntax related to table expressions <a href="#table-expressions">further down</a>. &lt;br&gt;</p>
<pre><code>&quot;In the examples, integer values are specified for the index.
DATA(lv1) = itab[ 1 ].

TRY.
  DATA(lv2) = itab[ 2 ].
  CATCH cx_sy_itab_line_not_found.
  ...
ENDTRY.

DATA(lv3) = itab[ KEY primary_key INDEX 3 ].

&quot;Copying a table line via table expression and embedding in constructor expression
DATA(lv4) = VALUE #( itab[ 4 ] ).

&quot;Reading into data reference variable using the REF operator
DATA(lv5_ref) = REF #( itab[ 5 ] ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Avoiding an exception when using table expressions &lt;/td&gt; &lt;td&gt;</p>
<p>When you read a non-existent line using a table expression, you may not want to throw an exception. You can also embed the table expression in a constructor expression using the <code>OPTIONAL</code> addition. This way, an unsuccessful read will not trigger the exception. The result returned is a line with initial values. Alternatively, you can use the <code>DEFAULT</code> addition to return a default line in case of an unsuccessful read operation, which can also be another table expression or constructor expression.</p>
<p>&lt;br&gt;</p>
<pre><code>DATA(line1) = VALUE #( itab[ 6 ] OPTIONAL ).

DATA(line2) = VALUE #( itab[ 7 ] DEFAULT itab[ 1 ]  ).
DATA(line3) = VALUE #( itab[ 8 ] DEFAULT VALUE #( ) ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Reading Single Lines Using Table Keys</h3>
<p>Lines can be read by explicitly specifying the table keys or the alias names, if any.</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Statement &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;READ TABLE&lt;/code&gt; &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Example internal table with primary and secondary table key and alias names
&quot;Assumption: All components are of type i

DATA it TYPE SORTED TABLE OF struc
  WITH NON-UNIQUE KEY primary_key ALIAS pk COMPONENTS a b
  WITH NON-UNIQUE SORTED KEY sec_key ALIAS sk COMPONENTS c d.

&quot;Primary table key
READ TABLE it INTO wa WITH TABLE KEY primary_key COMPONENTS a = 1 b = 2.

&quot;Alias
READ TABLE it INTO wa WITH TABLE KEY pk COMPONENTS a = 1 b = 2.

&quot;Secondary table key
READ TABLE it INTO wa WITH TABLE KEY sec_key COMPONENTS c = 3 d = 4.

&quot;Alias
READ TABLE it INTO wa WITH TABLE KEY sk COMPONENTS c = 3 d = 4.

&quot;Reading a line based on keys specified in a work area
&quot;Work area containing primary and secondary table key values; the line type
&quot;must be compatible to the internal table
DATA(pr_keys) = VALUE struc( a = 1 b = 2 ).

DATA(sec_keys) = VALUE struc( c = 3 d = 4 ).

READ TABLE it FROM pr_keys INTO wa.

&quot;If USING KEY is not specified, the primary table key is used.
&quot;If it is used, the specified table key is used.
READ TABLE it FROM pr_keys USING KEY primary_key INTO wa.

READ TABLE it FROM sec_keys USING KEY sec_key INTO wa.

&quot;Alias
READ TABLE it FROM sec_keys USING KEY sk INTO wa.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Table expressions &lt;/td&gt; &lt;td&gt;</p>
<p>Find more information about syntax related to table expressions <a href="#table-expressions">further down</a>.</p>
<pre><code>line = it[ TABLE KEY primary_key COMPONENTS a = 1 b = 2 ].

&quot;The addition COMPONENTS is optional; same as above
line = it[ TABLE KEY primary_key a = 1 b = 2 ].

&quot;Primary key alias
line = it[ TABLE KEY pk a = 1 b = 2 ].

&quot;Secondary table key
line = it[ TABLE KEY sec_key c = 3 d = 4 ].

&quot;Secondary table key alias
line = it[ TABLE KEY sk c = 3 d = 4 ].

&quot;Note the section further down about just using KEY (instead of TABLE KEY).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Reading Single Lines Using a Free Key</h3>
<p>The specified components used as keys need not be part of a table key.</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Statement &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; &lt;code&gt;READ TABLE&lt;/code&gt; &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Note: Table keys are specified with the ... WITH TABLE KEY ... addition, 
&quot;free keys with ... WITH KEY ....
READ TABLE it INTO wa WITH KEY b = 2.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Table expressions &lt;/td&gt; &lt;td&gt;</p>
<pre><code>line = it[ b = 2 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Examples of Addressing Individual Components of Read Lines</h3>
<p>When reading single lines in general, you can also address individual components of the line using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencomponent_selector_glosry.htm">component selector</a> <code>-</code> (or the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_component_select_glosry.htm">object component selector</a> <code>-&gt;</code> or the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendereferencing_operat_glosry.htm">dereferencing operator</a> <code>-&gt;*</code> in the case of data reference variables).</p>
<p>You can also use <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapassign.htm"><code>ASSIGN</code></a> statements to assign components (and more) to <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfield_symbol_glosry.htm">field symbols</a>. See the <a href="#example-exploring-read-table-statements-and-table-expressions">Example: Exploring READ TABLE Statements and Table Expressions</a> below.</p>
<pre><code>DATA(comp1) = it[ b = 2 ]-c.

READ TABLE it INTO DATA(wa) WITH KEY b = 2.
DATA(comp2) = wa-c.

READ TABLE it ASSIGNING FIELD-SYMBOL(&lt;fs&gt;) WITH KEY b = 2.
DATA(comp3) = &lt;fs&gt;-c.

READ TABLE it REFERENCE INTO DATA(dref) WITH KEY b = 2.
DATA(comp4) = dref-&gt;c.

&quot;It is also possible to specify the dereferencing operator
&quot;with the component selector.
DATA(comp5) = dref-&gt;*-c.

&quot;Note: When using field symbols and data reference variables as target areas, 
&quot;and you modify their content (the examples above only show reads), you can directly 
&quot;edit the line in the internal table (because the field symbols/data reference 
&quot;variable points to the line). This does not apply to work areas that creates 
&quot;local copies. However, you can modify the content of the work area and then, 
&quot;for example, use a MODIFY statement to edit the line in the internal table as 
&quot;shown further down. </code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Excursions with READ TABLE Statements</h3>
<h4>System Field Setting in READ TABLE Statements</h4>
<ul>
<li>For example, for checking if a line is found (<code>sy-subrc</code>) and stored in the target area, and what the index of the line is (<code>sy-tabix</code>).</li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapread_table.htm">here</a></li>
</ul>
<pre><code>TYPES: BEGIN OF s_demo,
         comp1 TYPE i,
         comp2 TYPE i,
       END OF s_demo,
       tab_type_sorted             TYPE SORTED TABLE OF s_demo WITH UNIQUE KEY comp1,
       tab_type_std                TYPE STANDARD TABLE OF s_demo WITH NON-UNIQUE KEY comp1,
       tab_type_hashed             TYPE HASHED TABLE OF s_demo WITH UNIQUE KEY comp1,
       tab_type_std_sorted_sec_key TYPE STANDARD TABLE OF s_demo WITH EMPTY KEY
                                                       WITH NON-UNIQUE SORTED KEY sec_so COMPONENTS comp1,
       tab_type_std_hash_sec_key   TYPE STANDARD TABLE OF s_demo WITH EMPTY KEY
                                                       WITH UNIQUE HASHED KEY sec_ha COMPONENTS comp1.                                                                 .

DATA(itab_sorted) = VALUE tab_type_sorted( ( comp1 = 1 comp2 = 11 )
                                           ( comp1 = 2 comp2 = 22 )
                                           ( comp1 = 3 comp2 = 33 ) ).

DATA itab_std TYPE tab_type_std.
itab_std = itab_sorted.
DATA itab_hashed TYPE tab_type_hashed.
itab_hashed = itab_sorted.
DATA itab_std_so_sec TYPE tab_type_std_sorted_sec_key.
itab_std_so_sec = itab_sorted.
DATA itab_std_ha_sec TYPE tab_type_std_hash_sec_key.
itab_std_ha_sec = itab_sorted.

&quot;--------------- Line found ---------------

&quot;Standard table
READ TABLE itab_std ASSIGNING FIELD-SYMBOL(&lt;fs1&gt;) WITH TABLE KEY primary_key COMPONENTS comp1 = 1.
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 1.
ASSERT &lt;fs1&gt; IS ASSIGNED.

&quot;Sorted table
READ TABLE itab_sorted ASSIGNING FIELD-SYMBOL(&lt;fs2&gt;) WITH TABLE KEY primary_key COMPONENTS comp1 = 1.
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 1.
ASSERT &lt;fs2&gt; IS ASSIGNED.

&quot;Note: sy-tabix is not set here as a hash key is used.
READ TABLE itab_hashed ASSIGNING FIELD-SYMBOL(&lt;fs3&gt;) WITH TABLE KEY primary_key COMPONENTS comp1 = 1.
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 0.
ASSERT &lt;fs3&gt; IS ASSIGNED.

&quot;Standard table, sorted secondary table key
READ TABLE itab_std_so_sec ASSIGNING FIELD-SYMBOL(&lt;fs4&gt;) WITH TABLE KEY sec_so COMPONENTS comp1 = 1.
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 1.
ASSERT &lt;fs4&gt; IS ASSIGNED.

&quot;Standard table, hashed secondary table key
READ TABLE itab_std_ha_sec ASSIGNING FIELD-SYMBOL(&lt;fs5&gt;) WITH TABLE KEY sec_ha COMPONENTS comp1 = 1.
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 0.
ASSERT &lt;fs5&gt; IS ASSIGNED.

&quot;--------------- Line not found ---------------

&quot;Standard table, primary table key
READ TABLE itab_std ASSIGNING FIELD-SYMBOL(&lt;fs6&gt;) WITH TABLE KEY primary_key COMPONENTS comp1 = 10.
ASSERT sy-subrc = 4.
ASSERT sy-tabix = 0.
ASSERT &lt;fs6&gt; IS NOT ASSIGNED.

&quot;Sorted table, primary table key
&quot;Note: A binary search is performed in this case and a line was not found (sy-subrc = 8).
&quot;      The sy-tabix value is set to the number of table lines plus 1. The value 8 for sy-subrc
&quot;      also means not found, but indicates that sy-tabix is set in a different way.
READ TABLE itab_sorted ASSIGNING FIELD-SYMBOL(&lt;fs7&gt;) WITH TABLE KEY primary_key COMPONENTS comp1 = 10.
ASSERT sy-subrc = 8.
ASSERT sy-tabix = 4.
ASSERT &lt;fs7&gt; IS NOT ASSIGNED.

&quot;Hashed table, primary table key
READ TABLE itab_hashed ASSIGNING FIELD-SYMBOL(&lt;fs8&gt;) WITH TABLE KEY primary_key COMPONENTS comp1 = 10.
ASSERT sy-subrc = 4.
ASSERT sy-tabix = 0.
ASSERT &lt;fs8&gt; IS NOT ASSIGNED.

&quot;Standard table, sorted secondary table key
READ TABLE itab_std_so_sec ASSIGNING FIELD-SYMBOL(&lt;fs9&gt;) WITH TABLE KEY sec_so COMPONENTS comp1 = 10.
ASSERT sy-subrc = 8.
ASSERT sy-tabix = 4.
ASSERT &lt;fs9&gt; IS NOT ASSIGNED.

&quot;Standard table, hashed secondary table key
READ TABLE itab_std_ha_sec ASSIGNING FIELD-SYMBOL(&lt;fs10&gt;) WITH TABLE KEY sec_ha COMPONENTS comp1 = 10.
ASSERT sy-subrc = 4.
ASSERT sy-tabix = 0.
ASSERT &lt;fs10&gt; IS NOT ASSIGNED.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Specifying a WHERE Condition in READ TABLE Statements</h4>
<ul>
<li>The syntax <code>READ TABLE ... WHERE ...</code> searches for the first line that matches a <code>WHERE</code> condition.</li>
<li>The value of <code>sy-tabix</code> is set when a line is found, while <code>sy-subrc</code> is set to either 0 or 4.</li>
<li>The <code>WHERE</code> condition can use comparison and predicate expressions (see the code snippet comments below for examples).</li>
<li>Syntax options:</li>
<li>The read result must be specified before the <code>WHERE</code> condition.</li>
<li>The <code>TRANSPORTING NO FIELDS</code> and <code>USING KEY</code> additions are possible.</li>
<li>Dynamic <code>WHERE</code> conditions are possible.</li>
<li>If a <code>READ TABLE</code> statement can be expressed using <code>WITH KEY</code>, this approach - instead of using a <code>WHERE</code> condition - is recommended as it is more performant. A syntax warning occurs but can be suppressed with the pragma <code>##read_where_ok</code>.</li>
<li>For optimized searches with a <code>WHERE</code> condition:</li>
<li>No optimized search in standard tables without a secondary table key.</li>
<li>For sorted and hashed keys (sorted/hashed tables, secondary table keys), the search is optimized as described <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenitab_where_optimization.htm">here</a>, if specifications are transferable to a key access.</li>
</ul>
<pre><code>&quot;Creating and populating demo internal tables
TYPES: BEGIN OF s_demo,
            comp1 TYPE i,
            comp2 TYPE c LENGTH 5,
            comp3 TYPE i,
            comp4 TYPE c LENGTH 5,
        END OF s_demo,
        t_type_so TYPE SORTED TABLE OF s_demo WITH UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp3.

DATA(itab) = VALUE t_type_so( ( comp1 = 1 comp2 = &#x27;lorem&#x27; comp3 = 30 comp4 = &#x27;ipsum&#x27; )
                              ( comp1 = 2 comp2 = &#x27;dolor&#x27; comp3 = 20 comp4 = &#x27;sit&#x27; )
                              ( comp1 = 3 comp2 = &#x27;amet&#x27; comp3 = 40 comp4 = &#x27;hello&#x27; )
                              ( comp1 = 4 comp2 = &#x27;world&#x27; comp3 = 50 comp4 = &#x27;ABAP&#x27; )
                              ( comp1 = 5 comp2 = &#x27;test&#x27; comp3 = 10 comp4 = &#x27;&#x27; ) ).

DATA wa TYPE s_demo.

*&amp;---------------------------------------------------------------------*
*&amp; WHERE condition with comparison expressions
*&amp;---------------------------------------------------------------------*

&quot;Examples: =/EQ, &lt;&gt;/NE, &gt;/GT, &lt;/LT, &gt;=,GE, &lt;=/LE,
&quot;          CO, CN, CA, NA, CS, NS, CP, NP,
&quot;          [NOT] BETWEEN ... AND
&quot;          [NOT] IN ranges_tables

READ TABLE itab INTO wa WHERE comp1 &gt; 3.
ASSERT sy-tabix = 4.

&quot;&#x27;or&#x27; also available in other lines in this component, but the first found line
&quot;is returned (lines 1, 2, 4)
READ TABLE itab INTO wa WHERE comp2 CS &#x27;or&#x27;.
ASSERT sy-tabix = 1.

&quot;&#x27;d&#x27; occurs in lines 2, 4
READ TABLE itab INTO wa WHERE comp2 CS &#x27;d&#x27;.
ASSERT sy-tabix = 2.

READ TABLE itab INTO wa WHERE comp2 CS &#x27;d&#x27;.
ASSERT sy-tabix = 2.

*&amp;---------------------------------------------------------------------*
*&amp; WHERE condition with predicate expressions
*&amp;---------------------------------------------------------------------*

&quot;Examples: IS [NOT] INITIAL
&quot;          IS [NOT] BOUND
&quot;          IS [NOT] INSTANCE OF

READ TABLE itab INTO wa WHERE comp1 &gt; 4 AND comp4 IS INITIAL.
ASSERT sy-tabix = 5.

*&amp;---------------------------------------------------------------------*
*&amp; WITH KEY instead of WHERE condition
*&amp;---------------------------------------------------------------------*

&quot;Syntax warning in READ TABLE ... WHERE ... statements
READ TABLE itab INTO wa WHERE comp4 IS INITIAL.
ASSERT sy-tabix = 5.

&quot;For a better performance, the previous statement should be
&quot;replaced by a READ TABLE ... WITH KEY ... statement.
READ TABLE itab INTO wa WITH KEY comp4 = &#x27;&#x27;.
ASSERT sy-tabix = 5.

&quot;You can also suppress the syntax warning by a pragma.
READ TABLE itab INTO wa WHERE comp4 IS INITIAL ##read_where_ok.
ASSERT sy-tabix = 5.

*&amp;---------------------------------------------------------------------*
*&amp; Further additions
*&amp;---------------------------------------------------------------------*

&quot;TRANSPORTING NO FIELDS addition is possible
READ TABLE itab TRANSPORTING NO FIELDS WHERE comp2 CS &#x27;d&#x27;.
ASSERT sy-tabix = 2.

&quot;USING KEY addition
READ TABLE itab USING KEY primary_key INTO wa WHERE comp2 CS &#x27;t&#x27;.
ASSERT sy-tabix = 3.

READ TABLE itab USING KEY sk INTO wa WHERE comp3 &gt; 40.
ASSERT sy-tabix = 5.

*&amp;---------------------------------------------------------------------*
*&amp; Excursions
*&amp;---------------------------------------------------------------------*

&quot;Note the comparison rules for character-like data types
READ TABLE itab INTO wa WHERE comp2 = &#x27;lorem&#x27; ##read_where_ok.
ASSERT sy-tabix = 1.

&quot;In the following case, the length of the comp2 value increased to match the
&quot;length of the specified text field, i.e. the surplus characters from the right
&quot;side text field are not truncated. As a consequence, the line is not found.

DATA(some_text) = &#x27;loremXYZ&#x27;.

READ TABLE itab INTO wa WHERE comp2 = some_text ##read_where_ok.
ASSERT sy-tabix = 0 AND sy-subrc &lt;&gt; 0.

&quot;When using READ TABLE ... WITH KEY ... the behavior is different. In that
&quot;case, the surplus characters are truncated because of a conversion. Therefore,
&quot;the following statement finds a line.
READ TABLE itab INTO wa WITH KEY comp2 = some_text.
ASSERT sy-tabix = 1 AND sy-subrc = 0.

&quot;Note: The read target can only be placed before WHERE conditions.
&quot;The following statements are not possible.
&quot;READ TABLE itab WHERE comp2 CS &#x27;d&#x27; INTO wa.
&quot;READ TABLE itab WHERE comp2 CS &#x27;d&#x27; TRANSPORTING NO FIELDS.

&quot;READ TABLE ... WHERE ... statements can replace LOOP AT ... WHERE ...
&quot;statements including EXIT.
LOOP AT itab INTO wa WHERE comp2 CS &#x27;d&#x27;.
    ASSERT sy-tabix = 2.
    EXIT.
ENDLOOP.

*&amp;---------------------------------------------------------------------*
*&amp; Dynamic WHERE condition
*&amp;---------------------------------------------------------------------*

&quot;Character-like data objects or standard tables with character-like line type
&quot;can be specified

DATA(dyn_where_cond_str) = `comp2 CS &#x27;d&#x27;`.

READ TABLE itab INTO wa WHERE (dyn_where_cond_str).
ASSERT sy-tabix = 2.

DATA(dyn_where_cond_tab) = VALUE string_table( ( `comp2` ) ( `CS` ) ( `&#x27;d&#x27;` ) ).
READ TABLE itab INTO wa WHERE (dyn_where_cond_tab).
ASSERT sy-tabix = 2.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>COMPARING and TRANSPORTING Additions: Comparing Fields and Specifying Fields for Transport</h4>
<p><code>... TRANSPORTING NO FIELDS</code>: It is only checked whether the line exists. No target area is specified. As mentioned above, the system fields are filled.</p>
<pre><code>DATA(stringtab) = VALUE string_table( ( `abc` ) ( `def` ) ( `ghi` ) ).

READ TABLE stringtab WITH KEY table_line = `def` TRANSPORTING NO FIELDS.
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 2.

READ TABLE stringtab WITH KEY table_line = `xyz` TRANSPORTING NO FIELDS.
ASSERT sy-subrc = 4.
ASSERT sy-tabix = 0.</code></pre>
<p><code>... TRANSPORTING ...</code>: Specifying fields to be transported. The addition cannot be used with the <code>ASSIGNING</code> and <code>REFERENCE</code> additions.</p>
<pre><code>TYPES: BEGIN OF demo_struc_tr,
          comp1 TYPE i,
          comp2 TYPE c LENGTH 3,
        END OF demo_struc_tr,
        ty_tab_demo_tr TYPE TABLE OF demo_struc_tr WITH EMPTY KEY.

DATA(it_tr) = VALUE ty_tab_demo_tr( ( comp1 = 1 comp2 = `abc` )
                                    ( comp1 = 2 comp2 = `def` ) ).

READ TABLE it_tr INTO DATA(line_tr) INDEX 1 TRANSPORTING comp2.
ASSERT line_tr-comp1 IS INITIAL.

&quot;ALL FIELDS addition is available, explicitly stating that all fields
&quot;should be transported.
READ TABLE it_tr INTO line_tr INDEX 1 TRANSPORTING ALL FIELDS.</code></pre>
<p><code>... COMPARING ...</code>:</p>
<ul>
<li>Can be used together with and in front of <code>TRANSPORTING ...</code></li>
<li>Compares specified components</li>
<li>The <code>COMPARING ALL FIELDS</code> compares all components, <code>COMPARING NO FIELDS</code> compares no components</li>
<li>Setting of <code>sy-subrc</code>: 0 is set if the content of compared components is identical,</li>
</ul>
<p>otherwise it is 2. Found lines are nevertheless assigned independently of the comparison.</p>
<pre><code>TYPES: BEGIN OF demo_struc_compare,
          comp1 TYPE i,
          comp2 TYPE c LENGTH 3,
          comp3 TYPE string,
        END OF demo_struc_compare,
        ty_tab_demo_compare TYPE TABLE OF demo_struc_compare WITH EMPTY KEY.

DATA(it_compare) = VALUE ty_tab_demo_compare( ( comp1 = 1 comp2 = &#x27;abc&#x27; comp3 = `zzz` )
                                              ( comp1 = 2 comp2 = &#x27;def&#x27; comp3 = `yyy` )
                                              ( comp1 = 2 comp2 = &#x27;ghi&#x27; comp3 = `yyy` ) ).

DO 3 TIMES.
  DATA(line_compare) = VALUE demo_struc_compare( comp1 = 1 ).
  READ TABLE it_compare INTO line_compare INDEX sy-index COMPARING comp1.
  CASE sy-index.
    WHEN 1.
      ASSERT sy-subrc = 0.
    WHEN 2.
      ASSERT sy-subrc = 2.
    WHEN 3.
      ASSERT sy-subrc = 2.
  ENDCASE.

  line_compare = VALUE demo_struc_compare( comp3 = `yyy` ).
  READ TABLE it_compare INTO line_compare INDEX sy-index COMPARING comp3 TRANSPORTING comp2 comp3.

  CASE sy-index.
    WHEN 1.
      ASSERT sy-subrc = 2.
      ASSERT line_compare-comp1 IS INITIAL.
    WHEN 2.
      ASSERT sy-subrc = 0.
      ASSERT line_compare-comp1 IS INITIAL.
    WHEN 3.
      ASSERT sy-subrc = 0.
      ASSERT line_compare-comp1 IS INITIAL.
  ENDCASE.
ENDDO.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>CASTING and ELSE UNASSIGN Additions when Specifying Field Symbols as Target Areas</h4>
<pre><code>TYPES c3 TYPE c LENGTH 3.
TYPES ty_tab_casting TYPE TABLE OF c3 WITH EMPTY KEY.
DATA(it_casting) = VALUE ty_tab_casting( ( &#x27;abc&#x27; ) ( &#x27;def&#x27; ) ).

&quot;Field symbol created inline (i.e. with the generic type &#x27;data&#x27; implicitly)
&quot;In this case, the CASTING and ELSE UNASSIGN additions are not available.
READ TABLE it_casting ASSIGNING FIELD-SYMBOL(&lt;a&gt;) INDEX 1.

&quot;******* CASTING addition *******
&quot;To use the addition, the field symbol must be either completely typed, or
&quot;typed with one of the generic built-in ABAP types c, n, p, or x.
TYPES c2 TYPE c LENGTH 2.
FIELD-SYMBOLS &lt;b&gt; TYPE c2.

READ TABLE it_casting ASSIGNING &lt;b&gt; CASTING INDEX 2.
ASSERT &lt;b&gt; = &#x27;de&#x27;.

&quot;******* ELSE UNASSIGN addition *******
&quot;The field symbol is unassigned if no table line is found. The addition
&quot;can be used together with the CASTING addition.
&quot;The following example loops 3 times across an internal table that has
&quot;two lines. The sy-index value is used as the index value of the READ TABLE
&quot;statement. The example demonstrates that when a line is not found, the field
&quot;symbol is unassigned. In case of the first READ TABLE statement that does not
&quot;specify ELSE UNASSIGN, the field symbol remains assigned.

FIELD-SYMBOLS &lt;c&gt; TYPE c2.
DO 3 TIMES.
  READ TABLE it_casting ASSIGNING FIELD-SYMBOL(&lt;d&gt;) INDEX sy-index.
  READ TABLE it_casting ASSIGNING FIELD-SYMBOL(&lt;e&gt;) ELSE UNASSIGN INDEX sy-index.
  READ TABLE it_casting ASSIGNING &lt;c&gt; CASTING ELSE UNASSIGN INDEX sy-index.
  IF sy-index = 3.
    ASSERT &lt;d&gt; = `def`.
    ASSERT &lt;e&gt; IS NOT ASSIGNED.
    ASSERT &lt;c&gt; IS NOT ASSIGNED.
  ENDIF.
ENDDO.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>BINARY SEARCH Addition: Optimized Read Access When Specifying Free Keys</h4>
<p>You may stumble on the <code>BINARY SEARCH</code> addition to <code>READ TABLE</code> statements for an optimized access. However, it is recommended to use sorted tables or secondary table keys for an optimized access. Find information and an example in the expandable section below.</p>
<pre><code>READ TABLE itab WITH KEY ... BINARY SEARCH ...</code></pre>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more information and a code example&lt;/summary&gt; &lt;!-- --&gt;</p>
<ul>
<li><code>READ TABLE</code> without <code>BINARY SEARCH</code>: Table is accessed linearly</li>
<li><code>READ TABLE</code> with <code>BINARY SEARCH</code>: Table is accessed using a binary search</li>
<li>Using the <code>BINARY SEARCH</code> addition is particularly more efficient for larger tables when accessing data often.</li>
<li>The table must be sorted in ascending order based on the keys being searched.</li>
<li><code>BINARY SEARCH</code> is suitable for standard tables that do not have a secondary key defined and when you need to make multiple read accesses to the table (however, note the costs of a previous sorting)</li>
<li><code>BINARY SEARCH</code> can only be used with index tables and not with hashed tables. If the table is a sorted table and the read access uses a free key, the addition can only be applied when the initial part of the table key is specified. I.e. if key components are <code>a</code>, <code>b</code>, and <code>c</code>, the addition can be used by specifying <code>a</code> alone, <code>a</code> and <code>b</code>, or <code>a</code>, <code>b</code>, and <code>c</code>. However, it means that the syntax can be specified without errors. The <code>BINARY SEARCH</code> specifiation has no effect and is redundant. Syntactically not possible with <code>BINARY SEARCH</code> (for example): <code>b</code> and <code>c</code> without <code>a</code>, or any other non-key component (because it cannot be sorted according to the non-key component).</li>
<li>Depending on the number of times you need to access the internal table, it is recommended to work with sorted tables or tables with secondary keys. If you only need to read one or a few data sets, consider the administrative costs of setting up the index.</li>
<li>Note: The <code>BINARY SEARCH</code> addition is not available for table expressions. If <code>KEY ...</code> is specified, an optimized search is performed by default. There are no performance differences between using the <code>READ TABLE</code> statement and table expressions.</li>
</ul>
<p>To try out the following example, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console.</p>
<p>The example includes multiple reads on standard internal tables using a <code>READ TABLE</code> statement ...</p>
<ul>
<li>without <code>BINARY SEARCH</code> by applying a sorting of the table a) every time and b) only once (the assumption is that after sorting once, only read accesses follow).</li>
<li>with <code>BINARY SEARCH</code> (by applying a sorting of the table a) every time and b) only once (the assumption is that after sorting once, only read accesses follow).</li>
<li>with a secondary table key whose components correspond to free key used in the previous statements.</li>
</ul>
<p>The total runtime of multiple reads is determined to have a more representative runtime evaluation. The example is intended to demonstrate the performance gain using the <code>BINARY SEARCH</code> addition (and also using a secondary table key). Particularly, the result using <code>BINARY SEARCH</code> and <code>SORT</code> applied only once as well as the reads using the secondary table key should be relatively fast.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

    METHODS:
      read_wo_binary_search1 RETURNING VALUE(result) TYPE decfloat34,
      read_wo_binary_search2 RETURNING VALUE(result) TYPE decfloat34,
      read_with_binary_search1 RETURNING VALUE(result) TYPE decfloat34,
      read_with_binary_search2 RETURNING VALUE(result) TYPE decfloat34,
      read_w_secondary_table_key RETURNING VALUE(result) TYPE decfloat34.

    DATA: ts1         TYPE utclong,
          runtime_tab TYPE TABLE OF decfloat34 WITH EMPTY KEY.

    &quot;Line type and internal table declarations
    TYPES: BEGIN OF demo_struc,
             idx TYPE i,
             str TYPE string,
             num TYPE i,
           END OF demo_struc.

    &quot;Tables with empty primary table key
    DATA itab_std TYPE STANDARD TABLE OF demo_struc WITH EMPTY KEY.
    &quot;Table with empty primary table key, secondary table key specified
    DATA itab_sec TYPE STANDARD TABLE OF demo_struc WITH EMPTY KEY
                                                    WITH NON-UNIQUE SORTED KEY sk COMPONENTS idx.
    CONSTANTS count TYPE i VALUE 1000.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;Populating internal tables
    DO count TIMES.
      INSERT VALUE #( idx = sy-index
                      str = |INDEX{ sy-index }|
                      num = sy-index ) INTO TABLE itab_std.
    ENDDO.
    itab_sec = itab_std.

    DATA(res1) = read_wo_binary_search1( ).
    out-&gt;write( `Total runtime for read operations using READ TABLE with a free key, without the BINARY SEARCH addition, SORT always applied:` ).
    out-&gt;write( |{ res1 STYLE = SIMPLE DECIMALS = 10 }| ).

    DATA(res2) = read_wo_binary_search2( ).
    out-&gt;write( `Total runtime for read operations using READ TABLE with a free key, without the BINARY SEARCH addition, SORT applied once:` ).
    out-&gt;write( |{ res2 STYLE = SIMPLE DECIMALS = 10 }| ).

    DATA(res3) = read_with_binary_search1( ).
    out-&gt;write( `Total runtime for read operations using READ TABLE with a free key, with BINARY SEARCH addition, SORT always applied:` ).
    out-&gt;write( |{ res3 STYLE = SIMPLE DECIMALS = 10 }| ).

    DATA(res4) = read_with_binary_search2( ).
    out-&gt;write( `Total runtime for read operations using READ TABLE with a free key, with BINARY SEARCH addition, SORT applied once:` ).
    out-&gt;write( |{ res4 STYLE = SIMPLE DECIMALS = 10 }| ).

    DATA(res5) = read_w_secondary_table_key( ).
    out-&gt;write( `Total runtime for read operations using READ TABLE and a secondary table key that corresponds to the free key in the other table:` ).
    out-&gt;write( |{ res5 STYLE = SIMPLE DECIMALS = 10 }| ).
  ENDMETHOD.

  METHOD read_wo_binary_search1.

    ts1 = utclong_current( ).
    DO count TIMES.
**********************************************************************
      SORT itab_std BY str num.
      READ TABLE itab_std WITH KEY idx = sy-index TRANSPORTING NO FIELDS.
**********************************************************************
    ENDDO.
    cl_abap_utclong=&gt;diff( EXPORTING high    = utclong_current( )
                                     low     = ts1
                           IMPORTING seconds = result ).

  ENDMETHOD.

  METHOD read_wo_binary_search2.

    ts1 = utclong_current( ).
    DO count TIMES.
**********************************************************************
      IF sy-index = 1.
        SORT itab_std BY str num.
      ENDIF.
      READ TABLE itab_std WITH KEY idx = sy-index TRANSPORTING NO FIELDS.
**********************************************************************
    ENDDO.
    cl_abap_utclong=&gt;diff( EXPORTING high    = utclong_current( )
                                     low     = ts1
                           IMPORTING seconds = result ).

  ENDMETHOD.

  METHOD read_with_binary_search1.

    ts1 = utclong_current( ).
    DO count TIMES.
**********************************************************************
      SORT itab_std BY str num.
      READ TABLE itab_std WITH KEY idx = sy-index BINARY SEARCH TRANSPORTING NO FIELDS.
**********************************************************************
    ENDDO.
    cl_abap_utclong=&gt;diff( EXPORTING high    = utclong_current( )
                                     low     = ts1
                           IMPORTING seconds = result ).

  ENDMETHOD.

  METHOD read_with_binary_search2.

    ts1 = utclong_current( ).
    DO count TIMES.
**********************************************************************
      IF sy-index = 1.
        SORT itab_std BY str num.
      ENDIF.
      READ TABLE itab_std WITH KEY idx = sy-index BINARY SEARCH TRANSPORTING NO FIELDS.
**********************************************************************
    ENDDO.
    cl_abap_utclong=&gt;diff( EXPORTING high    = utclong_current( )
                                     low     = ts1
                           IMPORTING seconds = result ).
  ENDMETHOD.

  METHOD read_w_secondary_table_key.

    ts1 = utclong_current( ).
    DO count TIMES.
**********************************************************************
      READ TABLE itab_sec WITH TABLE KEY sk COMPONENTS idx = sy-index TRANSPORTING NO FIELDS.
**********************************************************************
    ENDDO.
    cl_abap_utclong=&gt;diff( EXPORTING high    = utclong_current( )
                                     low     = ts1
                           IMPORTING seconds = result ).

  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<h3>Exploring READ TABLE Statements and Table Expressions</h3>
<p>Expand the following collapsible section for example code. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is not set up to display output in the console. You may want to set a break point at the first position possible and walk through the example in the debugger. This will allow you to double-click on data objects and observe how the different statements affect their contents.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;Creating and populating an internal table
    TYPES: BEGIN OF st_h,
             a TYPE i,
             b TYPE c LENGTH 2,
             c TYPE string,
           END OF st_h,
           ty_tab_f TYPE SORTED TABLE OF st_h WITH UNIQUE KEY a WITH NON-UNIQUE SORTED KEY sk COMPONENTS b.

    DATA(it_j) = VALUE ty_tab_f( ( a = 1 b = &#x27;zz&#x27; c = `B` )
                                 ( a = 2 b = &#x27;xx&#x27; c = `D` )
                                 ( a = 3 b = &#x27;yy&#x27; c = `F` ) ).

    &quot;******* 1) Reading a single line by index: READ TABLE statements  *******

    DATA struc_e TYPE st_h.
    &quot;In the following example ...
    &quot;- a work area is specified as target area.
    &quot;- USING KEY is not specified, i.e. the primary table index is used by default.
    READ TABLE it_j INTO struc_e INDEX 2.

    &quot;Reading into a work area that is created inline
    READ TABLE it_j INTO DATA(struc_f) INDEX 3.

    &quot;Specifying other target areas: Field symbols and data reference variables
    &quot;Here, the target areas are created inline
    READ TABLE it_j ASSIGNING FIELD-SYMBOL(&lt;fs_c&gt;) INDEX 1.
    READ TABLE it_j REFERENCE INTO DATA(dref_c) INDEX 1.

    &quot;******* USING KEY addition *******
    &quot;Reading by index and specifying which table index to use

    &quot;In the following example, the primary key is specified explicitly and
    &quot;addressed using the default name primary_key. It has the same effect
    &quot;as the statement below because the primary table index is used by
    &quot;default.
    READ TABLE it_j INTO struc_e INDEX 1 USING KEY primary_key.
    READ TABLE it_j INTO struc_e INDEX 1.

    &quot;Specifying the secondary key to use the secondary table index
    READ TABLE it_j INTO struc_e INDEX 1 USING KEY sk.

    &quot;Using alias names
    DATA it_j_alias TYPE SORTED TABLE OF st_h
     WITH UNIQUE KEY primary_key ALIAS pk COMPONENTS a
     WITH NON-UNIQUE SORTED KEY sk ALIAS sk_alias COMPONENTS b.

    it_j_alias = it_j.
    READ TABLE it_j_alias INTO struc_e INDEX 1 USING KEY pk.
    READ TABLE it_j_alias INTO struc_e INDEX 1 USING KEY sk_alias.
    &quot;The following examples use the other key names
    READ TABLE it_j_alias INTO struc_e INDEX 1 USING KEY primary_key.
    READ TABLE it_j_alias INTO struc_e INDEX 1 USING KEY sk.

    &quot;******* Excursion: System field setting with READ TABLE statements *******
    &quot;Checking if a line is found and stored in the target area

    READ TABLE it_j INTO struc_e INDEX 999.
    IF sy-subrc = 0.
      ...
    ELSE.
      ... &quot;This branch is executed in the example since the line is not found.
      ASSERT sy-tabix = 0.
    ENDIF.

    READ TABLE it_j INTO struc_e INDEX 1.
    ASSERT sy-subrc = 0.
    ASSERT sy-tabix = 1.

    &quot;******* 2) Reading a single line by index: Table expressions *******

    &quot;Many of the following examples show the assignment of a line
    &quot;using a table expression that is specified on the right side
    &quot;of an assignment.
    &quot;The target area is declared inline on the left side of an assignment.
    DATA(struc_g) = it_j[ 1 ].

    &quot;If a table line is not found, a catchable exception is raised. It can
    &quot;be caught in a TRY control structure.
    TRY.
        struc_g = it_j[ 999 ].
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.

    &quot;Table expression on the left side of an assignment
    &quot;The following example does not work since the example is a key table.
    &quot;it_j[ 1 ] = VALUE #( ).

    &quot;Creating and populating a table without a key
    DATA it_j_no_key TYPE TABLE OF st_h WITH EMPTY KEY.
    it_j_no_key = VALUE #( ( a = 1 b = &#x27;a&#x27; c = `b` )
                           ( a = 2 b = &#x27;c&#x27; c = `d` )
                           ( a = 3 b = &#x27;e&#x27; c = `f` ) ).

    it_j_no_key[ 1 ] = VALUE #( b = &#x27;z&#x27; ).
    ASSERT it_j_no_key[ 1 ]-a IS INITIAL.
    ASSERT it_j_no_key[ 1 ]-b = &#x27;z&#x27;.
    ASSERT it_j_no_key[ 1 ]-c IS INITIAL.

    it_j_no_key[ 2 ]-b = &#x27;x&#x27;.
    it_j_no_key[ 3 ] = VALUE #( ).
    ASSERT it_j_no_key[ 3 ] IS INITIAL.

    &quot;******* KEY addition *******
    &quot;Reading by index and specifying which table index to use

    &quot;Primary key&#x27;s default name
    struc_g = it_j_alias[ KEY primary_key INDEX 1 ].
    &quot;Primary key alias
    struc_g = it_j_alias[ KEY pk INDEX 1 ].
    &quot;Secondary key
    struc_g = it_j_alias[ KEY sk INDEX 1 ].
    &quot;Secondary key alias
    struc_g = it_j_alias[ KEY sk_alias INDEX 1 ].

    &quot;******* Excursion: Other ways of assignments with table expressions *******
    &quot;The examples only read by index. Reading by keys is also possible.

    &quot;Assigning a line read by index to a field symbol
    FIELD-SYMBOLS &lt;fs_d&gt; TYPE st_h.
    ASSIGN it_j[ 1 ] TO &lt;fs_d&gt;.
    &quot;The field symbol created inline has a generic type
    ASSIGN it_j[ 1 ] TO FIELD-SYMBOL(&lt;fs_d_inl&gt;).

    &quot;Copying a table line via a table expression and embedding it in a
    &quot;constructor expression
    struc_g = VALUE #( it_j[ 3 ] ).

    &quot;Reading into data reference variable using the REF operator
    DATA(dref_d) = REF #( it_j[ 2 ] ).

    &quot;******* OPTIONAL/DEFAULT additions *******
    &quot;OPTIONAL: The default value is an initial data object with the data type
    DATA(struc_h) = VALUE #( it_j[ 999 ] OPTIONAL ).
    ASSERT struc_h = VALUE st_h( ).

    &quot;DEFAULT: Specifying a default value for table lines not found
    &quot;This value must be convertible to the type of the table expression.
    &quot;It can also be a table expression.
    struc_h = VALUE #( it_j[ 999 ] DEFAULT it_j[ 1 ] ).
    struc_h = VALUE #( it_j[ 999 ] DEFAULT VALUE st_h( c = `null` ) ).

**********************************************************************

    &quot;******* 1) Reading a single line using table keys: READ TABLE statements  *******

    &quot;Creating and populating a demo internal table
    TYPES: BEGIN OF st_i,
             num  TYPE i,
             str  TYPE string,
             char TYPE c LENGTH 2,
           END OF st_i.

    DATA it_k TYPE SORTED TABLE OF st_i
      WITH NON-UNIQUE KEY primary_key ALIAS pk COMPONENTS num
      WITH NON-UNIQUE SORTED KEY sec_key ALIAS sk COMPONENTS char.

    it_k = VALUE #( ( num = 1 str = `A` char = &#x27;zz&#x27; )
                    ( num = 2 str = `C` char = &#x27;yy&#x27; )
                    ( num = 3 str = `E` char = &#x27;xx&#x27; ) ).

    &quot;The following examples use a work area as target. Other target areas are
    &quot;possible.

    &quot;Primary table key
    READ TABLE it_k INTO DATA(struc_i) WITH TABLE KEY primary_key COMPONENTS num = 3.
    &quot;Primary table key alias
    READ TABLE it_k INTO struc_i WITH TABLE KEY pk COMPONENTS num = 2.
    &quot;Secondary table key
    READ TABLE it_k INTO struc_i WITH TABLE KEY sec_key COMPONENTS char = &#x27;xx&#x27;.
    &quot;Secondary table key alias
    READ TABLE it_k INTO struc_i WITH TABLE KEY sk COMPONENTS char = &#x27;yy&#x27;.

    &quot;Reading a line based on keys specified in a work area
    &quot;It is a work area containing primary and secondary table key values.
    &quot;the line type must be compatible to the internal table.
    TYPES st_j LIKE LINE OF it_k.
    DATA(pr_key) = VALUE st_j( num = 1 ).
    DATA(sec_key) = VALUE st_j( char = &#x27;yy&#x27; ).

    READ TABLE it_k FROM pr_key INTO struc_i.

    &quot;If USING KEY is not specified, the primary table key is used by default.
    &quot;Explicitly specifying the primary table key
    READ TABLE it_k FROM pr_key USING KEY primary_key INTO struc_i.
    &quot;Primary table key alias
    READ TABLE it_k FROM pr_key USING KEY pk INTO struc_i.
    &quot;Secondary table key
    READ TABLE it_k FROM sec_key USING KEY sec_key INTO struc_i.
    &quot;Secondary table key alias
    READ TABLE it_k FROM sec_key USING KEY sk INTO struc_i.

    &quot;******* 2) Reading a single line using table keys: Table expressions *******

    &quot;The addition COMPONENTS is optional. Therefore, the statement below
    &quot;has the same effect.
    &quot;Explicitly specifying the primary table key
    struc_i = it_k[ KEY primary_key COMPONENTS num = 1 ].
    struc_i = it_k[ KEY primary_key num = 1 ].

    &quot;Primary table key alias
    struc_i = it_k[ KEY pk num = 2 ].
    &quot;Secondary table key
    struc_i = it_k[ KEY sec_key char = &#x27;xx&#x27; ].
    &quot;Secondary table key alias
    struc_i = it_k[ KEY sk char = &#x27;zz&#x27; ].

**********************************************************************

    &quot;Reading a single line using a free key
    &quot;Note: Instead if READ TABLE ... WITH TABLE KEY ..., it is ... WITH KEY.

    READ TABLE it_k INTO DATA(struc_j) WITH KEY str = `A`.
    struc_j = it_k[ str = `C` ].

**********************************************************************

    &quot;Examples for addressing individual components of read lines
    &quot;The assertions emphasize the difference of work areas and field
    &quot;symbols/data reference variables as target areas. Modifying the
    &quot;contents of the field symbols/data reference variables means
    &quot;modifying the internal table content.

    DATA(comp_a) = it_k[ str = `C` ]-num.

    READ TABLE it_k INTO DATA(struc_k) WITH KEY str = `A`.
    struc_k-num = 123.
    ASSERT NOT it_k[ str = `C` ]-num = 123.
    DATA(comp_b) = struc_k-num.

    READ TABLE it_k ASSIGNING FIELD-SYMBOL(&lt;fs_e&gt;) WITH KEY str = `C`.
    &quot;Note: The example table is a sorted table with &#x27;num&#x27; as part of
    &quot;a unique key. The field value cannot be modified.
    &quot;&lt;fs_e&gt;-num = 123.
    &lt;fs_e&gt;-char = &#x27;hi&#x27;.
    ASSERT it_k[ str = `C` ]-char = &#x27;hi&#x27;.
    DATA(comp_c) = &lt;fs_e&gt;-char.

    READ TABLE it_k REFERENCE INTO DATA(dref_e) WITH KEY str = `E`.
    dref_e-&gt;char = &#x27;##&#x27;.
    ASSERT it_k[ str = `E` ]-char = &#x27;##&#x27;.
    DATA(comp_d) = dref_e-&gt;num.

    &quot;It is also possible to specify the dereferencing operator together
    &quot;with the component selector.
    DATA(comp_e) = dref_e-&gt;*-char.

**********************************************************************

    &quot;Excursion: Using ASSIGN statements to assign to field symbols

    TYPES ty_tab_h TYPE TABLE OF st_i WITH EMPTY KEY.
    DATA(it_l) = VALUE ty_tab_h( ( num = 1 str = `A` char = &#x27;zz&#x27; )
                                 ( num = 2 str = `C` char = &#x27;yy&#x27; )
                                 ( num = 3 str = `E` char = &#x27;xx&#x27; ) ).

    FIELD-SYMBOLS &lt;fs_f&gt; TYPE st_i-num.
    ASSIGN it_l[ str = `C` ]-num TO &lt;fs_f&gt;.
    &lt;fs_f&gt; = 99.
    ASSERT it_l[ str = `C` ]-num = 99.

    FIELD-SYMBOLS &lt;fs_g&gt; TYPE st_i-char.
    ASSIGN it_l[ str = `C` ]-char TO &lt;fs_g&gt;.
    &lt;fs_g&gt; = &#x27;aa&#x27;.

    &quot;Field symbol created inline
    ASSIGN &lt;fs_g&gt; TO FIELD-SYMBOL(&lt;fs_h&gt;).
    &lt;fs_h&gt; = &#x27;bb&#x27;.

    READ TABLE it_l REFERENCE INTO DATA(dref_f) WITH KEY str = `E`.
    ASSIGN dref_f-&gt;char TO FIELD-SYMBOL(&lt;fs_i&gt;).
    &lt;fs_i&gt; = &#x27;cc&#x27;.

    &quot;The following example use the generic type any so that anything
    &quot;can be assigned.
    FIELD-SYMBOLS &lt;fs_j&gt; TYPE any.

    &quot;Assigning component
    ASSIGN it_l[ str = `C` ]-num TO &lt;fs_j&gt;.
    &lt;fs_j&gt; = 123.

    &quot;Assigning line
    ASSIGN it_l[ str = `C` ] TO &lt;fs_j&gt;.
    &lt;fs_j&gt; = VALUE st_i( num = 789 str = `U` char = &#x27;vv&#x27; ).

    &quot;Assigning the entire table
    ASSIGN it_l TO &lt;fs_j&gt;.
    &lt;fs_j&gt; = VALUE ty_tab_h( ( num = 1 str = `AB` char = &#x27;ap&#x27; ) ).

**********************************************************************

    &quot;COMPARING / TRANSPORTING additions to READ TABLE statements
    &quot;Comparing fields and specifying fields to be transported

    DATA(it_m) = VALUE ty_tab_h( ( num = 1 str = `Z` char = &#x27;##&#x27; )
                                 ( num = 2 str = `Y` char = &#x27;yy&#x27; )
                                 ( num = 3 str = `X` char = &#x27;##&#x27; )
                                 ( num = 4 str = `W` char = &#x27;ww&#x27; )
                                 ( num = 5 str = `W` char = &#x27;##&#x27; )
                                 ( num = 6 str = `V` char = &#x27;##&#x27; )
                                 ( num = 7 str = `V` char = &#x27;##&#x27; )
                                 ( num = 7 str = `V` char = &#x27;##&#x27; )
                                 ( num = 8 str = `V` char = &#x27;vv&#x27; ) ).

    &quot;******* TRANSPORTING NO FIELDS addition *******
    &quot;It is only checked whether the line exists. No target area is specified.
    &quot;The system fields sy-subrc and sy-tabix are filled. Check also the
    &quot;line_exists and line_index functions.

    READ TABLE it_m WITH KEY str = `X` TRANSPORTING NO FIELDS.
    ASSERT sy-subrc = 0.
    DATA(sysubrc) = sy-subrc.
    ASSERT sy-tabix = 3.
    DATA(sytabix) = sy-tabix.

    READ TABLE it_m WITH KEY str = `nope` TRANSPORTING NO FIELDS.
    ASSERT sy-subrc = 4.
    ASSERT sy-tabix = 0.

    &quot;******* TRANSPORTING ... addition *******
    &quot;Specifying fields to be transported; cannot be used with the ASSIGNING
    &quot;and REFERENCE additions

    READ TABLE it_m INTO DATA(struc_l) INDEX 1 TRANSPORTING num char.
    ASSERT struc_l-str IS INITIAL.

    &quot;If ALL FIELDS is specified, all fields are assigned, which corresponds to the
    &quot;example below.
    READ TABLE it_m INTO struc_l INDEX 1 TRANSPORTING ALL FIELDS.
    READ TABLE it_m INTO struc_l INDEX 1.

    &quot;******* COMPARING addition *******
    &quot;- Can be used together with and in front of TRANSPORTING ...
    &quot;- Compares the specified components
    &quot;- ALL FIELDS compares all components, NO FIELDS compares no components
    &quot;- Setting of sy-subrc: 0 is set if the content of compared components is identical,
    &quot;  otherwise it is 2. Found lines are nevertheless assigned independently of the comparison.

    &quot;The following examples use a WHILE loop to read all table lines (sy-index represents the
    &quot;index value of the primary table index) into a work area.
    &quot;The work area is filled before the read for the comparison. Depending on the comparison
    &quot;result (by checking the sy-subrc value), the lines are added to different internal tables
    &quot;for demonstration purposes. In addition, the &#x27;num&#x27; component value is added to a string.
    &quot;The examples explore several syntax options.

    DATA struc_m LIKE LINE OF it_m.
    DATA it_n LIKE it_m.
    DATA it_o LIKE it_m.
    DATA nums_subrc_0 TYPE string.
    DATA nums_subrc_2 TYPE string.
    DATA(subrc) = 0.

    &quot;Specifying ALL FIELDS
    WHILE subrc = 0.
      DATA(idx) = sy-index.
      struc_m = VALUE #( num = 7 str = `V` char = &#x27;##&#x27; ).
      READ TABLE it_m INTO struc_m INDEX idx COMPARING ALL FIELDS TRANSPORTING ALL FIELDS.
      subrc = COND #( WHEN sy-subrc = 0 THEN 0 ELSE sy-subrc ).
      IF subrc = 0.
        APPEND struc_m TO it_n.
        nums_subrc_0 &amp;&amp;= struc_m-num.
      ELSEIF subrc = 2.
        APPEND struc_m TO it_o.
        nums_subrc_2 &amp;&amp;= struc_m-num.
        subrc = 0.
      ELSE.
        EXIT.
      ENDIF.
    ENDWHILE.

    ASSERT nums_subrc_0 = `77`.
    ASSERT nums_subrc_2 = `1234568`.
    CLEAR: subrc, struc_m, it_n, it_o, nums_subrc_0, nums_subrc_2.

    &quot;Specifying specific fields for the comparison and transport
    WHILE subrc = 0.
      idx = sy-index.
      struc_m = VALUE #( num = 1234 str = `NOPE` char = &#x27;##&#x27; ).
      READ TABLE it_m INTO struc_m INDEX idx COMPARING char TRANSPORTING num.
      subrc = COND #( WHEN sy-subrc = 0 THEN 0 ELSE sy-subrc ).
      IF subrc = 0.
        APPEND struc_m TO it_n.
        nums_subrc_0 &amp;&amp;= struc_m-num.
      ELSEIF subrc = 2.
        APPEND struc_m TO it_o.
        nums_subrc_2 &amp;&amp;= struc_m-num.
        subrc = 0.
      ELSE.
        EXIT.
      ENDIF.
    ENDWHILE.

    ASSERT nums_subrc_0 = `135677`.
    ASSERT nums_subrc_2 = `248`.
    CLEAR: subrc, struc_m, it_n, it_o, nums_subrc_0, nums_subrc_2.

    WHILE subrc = 0.
      idx = sy-index.
      struc_m = VALUE #( num = 9999 char = &#x27;##&#x27; str = `V` ).
      READ TABLE it_m INTO struc_m INDEX idx COMPARING char str TRANSPORTING num.
      subrc = COND #( WHEN sy-subrc = 0 THEN 0 ELSE sy-subrc ).
      IF subrc = 0.
        APPEND struc_m TO it_n.
        nums_subrc_0 &amp;&amp;= struc_m-num.
      ELSEIF subrc = 2.
        APPEND struc_m TO it_o.
        nums_subrc_2 &amp;&amp;= struc_m-num.
        subrc = 0.
      ELSE.
        EXIT.
      ENDIF.
    ENDWHILE.

    ASSERT nums_subrc_0 = `677`.
    ASSERT nums_subrc_2 = `123458`.

**********************************************************************
    &quot;CASTING / ELSE UNASSIGN additions to READ TABLE statements
    &quot;Additions when assigning the read result to a field symbol

    TYPES c3 TYPE c LENGTH 3.
    TYPES ty_tab_g TYPE TABLE OF c3 WITH EMPTY KEY.
    DATA(itq) = VALUE ty_tab_g( ( &#x27;abc&#x27; ) ( &#x27;def&#x27; ) ).

    &quot;Field symbol created inline (i.e. with the generic type &#x27;data&#x27; implicitly)
    &quot;In this case, the CASTING and ELSE UNASSIGN additions are not available.
    READ TABLE itq ASSIGNING FIELD-SYMBOL(&lt;fs_k&gt;) INDEX 1.

    &quot;******* CASTING addition *******
    &quot;To use the addition, the field symbol must be either completely typed, or
    &quot;typed with one of the generic built-in ABAP types c, n, p, or x.
    TYPES c2 TYPE c LENGTH 2.
    FIELD-SYMBOLS &lt;fs_l&gt; TYPE c2.

    READ TABLE itq ASSIGNING &lt;fs_l&gt; CASTING INDEX 2.
    ASSERT &lt;fs_l&gt; = &#x27;de&#x27;.

    &quot;******* ELSE UNASSIGN addition *******
    &quot;The field symbol is unassigned if no table line is found. The addition
    &quot;can be used together with the CASTING addition.
    &quot;The following example loops 3 times across an internal table that has
    &quot;two lines. The sy-index value is used as the index value of the READ TABLE
    &quot;statement. The example demonstrates that when a line is not found, the field
    &quot;symbol is unassigned. In case of the first READ TABLE statement that does not
    &quot;specify ELSE UNASSIGN, the field symbol remains assigned.

    DATA string_a TYPE string.
    FIELD-SYMBOLS &lt;fs_o&gt; TYPE c2.
    DO 3 TIMES.
      READ TABLE itq ASSIGNING FIELD-SYMBOL(&lt;fs_m&gt;) INDEX sy-index.
      READ TABLE itq ASSIGNING FIELD-SYMBOL(&lt;fs_n&gt;) ELSE UNASSIGN INDEX sy-index.
      READ TABLE itq ASSIGNING &lt;fs_o&gt; CASTING ELSE UNASSIGN INDEX sy-index.
      IF sy-index = 3.
        ASSERT &lt;fs_m&gt; = `def`.
        ASSERT &lt;fs_n&gt; IS NOT ASSIGNED.
        ASSERT &lt;fs_o&gt; IS NOT ASSIGNED.
      ENDIF.
    ENDDO.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Accessing Single Table Lines via Table Expressions</h2>
<p>The previous sections also covered table expressions. This section provides a summary of their use.</p>
<ul>
<li>Table expressions typically use the name of an internal table followed by square brackets with content <code>[ ... ]</code>, specifying a table line.</li>
<li>They allow read and write access to internal tables at various positions.</li>
<li>These expressions are a concise form of <code>READ TABLE</code> statements, enabling read and write operations in operand positions.</li>
<li>Unlike <code>READ TABLE</code>, table expressions do not change the <code>sy-tabix</code> system field (except with <code>ASSIGN</code>).</li>
<li>After the brackets, you can specify chaining (component and object component selectors or additional square brackets). Without chaining, the entire line is respected.</li>
<li>Note that not found lines raise the catchable exception <code>CX_SY_ITAB_LINE_NOT_FOUND</code>.</li>
</ul>
<p>The following table demonstrates a selection of subjects where table expressions are applicable.</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Subject &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Reading table lines by index &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>When only specifying the index number in the square brackets, it means referring to the primary table index.</li>
<li>In this case, the internal table must be an index table.</li>
<li>Using the <code>KEY ... INDEX ...</code> addition, you can specify the table index explicitly. Either use the predefined name <code>primary_key</code> for the primary key explicitly (or an alias name, if specified), or the secondary key name (or an alias name, if specified).</li>
<li>The <code>KEY</code> addition here works like <code>USING KEY</code> in <code>READ TABLE</code> statements.</li>
<li>Note that the demo internal tables in the snippet are used in the following examples of the section.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;Creating and populating demo internal tables
&quot;Note: These demo tables are relevant for most of the code snippets
&quot;in this section.
TYPES: BEGIN OF s_demo,
          comp1 TYPE i,
          comp2 TYPE i,
          comp3 TYPE i,
          comp4 TYPE c LENGTH 3,
        END OF s_demo,
        ttyp        TYPE SORTED TABLE OF s_demo WITH UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp2 comp3,
        ttyp_hashed TYPE HASHED TABLE OF s_demo WITH UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp2 comp3,
        ttyp2       TYPE SORTED TABLE OF s_demo WITH UNIQUE KEY comp1 comp2 WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp3.

DATA(itab) = VALUE ttyp( ( comp1 = 1 comp2 = 30 comp3 = 31 comp4 = &#x27;aaa&#x27; )
                         ( comp1 = 2 comp2 = 20 comp3 = 21 comp4 = &#x27;bbb&#x27; )
                         ( comp1 = 3 comp2 = 10 comp3 = 11 comp4 = &#x27;ccc&#x27; ) ).

DATA itab_hashed TYPE ttyp_hashed.
itab_hashed = itab.
DATA itab_so TYPE ttyp2.
itab_so = itab.

DATA line TYPE s_demo.

&quot;Reading table line by index
&quot;Just specifying the index number means referring to the primary table index.
&quot;In this case, the internal table must be an index table.

&quot;In the example, the entire table line is assigned to a variable
line = itab[ 2 ].

&quot;KEY ... INDEX ... additions
&quot;For reading a line according to a table index.
&quot;The following example has the same effect as above. Here, the default
&quot;name of the primary key is specified explicitly.
line = itab[ KEY primary_key INDEX 2 ].

&quot;Secondary table key specified, using secondary table index
line = itab[ KEY sk INDEX 1 ].

&quot;This syntax is not possible for hashed tables.
&quot;DATA(line_hashed_tab1) = itab_hashed[ 2 ].
&quot;DATA(line_hashed_tab2) = itab_hashed[ KEY primary_key INDEX 2 ].
&quot;Secondary table index access is possible for hashed tables
DATA(line_hashed_tab3) = itab_hashed[ KEY sk INDEX 2 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Reading table lines by table key &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li><code>TABLE KEY</code> addition</li>
<li>Requires the table key to be fully specified, i.e. all components of the key (primary table key or secondary table key) must be specified, and no other components can be specified.</li>
<li>Works like the <code>WITH TABLE KEY</code> addition in <code>READ TABLE</code> statements. However, in table expressions, the <code>COMPONENTS</code> addition is optional and the key name must be specified.</li>
<li>Usually, specifying the primary table key explicitly (using predefined name <code>primary_key</code> or an alias, if available) is not required. You can instead also just specify a free key search (i.e. not using any <code>TABLE KEY</code>/<code>KEY</code> addition) and specify all primary table key components. Then, an optimized search is also performed for sorted and hashed tables.</li>
<li><code>KEY</code> addition without <code>TABLE</code>:</li>
<li>This syntax option reads a line in accordance with a specified free key.</li>
<li>It is not mandatory to specify all components of a sorted table key (the initial left part at least is required). The search is then partly optimized (unlike fully optimized when all key components are specified).</li>
<li>Example purposes of such a specification: Determining the existence of a line, or determining a line number for the starting point for a loop starting at that position.</li>
<li>Additionally, in case of sorted and secondary table keys, other components not being part of the key can be specified.</li>
<li>Same as above, specifying the addition <code>COMPONENTS</code> is optional.</li>
<li>Just using <code>KEY</code> and specifying all key components following the key name works like specifying <code>TABLE KEY</code>. The <code>TABLE KEY</code> addition just ensures that indeed all key components are specified.</li>
<li>Not using any <code>TABLE KEY</code>/<code>KEY</code> additions:</li>
<li>In case of sorted and hashed tables and without using any <code>TABLE KEY</code>/<code>KEY</code> additions, an optimized search is performed when some or all components of the primary table key are specified as free keys.</li>
<li>Notes:</li>
<li>Binary searches cannot be enforced with table expressions (unlike <code>READ TABLE</code> statements).</li>
<li>Key names can also be specified dynamically using parentheses <code>(...)</code>.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; TABLE KEY addition
*&amp;---------------------------------------------------------------------*

&quot;Explicitly specifying the primary table key
line = itab[ TABLE KEY primary_key COMPONENTS comp1 = 1 ].

&quot;The following statement is not possible as no other components can be specified.

&quot;line = itab[ TABLE KEY primary_key COMPONENTS comp1 = 1 comp2 = 30 ].

&quot;The addition COMPONENTS is optional; the following example is the same as above
line = itab[ TABLE KEY primary_key comp1 = 1 ].

&quot;Specifying a secondary table key
line = itab[ TABLE KEY sk COMPONENTS comp2 = 20 comp3 = 21 ].

&quot;Optional COMPONENTS addition
line = itab[ TABLE KEY sk comp2 = 20 comp3 = 21 ].

&quot;Fully specifying the table key components is required with TABLE KEY. So, the
&quot;following statement is not possible.

&quot;line = itab[ TABLE KEY sk comp2 = 20 ].

*&amp;---------------------------------------------------------------------*
*&amp; KEY addition
*&amp;---------------------------------------------------------------------*

&quot;Using KEY and specifying all key components work like specifying TABLE KEY
line = itab[ KEY primary_key COMPONENTS comp1 = 1 ].
line = itab[ KEY primary_key comp1 = 1 ].
line = itab[ KEY sk COMPONENTS comp2 = 20 comp3 = 21 ].
line = itab[ KEY sk comp2 = 20 comp3 = 21 ].

&quot;Unlike TABLE KEY, KEY does not enforce all key components to be specified
line = itab[ KEY sk comp2 = 20 ].

&quot;In case of sorted and secondary table keys, other components not being part
&quot;of the key can be specified
line = itab[ KEY primary_key comp1 = 1 comp4 = &#x27;aaa&#x27; ].
line = itab[ KEY sk comp2 = 20 comp4 = &#x27;bbb&#x27; ].

&quot;The following statements are not possible. The initial, left part of
&quot;the key must be specified. In the example case, it is comp2.

&quot;line = itab[ KEY sk comp3 = 21 comp4 = &#x27;bbb&#x27; ].
&quot;line = itab[ KEY sk comp4 = &#x27;bbb&#x27; ].

&quot;The following statement triggers a syntax warning because the initial
&quot;part of a table key is specified, but the key name is not specified.
&quot;In this case, the search is not optimized as the component is not
&quot;part of the primary table key of the sorted table. You may optimize
&quot;it by specifying the key.

&quot;line = itab[ comp2 = 10 ].

&quot;The syntax warning can be suppressed by a pragma.
line = itab[ comp2 = 10 ] ##primkey[sk].

&quot;Specifying the key name
line = itab[ KEY sk comp2 = 10 ].

*&amp;---------------------------------------------------------------------*
*&amp; No TABLE KEY/KEY additions
*&amp;---------------------------------------------------------------------*

&quot;Specifying a free key search, but including all components of the primary
&quot;table key
&quot;For a sorted table as in the example, the search is fully optimized.
line = itab[ comp1 = 1 ].

&quot;Partly optimized (only a part of the primary table key of the sorted
&quot;example table is specified)
line = itab_so[ comp1 = 1 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Reading table lines using free keys &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>Free keys are specified without any <code>... KEY ...</code> addition. This corresponds to <code>WITH KEY</code> specifications in <code>READ TABLE</code> statements.</li>
<li>However, in case of sorted and hashed tables, an optimized search is performed when some or all of the key components are specified as free keys (without any <code>... KEY ...</code> addition).</li>
<li>Note that binary searches cannot be enforced with table expressions (unlike <code>READ TABLE</code> statements).</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;The search is and cannot be optimized as the component is not part of 
&quot;the primary table key of the table. Plus, no appropriate 
&quot;secondary table key can be applied.
line = itab[ comp4 = &#x27;ccc&#x27; ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Using the read result in various positions &lt;/td&gt; &lt;td&gt;</p>
<p>This is to emphasize that table expressions can be used in various read positions. The snippet shows a few examples. Check the notes in the ABAP Keyword Documentation topics about the use.</p>
<p>&lt;br&gt;</p>
<pre><code>DATA(first_line) = itab[ 1 ].

&quot;Using table expressions as arguments of built-in functions
IF line_exists( itab[ 1 ] ).
    ...
ELSE.
    ...
ENDIF.

ASSERT line_index( itab[ comp4 = &#x27;ccc&#x27; ] ) = 3.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Assigning table lines to a field symbol &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Works like READ TABLE ... ASSIGNING ...
ASSIGN itab[ 2 ] TO FIELD-SYMBOL(&lt;line&gt;).
&quot;Note: Table expressions do not set the sy-tabix
&quot;value, except when used with ASSIGN.
ASSERT sy-tabix = 2.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Assigning table lines to a field symbol and setting &lt;code&gt;sy&lt;/&gt; components &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>Assigning a non-existent line results in a <code>sy-subrc</code> value that is not 0. An exception is not raised. A found line results in value 0 for <code>sy-subrc</code>.</li>
<li>If the assignment fails, <code>sy-subrc</code> is set to 4, unless the end of the table is reached during a binary search in sorted tables, in which case <code>sy-subrc</code> is set to 8.</li>
<li>Unlike in other use cases of table expressions, <code>sy-tabix</code> is set here. When the line is found, <code>sy-tabix</code> is set according to the primary or secondary table index used. In case of hash keys, it is 0. In case of a not found line and <code>sy-subrc</code> is 8, and the end of the searched table is reached, <code>sy-tabix</code> is set to the number of lines + 1.</li>
<li>Regarding the assignment, you can also check the <a href="06_Dynamic_Programming.md">Dynamic Programming</a> cheat sheet regarding dynamic <code>ASSIGN</code> statements and the <code>ELSE UNASSIGN</code> addition.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>TYPES: BEGIN OF s_demo,
         comp1 TYPE i,
         comp2 TYPE i,
       END OF s_demo,
       tab_type_sorted TYPE SORTED TABLE OF s_demo WITH UNIQUE KEY comp1,
       tab_type_std    TYPE STANDARD TABLE OF s_demo WITH NON-UNIQUE KEY comp1,
       tab_type_hashed TYPE HASHED TABLE OF s_demo WITH UNIQUE KEY comp1.

DATA(itab_sorted) = VALUE tab_type_sorted( ( comp1 = 1 comp2 = 11 )
                                           ( comp1 = 2 comp2 = 22 )
                                           ( comp1 = 3 comp2 = 33 ) ).

DATA itab_std TYPE tab_type_std.
itab_std = itab_sorted.
DATA itab_hashed TYPE tab_type_hashed.
itab_hashed = itab_sorted.

ASSIGN itab_std[ KEY primary_key COMPONENTS comp1 = 1 ] TO FIELD-SYMBOL(&lt;fs1&gt;).
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 1.
ASSERT &lt;fs1&gt; IS ASSIGNED.

ASSIGN itab_sorted[ KEY primary_key COMPONENTS comp1 = 1 ] TO FIELD-SYMBOL(&lt;fs2&gt;).
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 1.
ASSERT &lt;fs2&gt; IS ASSIGNED.

ASSIGN itab_hashed[ KEY primary_key COMPONENTS comp1 = 1 ] TO FIELD-SYMBOL(&lt;fs3&gt;).
ASSERT sy-subrc = 0.
ASSERT sy-tabix = 0.
ASSERT &lt;fs3&gt; IS ASSIGNED.

ASSIGN itab_std[ KEY primary_key COMPONENTS comp1 = 10 ] TO FIELD-SYMBOL(&lt;fs4&gt;).
ASSERT sy-subrc = 4.
ASSERT sy-tabix = 0.
ASSERT &lt;fs4&gt; IS NOT ASSIGNED.

ASSIGN itab_sorted[ KEY primary_key COMPONENTS comp1 = 10 ] TO FIELD-SYMBOL(&lt;fs5&gt;).
ASSERT sy-subrc = 8.
ASSERT sy-tabix = 4.
ASSERT &lt;fs5&gt; IS NOT ASSIGNED.

ASSIGN itab_hashed[ KEY primary_key COMPONENTS comp1 = 10 ] TO FIELD-SYMBOL(&lt;fs6&gt;).
ASSERT sy-subrc = 4.
ASSERT sy-tabix = 0.
ASSERT &lt;fs6&gt; IS NOT ASSIGNED.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Data reference variables pointing to a table line &lt;/td&gt; &lt;td&gt;</p>
<pre><code>DATA ref TYPE REF TO data.
ref = NEW s_demo(  ).
ref-&gt;* = itab[ 1 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Specifying table expressions as operands in constructor expressions with &lt;code&gt;VALUE&lt;/code&gt; and &lt;code&gt;REF&lt;/code&gt; &lt;/td&gt; &lt;td&gt;</p>
<pre><code>line = VALUE #( itab[ 2 ] ).
&quot;Works like READ TABLE ... REFERENCE INTO ...
DATA(line_ref) = REF #( itab[ 3 ] ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Specifying a default value for lines that are not found or marking as optional to avoid an exception &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>You can specify default values for lines that are not found to avoid an exception.</li>
<li>The first example shows catching the execption with a <code>TRY</code> control structure.</li>
<li>The <code>OPTIONAL</code> and <code>DEFAULT</code> additions can be used in the context of statements using table expressions and constructor expressions (<code>VALUE</code> and <code>REF</code> are possible).</li>
<li>When using <code>OPTIONAL</code>, the type-specific initial value is set. After the <code>DEFAULT</code> addition, a value is expected that is convertible to the target type. You may also specify another table expression for an alternative table line to be searched.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;Accessing a non-existent table line raises a catchable exception
TRY.
    line = itab[ 4 ].
    CATCH cx_sy_itab_line_not_found.
ENDTRY.

line = VALUE #( itab[ 4 ] OPTIONAL ).

line = VALUE #( itab[ 5 ] DEFAULT itab[ 1 ]  ).
line = VALUE #( itab[ 6 ] DEFAULT VALUE #( ) ).</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Field symbols and dereferenced data references specified before the square brackets &lt;/td&gt; &lt;td&gt;</p>
<p>The previous examples use concrete internal table names specified before the square brackets. Field symbols and dereferenced data references are also possible.</p>
<p>&lt;br&gt;</p>
<pre><code>ASSIGN itab TO FIELD-SYMBOL(&lt;tab&gt;).
line = &lt;tab&gt;[ 1 ].

DATA dref TYPE REF TO ttyp.
dref = NEW #(  ).

dref-&gt;* = itab.
line = dref-&gt;*[ 2 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Reading multiple lines &lt;/td&gt; &lt;td&gt;</p>
<p>Using table expressions, you can read entire lines individually. This is just to emphasize that when reading multiple lines sequentially, <code>LOOP</code> statements and <code>FOR</code> loops are recommended.</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;Instead of something like this ...
DO.
    ASSIGN itab[ sy-index ] TO FIELD-SYMBOL(&lt;fs&gt;).
    IF sy-subrc = 0.
     ...
     &quot;&lt;fs&gt;-comp1 = ...
    ELSE.
     EXIT.
    ENDIF.
ENDDO.

&quot;... a LOOP statement, for example.
LOOP AT itab ASSIGNING &lt;fs&gt;.
    ...
    &quot;&lt;fs&gt;-comp1 = ...
ENDLOOP.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Reading individual components of table lines &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Reading individual components of table lines using chainings after the
&quot;closing square bracket

&quot;Read component via line read using ...
&quot;... index
DATA(compa) = itab[ 1 ]-comp1.
&quot;... table key
DATA(compb) = itab[ TABLE KEY primary_key comp1 = 1 ]-comp2.
DATA(compc) = itab[ TABLE KEY sk comp2 = 30 comp3 = 31 ]-comp1.
&quot;... free key
DATA(compd) = itab[ comp4 = &#x27;ccc&#x27; ]-comp1.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Chaining table expressions in the context of nested internal tables &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>Table expressions can be chained if the table expression result is a table itself.</li>
<li>Note the pitfall remarks further down.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;Creating deep internal table
TYPES: BEGIN OF s_sub,
            comp1 TYPE i,
            comp2 TYPE i,
        END OF s_sub,
        tab_type_sub TYPE TABLE OF s_sub WITH EMPTY KEY,
        BEGIN OF s,
            compa TYPE i,
            compb TYPE TABLE OF tab_type_sub WITH EMPTY KEY,
        END OF s,
        tab_type TYPE TABLE OF s WITH EMPTY KEY.

&quot;Expressions helpful when populating
DATA(deep_tab) = VALUE tab_type( ( compa = 1
                                   compb = VALUE #( ( VALUE #( ( comp1 = 3 comp2 = 4 ) ( comp1 = 5 comp2 = 6 ) ) )
                                                    ( VALUE #( ( comp1 = 7 comp2 = 8 ) ( comp1 = 9 comp2 = 10 ) ) ) ) )
                                    ( compa = 2
                                      compb = VALUE #( ( VALUE #( ( comp1 = 11 comp2 = 12 ) ( comp1 = 13 comp2 = 14 ) ) )
                                                       ( VALUE #( ( comp1 = 15 comp2 = 16 ) ( comp1 = 17 comp2 = 18 ) ) ) ) ) ).



DATA(num1) = deep_tab[ 2 ]-compb[ 1 ][ 2 ]-comp2.
ASSERT num1 = 14.

&quot;A statement such as the previous one instead of, for example, multiple statements as follows.
READ TABLE deep_tab INDEX 2 INTO DATA(wa1).
READ TABLE wa1-compb INDEX 1 INTO DATA(wa2).
READ TABLE wa2 INTO DATA(wa3) INDEX 2.

DATA(num2) = wa3-comp2.

ASSERT num2 = num1.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Table expression result having a reference type enabling chainings with the object component selector &lt;/td&gt; &lt;td&gt;</p>
<pre><code>DATA itab_ref TYPE TABLE OF REF TO s_demo WITH EMPTY KEY.
itab_ref = VALUE #( ( NEW s_demo( comp1 = 1 comp2 = 30 comp3 = 31 comp4 = &#x27;aaa&#x27; ) ) ).

&quot;Reading entire line by dereferencing
DATA(deref_line) = itab_ref[ 1 ]-&gt;*.
&quot;Reading component by dereferencing
DATA(dref_compa) = itab_ref[ 1 ]-&gt;comp3.
&quot;The following syntax is also possible (dereferencing operator followed
&quot;by the component selector).
DATA(dref_compb) = itab_ref[ 1 ]-&gt;*-comp4.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Table expressions in write positions: Writes on the entire line &lt;/td&gt; &lt;td&gt;</p>
<p>Note that you cannot perform writes on entire lines in the context of key tables as key values cannot be changed.</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;The demo table is a key table. Therefore, writes on entire lines produce runtime errors.
&quot;itab[ 3 ] = VALUE #( ).

&quot;Creating a standard table
DATA itab_std TYPE TABLE OF s_demo WITH NON-UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp2 comp3.
itab_std = itab.

&quot;Here, writes on entire lines are allowed.
itab_std[ 3 ] = VALUE #( comp1 = 123 comp4 = &#x27;zzz&#x27; ).
CLEAR itab_std[ 3 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Table expressions in write positions: Writes on individual components &lt;/td&gt; &lt;td&gt;</p>
<pre><code>itab[ 3 ]-comp4 = &#x27;yyy&#x27;.
itab_ref[ 1 ]-&gt;comp3 = 123.
&quot;No key value change allowed in key tables
&quot;The following statement causes a runtime error.
&quot;itab[ 1 ]-comp1 = 987.

&quot;Key value change allowed for standard tables.
itab_std[ 3 ]-comp1 = 456.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Dynamic key and component name specifications &lt;/td&gt; &lt;td&gt;</p>
<p>For more information about dynamic programming, refer to the <a href="06_Dynamic_Programming.md">Dynamic Programming</a> cheat sheet.</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;Dynamic key and component specifications
line = itab[ KEY (&#x27;PRIMARY_KEY&#x27;) COMPONENTS comp1 = 1 ].
line = itab[ KEY (&#x27;PRIMARY_KEY&#x27;) comp1 = 1 ].
line = itab[ KEY (&#x27;SK&#x27;) COMPONENTS comp2 = 20 comp3 = 21 ].
line = itab[ KEY (&#x27;SK&#x27;) comp2 = 20 comp3 = 21 ].

line = itab[ KEY (&#x27;PRIMARY_KEY&#x27;) (&#x27;COMP1&#x27;) = 1 ].
line = itab[ KEY (&#x27;SK&#x27;) (&#x27;COMP2&#x27;) = 20 (&#x27;COMP3&#x27;) = 21 ].

DATA(key_name) = &#x27;PRIMARY_KEY&#x27;.
DATA(comp_name) = &#x27;COMP1&#x27;.
line = itab[ KEY (key_name) (comp_name) = 1 ].
line = itab[ KEY primary_key (comp_name) = 1 ].</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Pitfalls regarding table expressions &lt;/td&gt; &lt;td&gt;</p>
<ul>
<li>The following example emphasizes that table expressions - expression enabling in modern ABAP as such - comes in very handy.</li>
<li>However, also take the maintainability, debuggability and readbility of your code into consideration.</li>
<li>The example includes the table expression chaining example from above (that may be fairly hard to understand) and a nonsensical, bad example overusing table expressions (affecting performance).</li>
<li>Expand the following collapsible section for example code. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console.</li>
</ul>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;-------------- Chaining table expressions -------------

    &quot;Creating a deep internal table
    TYPES: BEGIN OF s_sub,
             comp1 TYPE i,
             comp2 TYPE i,
           END OF s_sub,
           tab_type_sub TYPE TABLE OF s_sub WITH EMPTY KEY,
           BEGIN OF s,
             compa TYPE i,
             compb TYPE TABLE OF tab_type_sub WITH EMPTY KEY,
           END OF s,
           tab_type TYPE TABLE OF s WITH EMPTY KEY.

    &quot;Expressions helpful when populating
    DATA(deep_tab) = VALUE tab_type( ( compa = 1
                                       compb = VALUE #( ( VALUE #( ( comp1 = 3 comp2 = 4 ) ( comp1 = 5 comp2 = 6 ) ) )
                                                        ( VALUE #( ( comp1 = 7 comp2 = 8 ) ( comp1 = 9 comp2 = 10 ) ) ) ) )
                                     ( compa = 2
                                       compb = VALUE #( ( VALUE #( ( comp1 = 11 comp2 = 12 ) ( comp1 = 13 comp2 = 14 ) ) )
                                                        ( VALUE #( ( comp1 = 15 comp2 = 16 ) ( comp1 = 17 comp2 = 18 ) ) ) ) ) ).

    &quot;Chained table expressions in the context of reading a value from a
    &quot;nested internal table
    &quot;Such a chaining works if the table expression result is a table itself.
    &quot;Such statements are fairly short, using few lines of code, however,
    &quot;they may be hard to understand and debug.
    DATA(num1) = deep_tab[ 2 ]-compb[ 1 ][ 2 ]-comp2.

    out-&gt;write( num1 ).

    &quot;A statement as above instead of multiple statements, for example, as follows.
    READ TABLE deep_tab INDEX 2 INTO DATA(wa1).
    READ TABLE wa1-compb INDEX 1 INTO DATA(wa2).
    READ TABLE wa2 INTO DATA(wa3) INDEX 2.
    DATA(num2) = wa3-comp2.

    out-&gt;write( num2 ).

    &quot;-------------- Overusing table expression -------------
    &quot;The following examples performes many reads and writes
    &quot;using table expressions. The first example includes multiple
    &quot;table expressions, while the second one is differently designed,
    &quot;without many table expressions. The runtimes of the loops are
    &quot;stored, demonstrating that the second example has a significantly
    &quot;reduced runtime compared to the first example.

    CONSTANTS: num_of_repetitions     TYPE i VALUE 10,
               num_of_assignment_runs TYPE i VALUE 1000.

    TYPES: BEGIN OF struct,
             a TYPE i,
             b TYPE i,
             c TYPE i,
             d TYPE i,
             e TYPE i,
             f TYPE i,
           END OF struct.

    DATA it TYPE TABLE OF struct WITH EMPTY KEY.

    it = VALUE #( ( a = 0 b = 0 c = 0 d = 0 e = 0 f = 0 )
                  ( a = 1 b = 1 c = 1 d = 1 e = 1 f = 1 ) ).
    DATA(it_original) = it.

    DATA runtime_tab TYPE TABLE OF decfloat34 WITH EMPTY KEY.

    DO num_of_repetitions TIMES.
      DATA(ts1) = utclong_current( ).
      DO num_of_assignment_runs TIMES.
        it[ 1 ]-a = sy-index.
        it[ 1 ]-b = sy-index.
        it[ 1 ]-c = sy-index.
        it[ 2 ]-d = sy-index.
        it[ 2 ]-e = sy-index.
        it[ 2 ]-f = sy-index.
        it[ 1 ]-d = it[ 2 ]-d.
        it[ 1 ]-e = it[ 2 ]-e.
        it[ 1 ]-f = it[ 2 ]-f.
        INSERT it[ 1 ] INTO TABLE it.
        INSERT it[ 2 ] INTO TABLE it.
      ENDDO.
      DATA(ts2) = utclong_current( ).
      cl_abap_utclong=&gt;diff( EXPORTING high    = ts2
                                       low     = ts1
                             IMPORTING seconds = DATA(seconds) ).

      APPEND seconds TO runtime_tab.
    ENDDO.

    SORT runtime_tab BY table_line ASCENDING.

    out-&gt;write( `Multiple table expressions, fastest run:` ).
    out-&gt;write( runtime_tab[ 1 ] ).

    CLEAR runtime_tab.
    it = it_original.

    DO num_of_repetitions TIMES.
      ts1 = utclong_current( ).
      DO num_of_assignment_runs TIMES.
        ASSIGN it[ 1 ] TO FIELD-SYMBOL(&lt;fs1&gt;).
        ASSIGN it[ 2 ] TO FIELD-SYMBOL(&lt;fs2&gt;).
        &lt;fs1&gt;-a = sy-index.
        &lt;fs1&gt;-b = sy-index.
        &lt;fs1&gt;-c = sy-index.
        &lt;fs2&gt;-d = sy-index.
        &lt;fs2&gt;-e = sy-index.
        &lt;fs2&gt;-f = sy-index.
        &lt;fs1&gt;-d = &lt;fs2&gt;-d.
        &lt;fs1&gt;-e = &lt;fs2&gt;-e.
        &lt;fs1&gt;-f = &lt;fs2&gt;-f.
        INSERT &lt;fs1&gt; INTO TABLE it.
        INSERT &lt;fs2&gt; INTO TABLE it.
      ENDDO.
      ts2 = utclong_current( ).
      cl_abap_utclong=&gt;diff( EXPORTING high    = ts2
                                       low     = ts1
                             IMPORTING seconds = seconds ).

      APPEND seconds TO runtime_tab.
    ENDDO.

    SORT runtime_tab BY table_line ASCENDING.

    out-&gt;write( `Avoiding overusing table expressions, fastest run:` ).
    out-&gt;write( runtime_tab[ 1 ] ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Processing Multiple Internal Table Lines Sequentially</h2>
<p>If you are interested not only in single table lines, but in the entire table content or in specific parts of it, you can use <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abaploop_at_itab.htm"><code>LOOP AT</code></a> statements to process table lines sequentially. As above, you can use multiple options for target areas: work area, field symbol, data reference. There are multiple additions to the <code>LOOP AT</code> statements to further restrict the table content to be processed.</p>
<pre><code>&quot;The target is an existing work area.
DATA wa LIKE LINE OF it.

LOOP AT it INTO wa. 
  &quot;No addition of the loop statement; all lines are processed
  &quot;Statements in this block are relevant for each individual table line.
  ...
ENDLOOP.

&quot;Work area declared inline
LOOP AT itab INTO DATA(wa_inl).
  ...
ENDLOOP.

&quot;Field symbols
FIELD-SYMBOLS &lt;fs&gt; LIKE LINE OF it.

LOOP AT it ASSIGNING &lt;fs&gt;.
  ...
ENDLOOP.

LOOP AT it ASSIGNING FIELD-SYMBOL(&lt;fs_inl&gt;).
  ...
ENDLOOP.

&quot;Data reference variables
DATA dref TYPE REF TO dbtab.

LOOP AT it REFERENCE INTO dref.
  ...
ENDLOOP.

LOOP AT it REFERENCE INTO DATA(dref_inl).
  ...
ENDLOOP.

&quot;Table key specification (snippet uses example table from above)
&quot;The specified table key affects the order in which the table lines
&quot;are accessed and the evaluation of the other conditions.

LOOP AT it INTO wa USING KEY primary_key.
&quot;LOOP AT it INTO wa USING KEY pk.            &quot;primary key alias
&quot;LOOP AT it INTO wa USING KEY sec_key.       &quot;secondary key
&quot;LOOP AT it INTO wa USING KEY sk.            &quot;secondary key alias
  ...
ENDLOOP.</code></pre>
<p>Simple form:</p>
<ul>
<li>The order in which tables are iterated depends on the table category.</li>
<li>Note the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abaploop_at_itab_cond.htm#!ABAP_ADDITION_3@3@"><code>STEP</code></a> addition, which is also available for other ABAP statements.</li>
<li>Index tables are looped over in ascending order by the index.</li>
<li>Hashed tables are looped in the order in which the lines were added to the table. You can also sort the table before the loop.</li>
<li>During the loop, the system field <code>sy-tabix</code> is set to the number of the currently processed table</li>
</ul>
<p>line. This is not true for hashed tables. There, <code>sy-tabix</code> is <code>0</code>.</p>
<ul>
<li>Note that if you want to work with the value of <code>sy-tabix</code>, you</li>
</ul>
<p>should do so immediately after the <code>LOOP</code> statement (e.g. by storing the value in a helper variable) to avoid possible overwriting in statements contained in the loop block.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Restricting the Area of a Table to Be Looped Over</h3>
<p>The additions of <code>LOOP</code> statements come into play when you want to restrict the table content to be respected by the loop because you do not want to loop over the entire table. Note that the examples only show work areas as target objects to hold the table line read. Other options are possible as shown above.</p>
<pre><code>&quot;FROM/TO: Only for index tables

&quot;Specifying an index range
LOOP AT it INTO wa FROM 2 TO 5.
  ...
ENDLOOP.

&quot;From specified line until the end
LOOP AT it INTO wa FROM 2.
  ...
ENDLOOP.

&quot;From first line until the specified line
LOOP AT it INTO wa TO 5.
  ...
ENDLOOP.

&quot;WHERE clause: Restricting lines based on logical expression

LOOP AT it INTO wa WHERE a &gt; 1 AND b &lt; 4.
  ...
ENDLOOP.

&quot;No interest in the table content; only relevant system fields are populated

&quot;Mandatory WHERE clause
LOOP AT it TRANSPORTING NO FIELDS WHERE a &lt; 5.
  ...
ENDLOOP.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Defining the Step Size and the Direction of Loop Passes</h3>
<p>Find more information in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abaploop_at_itab_cond.htm#!ABAP_ADDITION_3@3@"><code>STEP</code></a> topic. The addition is also available for other ABAP statements.</p>
<pre><code>&quot;STEP addition for defining the step size and the direction of the loop
&quot;- Step size: Specified by the absolute value of an integer
&quot;- Direction: Specified by a positive (forward loop) or negative 
&quot;  (loop in reverse order) integer 

&quot;Reversing the loop order using a negative integer
&quot;Each line is read indicated by the absolute value 1
LOOP AT it INTO wa STEP -1.
  ...
ENDLOOP.

&quot;Forward loop by specifiying a positive integer
&quot;In the example, every second line is read.
&quot;Note: Omitting STEP means STEP 1 by default.
LOOP AT it INTO wa STEP 2.
  ...
ENDLOOP.

&quot;STEP with other additions
&quot;The example uses the additions FROM and TO.
&quot;Note: If the value after STEP is negative, the value
&quot;after FROM must be greater than the value after TO.
LOOP AT it INTO wa FROM 6 TO 3 STEP -2.
  ...
ENDLOOP.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Iteration Expressions</h3>
<p>Iteration expressions with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfor.htm"><code>FOR</code></a> as part of certain constructor expressions allow you to create content of an internal table by evaluating one or more source tables.</p>
<pre><code>TYPES ty_int_tab TYPE TABLE OF i WITH EMPTY KEY.
DATA(int_table_a) = VALUE ty_int_tab( ( 1 ) ( 2 ) ( 3 ) ( 4 ) ( 5 ) ).
DATA int_table_b TYPE ty_int_tab.
int_table_b = VALUE #( FOR wa_b IN int_table_a ( wa_b * 2 ) ).
&quot;Table Content: 2 / 4 / 6 / 8 / 10

&quot;Instead of, for example, a LOOP statement as follows:
DATA int_table_c TYPE ty_int_tab.
LOOP AT int_table_a INTO DATA(wa_c).
  INSERT wa_c * 3 INTO TABLE int_table_c.
ENDLOOP.
&quot;Table content: 3 / 6 / 9 / 12 / 15</code></pre>
<p>The expressions are covered in the <a href="05_Constructor_Expressions.md">Constructor Expressions</a> cheat sheet:</p>
<ul>
<li><a href="05_Constructor_Expressions.md#iteration-expressions-using-for">Iteration Expressions Using FOR</a></li>
<li>Special reduction operator <code>REDUCE</code> that is based on iteration expressions: <a href="05_Constructor_Expressions.md#reduce">REDUCE</a></li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Interrupting and Exiting Loops</h3>
<p>ABAP keywords such as</p>
<ul>
<li><code>CONTINUE</code> (terminating the current loop pass, continuing with the next)</li>
<li><code>CHECK</code> (conditional termination termination of the current loop pass if the logical expression is false), and</li>
<li><code>EXIT</code> (loop terminated completely),</li>
</ul>
<p>are available to exit and interrupt loops. Find more information in the <a href="13_Program_Flow_Logic.md#interrupting-and-exiting-loops">Program Flow Logic</a> cheat sheet.</p>
<pre><code>DATA(str_table) = VALUE string_table( ( `a` ) ( `b` ) ( `c` ) ( `d` ) ( `e` ) ( `f` ) ).
LOOP AT str_table INTO DATA(wa).
  DATA(tabix) = sy-tabix.
  IF wa = `e`.
    EXIT.
  ENDIF.
ENDLOOP.
ASSERT tabix = 5.</code></pre>
<p>In the following example, the loop is exited using the <code>EXIT</code> statement when a certain condition is met.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Inserting and Deleting Lines in Internal Tables in Loops</h3>
<ul>
<li>In a loop, you can change the values of the current table line&#x27;s components. You can also delete or insert entire lines.</li>
<li>The position of these new or deleted lines is determined by the table index or a sorted key. For hashed tables or hash keys, the position is determined by the insertion order (though this can be changed with the <code>SORT</code> statement).</li>
<li>After the currently processed table line, ...</li>
<li>inserting a line: The new line is processed in the next loop pass (watch out for a non-terminating loop).</li>
<li>deleting a line: The deleted line is not processed in the next loop pass.</li>
<li>Before the currently processed table line, ...</li>
<li>inserting a line: The internal loop counter (<code>sy-tabix</code>) increases accordingly (in the case of index tables, sorted keys).</li>
<li>deleting a line: The internal loop counter (<code>sy-tabix</code>) decreases accordingly.</li>
<li>You cannot replace or clear the entire table body in a loop. If detected statically, a syntax error is displayed.</li>
</ul>
<p>The following examples explore the insertion and deletion of lines when looping across internal tables:</p>
<pre><code>&quot;Creating and populating a demo standard internal table to
&quot;work with in the example loops
TYPES: BEGIN OF s_loop_mod,
            text TYPE string,
            num  TYPE i,
        END OF s_loop_mod,
        t_loop_mod TYPE TABLE OF s_loop_mod WITH EMPTY KEY.

&quot;Inserting 10 entries into the demo table
DATA(itab_original) = VALUE t_loop_mod( FOR x = 1 WHILE x &lt;= 10 ( text = x ) ).
DATA(itab) = itab_original.

*&amp;---------------------------------------------------------------------*
*&amp; Inserting a line after the current line
*&amp;---------------------------------------------------------------------*

&quot;The example inserts a line after the currently processed line
&quot;using an INSERT statement and specifying the index value
&quot;(sy-tabix value + 1). The &#x27;num&#x27; component is assigned the
&quot;current sy-tabix value.
&quot;Note: In all statements, the sy-tabix value is stored in a
&quot;variable right after the LOOP statement. Assume that multiple
&quot;statements are included before the statement that actually uses
&quot;the current sy-tabix value. Other statements that potentially
&quot;change the sy-tabix value might interfere.
&quot;An EXIT statement takes care of exiting the loop. In the example,
&quot;all values of the &#x27;num&#x27; component in the original table lines
&quot;(except the first line) are initial as the loop is exited.
LOOP AT itab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
  DATA(tabix) = sy-tabix.
  &lt;fs&gt;-num = tabix.
  INSERT VALUE #( text = tabix ) INTO itab INDEX tabix + 1.
  IF tabix = 50.
   EXIT.
  ENDIF.
ENDLOOP.

*Internal table content (parts of it):
*TEXT    NUM
*1       1
*1       2
*2       3
*3       4
*4       5
*...
*48      49
*49      50
*50      0
*2       0
*3       0
*4       0
*...

*&amp;---------------------------------------------------------------------*
*&amp; Deleting a line after the current line
*&amp;---------------------------------------------------------------------*

&quot;The example deletes a line after the current line using a
&quot;DELETE statement and the INDEX addition. The index value
&quot;is specified using the current sy-tabix value + 1.
&quot;The &#x27;num&#x27; value in the resulting internal table includes
&quot;the sy-tabix value.

itab = itab_original.
LOOP AT itab ASSIGNING &lt;fs&gt;.
  tabix = sy-tabix.
  &lt;fs&gt;-num = tabix.
  DELETE itab INDEX tabix + 1.
ENDLOOP.

*Internal table content:
*TEXT    NUM
*1       1
*3       2
*5       3
*7       4
*9       5

*&amp;---------------------------------------------------------------------*
*&amp; Inserting a line before the current line
*&amp;---------------------------------------------------------------------*

&quot;The example insert a line before the currently processed line using
&quot;an INSERT statement. The current sy-tabix value is used as INDEX value,
&quot;moving down the currently processed table line one position.
&quot;In that case, the sy-tabix value increases accordingly.
&quot;Logic:
&quot;- For example, the first line is processed, sy-tabix has the value 1.
&quot;- A line is inserted at this position, moving down the currently processed line
&quot;  one position. The moved line is then in the second position (as a new line exists
&quot;  in the first position).
&quot;- In the next loop pass, the loop is continued with the third line, i.e.
&quot;  sy-tabix has the value 3 in the second loop pass.
&quot;The example includes modifications of the table components. The &#x27;num&#x27; value
&quot;is assigned the table index value after inserting the new line. The
&quot;&#x27;num&#x27; value of existing table lines includes the value of the index before
&quot;inserting the new line + 1.

itab = itab_original.
FIELD-SYMBOLS &lt;line&gt; TYPE s_loop_mod.
DATA new_line_counter TYPE i.
DATA tabix_copy TYPE i.

LOOP AT itab ASSIGNING &lt;fs&gt;.
  tabix = sy-tabix.
  new_line_counter += 1.

  &quot;Asserting that sy-tabix value has changed accordingly.
  IF tabix &lt;&gt; 1.
    ASSERT tabix = tabix_copy + 2.
  ENDIF.

  DATA(new_line_text) = |---- New line { new_line_counter } ----|.
  INSERT VALUE #( text = new_line_text ) INTO itab INDEX tabix ASSIGNING &lt;line&gt;.

  DATA(idx_new) = line_index( itab[ text = new_line_text num = 0 ] ).
  &lt;line&gt;-num = idx_new.

  DATA(idx_existing) = line_index( itab[ text = &lt;fs&gt;-text num = 0 ] ).
  DATA(new_text) = |{ &lt;fs&gt;-text }(existing line, index before insertion: { tabix })|.
  &lt;fs&gt;-text = new_text.
  &lt;fs&gt;-num = idx_existing.

  tabix_copy = tabix.
ENDLOOP.

*Internal table content:
*TEXT                                              NUM
*---- New line 1 ----                              1
*1 (existing line, index before insertion: 1)      2
*---- New line 2 ----                              3
*2 (existing line, index before insertion: 3)      4
*---- New line 3 ----                              5
*3 (existing line, index before insertion: 5)      6
*---- New line 4 ----                              7
*4 (existing line, index before insertion: 7)      8
*---- New line 5 ----                              9
*5 (existing line, index before insertion: 9)      10
*---- New line 6 ----                              11
*6 (existing line, index before insertion: 11)     12
*---- New line 7 ----                              13
*7 (existing line, index before insertion: 13)     14
*---- New line 8 ----                              15
*8 (existing line, index before insertion: 15)     16
*---- New line 9 ----                              17
*9 (existing line, index before insertion: 17)     18
*---- New line 10 ----                             19
*10 (existing line, index before insertion: 19)    20

*&amp;---------------------------------------------------------------------*
*&amp; Deleting a line before the current line
*&amp;---------------------------------------------------------------------*

&quot;The example explores the deletion of a line before the currently
&quot;processed line. The previous line in the table is deleted if
&quot;the value of &#x27;text&#x27; (an integer was inserted) is an even number.
&quot;The DELETE statement specifies the index with the current sy-tabix
&quot;value - 1. On deletion, the sy-tabix value is decreased accordingly.
&quot;Before a potential deletion, the currently processed table line is
&quot;copied to another table to visualize the current sy-tabix value in
&quot;the &#x27;num&#x27; component.

itab = itab_original.
DATA itab_copy LIKE itab.

LOOP AT itab ASSIGNING &lt;fs&gt;.
    tabix = sy-tabix.

    &lt;fs&gt;-num = tabix.
    INSERT &lt;fs&gt; INTO TABLE itab_copy.

    TRY.
      IF CONV i( &lt;fs&gt;-text ) MOD 2 = 0.
        DELETE itab INDEX tabix - 1.
      ENDIF.
     CATCH cx_sy_conversion_no_number.
    ENDTRY.

ENDLOOP.

*Internal table content (itab):
*TEXT    NUM
*2       2
*4       3
*6       4
*8       5
*10      6

*Internal table content (itab_copy):
*TEXT    NUM
*1       1
*2       2
*3       2
*4       3
*5       3
*6       4
*7       4
*8       5
*9       5
*10      6

*&amp;---------------------------------------------------------------------*
*&amp; Deleting the currently processed table line
*&amp;---------------------------------------------------------------------*

&quot;The example explores deleting the currently processed table line using
&quot;a string table. So, the DELETE statement specifies the current sy-tabix
&quot;value for INDEX. In that case, the next line moves up one position, and
&quot;the sy-tabix value remains the same, i.e. when sy-tabix is 2, and the
&quot;line is deleted, the value remains 2 and processes the line moved up.

&quot;Creating and populating a demo internal table
DATA(str_tab) = VALUE string_table( ( `a` ) ( `#` ) ( `c` ) ( `#` ) ( `e` )
                                    ( `f` ) ( `g` ) ( `#` ) ( `i` ) ( `j` ) ).

LOOP AT str_tab REFERENCE INTO DATA(dref).
  tabix = sy-tabix.
  IF dref-&gt;* CS `#`.
    DELETE str_tab INDEX tabix.
  ENDIF.
ENDLOOP.

*Internal table content:
*a
*c
*e
*f
*g
*i
*j

*&amp;---------------------------------------------------------------------*
*&amp; Statements clearing the entire internal table are not allowed in loops
*&amp;---------------------------------------------------------------------*

&quot;The entire internal table cannot be deleted within loops.
&quot;The following statements commented out are not possible.
LOOP AT str_tab REFERENCE INTO dref.
  &quot;CLEAR str_tab.
  &quot;str_tab = VALUE #( ).
ENDLOOP.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Modifying Internal Table Content</h2>
<p>As mentioned above, you can modify the content of internal table lines directly in the context of <code>READ TABLE</code> and <code>LOOP AT</code> statements using field symbols and data reference variables. You can also use table expressions for direct modification (as also covered in section <a href="#table-expressions">Table Expressions</a>). Note that the key fields of the primary table key of sorted and hashed tables are always read-only. If you try to modify a key field, a runtime error occurs. However, this is not checked until runtime.</p>
<h3>Modifying Read Table Lines</h3>
<pre><code>&quot;Declaring and populating demo internal tables
TYPES: BEGIN OF ty_struc,
          comp1 TYPE i,
          comp2 TYPE string,
          comp3 TYPE c LENGTH 3,
       END OF ty_struc.

DATA it_st TYPE TABLE OF ty_struc WITH NON-UNIQUE KEY comp1.
DATA it_so TYPE SORTED TABLE OF ty_struc WITH UNIQUE KEY comp1.
DATA it_ha TYPE HASHED TABLE OF ty_struc WITH UNIQUE KEY comp1.

it_st = VALUE #( ( comp1 = 1 comp2 = `AAAAAA` comp3 = &#x27;bbb&#x27; )
                 ( comp1 = 2 comp2 = `CCCCCC` comp3 = &#x27;ddd&#x27; )
                 ( comp1 = 3 comp2 = `EEEEEE` comp3 = &#x27;fff&#x27; )
                 ( comp1 = 4 comp2 = `GGGGGG` comp3 = &#x27;hhh&#x27; )
               ).

it_so = it_st.
it_ha = it_st.

*&amp;---------------------------------------------------------------------*
*&amp; Modifying internal table content by changing the content of READ 
*&amp; TABLE statement target areas
*&amp;---------------------------------------------------------------------*

&quot;Reading table line into a target area
READ TABLE it_st INTO DATA(wa) INDEX 1.
READ TABLE it_so ASSIGNING FIELD-SYMBOL(&lt;fs&gt;) INDEX 2.
READ TABLE it_ha REFERENCE INTO DATA(dref) WITH TABLE KEY comp1 = 3. &quot;No reading by index in case of hashed tables

*&amp;---------------------------------------------------------------------*
*&amp; Modification examples
*&amp;---------------------------------------------------------------------*

&quot;Modifying all non-key components using the VALUE operator and
&quot;the BASE addition
&lt;fs&gt; = VALUE #( BASE &lt;fs&gt; comp2 = `IIIIII` comp3 = &#x27;jjj&#x27; ).

&quot;In the following example, the key value is assigned a new
&quot;value. Key values are protected against change in case of key tables.
&quot;A runtime error occurs.
&quot;&lt;fs&gt; = VALUE #( comp1 = 5 comp2 = `IIIIII` comp3 = &#x27;jjj&#x27; ).

dref-&gt;* = VALUE #( BASE dref-&gt;* comp2 = `KKKKKK` comp3 = &#x27;lll&#x27; ).

&quot;Same as above. Key values cannot be changed in this case.
&quot;dref-&gt;* = VALUE #( comp1 = 5 comp2 = `MMMMMM` comp3 = &#x27;nnn&#x27; ).

&quot;Using a MODIFY statement outlined below for changing internal
&quot;table content based on a read line in a work area
MODIFY TABLE it_st FROM VALUE #( BASE wa comp2 = `OOOOOO` comp3 = &#x27;ppp&#x27; ).

&quot;Modifying individual components
READ TABLE it_st INTO wa INDEX 2.
READ TABLE it_so ASSIGNING &lt;fs&gt; INDEX 3.
READ TABLE it_ha REFERENCE INTO dref WITH TABLE KEY comp1 = 4.

&quot;Using VALUE/BASE
&lt;fs&gt; = VALUE #( BASE &lt;fs&gt; comp2 = `QQQQQQ` ).
dref-&gt;* = VALUE #( BASE dref-&gt;* comp2 = `RRRRRR` ).
MODIFY TABLE it_st FROM VALUE #( BASE wa comp2 = `SSSSSS` ).

&quot;Using the component selector
&lt;fs&gt;-comp3 = &#x27;ttt&#x27;.

READ TABLE it_st INTO wa INDEX 3.
wa-comp3 = &#x27;uuu&#x27;.
MODIFY TABLE it_st FROM wa.

&quot;Object component selector in case of dereferencing ...
dref-&gt;comp2 = `VVVVVV`.
&quot;... which is a more comfortable option compared to using the
&quot;dereferencing and component selector operators in the following way.
dref-&gt;*-comp3 = &#x27;www&#x27;.

*&amp;---------------------------------------------------------------------*
*&amp; Modifying internal table content using table expressions
*&amp;---------------------------------------------------------------------*

&quot;Changing the entire table line of a standard table
&quot;In standard tables, the key value change is allowed.
it_st[ 3 ] = VALUE #( comp1 = 9 comp2 = `XXXXXX` comp3 = &#x27;yyy&#x27; ).
&quot;As above, the sorted table is a key table having a unique key,
&quot;therefore a write cannot be performed on the entire entry. Runtime
&quot;errors can occur.
&quot;it_so[ 3 ] = VALUE #( comp2 = `XXXXXX` comp3 = &#x27;yyy&#x27; ).
&quot;The same applies to hashed tables.
&quot;it_ha[ comp2 = `OOOOOO` ] = VALUE #( comp2 = `XXXXXX` comp3 = &#x27;yyy&#x27; ).

&quot;Changing individual components
it_st[ 3 ]-comp2 = `ZZZZZZ`.
it_so[ 3 ]-comp3 = &#x27;A1&#x27;.
it_ha[ comp2 = `CCCCCC` ]-comp2 = `B2`.
&quot;As above, no key field change in key tables. Allowed in standard
&quot;tables.
&quot;it_so[ 3 ]-comp1 = 10.
&quot;it_ha[ comp2 = `AAAAAA` ]-comp1 = `C3`.
it_st[ 1 ]-comp1 = 99.

*&amp;---------------------------------------------------------------------*
*&amp; Modifying table content in all table rows in a loop
*&amp;---------------------------------------------------------------------*

&quot;For more syntax options regarding loops, check the section above.
&quot;Target area: field symbol
LOOP AT it_st ASSIGNING FIELD-SYMBOL(&lt;lo&gt;).
  &lt;lo&gt;-comp2 = sy-tabix.
ENDLOOP.

*&amp;---------------------------------------------------------------------*
*&amp; Modifying table content restricting the rows that are looped across
*&amp;---------------------------------------------------------------------*

&quot;Target area: data reference variable
LOOP AT it_st reference into data(lo) FROM 2 TO 3.
  lo-&gt;comp3 = sy-tabix.
ENDLOOP.

&quot;Target area: work area
LOOP AT it_so into data(wa_lo) where comp1 &lt; 4.
  wa_lo-comp2 = sy-tabix.
  MODIFY TABLE it_so FROM wa_lo.
ENDLOOP.</code></pre>
<p>The following examples demonstrate direct modification of recently read table lines:</p>
<h3>Modifying Table Lines Using ABAP MODIFY Statements</h3>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmodify_itab.htm"><code>MODIFY</code></a> statements provide multiple ways of changing the content of single and multiple table lines by specifying the table key or a table index, without first reading the lines into a target area. Do not confuse the ABAP statement <code>MODIFY</code> with the ABAP SQL statement <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABAPMODIFY_DBTAB.html"><code>MODIFY</code></a>.</p>
<pre><code>&quot;Addition FROM ...; specified key values determine the line to be modified

&quot;line: existing line including key values
MODIFY TABLE it FROM line.

&quot;line constructed inline
MODIFY TABLE it FROM VALUE #( a = 1 b = 2 ... ).

&quot;Respecting only specified fields with the addition TRANSPORTING
&quot;In case of sorted/hashed tables, key values cannot be specified.
MODIFY TABLE it FROM line TRANSPORTING b c.

&quot;Modification via index
&quot;Note that it is only MODIFY, not MODIFY TABLE.
&quot;Example: It modifies the line with number 1 in the primary table index.
MODIFY it FROM line INDEX 1.

&quot;Without the addition TRANSPORTING, the entire line is changed.
&quot;Example: It modifies specific values.
MODIFY it FROM line INDEX 1 TRANSPORTING b c.

&quot;USING KEY addition
&quot;If the addition is not specified, the primary table key is used;
&quot;otherwise, it is the explicitly specified table key that is used.
&quot;Example: It is the same as MODIFY it FROM line INDEX 1.
MODIFY it FROM line USING KEY primary_key INDEX 1.

&quot;The statement below uses a secondary key and an index specification
&quot;for the secondary table index. Only specific fields are modified.
MODIFY it FROM line USING KEY sec_key INDEX 1 TRANSPORTING c d.

&quot;Modifying multiple lines in internal tables
&quot;All lines matching the logical expression in the WHERE clause are modified
&quot;as specified in line.
&quot;The additions TRANSPORTING and WHERE are both mandatory; USING KEY is optional.
MODIFY it FROM line TRANSPORTING b c WHERE a &lt; 5.</code></pre>
<div class="note">[!NOTE] - The system field `sy-subrc` is set to `0` if at least one line was changed. It is set to `4` if no lines were changed. - `MODIFY`, `DELETE`, and `INSERT` statements can be specified with and without the `TABLE` addition. With `TABLE` means an index access. Without `TABLE` means an access via the table key.</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Deleting Internal Table Content</h2>
<p>You can use <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapdelete_itab.htm"><code>DELETE</code></a> statements to delete single and multiple lines in internal tables. The following additions can be used: <code>USING KEY</code> (for specifying a table key), <code>FROM</code>/<code>TO</code> (for specifying row ranges), <code>STEP</code> (for specifying the step size), and <code>WHERE</code> (for specifying conditions).</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Deleting via index 
*&amp;---------------------------------------------------------------------*

&quot;Example: The first line in the table is deleted.
DELETE it INDEX 1.

&quot;If USING KEY is not used, INDEX can only be used with index tables.
&quot;If doing so, it determines the line from the primary table index.
&quot;If a secondary key is specified, the secondary table index is respected
&quot;Example: same as above
DELETE it INDEX 1 USING KEY primary_key.

&quot;Deleting an index range; FROM or TO alone can also be specified
DELETE it FROM 2 TO 5.

*&amp;---------------------------------------------------------------------*
*&amp; Deleting via keys
*&amp;---------------------------------------------------------------------*

&quot;The line must have a compatible type to the tables line type and
&quot;include key values. The first found line with the corresponding keys
&quot;is deleted.
&quot;If the key is empty, no line is deleted.
DELETE TABLE it FROM line.

&quot;Instead of specifying the keys using a data object (&quot;line&quot; above),
&quot;the keys can be specified separately. All key values must be specified.
&quot;Example: Respects keys from primary table index.
DELETE TABLE it WITH TABLE KEY a = 1.

&quot;You can also specify secondary keys.
&quot;Example: Same as above
DELETE TABLE it WITH TABLE KEY primary_key COMPONENTS a = 1.

DELETE TABLE it_sec WITH TABLE KEY sec_key COMPONENTS ...

*&amp;---------------------------------------------------------------------*
*&amp; Deleting multiple lines based on a WHERE condition
*&amp;---------------------------------------------------------------------*

&quot;Specifying the additions USING KEY, FROM, TO is also possible.
DELETE it WHERE a &lt; 6.

&quot;Excursion: Deleting in a LIKE-like fashion you may know from
&quot;ABAP SQL statements.
&quot;The LIKE addition is not available for the WHERE clause in DELETE
&quot;statements for internal tables as is the case for ABAP SQL DELETE statements.
DATA(str_table) = VALUE string_table( ( `abcZ` ) ( `Zdef` ) ( `gZhi` ) 
                                      ( `Zjkl` ) ( `Zmno` ) ( `pqrZ` ) ).    

&quot;You can, for example, use logical operators such as CP (conforms to pattern)
&quot;All lines that begin with Z are to be deleted.
DELETE str_table WHERE table_line CP `Z*`.
&quot;Result: abcZ / gZhi / pqrZ

*&amp;---------------------------------------------------------------------*
*&amp; Deleting the current line inside a LOOP statement
*&amp;---------------------------------------------------------------------*

&quot;The following example illustrates deleting the current table line
&quot;using a DELETE statement within a LOOP statement. Lines with even
&quot;numbers are deleted.
&quot;Note:
&quot;- The short form of the DELETE statement always deletes the
&quot;  current first line implicitly. It is only possible within a LOOP
&quot;  statement and the delete operation is performed on the same internal
&quot;  table.
&quot;- The field symbol (or reference variable) should not be used after
&quot;  the DELETE statement any more.
DATA itab1 TYPE TABLE OF i WITH EMPTY KEY.
itab1 = VALUE #( ( 1 ) ( 2 ) ( 3 ) ( 4 ) ( 5 ) ( 6 ) ( 7 ) ( 8 ) ( 9 ) ( 10 ) ).

LOOP AT itab1 ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
  IF &lt;fs&gt; MOD 2 = 0.
    DELETE itab1.
  ENDIF.
ENDLOOP.

*Table content:
*1
*3
*5
*7
*9

&quot;The following, similar example (uneven numbers are deleted) uses a
&quot;table which is looped over by specifying the addition USING KEY.
&quot;In this case (using LOOP ... USING KEY ...), the short form of the
&quot;DELETE statement cannot be used. Use the DELETE statement with the
&quot;addition USING KEY loop_key to delete the current first line.
&quot;loop_key is a predefined name to be used with DELETE and within
&quot;loops that specify LOOP ... USING KEY .... No other key name is
&quot;possible here.
DATA itab2 TYPE TABLE OF i WITH NON-UNIQUE KEY table_line.
itab2 = VALUE #( ( 1 ) ( 2 ) ( 3 ) ( 4 ) ( 5 ) ( 6 ) ( 7 ) ( 8 ) ( 9 ) ( 10 ) ).

LOOP AT itab2 USING KEY primary_key REFERENCE INTO DATA(dref2).
  IF dref2-&gt;* MOD 2 &lt;&gt; 0.
    DELETE itab2 USING KEY loop_key.
  ENDIF.
ENDLOOP.

*Table content:
*2
*4
*6
*8
*10</code></pre>
<h3>Deleting Adjacent Duplicate Lines</h3>
<p><code>DELETE ADJACENT DUPLICATES</code> statements allow you to delete all adjacent lines except for the first line that have the same content in certain components. You usually need to perform some appropriate sorting before using these statements.</p>
<pre><code>&quot;Implicitly using the primary table key
DELETE ADJACENT DUPLICATES FROM it.

&quot;Deletion respecting the values of the entire line
DELETE ADJACENT DUPLICATES FROM it COMPARING ALL FIELDS.

&quot;Only lines are deleted with matching content in specific fields
DELETE ADJACENT DUPLICATES FROM it COMPARING a c.

&quot;Deletion respecting a specified table key
&quot;Same as first example above
DELETE ADJACENT DUPLICATES FROM it USING KEY primary_key.

DELETE ADJACENT DUPLICATES FROM it USING KEY sec_key.</code></pre>
<div class="note">[!NOTE] The system field `sy-subrc` is set to `0` if at least one line has been deleted. It is set to `4` if no lines were deleted.</div>
<h3>Deleting the Entire Internal Table Content</h3>
<p>The <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclear.htm"><code>CLEAR</code></a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapfree_dataobject.htm"><code>FREE</code></a> statements allow you to delete the entire table content.</p>
<p>The difference between the two is in the handling of the memory space originally allocated to the table. When a table is cleared with <code>CLEAR</code>, the content is removed, but the memory space initially requested remains allocated. If the table is populated again later, the memory space is still available, which is a performance advantage over clearing an internal table with <code>FREE</code>. Such a statement also deletes the table content, but it also releases the memory space. Note that an assignment using the <code>VALUE</code> operator without entries in the parentheses clears the internal table.</p>
<pre><code>CLEAR it.

&quot;This statement additionally releases memory space.
FREE it.

&quot;Assignment using the VALUE operator without entries in the parentheses
it = VALUE #( ). 

&quot;The same applies to data reference variables pointing to internal tables.
it_ref = NEW #( ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Sorting Internal Tables</h2>
<ul>
<li>Sorted tables are stored in the memory in an automatically sorted</li>
</ul>
<p>order, hence, they cannot be sorted explicitly with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapsort_itab.htm"><code>SORT</code></a>.</p>
<ul>
<li>For standard and hashed tables, the order can be changed.</li>
<li>When using <code>SORT</code> statements, the sort order is derived either</li>
</ul>
<p>by the primary table key (Note: Secondary keys cannot be used for the sorting.) or by explicitly specifying the fields to be sorted by.</p>
<ul>
<li>Explicit specification is the recommended way because it is</li>
</ul>
<p>easier to understand and can prevent unwanted sorting results, especially with tables with standard key.</p>
<ul>
<li>Note that sorting is unstable by default concerning preserving the order of table lines with identical sort keys. Sorting results can vary if the table is sorted multiple times.</li>
<li>You can also sort dynamically. For more information, refer to the <a href="06_Dynamic_Programming.md">Dynamic Programming</a> cheat sheet.</li>
</ul>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt; Subject &lt;/td&gt; &lt;td&gt; Details/Code Snippet &lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt; Sorting by primary table key &lt;/td&gt; &lt;td&gt;</p>
<pre><code>&quot;Implicit sorting by primary table key and in ascending order by default
SORT itab.

&quot;Optional additions to determine the sort order
&quot;As mentioned above, ASCENDING is used implicitly. Here, specifying it explicitly.
SORT itab ASCENDING.
SORT itab DESCENDING.</code></pre>
<pre><code>&quot;Is basically the same as it2
DATA it1 TYPE TABLE OF zdemo_abap_fli.

DATA it2 TYPE STANDARD TABLE OF zdemo_abap_fli WITH DEFAULT KEY.

&quot;Respecting the standard key when sorting
SORT it1.</code></pre>
<p>The effect of sorting can have an unexpected result if you use the simple form of the statement and do not explicitly specify the keys. If an internal table has a structured line type and (perhaps inadvertently) the standard key as the primary table key, that is, all character-like and byte-like components make up the primary table key, all these components are taken into account when the table is sorted. Plus: Suppose there are only elementary numeric components in an internal table with a structured line type. In this case, sorting has no effect because the primary table key is considered empty. This is also applies to tables declared with <code>EMPTY KEY</code>.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Sorting by explicitly specifying components &lt;/td&gt; &lt;td&gt;</p>
<p>You can sort by any component of the internal table. It is also possible to specify the sort order (even component-wise). Explicitly specifying the components has the advantage that your code is easier to understand and you can avoid unexpected results if you accidentally use <code>SORT</code> without the <code>BY</code> addition on empty and standard table keys.</p>
<p>&lt;br&gt;</p>
<pre><code>DATA it3 TYPE TABLE OF struc WITH NON-UNIQUE KEY a.

&quot;Sorting by primary table key a
SORT itab.

&quot;Specifying the component to sort for; here, it is the same as the key;
&quot;this way, the sorting is easier to understand
SORT itab BY a.

&quot;Syntax showing multiple component sorting with component-wise sort order
SORT itab BY a b ASCENDING c DESCENDING.

&quot;Sorting respecting the entire line (e. g. in the context of tables with
&quot;empty or standard keys)
SORT itab BY table_line.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Stable sorting &lt;/td&gt; &lt;td&gt;</p>
<p>By default, the sorting is unstable concerning preserving the order of table lines with identical sort keys. To ensure a stable sorting with preserving the order of table lines with identical sort keys, you can use the <code>STABLE</code> addition.&lt;br&gt;&lt;br&gt; This code example explores the difference in sorting results when using <code>STABLE</code> versus not using it with the <code>SORT</code> statement. A <code>SELECT</code> statement retrieves data from an internal table, specifying an <code>ORDER BY</code> clause for two columns. <code>SORT</code> statements sort the internal table. Using <code>STABLE</code> demonstrates that the relative order of the columns specified in the <code>ORDER BY</code> clause is preserved. Without <code>STABLE</code>, this relative order is not necessarily maintained. The <code>CL_ABAP_DIFF</code> class is used to compare the internal table content.</p>
<p>&lt;br&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.
    TYPES: BEGIN OF demo_flights,
             carrid   TYPE c LENGTH 3,
             connid   TYPE n LENGTH 4,
             cityfrom TYPE c LENGTH 20,
             cityto   TYPE c LENGTH 20,
           END OF demo_flights,
           ty_flights TYPE TABLE OF demo_flights WITH EMPTY KEY.

    DATA(itab) = VALUE ty_flights(
      ( carrid = &#x27;AA&#x27; connid = &#x27;0017&#x27; cityfrom = &#x27;NEW YORK&#x27; cityto = &#x27;SAN FRANCISCO&#x27; )
      ( carrid = &#x27;AA&#x27; connid = &#x27;0064&#x27; cityfrom = &#x27;SAN FRANCISCO&#x27; cityto = &#x27;NEW YORK&#x27; )
      ( carrid = &#x27;AZ&#x27; connid = &#x27;0555&#x27; cityfrom = &#x27;ROME&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;AZ&#x27; connid = &#x27;0788&#x27; cityfrom = &#x27;ROME&#x27; cityto = &#x27;TOKYO&#x27; )
      ( carrid = &#x27;AZ&#x27; connid = &#x27;0789&#x27; cityfrom = &#x27;TOKYO&#x27; cityto = &#x27;ROME&#x27; )
      ( carrid = &#x27;AZ&#x27; connid = &#x27;0790&#x27; cityfrom = &#x27;ROME&#x27; cityto = &#x27;OSAKA&#x27; )
      ( carrid = &#x27;DL&#x27; connid = &#x27;0106&#x27; cityfrom = &#x27;NEW YORK&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;DL&#x27; connid = &#x27;1699&#x27; cityfrom = &#x27;NEW YORK&#x27; cityto = &#x27;SAN FRANCISCO&#x27; )
      ( carrid = &#x27;DL&#x27; connid = &#x27;1984&#x27; cityfrom = &#x27;SAN FRANCISCO&#x27; cityto = &#x27;NEW YORK&#x27; )
      ( carrid = &#x27;JL&#x27; connid = &#x27;0407&#x27; cityfrom = &#x27;TOKYO&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;JL&#x27; connid = &#x27;0408&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;TOKYO&#x27; )
      ( carrid = &#x27;LH&#x27; connid = &#x27;0400&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;NEW YORK&#x27; )
      ( carrid = &#x27;LH&#x27; connid = &#x27;0401&#x27; cityfrom = &#x27;NEW YORK&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;LH&#x27; connid = &#x27;0402&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;NEW YORK&#x27; )
      ( carrid = &#x27;LH&#x27; connid = &#x27;2402&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;BERLIN&#x27; )
      ( carrid = &#x27;LH&#x27; connid = &#x27;2407&#x27; cityfrom = &#x27;BERLIN&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;QF&#x27; connid = &#x27;0005&#x27; cityfrom = &#x27;SINGAPORE&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;QF&#x27; connid = &#x27;0006&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;SINGAPORE&#x27; )
      ( carrid = &#x27;SQ&#x27; connid = &#x27;0988&#x27; cityfrom = &#x27;SINGAPORE&#x27; cityto = &#x27;TOKYO&#x27; )
      ( carrid = &#x27;UA&#x27; connid = &#x27;0941&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;SAN FRANCISCO&#x27; )
      ( carrid = &#x27;UA&#x27; connid = &#x27;3504&#x27; cityfrom = &#x27;SAN FRANCISCO&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;UA&#x27; connid = &#x27;3516&#x27; cityfrom = &#x27;NEW YORK&#x27; cityto = &#x27;FRANKFURT&#x27; )
      ( carrid = &#x27;UA&#x27; connid = &#x27;3517&#x27; cityfrom = &#x27;FRANKFURT&#x27; cityto = &#x27;NEW YORK&#x27; ) ).

    SELECT carrid, connid, cityfrom, cityto
           FROM @itab AS tab
           ORDER BY carrid, connid
           INTO TABLE @DATA(flights).

    DATA(fl_stable) = flights.
    DATA(fl_non_stable) = flights.

    SORT fl_stable STABLE BY cityfrom cityto.
    SORT fl_non_stable BY cityfrom cityto.

    DATA is_identical TYPE abap_boolean.
    DATA(comparison) = cl_abap_diff=&gt;create( ).
    TRY.
        DATA(comp_result) = comparison-&gt;diff( EXPORTING target = fl_stable
                                                        source = fl_non_stable
                                              IMPORTING flag_identical = is_identical ).
        IF is_identical = abap_true.
          out-&gt;write( `Comparison result: Identical` ).
        ELSE.
          out-&gt;write( `Comparison result: Not identical` ).
        ENDIF.
      CATCH cx_abap_diff INTO DATA(error).
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.
    out-&gt;write( comp_result ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Grouping Internal Tables</h2>
<p>To group internal tables, there are additions for <code>LOOP AT</code> statements.</p>
<p><code>LOOP AT ... GROUP BY</code></p>
<ul>
<li>Groups the lines of internal tables based on a group key. Each group key represents a group, and the lines read are members of that group.</li>
<li>Performs a loop across these groups.</li>
<li>You can execute a nested loop across the members of each group using <code>LOOP AT GROUP</code> statements.</li>
<li>The statements offer a variety of syntax options, such as the following. For more information, refer to the ABAP Keyword Documentation:</li>
</ul>
<ul>
<li><code>ASCENDING</code>/<code>DESCENDING</code> additions to specify the sort order in the group loop. You can also sort beforehand with <code>SORT</code> statements.</li>
<li><code>WITHOUT MEMBERS</code>: This creates groups (requiring group key binding) without assigning actual component values. Access to group lines is not possible. Use this when access is not necessary and you want to enhance read performance.</li>
<li>Specifying the group key after <code>GROUP BY</code></li>
<li>This can be a single data object or multiple keys within parentheses, which define a structure with specific components.</li>
<li>In simple cases, component values of the table line can be assigned to the group key or the components of the group key. However, expressions are also possible on the right side of the group key assignments.</li>
<li>Storing group-specific information in structured group keys.</li>
<li><code>GROUP SIZE</code> to count group members, e.g., <code>gs = GROUP SIZE</code>.</li>
<li><code>GROUP INDEX</code> to index group members, e.g., <code>gi = GROUP INDEX</code>.</li>
<li>You can choose component names (such as <code>gs</code> and <code>gi</code> in the previous example) freely.</li>
<li>To use these components, you need a group key binding.</li>
<li>Group key binding</li>
<li>Can be specified at the end of the statement using <code>INTO</code> and a data object. You can also use data references (<code>REFERENCE INTO</code>) or field symbols (<code>ASSIGNING ...</code>).</li>
<li>If specified, the current key&#x27;s group key is assigned to the specified data object (data reference, or field symbol). You can then address the group in nested loops with <code>LOOP AT GROUP</code>.</li>
<li>Note: You can access the read result (e.g., <code>dobj</code> in <code>LOOP AT itab INTO dobj</code>), but the component values are initial when the group key binding is specified. You can address the group and its component values in nested loops with <code>LOOP AT GROUP</code>.</li>
<li>If you do not specify the group key binding, it defaults to a representative binding. In each loop pass, the first line of the current group is assigned to the target area. You can process this <em>representative</em> further in nested loops with <code>LOOP AT GROUP</code>. With representative binding, the <code>sy-tabix</code> value is set as if the loop was specified without grouping.</li>
</ul>
<p><code>LOOP AT GROUP</code>:</p>
<ul>
<li>Allows for a nested loop across group members.</li>
<li>Only applicable with <code>LOOP ... GROUP BY</code> statements (provided <code>WITHOUT MEMBERS</code> is not specified)</li>
<li>After using <code>LOOP AT GROUP</code>, you can specify ...</li>
<li>the read result that serves as a representative in representative binding: <code>LOOP AT it INTO DATA(wa) GROUP BY ... LOOP AT GROUP wa ...</code>.</li>
<li>the group key binding: <code>LOOP AT it INTO DATA(wa) GROUP BY ... INTO DATA(gkb). ... LOOP AT GROUP gkb ...</code>.</li>
<li>Additional syntax options like a <code>WHERE</code> condition and further grouping are also available.</li>
</ul>
<p>More information:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abaploop_at_itab_group_by.htm">ABAP Keyword Documentation</a></li>
<li>Iteration expressions can also handle table grouping (<code>FOR ... IN GROUP</code>). For example, see the <a href="05_Constructor_Expressions.md">Constructor Expressions</a> cheat sheet.</li>
<li><a href="11_Internal_Tables_Grouping.md">Internal Tables: Grouping</a> cheat sheet</li>
</ul>
<p>The example class below demonstrates internal table grouping options. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the following code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is designed to display results in the console.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.
    TYPES: BEGIN OF demo_struct,
             comp1 TYPE c LENGTH 1,
             comp2 TYPE i,
             comp3 TYPE abap_boolean,
             comp4 TYPE string,
           END OF demo_struct,
           tab_type TYPE TABLE OF demo_struct WITH EMPTY KEY.
    DATA str_table TYPE string_table.

    &quot;Populating a demo internal table as the basis of the syntax example
    &quot;Note: The example loops only use data objects as targets, not data references
    &quot;or field symbols.
    DATA(it) = VALUE tab_type( ( comp1 = &#x27;d&#x27; comp2 = 0 comp3 = abap_false )
                               ( comp1 = &#x27;a&#x27; comp2 = 1 comp3 = abap_true )
                               ( comp1 = &#x27;a&#x27; comp2 = 2 comp3 = abap_false )
                               ( comp1 = &#x27;e&#x27; comp2 = 11 comp3 = abap_true )
                               ( comp1 = &#x27;b&#x27; comp2 = 5 comp3 = abap_true )
                               ( comp1 = &#x27;b&#x27; comp2 = 6 comp3 = abap_false )
                               ( comp1 = &#x27;a&#x27; comp2 = 3 comp3 = abap_false )
                               ( comp1 = &#x27;b&#x27; comp2 = 4 comp3 = abap_true )
                               ( comp1 = &#x27;c&#x27; comp2 = 10 comp3 = abap_true )
                               ( comp1 = &#x27;e&#x27; comp2 = 1 comp3 = abap_false )
                               ( comp1 = &#x27;d&#x27; comp2 = 7 comp3 = abap_true )
                               ( comp1 = &#x27;a&#x27; comp2 = 4 comp3 = abap_true )
                               ( comp1 = &#x27;e&#x27; comp2 = 111 comp3 = abap_true ) ).

    &quot;The following example (and several others below) does not specify a nested loop.
    &quot;It does not specify a group key binding either. This means that the work area
    &quot;contains the first line of each group, representing the group in the loop
    &quot;(representative binding). The comp4 component is assigned the sy-tabix value,
    &quot;which is the number of the line in the table without the grouping.
    DATA ita LIKE it.
    LOOP AT it INTO DATA(waa) GROUP BY waa-comp1.
      waa-comp4 = sy-tabix.
      APPEND waa TO ita.
    ENDLOOP.
    out-&gt;write( data = ita name = `ita` ).

    &quot;Specifying sort order
    DATA itb LIKE it.
    LOOP AT it INTO DATA(wab) GROUP BY wab-comp1 ASCENDING.
      wab-comp4 = sy-tabix.
      APPEND wab TO itb.
    ENDLOOP.
    out-&gt;write( data = itb name = `itb` ).

    &quot;WITHOUT MEMBERS addition; a group key binding is required
    &quot;after WITHOUT MEMBERS
    &quot;The group key binding is added to a string table for visualizing its
    &quot;content.
    &quot;Note: The component values are initial when the group key binding is
    &quot;specified.
    LOOP AT it INTO DATA(wac) GROUP BY wac-comp1 WITHOUT MEMBERS INTO DATA(keyc).
      ASSERT wac IS INITIAL.
      APPEND keyc TO str_table.
    ENDLOOP.
    out-&gt;write( data = str_table name = `str_table` ).

    &quot;Using a structured group key
    &quot;The following example just assigns component values to the group key. In this case,
    &quot;the grouping is performed with more than just one criterion as in the previous examples.
    &quot;As a result, table lines are added to the other table in descending order based on the
    &quot;two component values.
    DATA itd LIKE it.
    LOOP AT it INTO DATA(wad) GROUP BY ( key1 = wad-comp1 key2 = wad-comp2 ) DESCENDING.
      APPEND wad TO itd.
    ENDLOOP.
    out-&gt;write( data = itd name = `itd` ).

    &quot;In the following example, the group is sorted in ascending order. Note that the
    &quot;group index value uses the original position in the group index. The group key
    &quot;binding information is added to a string table for visualizing its content.
    CLEAR str_table.
    LOOP AT it INTO DATA(wae) GROUP BY ( key = wae-comp1 gi = GROUP INDEX gs = GROUP SIZE ) ASCENDING INTO DATA(keye).      
      APPEND |Key component: &#x27;{ keye-key }&#x27;, group index: &#x27;{ keye-gi }&#x27;, group size: &#x27;{ keye-gs }&#x27;| TO str_table.
    ENDLOOP.
    out-&gt;write( data = str_table name = `str_table` ).

    &quot;LOOP AT GROUP: Nested loop across group members
    &quot;Unlike the previous example, the example uses a nested loop across the groups (the group key binding is
    &quot;specified after LOOP AT GROUP). There, the component values of the members can be accessed.
    DATA itf LIKE it.
    LOOP AT it INTO DATA(waf) GROUP BY ( key = waf-comp1 gi = GROUP INDEX gs = GROUP SIZE ) ASCENDING INTO DATA(keyf).      
      LOOP AT GROUP keyf INTO DATA(memberf).
        APPEND VALUE #( comp1 = memberf-comp1 comp2 = memberf-comp2 comp3 = memberf-comp3
        comp4 = |Key component: &#x27;{ keyf-key }&#x27;, group index: &#x27;{ keyf-gi }&#x27;, group size: &#x27;{ keyf-gs }&#x27;|
        ) TO itf.
      ENDLOOP.
    ENDLOOP.
    out-&gt;write( data = itf name = `itf` ).

    &quot;The objective of this example is to extract the line with the highest value in a particular
    &quot;column within a group from the original table to another.
    &quot;The example uses representative binding, i.e. the representative of the group is specified
    &quot;in the work area, not in a group key binding.
    DATA itg LIKE it.
    LOOP AT it INTO DATA(wag) GROUP BY wag-comp1 ASCENDING.
      LOOP AT GROUP wag INTO DATA(memberg) GROUP BY memberg-comp2 DESCENDING.
        APPEND memberg TO itg.
        EXIT.
      ENDLOOP.
    ENDLOOP.
    out-&gt;write( data = itg name = `itg` ).

    &quot;The following example is similar to the previous example, and yields the same result.
    &quot;Here, the group key binding is specified after LOOP AT GROUP.
    DATA ith LIKE it.
    LOOP AT it INTO DATA(wah) GROUP BY wah-comp1 ASCENDING.
      LOOP AT GROUP wah INTO DATA(memberh) GROUP BY memberh-comp2 DESCENDING.
        APPEND memberh TO ith.
        EXIT.
      ENDLOOP.
    ENDLOOP.
    ASSERT itg = ith.
    out-&gt;write( data = ith name = `ith` ).

    &quot;Additional syntax options, like specifying a WHERE condition in both nested and outer
    &quot;loops, are possible. The example below shows that the LOOP AT GROUP statement assigns
    &quot;the value of sy-tabix to the value that would be set for the current line in the LOOP
    &quot;without grouping.
    DATA iti LIKE it.
    LOOP AT it INTO DATA(wai) GROUP BY wai-comp1 ASCENDING.
      LOOP AT GROUP wai INTO DATA(memberi) WHERE comp3 = abap_true.
        APPEND VALUE #( comp1 = memberi-comp1 comp2 = memberi-comp2 comp3 = memberi-comp3
        comp4 = |sy-tabix: &#x27;{ sy-tabix }&#x27;|
        ) TO iti.
      ENDLOOP.
    ENDLOOP.
    out-&gt;write( data = iti name = `iti` ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>The following example compares a <code>LOOP AT</code> statement with a <code>FOR</code> loop in a constructor expression. The goal is to highlight their syntactical similarities and differences, such as the greater flexibility of <code>LOOP AT</code> statements in terms of additional specifications within the loop and the common use of <code>FOR</code> loops to create an internal table.</p>
<pre><code>SELECT *
    FROM zdemo_abap_flsch
    INTO TABLE @DATA(fl_tab).

DATA memb LIKE fl_tab.
TYPES tab_type LIKE memb.
DATA member_tab TYPE TABLE OF tab_type WITH EMPTY KEY.
DATA tab TYPE string_table.

&quot;-------------------------- LOOP AT statement --------------------------
LOOP AT fl_tab INTO DATA(flight)
    GROUP BY ( carrier = flight-carrid cityfr = flight-cityfrom
                size = GROUP SIZE index = GROUP INDEX )
                ASCENDING REFERENCE INTO DATA(group_ref).

    APPEND |carrier = &quot;{ group_ref-&gt;carrier }&quot;, cityfr = &quot;{ group_ref-&gt;cityfr }&quot;, size = &quot;{ group_ref-&gt;size }&quot;, index = &quot;{ group_ref-&gt;index }&quot; | TO tab.

    CLEAR memb.
    LOOP AT GROUP group_ref ASSIGNING FIELD-SYMBOL(&lt;flight&gt;).
    memb = VALUE #( BASE memb ( &lt;flight&gt; ) ).
    ENDLOOP.

    APPEND memb TO member_tab.
ENDLOOP.

&quot;-------------------------- FOR loop --------------------------
TYPES: BEGIN OF demo_struc,
            member LIKE memb,
            info   TYPE string,
        END OF demo_struc.

TYPES ty_itab_constr TYPE TABLE OF demo_struc WITH EMPTY KEY.

DATA(itab_constr) = VALUE ty_itab_constr( FOR GROUPS g OF fl IN fl_tab
        GROUP BY ( c = fl-carrid cifr = fl-cityfrom
                    size = GROUP SIZE index = GROUP INDEX ) ASCENDING
        LET m = VALUE tab_type( FOR &lt;fl&gt; IN GROUP g ( &lt;fl&gt; ) ) IN
        ( member = m info = |carrid = &quot;{ g-c }&quot;, cityfrom = &quot;{ g-cifr }&quot;, size = &quot;{ g-size }&quot;, index = &quot;{ g-index }&quot; | ) ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Collecting Values</h2>
<ul>
<li>You can use <code>COLLECT</code> statements, for example, to add the values of numeric components to the corresponding values in an internal table.</li>
<li>It is recommended that you use it mainly for internal tables with a unique primary key, especially hashed tables.</li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcollect.htm">here</a></li>
</ul>
<pre><code>&quot;This example demonstrates how to insert data from a database table
&quot;into an internal table in a compressed way. Within a SELECT loop,
&quot;a COLLECT statement is used to consolidate lines with identical
&quot;primary key components (carrid and connid) by summing the number
&quot;of occupied seats in the numeric component (seatsocc).
&quot;Additionally, an internal table is filled by adding all read lines.
&quot;This table is looped across to simulate the effect of the COLLECT
&quot;statement.

DATA: BEGIN OF seats,
        carrid   TYPE zdemo_abap_fli-carrid,
        connid   TYPE zdemo_abap_fli-connid,
        seatsocc TYPE zdemo_abap_fli-seatsocc,
        END OF seats,
        seats_tab_col LIKE HASHED TABLE OF seats WITH UNIQUE KEY carrid connid,
        seats_tab_all LIKE TABLE OF seats WITH EMPTY KEY,
        seats_tab_loop_grp LIKE seats_tab_col.

SELECT carrid, connid, seatsocc
        FROM zdemo_abap_fli
        INTO @seats.
    COLLECT seats INTO seats_tab_col.
    APPEND seats TO seats_tab_all.
ENDSELECT.

LOOP AT seats_tab_all INTO DATA(wa) GROUP BY ( key1 = wa-carrid key2 = wa-connid ).
    INSERT VALUE #( carrid = wa-carrid connid = wa-connid ) INTO TABLE seats_tab_loop_grp ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    LOOP AT GROUP wa INTO DATA(member).
    &lt;fs&gt;-seatsocc = &lt;fs&gt;-seatsocc + member-seatsocc.
    ENDLOOP.
ENDLOOP.

ASSERT seats_tab_loop_grp = seats_tab_col.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Getting Information about Internal Tables, Table Lines, Table Types</h2>
<h3>Checking the Existence of a Line in an Internal Table</h3>
<p>This is relevant if you are not interested in the content of a table line, but only want to find out whether a line exists that matches to the index or key specifications. To do this, use a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapread_table.htm"><code>READ TABLE</code></a> statement with the <code>TRANSPORTING NO FIELDS</code> addition. The addition indicates that no actual content is to be read. If the search was successful and an entry exists, the system field <code>sy-subrc</code> is set to 0.</p>
<pre><code>&quot;Read using a key
READ TABLE it WITH KEY b = 2 TRANSPORTING NO FIELDS.

IF sy-subrc = 0.
  ...
ENDIF.

&quot;Read using the index
READ TABLE it INDEX 1 TRANSPORTING NO FIELDS.

IF sy-subrc = 0.
  ...
ENDIF.

&quot;Read using the key
IF line_exists( it[ b = 2 ] ).
  ...
ENDIF.

&quot;Read using the index
IF line_exists( it[ 1 ] ).
  ...
ENDIF.</code></pre>
<p>A newer way to check the existence of a line is the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenpredicate_function_glosry.htm">predicate function</a> <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenline_exists_function.htm"><code>line_exists( )</code></a>. This function expects a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentable_expression_glosry.htm">table expression</a> as an argument. See below for more on table expressions. Note that table expressions do not set system fields.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Checking the Index of a Line in an Internal Table</h3>
<p>If you want to find out about the index of a line in an internal table, you can also make use of the <code>READ TABLE</code> statement above. If the line is found, the system field <code>sy-tabix</code> is set to the number of the index. Otherwise, the built-in function <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenline_index_function.htm"><code>line_index( )</code></a> can be used. It returns the index of the found line or 0 if the line does not exist.</p>
<pre><code>DATA(itab) = VALUE string_table( ( `aaa` ) ( `bbb` ) ).
READ TABLE itab WITH KEY table_line = `bbb` TRANSPORTING NO FIELDS.
&quot;2
DATA(tabix) = sy-tabix.

&quot;1
DATA(idx) = line_index( itab[ table_line = `aaa` ] ).

&quot;Note: No primary table index with hashed tables
DATA(hashed_tab) = VALUE string_hashed_table( ( `a` ) ( `b` ) ( `c` ) ).
&quot;-1
DATA(hashed_primary_idx) = line_index( hashed_tab[ table_line = `c` ] ).

&quot;Index access in hashed tables only using a secondary table index
TYPES: BEGIN OF s,
          comp1 TYPE i,
          comp2 TYPE i,
        END OF s,
        ttype TYPE HASHED TABLE OF s WITH UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp2.

DATA(hashed_tab2) = VALUE ttype( ( comp1 = 1 comp2 = 10 )
                                 ( comp1 = 2 comp2 = 8 )
                                 ( comp1 = 3 comp2 = 9 ) ).

&quot;3
DATA(hashed_secondary_idx) = line_index( hashed_tab2[ KEY sk comp2 = 10 ] ).
&quot;1
hashed_secondary_idx = line_index( hashed_tab2[ KEY sk comp2 = 8 ] ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Checking How Many Lines Exist in an Internal Table</h3>
<p><code>lines( )</code> is another built-in function that you can use to check how many lines exist in an internal table. It returns an integer value.</p>
<pre><code>DATA(itab) = VALUE string_table( ( `a` ) ( `b` ) ( `c` ) ( `d` ) ( `e` ) ).

&quot;5
DATA(number_of_lines) = lines( itab ).

&quot;Excursion: Finding out the number of lines in a table by specifying concrete
&quot;component values, e.g. you want to find out how many lines exist in the table 
&quot;that have the value 1 for comp2
TYPES: BEGIN OF struct,
          comp1 TYPE c LENGTH 3,
          comp2 TYPE i,
        END OF struct,
        tab_type TYPE TABLE OF struct WITH EMPTY KEY.

DATA(it) = VALUE tab_type( ( comp1 = &#x27;a&#x27; comp2 = 1  )
                           ( comp1 = &#x27;b&#x27; comp2 = 1  )
                           ( comp1 = &#x27;c&#x27; comp2 = 1  )
                           ( comp1 = &#x27;d&#x27; comp2 = 2  )
                           ( comp1 = &#x27;e&#x27; comp2 = 3  )
                           ( comp1 = &#x27;f&#x27; comp2 = 4  )
                           ( comp1 = &#x27;g&#x27; comp2 = 5  ) ).

&quot;7
DATA(line_num) = lines( it ).

&quot;Finding out the number of lines in a table by component value, e.g.
&quot;using constructor expressions and specifying a WHERE clause.
&quot;The example creates an new internal table inline using VALUE and a FOR loop,
&quot;specified with a WHERE clause. The lines function is applied to the
&quot;table created inline.
&quot;3
DATA(line_num_filtered1) = lines( VALUE tab_type( FOR wa IN it WHERE ( comp2 = 1 ) ( wa ) ) ).

&quot;Using the REDUCE operator
&quot;The example adds 1 to the resulting integer if the comp2 value of the iterated line is greater than 1.
&quot;The lines function is not relevant in the example.
&quot;4
DATA(line_num_filtered2) = REDUCE i( INIT var = 0
                                     FOR &lt;line&gt; IN it
                                     WHERE ( comp2 &gt; 1 )
                                     NEXT var += 1 ).

&quot;Using the FILTER operator
&quot;Note: The source table must have at least one sorted key or a hash key for accessing.
&quot;If the table does not have such a primary table key, a secondary table key must be available.
TYPES: tab_type_sorted TYPE TABLE OF struct with NON-UNIQUE SORTED KEY sec_key COMPONENTS comp2.
DATA it_sorted type tab_type_sorted.
it_sorted = it.

&quot;The example creates an new internal table inline using FILTER,
&quot;specified with a WHERE clause. The lines function is applied to the
&quot;table created inline.
&quot;3
DATA(line_num_filtered3) = lines( FILTER #( it_sorted USING KEY sec_key WHERE comp2 = 1 ) ).
&quot;4
DATA(line_num_filtered4) = lines( FILTER #( it_sorted USING KEY sec_key WHERE comp2 &gt; 1 ) ).

&quot;Using LOOP statements
CLEAR number_of_lines.
&quot;No WHERE condition as all lines shall be processed
&quot;7
LOOP AT it REFERENCE INTO DATA(line).
  number_of_lines += 1.
ENDLOOP.

CLEAR number_of_lines.
&quot;3
LOOP AT it transporting no fields where comp2 = 1.
  number_of_lines += 1.
ENDLOOP.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Getting Table (Type) Information at Runtime</h3>
<p>Using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenrun_time_type_identific_glosry.htm">Runtime Type Identification (RTTI)</a>, you can get type information on internal tables and table types at runtime.</p>
<p>For more information, see the <a href="06_Dynamic_Programming.md">Dynamic Programming</a> ABAP cheat sheet.</p>
<pre><code>TYPES tab_type TYPE SORTED TABLE OF zdemo_abap_flsch
       WITH UNIQUE KEY carrid connid
       WITH NON-UNIQUE SORTED KEY sec_key ALIAS sk COMPONENTS countryfr cityto.
DATA itab TYPE tab_type.

DATA(tdo_d) = cl_abap_typedescr=&gt;describe_by_data( itab ).
&quot;DATA(tdo_d) = cl_abap_typedescr=&gt;describe_by_name( &#x27;TAB_TYPE&#x27; ).

&quot;Cast to get more specific information
DATA(tdo_itab) = CAST cl_abap_tabledescr( cl_abap_typedescr=&gt;describe_by_data( itab ) ).
&quot;DATA(tdo_itab) = CAST cl_abap_tabledescr( tdo_d ).

DATA(type_category_itab) = tdo_itab-&gt;kind.
DATA(relative_name_itab) = tdo_itab-&gt;get_relative_name( ).
... &quot;Explore more options by positioning the cursor behind -&gt; and choosing CTRL + Space
DATA(table_kind_itab) = tdo_itab-&gt;table_kind.
DATA(table_keys_itab) = tdo_itab-&gt;key.
DATA(table_keys_more_details_itab) = tdo_itab-&gt;get_keys( ).
DATA(table_has_unique_key_itab) = tdo_itab-&gt;has_unique_key.
DATA(table_key_alias_itab) = tdo_itab-&gt;get_key_aliases( ).
DATA(line_type_itab) = tdo_itab-&gt;get_table_line_type( ).
DATA(table_component_info_itab) = CAST cl_abap_structdescr( tdo_itab-&gt;get_table_line_type( ) ).
DATA(table_components_itab) = CAST cl_abap_structdescr( tdo_itab-&gt;get_table_line_type( ) )-&gt;components.
DATA(table_comps_more_info_itab) = CAST cl_abap_structdescr( tdo_itab-&gt;get_table_line_type( ) )-&gt;get_components( ).
DATA(applies_to_data_itab) = tdo_itab-&gt;applies_to_data( VALUE tab_type( ) ).</code></pre>
<p>RTTI example:</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Operations with Internal Tables Using ABAP SQL SELECT Statements</h2>
<h3>Internal Tables as Target Data Objects in SELECT Queries</h3>
<pre><code>SELECT FROM dbtab
  FIELDS comp1, comp2 ...
  WHERE ...
  INTO TABLE @DATA(itab_sel).</code></pre>
<p>Adding multiple lines from a database table to an internal table using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapselect.htm"><code>SELECT</code></a>, for example, based on a condition. In the case below, the internal table is created inline.</p>
<pre><code>SELECT FROM dbtab2
  FIELDS *
  WHERE ...
  APPENDING CORRESPONDING FIELDS OF TABLE @itab.

SELECT FROM dbtab2
  FIELDS *
  WHERE ...
  INTO CORRESPONDING FIELDS OF TABLE @itab.</code></pre>
<p>Adding multiple lines from a database table using <code>SELECT</code>, for example, based on a condition when the database table has a line type that is incompatible with the internal table. The <code>*</code> character means that all fields are selected. The other examples define specific fields. The <code>APPENDING CORRESPONDING FIELDS INTO TABLE</code> addition appends the selected data to the end of the table without deleting existing table entries. The <code>INTO CORRESPONDING FIELDS OF TABLE</code> addition adds lines and deletes existing table entries.</p>
<pre><code>SELECT db1~comp1, db1~comp2, db2~comp_abc, db2~comp_xyz ...
  FROM db1
  INNER JOIN db2 ON db1~comp1 = db2~comp1
  INTO TABLE @DATA(it_join_result).</code></pre>
<p>Combining data from multiple database tables into one internal table using an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninner_join_glosry.htm">inner join</a>. The following example uses the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapselect_join.htm"><code>INNER JOIN</code></a> addition. Note that the field list includes fields from both tables. The fields are referred to using <code>~</code>.</p>
<p>Populating an internal table from a database table using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensubquery_glosry.htm">subqueries</a>. The following two examples populate an internal table from a database table. In the first example, a subquery is specified in the <code>WHERE</code> clause with the <code>NOT IN</code> addition. It checks whether a value matches a value in a set of values specified in parentheses. The second example populates an internal table depending on data in another table. A subquery with the <code>EXISTS</code> addition is specified in the <code>WHERE</code> clause. In this case, the result of the subquery, which is another <code>SELECT</code> statement, is checked to see if an entry exists in a table based on the specified conditions.</p>
<pre><code>SELECT comp1, comp2, ...
  FROM dbtab
  WHERE comp1 NOT IN ( a, b, c ... )
  INTO TABLE @DATA(it_subquery_result1).

SELECT comp1, comp2, ...
  FROM db1
  WHERE EXISTS¬†( SELECT &#x27;X&#x27; FROM db2
                 WHERE comp1 = db1~comp1 )
  INTO TABLE @DATA(it_subquery_result2).</code></pre>
<p>Populating an internal table from a table based on the existence of data in another table using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenwhere_all_entries.htm"><code>FOR ALL ENTRIES</code></a> addition.</p>
<div class="note">[!NOTE] Make sure that the internal table you are reading from is not initial. Therefore, it is recommended that you use a subquery as shown above: `... ( SELECT ... FROM ... WHERE ... ) ...`.</div>
<pre><code>IF itab IS NOT INITIAL.

  SELECT dbtab~comp1, dbtab~comp2, ...
    FROM dbtab
    FOR ALL ENTRIES IN @itab
    WHERE comp1 = @itab-comp1
    INTO TABLE @DATA(it_select_result).

ENDIF.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>SELECT Queries with Internal Tables as Data Sources</h3>
<p><strong>General rule</strong>: Use <code>SELECT</code> with internal tables as a data source only when SQL functionality, such as joins, exceeds ABAP statements.</p>
<p><strong>Technical considerations</strong>:</p>
<ul>
<li>The <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_sql_inmemeng_glosry.htm">ABAP SQL in-memory engine</a> manages read access with ABAP SQL <code>SELECT</code> statements for tabular data within the memory of an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenas_abap_glosry.htm">Application Server ABAP (AS ABAP)</a>. The tabular data can include:</li>
<li>Database table data (such as data from DDIC database tables or CDS entities) buffered in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenas_abap_glosry.htm">table buffer</a> within AS ABAP. Buffering ability is determined by specifications in the artifacts. If the data isn&#x27;t buffered, the engine can&#x27;t handle it, and the SQL statement is processed on the database.</li>
<li>Internal tables present in the current internal session. They are treated like DDIC database tables. ABAP types are mapped to corresponding <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbuiltin_ddic_type_glosry.htm">built-in DDIC types</a>.</li>
<li>The ABAP SQL in-memory engine processes data on AS ABAP, not the database server. If a <code>SELECT</code> statement using internal tables as data sources includes elements beyond the engine&#x27;s capability (for example, most subqueries aren&#x27;t supported; see detailed restrictions <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENSQL_ENGINE_RESTR.html">here</a>), the internal table content is transferred to a temporary database table before the query executes. Only components involved in the read access are transferred.</li>
<li>In such cases, the SQL statement runs directly on the database server, not within AS ABAP. If the compiler identifies a statement the ABAP SQL in-memory engine can&#x27;t handle, a syntax warning occurs. You can suppress this warning using the pragma <code>##itab_db_select</code>.</li>
<li>Currently, queries with multiple internal tables can only proceed if the ABAP SQL in-memory engine can manage them on AS ABAP directly. Transferring more than one internal table to the database is currently not supported.</li>
<li>The result of the ABAP SQL in-memory engine processing is identical to processing the read access directly on the database.</li>
</ul>
<p><strong>Using internal tables as data sources in ABAP SQL SELECT statements</strong>:</p>
<pre><code>   SELECT comp1, comp2, ...
      FROM @itab AS it_alias
      WHERE ...
      INTO TABLE @DATA(itab_sel).</code></pre>
<pre><code>  TYPES: BEGIN OF s,
          comp1 TYPE i,
          comp2 TYPE i,
          comp3 TYPE i,
         END OF s.
  DATA itab TYPE TABLE OF s WITH KEY comp3.
  &quot;SELECT SINGLE * FROM @itab AS tab INTO @DATA(line).</code></pre>
<ul>
<li>You must specify internal tables as <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenhost_variable_glosry.htm">host variables</a> with an <code>@</code> prefix and give them an alias.</li>
<li>Internal tables are treated like DDIC database tables, which leads to specific behaviors such as:</li>
<li>They are handled like <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclient_independent_glosry.htm">client-independent</a> database tables, and the first table column is not automatically considered a client column. You can change this default behavior using the <code>DECLARE CLIENT</code> addition. For example, if the internal table specifies a key that is not specified at the beginning of the line type, you cannot use <code>SELECT</code> to retrieve data from the table.</li>
<li>You cannot use deep and nested components of internal tables in <code>SELECT</code> statements.</li>
<li>The type string is supported only if declared as a reference to the built-in dictionary type <code>sstring</code>.</li>
<li>A key advantage of using <code>SELECT</code> statements with internal tables as data sources is the access to ABAP SQL&#x27;s extensive functionalities, like aggregate expressions.</li>
<li>They can serve as an alternative to the <code>READ TABLE</code> or <code>LOOP AT</code> statements if the ABAP SQL in-memory engine can process the data without requiring a database transfer.</li>
<li>Note the general rule: You should use <code>SELECT</code> with internal tables as a data source only when SQL functionality exceeds that of ABAP statements, such as in joins. For tasks achievable with ABAP statements, it is preferable to use them as they are optimized for internal tables and offer better performance.</li>
</ul>
<p>The following example finds duplicate entries in internal tables. It uses a <code>SELECT</code> statement with the internal table as the data source. However, since the statement executes on the database, a syntax warning appears unless suppressed by the pragma. An advisable alternative is the internal-table-specific <code>LOOP AT</code> statement, which is optimized for operations on internal tables.</p>
<pre><code>TYPES: BEGIN OF s_demo,
          comp1 TYPE i,
          comp2 TYPE c LENGTH 3,
        END OF s_demo,
        ty_tab_demo TYPE TABLE OF s_demo WITH EMPTY KEY.

DATA(it) = VALUE ty_tab_demo(
  ( comp1 = 1 comp2 = &#x27;A&#x27; )
  ( comp1 = 1 comp2 = &#x27;B&#x27; )
  ( comp1 = 1 comp2 = &#x27;C&#x27; )
  ( comp1 = 2 comp2 = &#x27;A&#x27; )
  ( comp1 = 2 comp2 = &#x27;E&#x27; )
  ( comp1 = 1 comp2 = &#x27;D&#x27; )
  ( comp1 = 3 comp2 = &#x27;D&#x27; )
  ( comp1 = 3 comp2 = &#x27;G&#x27; )
  ( comp1 = 4 comp2 = &#x27;C&#x27; )
  ( comp1 = 5 comp2 = &#x27;A&#x27; ) ).

&quot;------------------ SELECT ------------------
&quot;Note: The SELECT command is executed on the database. Therefore, the pseudo
&quot;comment is include to suppress a syntax warning.
SELECT comp1, COUNT(*) AS count
  FROM @it AS it
  GROUP BY comp1
  HAVING COUNT(*) &gt; 1
  INTO TABLE @DATA(duplicates_sql) ##ITAB_DB_SELECT.

&quot;------------------ LOOP AT ------------------ 
TYPES: BEGIN OF duplicates_struc,
          comp1 TYPE s_demo-comp1,
          count TYPE i,
        END OF duplicates_struc,
        ty_tab_duplicates TYPE TABLE OF duplicates_struc WITH EMPTY KEY.

DATA duplicates_loop_at TYPE ty_tab_duplicates.
LOOP AT it INTO DATA(wa) GROUP BY ( comp = wa-comp1 size = GROUP SIZE ) ASCENDING REFERENCE INTO DATA(group_ref).

  IF group_ref-&gt;size &gt; 1.
    APPEND VALUE #( comp1 = group_ref-&gt;comp count = group_ref-&gt;size ) TO duplicates_loop_at.
  ENDIF.

ENDLOOP.</code></pre>
<p><strong>More information</strong>:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensql_engine_restr.htm">Restrictions</a></li>
<li>See details on the various ABAP SQL functionalities in the ABAP Keyword Documentation and in the <a href="03_ABAP_SQL.md">ABAP SQL cheat sheet</a>.</li>
</ul>
<p>The following example explores various <code>SELECT</code> queries with internal tables as data sources. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example uses objects of the ABAP cheat sheets repository and is set up to display output in the console.</p>
<p>Example:</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;----------- Exploiting ABAP SQL functionality with internal tables -----------

    TYPES int_tab_type TYPE TABLE OF i WITH EMPTY KEY.
    DATA(itab_a) = VALUE int_tab_type( ( 1 ) ( 32 ) ( 100 ) ( -24 ) ( 17 ) ( 99 ) ).

    &quot;SELECT query with an internal table as data source
    &quot;The example uses an aggregate expression. It is statically
    &quot;detected that the query cannot be processed by the ABAP SQL
    &quot;engine. The data must be passed to the database. Consequently,
    &quot;a syntax warning is displayed. It can be suppressed by a pragma.
    SELECT MAX( table_line ) AS max_val
      FROM @itab_a AS it
      INTO @DATA(max_a).

    &quot;100
    SELECT MAX( table_line ) AS max_val ##itab_db_select
     FROM @itab_a AS it
     INTO @DATA(max_b).

    out-&gt;write( max_a ).
    out-&gt;write( max_b ).

    &quot;Using the LIKE addition in the WHERE clause to extract internal table
    &quot;entries matching a specific pattern.
    TYPES: BEGIN OF s1,
             a TYPE c LENGTH 3,
             b TYPE i,
           END OF s1,
           it_type_1 TYPE TABLE OF s1 WITH EMPTY KEY.
    DATA(itab_b) = VALUE it_type_1( ( a = &#x27;abc&#x27; b = 1 )
                                    ( a = &#x27;zbc&#x27; b = 2 )
                                    ( a = &#x27;bde&#x27; b = 3 )
                                    ( a = &#x27;yde&#x27; b = 4 ) ).

    SELECT a, b
      FROM @itab_b AS it_alias
      WHERE a LIKE &#x27;%bc&#x27;
      INTO TABLE @DATA(select_like_result).

*A      B
*abc    1
*zbc    2

    out-&gt;write( select_like_result ).

    &quot;----------- Using a SELECT loop with an internal table as data source -----------

    TYPES: BEGIN OF s2,
             comp1 TYPE c LENGTH 2,
             comp2 TYPE i,
           END OF s2,
           it_type_2 TYPE TABLE OF s2 WITH EMPTY KEY.

    DATA(itab_c) = VALUE it_type_2( ( comp1 = &#x27;aa&#x27; comp2 = 2 )
                                    ( comp1 = &#x27;zz&#x27; comp2 = 9 )
                                    ( comp1 = &#x27;dd&#x27; comp2 = 1 )
                                    ( comp1 = &#x27;rr&#x27; comp2 = 7 )
                                    ( comp1 = &#x27;tt&#x27; comp2 = 5 )
                                    ( comp1 = &#x27;bb&#x27; comp2 = 6 ) ).

    DATA itab_d TYPE int_tab_type.

    &quot;The following SELECT loop specifies an internal table as data source.
    &quot;The loop sequence is defined by a sort order. Such a functionality is
    &quot;not available with LOOP AT.
    SELECT comp2
           FROM @itab_c AS it
           ORDER BY comp2 DESCENDING
           INTO @DATA(wa).
      INSERT wa INTO TABLE itab_d.
    ENDSELECT.

*9
*7
*6
*5
*2
*1

    out-&gt;write( itab_d ).

    &quot;------------------- Joins with internal tables -------------------

    TYPES: BEGIN OF s3,
             a TYPE c LENGTH 3,
             b TYPE c LENGTH 3,
             c TYPE i,
           END OF s3,
           it_type_3 TYPE TABLE OF s3 WITH EMPTY KEY.

    DATA(itab_e) = VALUE it_type_3( ( a = &#x27;aaa&#x27; b = &#x27;bbb&#x27; c = 1 )
                                    ( a = &#x27;ccc&#x27; b = &#x27;ddd&#x27; c = 1 )
                                    ( a = &#x27;eee&#x27; b = &#x27;fff&#x27; c = 2 ) ).

    DATA(itab_f) = VALUE it_type_3( ( a = &#x27;ggg&#x27; b = &#x27;hhh&#x27; c = 1 )
                                    ( a = &#x27;iii&#x27; b = &#x27;jjj&#x27; c = 1 )
                                    ( a = &#x27;kkk&#x27; b = &#x27;lll&#x27; c = 3 ) ).

    &quot;No syntax warning. The internal tables can be processed by the
    &quot;ABAP SQL engine.
    SELECT it_alias1~a, it_alias2~b
      FROM @itab_e AS it_alias1
      INNER JOIN @itab_f AS it_alias2 ON it_alias1~c = it_alias2~c
      INTO TABLE @DATA(itab_g).

*A      B
*aaa    hhh
*aaa    jjj
*ccc    hhh
*ccc    jjj

    out-&gt;write( itab_g ).

    &quot;Join with a database table and an internal table

    &quot;Preparing a demo database table and an internal table
    DELETE FROM zdemo_abap_tab1.
    INSERT zdemo_abap_tab1 FROM TABLE @( VALUE #( ( key_field = 1 char1 = &#x27;aaa&#x27; )
                                                  ( key_field = 2 char1 = &#x27;bbb&#x27; )
                                                  ( key_field = 3 char1 = &#x27;ccc&#x27; ) ) ).

    TYPES it_type_4 TYPE TABLE OF zdemo_abap_tab1 WITH EMPTY KEY.
    DATA(itab_h) = VALUE it_type_4( ( key_field = 1 char2 = &#x27;zzz&#x27; )
                                    ( key_field = 2 char2 = &#x27;yyy&#x27; ) ).

    SELECT db~key_field, db~char1, it~char2
      FROM zdemo_abap_tab1 AS db
      INNER JOIN @itab_h AS it ON it~key_field = db~key_field
      INTO TABLE @DATA(itab_i).

*KEY_FIELD    CHAR1    CHAR2
*1            aaa      zzz
*2            bbb      yyy

    out-&gt;write( itab_i ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Restrictions Regarding Internal Tables as Data Sources in ABAP SQL SELECT Statements</h4>
<ul>
<li>This excursion is intended to underscore the restrictions mentioned above and in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensql_engine_restr.htm">documentation</a> in more detail when selecting from internal tables.</li>
<li>Components having deep types cannot be included, for example, in the <code>SELECT</code> list or <code>WHERE</code> clause.</li>
<li>Among the non-allowed types of internal table components are strings (as they are deep types) and <code>utclong</code>.</li>
<li>Note that only those fields are checked (and sent to the database) that are actually used (as shown in the example below).</li>
<li>However, the type string is allowed if it is declared using the built-in dictionary type <code>sstring</code> (for example, a component typed with a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendata_element_glosry.htm">data element</a> or a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencds_simple_type_glosry.htm">CDS simple type</a> that uses <code>sstring</code>).</li>
</ul>
<p>The following example demonstrates various <code>SELECT</code> statements. A demo internal table has a component that is typed with a CDS simple type, which can be created as follows:</p>
<ul>
<li>In ADT, right-click your pacakage, and choose <em>New -&gt; Other Repository Object</em></li>
<li>Insert <em>type</em> and select <em>Type</em> under <em>Core Data Services</em>.</li>
<li>Choose <em>Next</em> and provide a name (e.g. <code>zdemo_abap_string</code>) and a description.</li>
<li>Choose <em>Finish</em>.</li>
<li>Find more information on CDS simple types <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencds_simple_types.htm">here</a>.</li>
</ul>
<p>The code of the CDS simple type may look as follows:</p>
<pre><code>@EndUserText.label: &#x27;String type&#x27;
define type zdemo_abap_string: abap.sstring( 1333 );</code></pre>
<p>Commented code example:</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Internal table having a component of type string
*&amp;---------------------------------------------------------------------*

&quot;Creating and populating an internal table that includes a component that is
&quot;typed with type string
TYPES: BEGIN OF s1,
         comp1 TYPE i,
         comp2 TYPE c LENGTH 3,
         comp3 TYPE string,
       END OF s1,
       tab_type_1 TYPE TABLE OF s1 WITH EMPTY KEY.
DATA(itab1) = VALUE tab_type_1( ( comp1 = 1 comp2 = &#x27;aaa&#x27; comp3 = `ABAP` ) ).

&quot;Columns of type string cannot be used in SELECT statements that
&quot;select from internal tables (* selects all fields). Therefore, the
&quot;following statements are commented out.

&quot;SELECT SINGLE * FROM @itab1 AS it WHERE comp1 = 1 INTO @DATA(res1).
&quot;SELECT SINGLE comp1 FROM @itab1 AS it where comp3 = `ABAP` INTO @DATA(res2).
&quot;SELECT SINGLE comp3 FROM @itab1 AS it where comp1 = 1 INTO @DATA(res3).

&quot;However, the following statements work. No component of type string is involved.
SELECT SINGLE comp1 FROM @itab1 AS it WHERE comp2 = &#x27;aaa&#x27; INTO @DATA(res4).
SELECT SINGLE comp1, comp2 FROM @itab1 AS it INTO @DATA(res5).

*&amp;---------------------------------------------------------------------*
*&amp; Internal table having a component typed with a CDS simple type (sstring)
*&amp;---------------------------------------------------------------------*

&quot;Creating and populating an internal table that includes a component that is typed
&quot;with a CDS simple type (sstring). Note: Built-in DDIC types such as sstring cannot
&quot;directly be used in ABAP statements, except for typed literals.
TYPES: BEGIN OF s2,
         comp1 TYPE i,
         comp2 TYPE c LENGTH 3,
         comp3 TYPE zdemo_abap_string,
       END OF s2,
       tab_type_2 TYPE TABLE OF s2 WITH EMPTY KEY.
DATA(itab2) = VALUE tab_type_2( ( comp1 = 1 comp2 = &#x27;aaa&#x27; comp3 = `ABAP` ) ).

&quot;Unlike above, the following SELECT statements are possible
SELECT SINGLE * FROM @itab2 AS it WHERE comp1 = 1 INTO @DATA(res6).
SELECT SINGLE comp1 FROM @itab2 AS it WHERE comp3 = `ABAP` INTO @DATA(res7).
SELECT SINGLE comp1, comp3 FROM @itab2 AS it WHERE comp2 = &#x27;aaa&#x27; AND comp3 = `ABAP` INTO @DATA(res8).
&quot;Note: `ABAP` represents a literal of type string. When specifying it here
&quot;like it is specified above, there is an implicit conversion. The following
&quot;example uses a typed literal.
SELECT SINGLE comp1, comp2, comp3 FROM @itab2 AS it WHERE comp3 = sstring`ABAP` INTO @DATA(res9).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Excursion: Joining/Merging Internal Tables into Internal Tables</h4>
<p>The following code snippets demonstrate joining/merging the content of two simple internal tables into another table. There may be several ways to achieve this. Here, the intention is to give an idea and, in particular, to emphasize SQL functionalities also available for internal tables (note the restrictions mentioned above and in the documentation).</p>
<p>Assumptions:</p>
<ul>
<li>The target table is either created inline or exists and includes components from the source tables or components that can be mapped.</li>
<li>One or more components are common between the source tables to perform a join or merge the table content.</li>
</ul>
<p>The code snippet shows a selection of syntax options and includes the following statements:</p>
<ul>
<li>Joins with <code>SELECT</code> (<code>INNER JOIN</code>, <code>LEFT OUTER JOIN</code>, CTE)</li>
<li>Loops with <code>LOOP AT</code> statements, <code>FOR</code> loops using the <code>VALUE</code> and <code>REDUCE</code> operators</li>
</ul>
<pre><code>&quot;Creating two internal tables whose content will be joined. The shared
&quot;value is represented by the key1 and key2 components.
&quot;Sorted tables are used in the example (having key1/key2 as unique keys)
&quot;to have unique values to perform joins.
TYPES: BEGIN OF s1,
            key1 TYPE i,
            a    TYPE c LENGTH 1,
            b    TYPE c LENGTH 1,
            c    TYPE c LENGTH 1,
        END OF s1,
        tab_type1 TYPE SORTED TABLE OF s1 WITH UNIQUE KEY key1,
        BEGIN OF s2,
            key2 TYPE i,
            d    TYPE c LENGTH 1,
            e    TYPE c LENGTH 1,
        END OF s2,
        tab_type2 TYPE SORTED TABLE OF s2 WITH UNIQUE KEY key2.

&quot;Populating demo internal tables
DATA(itab1) = VALUE tab_type1( ( key1 = 1 a = &#x27;a&#x27; b = &#x27;b&#x27;  c = &#x27;c&#x27; )
                               ( key1 = 2 a = &#x27;d&#x27; b = &#x27;e&#x27;  c = &#x27;f&#x27; )
                               ( key1 = 3 a = &#x27;g&#x27; b = &#x27;h&#x27;  c = &#x27;i&#x27; ) ).

DATA(itab2) = VALUE tab_type2( ( key2 = 1 d = &#x27;j&#x27; e = &#x27;k&#x27; )
                               ( key2 = 2 d = &#x27;l&#x27; e = &#x27;m&#x27; ) ).

&quot;SELECT statement, inner join
&quot;Note: With the inner join, the target table contains all
&quot;combinations of rows for whose columns the join condition
&quot;is true.
SELECT a~key1, a~a, a~b, b~d, b~e
    FROM @itab1 AS a
    INNER JOIN @itab2 AS b ON a~key1 = b~key2
    INTO TABLE @DATA(itab3).

*Result
*KEY1    A    B    D    E
*1       a    b    j    k
*2       d    e    l    m

&quot;SELECT statement, left outer join
&quot;In contrast to the inner join above, the target table here
&quot;also contains the table row of the first table for which
&quot;no equivalent row exists in the second table.
SELECT a~key1, a~a, a~b, b~d, b~e
    FROM @itab1 AS a
    LEFT OUTER JOIN @itab2 AS b ON a~key1 = b~key2
    INTO TABLE @DATA(itab4).

*Result
*KEY1    A    B    D    E
*1       a    b    j    k
*2       d    e    l    m
*3       g    h

*&amp;---------------------------------------------------------------------*
*&amp; Note: The following statements produce the same result as the
*&amp; previous example (itab4).
*&amp;---------------------------------------------------------------------*

&quot;Common table expression
WITH +it1 AS ( SELECT a~key1, a~a, a~b FROM @itab1 AS a ),
     +it2 AS ( SELECT b~key2, b~d, b~e FROM @itab2 AS b )
SELECT +it1~key1, +it1~a, +it1~b, +it2~d, +it2~e FROM +it1 LEFT JOIN +it2 ON +it1~key1 = +it2~key2
INTO TABLE @DATA(itab5).

&quot;LOOP statements
&quot;Using the CORRESPONDING operator to assign identically named components, 
&quot;BASE retains existing content
&quot;The assignment with CORRESPONDING ... BASE ... includes a table expression
&quot;in which table lines are read and inserted based on the key mapping. With the
&quot;OPTIONAL addition, errors can be avoided if a line does not exist.
DATA itab6 LIKE itab4.
LOOP AT itab1 INTO DATA(wa1).
    INSERT CORRESPONDING #( wa1 ) INTO TABLE itab6 REFERENCE INTO DATA(ref).
    ref-&gt;* = CORRESPONDING #( BASE ( ref-&gt;* ) VALUE #( itab2[ key2 = ref-&gt;key1 ] OPTIONAL ) ).
ENDLOOP.
&quot;Assume the second table&#x27;s shared component was also key1. In the second CORRESPONDING
&quot;you could then work with the EXCEPT addition to not overwrite the identically named
&quot;component.

&quot;Example similar to the previous one
&quot;Also here, a table expression is used to read a line from
&quot;the second internal table. The INSERT statement (without
&quot;CORRESPONDING) includes the concrete value assignments
&quot;with the VALUE operator.
DATA itab7 LIKE itab4.
LOOP AT itab1 INTO DATA(wa2).
    DATA(line) = VALUE #( itab2[ key2 = wa2-key1 ] OPTIONAL ).

    INSERT VALUE #( key1 = wa2-key1
                    a = wa2-a
                    b = wa2-b
                    d = line-d
                    e = line-e ) INTO TABLE itab7.
ENDLOOP.

&quot;Example using a FOR loop with the VALUE operator
TYPES tt_type3 LIKE itab4.
DATA(itab8) = VALUE tt_type3( FOR wa3 IN itab1
                              ( key1 = wa3-key1
                                a = wa3-a
                                b = wa3-b
                                d = VALUE #( itab2[ key2 = wa3-key1 ]-d OPTIONAL )
                                e = VALUE #( itab2[ key2 = wa3-key1 ]-e OPTIONAL ) ) ).

&quot;Similar example that includes a LET expression
DATA(itab9) = VALUE tt_type3( FOR wa4 IN itab1
                              LET tab_line = VALUE #( itab2[ key2 = wa4-key1 ] OPTIONAL ) IN
                              ( key1 = wa4-key1
                                a = wa4-a
                                b = wa4-b
                                d = tab_line-d
                                e = tab_line-e ) ).

&quot;Example using a FOR loop with the REDUCE operator and LET
DATA(itab10) = REDUCE tt_type3( INIT tab = VALUE #( )
                                FOR wa5 IN itab1
                                LET tableline = VALUE #( itab2[ key2 = wa5-key1 ] OPTIONAL ) IN
                                NEXT tab = VALUE #( BASE tab
                                ( key1 = wa5-key1
                                  a = wa5-a
                                  b = wa5-b
                                  d = tableline-d
                                  e = tableline-e ) ) ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursions</h2>
<h3>Improving Read Performance with Secondary Table Keys</h3>
<ul>
<li>Consider a scenario where you have a standard internal table that you access frequently using the primary table key or a free key. The table is declared without a secondary key.</li>
<li>In both cases, the read performance is slow because these accesses are not optimized in standard tables.</li>
<li>You can add a secondary key to optimize access and improve read performance.</li>
<li>The following example creates three demo internal tables: two without a secondary key, accessed by the primary key and a free key, and a third table declared with a secondary key. All tables are populated with many table lines. Then, in a <code>DO</code> loop, multiple reads are performed on the internal tables. Before and after the reads, the current timestamp is retrieved to evaluate runtime. Find additional details in the similarly structured example in the next section. The results should demonstrate a significant performance improvement for reads on an internal table using the secondary key.</li>
<li>To try the example out, create a demo class named <code>zcl_demo_abap</code> and insert the provided code. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display the results in the console.</li>
</ul>
<div class="note">[!NOTE] This example is for [exploration, experimentation, and demonstration](./README.md#%EF%B8%8F-disclaimer). It is not intended for accurate runtime or performance testing and is not a suitable method for such purposes. Due to its simplified nature, results may vary and not be entirely accurate, even across multiple runs.</div>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;! &lt;p class=&quot;shorttext&quot;&gt;&lt;strong&gt;Improving Read Performance with Secondary Table Keys&lt;/strong&gt;&lt;br/&gt;
&quot;! ABAP cheat sheet example class&lt;/p&gt;
&quot;!
&quot;! &lt;p&gt;The example class includes several methods that explore both inefficient and potentially more
&quot;! efficient code examples affecting performance.&lt;/p&gt;
&quot;! &lt;p&gt;In ADT, choose F9 in ADT to run the class.&lt;/p&gt;
&quot;!
&quot;! &lt;h2&gt;Note&lt;/h2&gt;
&quot;! &lt;ul&gt;&lt;li&gt;The example is for exploration, experimentation, and demonstration. It is not intended for accurate
&quot;! runtime or performance testing and is not a suitable method for such purposes. Due to its simplified nature,
&quot;! results may vary and not be entirely accurate, even across multiple runs.&lt;/li&gt;
&quot;! &lt;li&gt;Note the disclaimer in the ABAP cheat sheet repository.&lt;/li&gt;&lt;/ul&gt;
CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
    METHODS:
      primary_table_key RETURNING VALUE(result) TYPE decfloat34,
      free_key RETURNING VALUE(result) TYPE decfloat34,
      secondary_table_key RETURNING VALUE(result) TYPE decfloat34,
      store_runtime IMPORTING high TYPE utclong low  TYPE utclong,
      get_avg_runtime RETURNING VALUE(avg_runtime) TYPE decfloat34.

    &quot;Demo internal table
    TYPES: BEGIN OF demo_struc,
             comp1 TYPE i,
             comp2 TYPE c LENGTH 20,
             comp3 TYPE c LENGTH 20,
             comp4 TYPE c LENGTH 20,
             comp5 TYPE c LENGTH 20,
             comp6 TYPE c LENGTH 20,
             comp7 TYPE c LENGTH 20,
           END OF demo_struc.

    DATA: itab_empty_key       TYPE TABLE OF demo_struc WITH EMPTY KEY,
          itab_w_primary_key   TYPE TABLE OF demo_struc WITH NON-UNIQUE KEY comp1 comp2 comp3,
          itab_w_secondary_key TYPE TABLE OF demo_struc WITH EMPTY KEY WITH NON-UNIQUE SORTED KEY sk COMPONENTS comp1 comp2 comp3.

    DATA: ts1         TYPE utclong,
          runtime_tab TYPE TABLE OF decfloat34 WITH EMPTY KEY,
          description TYPE string,
          idx         TYPE i,
          idx_char    TYPE c LENGTH 20.

    CONSTANTS: loop_count TYPE i VALUE 5000.
ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;Populating demo tables
    DO loop_count TIMES.
      INSERT VALUE #( comp1 = sy-index comp2 = sy-index comp3 = sy-index comp4 = sy-index
                      comp5 = sy-index comp6 = sy-index comp7 = sy-index ) INTO TABLE itab_empty_key.
    ENDDO.
    itab_w_primary_key = itab_empty_key.
    itab_w_secondary_key = itab_empty_key.

    DATA(res1) = primary_table_key( ).
    out-&gt;write( `Average runtime for reading lines of a standard table using the primary table key:` ).
    out-&gt;write( |{ res1 STYLE = SIMPLE }| ).

    DATA(res2) = free_key( ).
    out-&gt;write( `Average runtime for reading lines of a standard table using a free key:` ).
    out-&gt;write( |{ res2 STYLE = SIMPLE }| ).

    DATA(res3) = secondary_table_key( ).
    out-&gt;write( `Average runtime for reading lines of a standard table using the secondary table key:` ).
    out-&gt;write( |{ res3 STYLE = SIMPLE }| ).
  ENDMETHOD.

  METHOD primary_table_key.
    &quot;Reading lines from an internal table using the primary table key

    DO loop_count TIMES.
      idx = sy-index.
      idx_char = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE itab_w_primary_key TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx comp2 = idx_char comp3 = idx_char.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    result = get_avg_runtime( ).
  ENDMETHOD.

  METHOD free_key.
    &quot;Reading lines from an internal table using a free key
    &quot;The free key corresponds to the primary and secondary table key components
    &quot;of the other demo internal tables.

    DO loop_count TIMES.
      idx = sy-index.
      idx_char = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE itab_empty_key TRANSPORTING NO FIELDS WITH KEY comp1 = idx comp2 = idx_char comp3 = idx_char.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    result = get_avg_runtime( ).
  ENDMETHOD.

  METHOD secondary_table_key.
    &quot;Reading lines from an internal table using the secondary table key

    DO loop_count TIMES.
      idx = sy-index.
      idx_char = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE itab_w_secondary_key TRANSPORTING NO FIELDS WITH TABLE KEY sk COMPONENTS comp1 = idx comp2 = idx_char comp3 = idx_char.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    result = get_avg_runtime( ).
  ENDMETHOD.

  METHOD store_runtime.
    cl_abap_utclong=&gt;diff( EXPORTING high    = high
                                     low     = low
                           IMPORTING seconds = DATA(seconds) ).

    APPEND seconds TO runtime_tab.
  ENDMETHOD.

  METHOD get_avg_runtime.
    &quot;Calculating the average runtime value
    DATA(sum) = REDUCE decfloat34( INIT s = VALUE #( )
                                   FOR wa IN runtime_tab
                                   NEXT s += wa ).

    TRY.
        avg_runtime = sum / lines( runtime_tab ).
      CATCH cx_sy_zerodivide.
    ENDTRY.

    CLEAR runtime_tab.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Example: Exploring Read Access Performance with Internal Tables</h3>
<ul>
<li>The following example explores the performance of read accesses to internal tables of various kinds in a simplified and self-contained manner.</li>
<li>The scenario is: Lines of a large internal table (2000 table lines) are read frequently.</li>
<li>The read accesses are executed using <code>READ TABLE</code> statements, which access the tables by:</li>
<li>Index (both primary and secondary table index)</li>
<li>Key (primary table key, secondary table key, free key)</li>
<li>To try the example out, create a demo class named <code>zcl_demo_abap</code> and insert the provided code. After activation, choose <em>F9</em> in ADT to execute the class. It may take some time to finish and display the output. The example is set up to display the results in the console.</li>
</ul>
<div class="note">[!NOTE] - This example is for [exploration, experimentation, and demonstration](./README.md#%EF%B8%8F-disclaimer) purposes only. It is not suitable for accurate runtime or performance testing. Due to its simplified nature (for example, the table line type is fairly simple, and the key components are all of type `i`) and various factors that can influence the runtime of an ABAP program, results may vary and may not be entirely accurate, even across multiple runs. The displayed results may not always accurately reflect the performance notes from the cheat sheet. - The example concentrates on a few demo internal tables, declared using various declaration options. - The runtime analysis in this self-contained example is as follows: The code snippet for which the runtime should be measured is enclosed by two timestamp retrievals using `utclong_current( )`. The runtime is analysed by calculating the delta of the two timestamp values, indicating the used runtime for the code snippet. To have a more meaningful example regarding the runtime analysis, the snippets are executed multiple times, indicated by enclosing them in a `DO` loop. The runtime delta value is added to an internal table collecting all runtime delta values for the specific code snippet for all loop iterations. Finally, the average runtime is calculated based on the collected runtime delta values. This average runtime value is added to an internal table with other information. The approach with multiple iterations and the average runtime value aims to provide a balanced view of runtime. Note that this implementation is for exploration and demonstration purposes only. - Among the demonstrated read operations are the following: - Index accesses (standard and sorted tables, hashed tables using the secondary table index) - Primary table key accesses (various tables specified with a single and multiple key components; examples with full key, partial and left-aligned key, partial and not left-aligned key) - Secondary table key access - Free key access - Dynamic key specifications - The following points should be reflected by the result: - Index access is generally very fast. - (Full) primary table key access in hashed tables is fast. - Standard tables with specified secondary keys offer optimized access. - Both full and partial, left-aligned key accesses in sorted tables are optimized. - Free key searches are generally slower (unless used with the BINARY SEARCH addition in standard tables). - Key access in standard tables is relatively slow, except for secondary table keys. - Dynamic key specifications require effort for evaluation. - The purpose of this example is to underscore the significance of choosing the right table categories for your internal tables, tailored to your specific use case and the frequency of table access.</div>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for general notes&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>Notes on ...</p>
<p>... primary table key and primary table index:</p>
<ul>
<li>Each internal table has a primary key, which can be custom-defined or use the standard key. In standard tables, it is also possible to use an empty key.</li>
<li>Be aware of the implications of specifying the standard key, either intentionally or unintentionally. The standard key combines all character-like components, such as <code>txt</code> in the example below, while the remaining components are of type <code>i</code>.</li>
<li>The primary key of a table is ...</li>
<li>a hash key in hashed tables,</li>
<li>a sorted key in sorted tables,</li>
<li>always non-unique in standard tables.</li>
<li>Key access is optimized for hashed and sorted tables, but not for standard tables.</li>
<li>You cannot modify key fields in hashed and sorted tables.</li>
<li>In standard tables, key access is not optimized regarding the primary table index (for the secondary table index, it is optimized) because the search is linear.</li>
<li>In sorted tables, optimized access is also possible for partial primary table key specifications. As a prerequisite, the key specification must be left-aligned and gap-free. For example, if the primary table key consists of components <code>a</code>, <code>b</code>, and <code>c</code>, optimized access is valid if only <code>a</code> and <code>b</code> are specified. If the keys are not left-aligned, such as in the case where <code>b</code> and <code>c</code> are specified without <code>a</code>, optimized access is not applicable, and a linear search will be used.</li>
<li>Generally, the larger the table, the slower the key access. The benefit of hash tables is that they offer constant access time, even for very large tables.</li>
<li>Sorted and standard tables have a primary table index, hence their designation as index tables.</li>
<li>The primary table index assigns a unique line number to each table line.</li>
<li>The index updates whenever a line is added or removed.</li>
<li>Unlike key access, access time via the index does not increase linearly with table size.</li>
</ul>
<p>... secondary table key and secondary table index:</p>
<ul>
<li>Secondary table keys, which can be sorted or hashed, are available for all table categories.</li>
<li>They enhance table access efficiency and performance.</li>
<li>Declaring a secondary table key generates a corresponding secondary table index. However, the index for a non-unique key does not update immediately upon adding or deleting a line (for unique keys, it does). The update happens when the internal table is accessed using the secondary table key.</li>
<li>In ABAP statements, you must specify the secondary table key explicitly. Otherwise, the primary table key is used implicitly.</li>
<li>Data access using the secondary table key is always optimized, even for standard tables. Thus, even older standard tables can gain from optimized access by adding secondary table keys later, without impacting existing table-related statements.</li>
<li>However, weigh the administrative costs of secondary table keys. Use them only in scenarios where they offer substantial benefits, like large internal tables that are filled once and rarely altered. Frequent modifications can lead to regular index updates, potentially impacting performance.</li>
</ul>
<p>... the use of table categories:</p>
<ul>
<li>Standard tables:</li>
<li>Suitable for ...</li>
<li>small, sequentially accessed tables that are often accessed by index</li>
<li>tables where sorting is not critical. However, you can explicitly sort using <code>SORT</code> statements, especially after table population.</li>
<li>tables that are populated frequently, as there is no need to check for unique entries regarding the primary table key.</li>
<li>Access speed:</li>
<li>Fast: By index, optimized key access with secondary table keys, and free keys using <code>READ TABLE ... BINARY SEARCH</code> statements (Note: It is recommended to use secondary table keys for an optimized access.)</li>
<li>Slow: Primary table and free key</li>
</ul>
<ul>
<li>Sorted tables:</li>
<li>Suitable for ...</li>
<li>large tables requiring consistent sorted content</li>
<li>tables frequently accessed both by index and in sequence</li>
<li>Access is fast by index, primary table key (always optimized; also optimized for an incomplete, left-aligned and gap-free primary table key specification), secondary table key (also optimized)</li>
</ul>
<ul>
<li>Hashed tables:</li>
<li>Suitable for ...</li>
<li>very large tables that are filled once and rarely altered</li>
<li>tables where key-based access is the primary method</li>
<li>tables that do not require index access on the primary table index</li>
<li>Access is ...</li>
<li>fast and optimized for both primary and secondary table keys</li>
<li>consistent for large internal tables due to a special hash algorithm</li>
</ul>
<p>&lt;/details&gt;</p>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;! &lt;p class=&quot;shorttext&quot;&gt;&lt;strong&gt;Exploring Read Access Performance with Internal Tables&lt;/strong&gt;&lt;br/&gt;
&quot;! ABAP cheat sheet example class&lt;/p&gt;
&quot;!
&quot;! &lt;p&gt;The example class includes several methods that explore both inefficient and potentially more
&quot;! efficient code examples affecting performance.&lt;/p&gt;
&quot;! &lt;p&gt;In ADT, choose F9 in ADT to run the class.&lt;/p&gt;
&quot;!
&quot;! &lt;h2&gt;Note&lt;/h2&gt;
&quot;! &lt;ul&gt;&lt;li&gt;The example is for exploration, experimentation, and demonstration. It is not intended for accurate
&quot;! runtime or performance testing and is not a suitable method for such purposes. Due to its simplified nature,
&quot;! results may vary and not be entirely accurate, even across multiple runs.&lt;/li&gt;
&quot;! &lt;li&gt;Note the disclaimer in the ABAP cheat sheet repository.&lt;/li&gt;&lt;/ul&gt;
CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    CLASS-METHODS class_constructor.

  PROTECTED SECTION.
  PRIVATE SECTION.

    &quot;Constants
    CONSTANTS loop_count_2000 TYPE i VALUE 2000.

    &quot;Components used for calculating the average runtime
    DATA: ts1         TYPE utclong,
          runtime_tab TYPE TABLE OF decfloat34 WITH EMPTY KEY,
          runtime     TYPE decfloat34,
          description TYPE string,
          idx         TYPE i.

    &quot;Collects timestamps to calculate the average runtime
    METHODS store_runtime IMPORTING high TYPE utclong
                                    low  TYPE utclong.

    &quot;Calculates the average runtime
    METHODS get_avg_runtime RETURNING VALUE(avg_runtime) TYPE decfloat34.

    TYPES: BEGIN OF ENUM enum_access,
             index,
             primary_table_key,
             secondary_table_key,
             free_key,
             secondary_table_index,
           END OF ENUM enum_access.

    TYPES: BEGIN OF ENUM enum_category,
             standard,
             sorted,
             hashed,
           END OF ENUM enum_category.

    TYPES: BEGIN OF result_struc,
             meth_name       TYPE string,
             avg_runtime     TYPE string,
             table_category  TYPE enum_category,
             access          TYPE enum_access,
             description     TYPE string,
             iteration_count TYPE i,
           END OF result_struc.

    DATA result_meth TYPE result_struc.
    DATA result_tab TYPE TABLE OF result_struc WITH EMPTY KEY.

    &quot;Exemplary methods containing READ TABLE statements
    METHODS:
      &quot;Standard tables
      std_01_index IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_02_pr_key_single_comp IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_03_mult_key_full IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_04_mult_key_partial_la IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_05_mult_key_partial_non IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_06_mult_key_dynamic IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_07_free_key IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_08_free_key_binary_search IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_09_sec_key_nonuni_sorted IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_10_sec_key_nonuni_sort_idx IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_10_sec_key_unique_sorted IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      std_11_sec_key_unique_hashed IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      &quot;Sorted tables
      sorted_01_index IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      sorted_02_pr_key_single_comp IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      sorted_03_mult_key_full IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      sorted_04_mult_key_partial_la IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      sorted_05_mult_key_partial_non IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      sorted_06_mult_key_dynamic IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      sorted_07_free_key IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      &quot;Hashed tables
      hashed_01_pr_key_single_comp IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      hashed_02_mult_key_full IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      hashed_03_mult_key_partial_la IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      hashed_04_mult_key_partial_non IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      hashed_05_mult_key_dynamic IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      hashed_06_free_key IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc,
      hashed_07_sec_key_nonun_so_idx IMPORTING name TYPE abap_methname RETURNING VALUE(result) TYPE result_struc.

    &quot;Demo internal tables used by the examples
    TYPES: BEGIN OF ty_tab,
             comp1 TYPE i,
             comp2 TYPE i,
             comp3 TYPE i,
             comp4 TYPE c LENGTH 20,
             comp5 TYPE c LENGTH 20,
             comp6 TYPE c LENGTH 20,
             comp7 TYPE i,
           END OF ty_tab.

    CLASS-DATA: &quot;Standard tables
      standard_tab                  TYPE TABLE OF ty_tab WITH NON-UNIQUE KEY comp1,
      standard_tab_mult_key_comp    TYPE TABLE OF ty_tab WITH NON-UNIQUE KEY comp1 comp2 comp3,
      &quot;standard_tab_empty_key        TYPE TABLE OF ty_tab WITH EMPTY KEY,
      standard_tab_non_uni_so_sec_k TYPE TABLE OF ty_tab WITH EMPTY KEY WITH NON-UNIQUE SORTED KEY non_uni_so_key COMPONENTS comp1,
      standard_tab_uni_so_sec_k     TYPE TABLE OF ty_tab WITH EMPTY KEY WITH UNIQUE SORTED KEY uni_so_key COMPONENTS comp1,
      standard_tab_hashed_sec_k     TYPE TABLE OF ty_tab WITH EMPTY KEY WITH UNIQUE HASHED KEY hashed_key COMPONENTS comp1,

      &quot;Sorted tables
      sorted_tab                    TYPE SORTED TABLE OF ty_tab WITH UNIQUE KEY comp1,
      sorted_tab_mult_key_comp      TYPE SORTED TABLE OF ty_tab WITH UNIQUE KEY comp1 comp2 comp3,
      sorted_tab_w_sec_key          TYPE SORTED TABLE OF ty_tab WITH UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY sec_key COMPONENTS comp2,

      &quot;Hashed tables
      hashed_tab                    TYPE HASHED TABLE OF ty_tab WITH UNIQUE KEY comp1,
      hashed_tab_mult_key_comp      TYPE HASHED TABLE OF ty_tab WITH UNIQUE KEY comp1 comp2 comp3,
      hashed_tab_w_sec_key          TYPE HASHED TABLE OF ty_tab WITH UNIQUE KEY comp1 WITH NON-UNIQUE SORTED KEY non_uni_so_key COMPONENTS comp2.

ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;Dynamically calling all exemplary methods (excluding the main method)
    &quot;The method names are retrieved using RTTI. Refer to the Dynamic Programming ABAP cheat sheet.
    DATA(oref) = NEW zcl_demo_abap( ).
    DATA(methods) = CAST cl_abap_classdescr( cl_abap_typedescr=&gt;describe_by_name( &#x27;ZCL_DEMO_ABAP&#x27; ) )-&gt;methods.
    DELETE methods WHERE name = &#x27;IF_OO_ADT_CLASSRUN~MAIN&#x27;.
    SORT methods BY name ASCENDING.

    LOOP AT methods INTO DATA(meth_wa).
      TRY.
          CALL METHOD oref-&gt;(meth_wa-name) EXPORTING name = meth_wa-name RECEIVING result = result_meth.
          APPEND result_meth TO result_tab.
        CATCH cx_root.
      ENDTRY.
    ENDLOOP.

    SORT result_tab BY avg_runtime ASCENDING.
    out-&gt;write( result_tab ).
  ENDMETHOD.

  METHOD store_runtime.
    cl_abap_utclong=&gt;diff( EXPORTING high    = high
                                     low     = low
                           IMPORTING seconds = DATA(seconds) ).

    APPEND seconds TO runtime_tab.
  ENDMETHOD.

  METHOD class_constructor.

    DO loop_count_2000 TIMES.
      INSERT VALUE #( comp1 = sy-index comp2 = sy-index comp3 = sy-index comp4 = sy-index
                      comp5 = sy-index comp6 = sy-index comp7 = sy-index  ) INTO TABLE sorted_tab.


    ENDDO.

    &quot;Populating standard tables
    INSERT LINES OF sorted_tab INTO TABLE standard_tab.
    INSERT LINES OF sorted_tab INTO TABLE standard_tab_mult_key_comp.
    INSERT LINES OF sorted_tab INTO TABLE standard_tab_non_uni_so_sec_k.
    INSERT LINES OF sorted_tab INTO TABLE standard_tab_uni_so_sec_k.
    INSERT LINES OF sorted_tab INTO TABLE standard_tab_hashed_sec_k.

    &quot;Populating sorted tables
    INSERT LINES OF sorted_tab INTO TABLE sorted_tab_mult_key_comp.
    INSERT LINES OF sorted_tab INTO TABLE sorted_tab_w_sec_key.

    &quot;populating hashed tables
    INSERT LINES OF sorted_tab INTO TABLE hashed_tab.
    INSERT LINES OF sorted_tab INTO TABLE hashed_tab_mult_key_comp.
    INSERT LINES OF sorted_tab INTO TABLE hashed_tab_w_sec_key.
  ENDMETHOD.

  METHOD get_avg_runtime.
    &quot;Calculating the average runtime value

    DATA(sum) = REDUCE decfloat34( INIT s = VALUE #( )
                                   FOR wa IN runtime_tab
                                   NEXT s += wa ).

    TRY.
        avg_runtime = sum / lines( runtime_tab ).
      CATCH cx_sy_zerodivide.
    ENDTRY.

    CLEAR runtime_tab.
  ENDMETHOD.

  METHOD std_01_index.
    description = `Read standard table lines by index`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab TRANSPORTING NO FIELDS INDEX idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = index
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_02_pr_key_single_comp.
    description = `Read standard table lines by primary table key (single key component)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_03_mult_key_full.
    description = `Read standard table lines by the full primary table key (multiple key component)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_mult_key_comp TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx comp2 = idx comp3 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_04_mult_key_partial_la.
    description = `Read standard table lines by the partial, left-aligned primary table key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_mult_key_comp TRANSPORTING NO FIELDS WITH KEY comp1 = idx comp2 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_05_mult_key_partial_non.
    description = `Read standard table lines by the partial, not left-aligned primary table key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_mult_key_comp TRANSPORTING NO FIELDS WITH KEY comp2 = idx comp3 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_06_mult_key_dynamic.
    description = `Read standard table lines by primary table key, dynamic key specifications`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_mult_key_comp TRANSPORTING NO FIELDS WITH TABLE KEY (&#x27;COMP1&#x27;) = idx (&#x27;COMP2&#x27;) = idx (&#x27;COMP3&#x27;) = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_07_free_key.
    description = `Read standard table lines by free key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab TRANSPORTING NO FIELDS WITH KEY comp7 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = free_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_08_free_key_binary_search.
    description = `Read standard table lines with free key and binary search`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      IF idx = 1.
        SORT standard_tab BY comp7 ASCENDING.
      ENDIF.
      READ TABLE standard_tab TRANSPORTING NO FIELDS WITH KEY comp7 = idx BINARY SEARCH.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = free_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_09_sec_key_nonuni_sorted.
    description = `Read standard table lines by secondary table key (non-unique sorted key)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_non_uni_so_sec_k TRANSPORTING NO FIELDS WITH TABLE KEY non_uni_so_key COMPONENTS comp1 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = secondary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_10_sec_key_nonuni_sort_idx.
    description = `Read standard table lines by secondary table index (non-unique sorted key)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_non_uni_so_sec_k TRANSPORTING NO FIELDS USING KEY non_uni_so_key INDEX idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = secondary_table_index
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_10_sec_key_unique_sorted.
    description = `Read standard table lines by secondary table key (unique sorted key)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_uni_so_sec_k TRANSPORTING NO FIELDS WITH TABLE KEY uni_so_key COMPONENTS comp1 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = secondary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD std_11_sec_key_unique_hashed.
    description = `Read standard table lines by secondary table key (unique hashed key)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE standard_tab_hashed_sec_k TRANSPORTING NO FIELDS WITH TABLE KEY hashed_key COMPONENTS comp1 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = standard
                      access      = secondary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_01_index.
    description = `Read sorted table lines by index`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab TRANSPORTING NO FIELDS INDEX idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = index
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_02_pr_key_single_comp.
    description = `Read sorted table lines by primary table key (single key component)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_03_mult_key_full.
    description = `Read sorted table lines by the full primary table key (multiple key components)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab_mult_key_comp TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx comp2 = idx comp3 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_04_mult_key_partial_la.
    description = `Read sorted table lines by partial, left-aligned primary table key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab_mult_key_comp TRANSPORTING NO FIELDS WITH KEY comp1 = idx comp2 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_05_mult_key_partial_non.
    description = `Read sorted table lines by partial, not left-aligned primary table key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab_mult_key_comp TRANSPORTING NO FIELDS WITH KEY comp2 = idx comp3 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_06_mult_key_dynamic.
    description = `Read sorted table lines by primary table key, dynamic key specification`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab_mult_key_comp TRANSPORTING NO FIELDS WITH TABLE KEY (&#x27;COMP1&#x27;) = idx (&#x27;COMP2&#x27;) = idx (&#x27;COMP3&#x27;) = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD sorted_07_free_key.
    description = `Read sorted table lines by free key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE sorted_tab TRANSPORTING NO FIELDS WITH KEY comp7 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = sorted
                      access      = free_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.


  METHOD hashed_01_pr_key_single_comp.
    description = `Read hashed table lines by primary table key (single key component)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD hashed_02_mult_key_full.
    description = `Read hashed table lines by the full primary table key (multiple key components)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab_mult_key_comp TRANSPORTING NO FIELDS WITH TABLE KEY comp1 = idx comp2 = idx comp3 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD hashed_03_mult_key_partial_la.
    description = `Read hashed table lines by partial, left-aligned primary table key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab_mult_key_comp TRANSPORTING NO FIELDS WITH KEY comp1 = idx comp2 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD hashed_04_mult_key_partial_non.
    description = `Read hashed table lines by partial, not left-aligned primary table key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab_mult_key_comp TRANSPORTING NO FIELDS WITH KEY comp2 = idx comp3 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD hashed_05_mult_key_dynamic.
    description = `Read hashed table lines by primary table key, dynamic key specification`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab_mult_key_comp TRANSPORTING NO FIELDS WITH TABLE KEY (&#x27;COMP1&#x27;) = idx (&#x27;COMP2&#x27;) = idx (&#x27;COMP3&#x27;) = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = primary_table_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD hashed_06_free_key.
    description = `Read hashed table lines by free key`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab TRANSPORTING NO FIELDS WITH KEY comp7 = idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = free_key
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.

  METHOD hashed_07_sec_key_nonun_so_idx.
    description = `Read hashed table lines by secondary table index (non-unique sorted key)`.
    DO loop_count_2000 TIMES.
      idx = sy-index.
      ts1 = utclong_current( ).
**********************************************************************
      READ TABLE hashed_tab_w_sec_key TRANSPORTING NO FIELDS USING KEY non_uni_so_key INDEX idx.
**********************************************************************
      store_runtime( high = utclong_current( ) low = ts1 ).
    ENDDO.
    runtime = get_avg_runtime( ).
    result = VALUE #( meth_name = name
                      avg_runtime = |{ runtime STYLE = SIMPLE }|
                      table_category = hashed
                      access      = secondary_table_index
                      description = description
                      iteration_count = idx ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Generic Table Types with Formal Parameters of Methods and Field Symbols</h3>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenformal_parameter_glosry.htm">Formal parameters</a> of methods or <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfield_symbol_glosry.htm">field symbols</a> can be specified with generic types.</li>
<li>At runtime, the actual data type is copied from the assigned <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenactual_parameter_glosry.htm">actual parameter</a> or memory area, i.e. they receive the complete data type only when an actual parameter is passed or a memory area is assigned.</li>
<li>Among them, there are generic table types. For more information, refer to the <a href="16_Data_Types_and_Objects.md">Data Types and Objects</a> cheat sheet and the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbuilt_in_types_generic.htm">documentation</a>.</li>
<li>The following example mainly demonstrates formal parameters of methods that are typed with generic table types. The method calls and the tables passed are only possible if the generic types fit. For example, you cannot pass a hashed table to a method whose importing parameter is typed with the generic type <code>INDEX TABLE</code>. Invalid method calls and table passing are commented out.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
      PUBLIC
      FINAL
      CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    CLASS-METHODS process_any_table IMPORTING itab TYPE ANY TABLE.
    CLASS-METHODS process_standard_tables1 IMPORTING itab TYPE STANDARD TABLE.
    CLASS-METHODS process_standard_tables2 IMPORTING itab TYPE TABLE.
    CLASS-METHODS process_sorted_tables IMPORTING itab TYPE SORTED TABLE.
    CLASS-METHODS process_hashed_tables IMPORTING itab TYPE HASHED TABLE.
    CLASS-METHODS process_index_tables IMPORTING itab TYPE INDEX TABLE.

ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA standard_tab TYPE TABLE OF string WITH EMPTY KEY.
    DATA sorted_tab TYPE SORTED TABLE OF string WITH NON-UNIQUE KEY table_line.
    DATA hashed_tab TYPE HASHED TABLE OF string WITH UNIQUE KEY table_line.

    &quot;ANY TABLE
    process_any_table( standard_tab ).
    process_any_table( sorted_tab ).
    process_any_table( hashed_tab ).

    &quot;(STANDARD) TABLE
    process_standard_tables1( standard_tab ).
    &quot;process_standard_tables1( sorted_tab ).
    &quot;process_standard_tables1( hashed_tab ).
    process_standard_tables2( standard_tab ).
    &quot;process_standard_tables2( sorted_tab ).
    &quot;process_standard_tables2( hashed_tab ).

    &quot;SORTED TABLE
    &quot;process_sorted_tables( standard_tab ).
    process_sorted_tables( sorted_tab ).
    &quot;process_sorted_tables( hashed_tab ).

    &quot;HASHED TABLE
    &quot;process_hashed_tables( standard_tab ).
    &quot;process_hashed_tables( sorted_tab ).
    process_hashed_tables( hashed_tab ).

    &quot;INDEX TABLE
    process_index_tables( standard_tab ).
    process_index_tables( sorted_tab ).
    &quot;process_index_tables( hashed_tab ).


    &quot;Note: Field symbols can also be typed with generic types.
    FIELD-SYMBOLS &lt;fs_std_table&gt; TYPE table.
    ASSIGN standard_tab TO &lt;fs_std_table&gt;.
    &quot;ASSIGN sorted_tab TO &lt;fs_std_table&gt;.
    &quot;ASSIGN hashed_tab TO &lt;fs_std_table&gt;.

    FIELD-SYMBOLS &lt;fs_index_table&gt; TYPE INDEX TABLE.
    ASSIGN standard_tab TO &lt;fs_index_table&gt;.
    ASSIGN sorted_tab TO &lt;fs_index_table&gt;.
    &quot;ASSIGN hashed_tab TO &lt;fs_index_table&gt;.

  ENDMETHOD.
  METHOD process_any_table.
  ENDMETHOD.

  METHOD process_hashed_tables.
  ENDMETHOD.

  METHOD process_index_tables.
  ENDMETHOD.

  METHOD process_sorted_tables.
  ENDMETHOD.

  METHOD process_standard_tables1.
  ENDMETHOD.

  METHOD process_standard_tables2.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Searching and Replacing Substrings in Internal Tables with Character-Like Data Types</h3>
<p>You can use <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapfind_itab.htm"><code>FIND ... IN TABLE</code></a> statements to search for substrings in internal tables (standard tables without secondary table keys; with character-like line type) line by line.</p>
<pre><code>DATA(str_table) = VALUE string_table( ( `aZbzZ` ) ( `cdZze` ) ( `Zzzf` ) ( `ghz` ) ).

&quot;Finding all occurrences in a table
&quot;Note: res_tab is of type match_result_tab 
&quot;You can also restrict the search range in an internal table; see an example in REPLACE ... IN TABLE
FIND ALL OCCURRENCES OF `Z`
  IN TABLE str_table
  RESULTS DATA(res_tab)
  RESPECTING CASE.

&quot;4 entries in table res_tab (tables in SUBMATCHES are initial since no regular expression is used)
&quot;1. line: 1, offset: 1, length: 1, submatches: (initial)
&quot;2. line: 1, offset: 4, length: 1, ...
&quot;3. line: 2, offset: 2, length: 1, ...
&quot;4. line: 3, offset: 0, length: 1, ...

&quot;Finding the first occurrence in a table
&quot;Note: res_struc, which is declared inline here, is of type match_result 
FIND FIRST OCCURRENCE OF `Z`
  IN TABLE str_table
  RESULTS DATA(res_struc)
  RESPECTING CASE.

&quot;Entries in structure res_struc 
&quot;line: 1, offset: 1, length: 1, submatches: (initial)

&quot;Alternative to the statement above (storing the information in individual data objects)
FIND FIRST OCCURRENCE OF `Z`
  IN TABLE str_table
  MATCH LINE DATA(line)    &quot;1
  MATCH OFFSET DATA(off)   &quot;1 
  MATCH LENGTH DATA(len)   &quot;1
  RESPECTING CASE.</code></pre>
<p>Replacements in internal tables with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapreplace_itab.htm"><code>REPLACE ... IN TABLE</code></a>:</p>
<pre><code>DATA(str_table_original) = VALUE string_table( ( `aZbzZ` ) ( `cdZze` ) ( `Zzzf` ) ( `ghz` ) ).
DATA(str_table) = str_table_original.

&quot;Replacing all occurrences in a table
&quot;RESULTS addition: Storing information in an internal table of type repl_result_tab
REPLACE ALL OCCURRENCES OF `Z`
  IN TABLE str_table
  WITH `#`
  RESULTS DATA(res_table)
  RESPECTING CASE.

&quot;str_table: a#bz# / cd#ze / #zzf / ghz
&quot;res_table:
&quot;LINE  OFFSET  LENGTH
&quot;1     1       1
&quot;1     4       1
&quot;2     2       1
&quot;3     0       1

str_table = str_table_original.

&quot;Replacing the first occurrence in a table
&quot;RESULTS addition: Storing information in a structure of type repl_result
REPLACE FIRST OCCURRENCE OF `Z`
  IN TABLE str_table
  WITH `#`
  RESULTS DATA(res_structure)
  RESPECTING CASE.

&quot;str_table: a#bzZ / cdZze / Zzzf / ghz
&quot;res_structure:
&quot;LINE  OFFSET  LENGTH
&quot;1     1       1

str_table = str_table_original.

&quot;Restricting the search range in an internal table
REPLACE ALL OCCURRENCES OF `Z`
  IN TABLE str_table
  FROM 1 TO 2
  WITH `#`
  RESPECTING CASE.

&quot;str_table: a#bz# / cd#ze / Zzzf / ghz

str_table = str_table_original.

&quot;Offsets can be optionally specified (also only the offset of start or end line possible)
REPLACE ALL OCCURRENCES OF `Z`
  IN TABLE str_table
  FROM 1 OFFSET 3 TO 2 OFFSET 2
  WITH `#`
  RESPECTING CASE.

&quot;str_table: aZbz# / cdZze / Zzzf / ghz</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Ranges Tables</h3>
<ul>
<li>Internal tables that have the predefined columns <code>SIGN</code>, <code>OPTION</code>, <code>LOW</code>, and <code>HIGH</code></li>
<li><code>SIGN</code>: type c with length 1; indicates whether each line is included (<code>I</code>) or excluded (<code>E</code>) from the result set.</li>
<li><code>OPTION</code>: type c with length 2; specifies the selection option for the condition. It uses comparison operators such as <code>EQ</code>, <code>NE</code>, <code>GE</code>, <code>GT</code>, <code>LE</code>, <code>LT</code>, <code>CP</code>, <code>NP</code>, <code>BT</code>, <code>NB</code>. Special rules apply for certain operators such as when using <code>CP</code> or <code>NP</code>, the <code>LOW</code> and <code>HIGH</code> columns must be character values.</li>
<li><code>LOW</code>: The lower comparison value.</li>
<li><code>HIGH</code>: The higher comparison value.</li>
<li>Declared with the <code>TYPE RANGE OF</code> addition in <code>DATA</code> and <code>TYPES</code> statements</li>
<li>Used to store range conditions that can be evaluated in expressions using the <code>IN</code> operator (each row in the table represents a separate comparison)</li>
</ul>
<pre><code>&quot;Populating an integer table with values from 1 to 20
TYPES int_tab_type TYPE TABLE OF i WITH EMPTY KEY.
DATA(inttab) = VALUE int_tab_type( FOR x = 1 WHILE x &lt;= 20 ( x ) ).

&quot;Declaring a ranges table
DATA rangestab TYPE RANGE OF i.

&quot;Populating a ranges table using VALUE
rangestab = VALUE #( sign   = &#x27;I&#x27;
                     option = &#x27;BT&#x27; ( low = 1  high = 3 )
                                   ( low = 6  high = 8 )
                                   ( low = 12 high = 15 )
                     option = &#x27;GE&#x27; ( low = 18 ) ).

&quot;Using a SELECT statement and the IN addition to retrieve internal table
&quot;content based on the ranges table specifications
SELECT * FROM @inttab AS tab
    WHERE table_line IN @rangestab
    INTO TABLE @DATA(result).
&quot;result: 1, 2, 3, 6, 7, 8, 12, 13, 14, 15, 18, 19, 20</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Comparing Content of Compatible Internal Tables</h3>
<p>You can compare internal tables:</p>
<ul>
<li>Using the methods of the <code>CL_ABAP_DIFF</code> class to compare the content of two compatible index tables programmatically.</li>
<li>Find more information in the class documentation and in the <a href="[06_Dynamic_Programming.md](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencl_abap_diff.htm">ABAP Keyword Documentation</a>). A code snippet is available in the <a href="22_Released_ABAP_Classes.md">Released ABAP Classes</a> cheat sheet.</li>
<li>Using the <a href="https://help.sap.com/docs/abap-cloud/abap-development-tools-user-guide/comparing-internal-tables-085ab2303ceb4478ad7958053c2ebeb3?locale=en-US&amp;version=LATEST">Table Comparison Tool</a> in ADT.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>BDEF Derived Types (ABAP EML)</h3>
<p>In the context of <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenarap_glosry.htm">ABAP RAP</a>, the operands of <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_eml_glosry.htm">ABAP EML</a> statements and parameters of <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabp_handler_method_glosry.htm">RAP handler methods</a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabp_saver_method_glosry.htm">RAP saver methods</a> are mainly special messenger tables for passing data and receiving results or messages: <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenrap_derived_type_glosry.htm">BDEF derived types</a> - special ABAP types (internal tables and structures) that are tailor-made for RAP purposes.</p>
<pre><code>&quot;Example declarations
&quot;For an EML create operation
DATA create_tab TYPE TABLE FOR CREATE entity.

&quot;For an update operation
DATA update_tab TYPE TABLE FOR UPDATE entity.

&quot;Type declaration using a BDEF derived type
TYPES der_typ TYPE TABLE FOR DELETE entity.</code></pre>
<p>Find more information in the <a href="08_EML_ABAP_for_RAP.md">ABAP for RAP: Entity Manipulation Language (ABAP EML)</a> ABAP cheat sheet.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Creating Internal Tables Dynamically</h3>
<p>Find more information in the <a href="06_Dynamic_Programming.md">Dynamic Programming cheat sheet</a>.</p>
<pre><code>DATA(some_type) = &#x27;STRING&#x27;.
DATA dataref TYPE REF TO data.

&quot;Creating an internal table using a CREATE DATA statement
&quot;by specifying the type name dynamically.
&quot;In the example, a standard table with elementary line type
&quot;and standard key is created.
CREATE DATA dataref TYPE TABLE OF (some_type).

TYPES: BEGIN OF demo_struc,
          comp1 TYPE c LENGTH 10,
          comp2 TYPE i,
          comp3 TYPE i,
        END OF demo_struc.

&quot;Internal table with structured line type and empty key.
CREATE DATA dataref TYPE TABLE OF (&#x27;DEMO_STRUC&#x27;) WITH EMPTY KEY.

&quot;Using a globally available table type
CREATE DATA dataref TYPE (&#x27;STRING_TABLE&#x27;).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>More Information</h2>
<p>Topic <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenitab.htm">Internal Tables</a> in the ABAP Keyword Documentation.</p>
<h2>Executable Example</h2>
<p><a href="./src/zcl_demo_abap_internal_tables.clas.abap">zcl_demo_abap_internal_tables</a></p>
<div class="note">[!NOTE] - The executable example covers the following topics, among others: Creating, populating, reading from, sorting, modifying internal tables - The steps to import and run the code are outlined [here](README.md#-getting-started-with-the-examples).</div>
<div class="note">- [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
</body>
</html>