<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>30 Generative AI - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>Generative AI</h1>
<ul>
<li><a href="#generative-ai">Generative AI</a></li>
<li><a href="#information-and-documentation">Information and Documentation</a></li>
<li><a href="#using-the-abap-ai-sdk">Using the ABAP AI SDK</a></li>
</ul>
<p>This ABAP cheat sheet provides references to detailed information on <em>Generative AI in ABAP Cloud</em> and explores <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenreleased_api_glosry.htm">released</a> ABAP classes available in the <em>ABAP AI SDK powered by Intelligent Scenario Lifecycle Management</em>.</p>
<h2>Information and Documentation</h2>
<ul>
<li>Blogs:</li>
<li><a href="https://community.sap.com/t5/technology-blogs-by-sap/the-power-of-joule-for-developers-abap-ai-capabilities/ba-p/14019716">The Power of Joule for Developers, ABAP AI Capabilities</a></li>
<li><a href="https://community.sap.com/t5/technology-blogs-by-sap/unleash-your-inner-abap-rockstar-with-joule-for-developers-abap-ai/ba-p/14019607">Unleash Your Inner ABAP Rockstar with Joule for developers , ABAP AI capabilities</a></li>
<li><a href="https://community.sap.com/t5/technology-blogs-by-sap/joule-speaks-abap/ba-p/14018226">Joule speaks ABAP!</a></li>
<li>SAP Help documentation and frequently asked questions</li>
<li><a href="https://help.sap.com/docs/abap-ai?locale=en-US">Joule for Developers, ABAP AI Capabilities</a></li>
<li><a href="https://help.sap.com/docs/abap-ai/generative-ai-in-abap-cloud/generative-ai-in-abap-cloud?locale=en-US">Generative AI in ABAP Cloud</a></li>
<li><a href="https://www.sap.com/documents/2024/11/6c0a3705-e77e-0010-bca6-c68f7e60039b.html">ABAP AI Strategy Frequently Asked Questions</a></li>
<li>Tutorial on GitHub</li>
<li><a href="https://github.com/SAP-samples/abap-platform-rap120">RAP120 - Build SAP Fiori Apps with ABAP Cloud and Joule</a></li>
<li>Devtoberfest session</li>
<li><a href="https://www.youtube.com/live/W1B8CWprDFM?si=lnkSGyPHp8lw4bQD">Boost your Coding Efficiency: Explore Joule’s ABAP Developer Capabilities</a></li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Using the ABAP AI SDK</h2>
<ul>
<li>This section explores released ABAP classes within the ABAP AI SDK for interacting with large language models (LLMs) in custom implementations.</li>
<li>Find more information in <a href="https://help.sap.com/docs/ABAP_AI/c7f5ef43ab274d078baf22f995fd2161/27c5d27b480043f0a9fd8e46ae8275a2.html?locale=en-US">Developing Your Own AI-Enabled Applications</a>.</li>
</ul>
<div class="note">[!NOTE] - The ABAP AI SDK is integrated with the *Intelligent Scenario Lifecycle Management (ISLM)*. Before using the ABAP AI SDK, administrative tasks outlined in the [documentation](https://help.sap.com/docs/ABAP_AI/c7f5ef43ab274d078baf22f995fd2161/339bd7a66c8545159cec357ce7f183d4.html?locale=en-US) have to be performed. - As a prerequisite to using the ABAP AI SDK, you have to create intelligent scenarios (ABAP repository objects containing various features to enable, for example, the instantiation of the completion API) and intelligent scenario models (defining, for example, which LLM is used). - The code snippets use the intelligent scenario name `ZDEMO_ABAP_INT_SCEN`. Assume that an example intelligent scenario model with the name `ZDEMO_ABAP_INT_SCEN_MODEL` exists, which includes a prompt template ID named `ZDEMO_PROMPT_TEMPLATE`. - [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
<p>Method calls that create an instance of the ISLM completion API, use a prompt as string, and retrieve the LLM answer:</p>
<pre><code>TRY.
    FINAL(ai_api) = cl_aic_islm_compl_api_factory=&gt;get( )-&gt;create_instance( &#x27;ZDEMO_ABAP_INT_SCEN&#x27; ).
    FINAL(result) = ai_api-&gt;execute_for_string( `Tell me a joke.` ).
    FINAL(completion) = result-&gt;get_completion( ).
  CATCH cx_aic_api_factory cx_aic_completion_api INTO FINAL(error).
    FINAL(error_text) = error-&gt;get_text( ).
ENDTRY.</code></pre>
<p>The following example class includes various calls exploring the APIs. Note that more methods are available, and that demo intelligent scenario and prompt template names are used in the example. Check the class and SAP Help Portal documentation. The example includes:</p>
<ul>
<li>Creating an instance of the ISLM completion API</li>
<li>Calling the LLM completion API with a prompt as string</li>
<li>Setting parameters</li>
<li>Retrieving information regarding the result</li>
<li>Calling the LLM completion API with a prompt as message list</li>
<li>Calling the prompt library API to use prompt templates</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
    CONSTANTS islm_scenario TYPE aic_islm_scenario_id=&gt;type VALUE &#x27;ZDEMO_ABAP_INT_SCEN&#x27;.
    CONSTANTS prompt_template TYPE aic_islm_prompt_template_id=&gt;type VALUE &#x27;ZDEMO_PROMPT_TEMPLATE&#x27;.
    DATA error TYPE REF TO cx_root.
    DATA ai_api TYPE REF TO if_aic_completion_api.
    DATA error_text TYPE string.
    DATA llm_result TYPE REF TO if_aic_completion_api_result.
    DATA llm_answer TYPE string.
ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

*&amp;---------------------------------------------------------------------*
*&amp; Creating an instance of the ISLM completion API
*&amp;---------------------------------------------------------------------*

    out-&gt;write( |1) Creating an instance of the ISLM completion API\n| ).

    TRY.
        ai_api = cl_aic_islm_compl_api_factory=&gt;get( )-&gt;create_instance( islm_scenario ).
      CATCH cx_aic_api_factory INTO error.
        error_text = error-&gt;get_text( ).
        out-&gt;write( error_text ).
    ENDTRY.

    IF error IS INITIAL.
      out-&gt;write( `An instance of the ISLM completion API has been created.` ).
    ELSE.
      RETURN.
    ENDIF.

    out-&gt;write( |\n| ).
    out-&gt;write( repeat( val = `_` occ = 70 ) ).
    out-&gt;write( |\n| ).

*&amp;---------------------------------------------------------------------*
*&amp; Calling the LLM completion API with a prompt as string
*&amp;---------------------------------------------------------------------*

    out-&gt;write( |2) Calling the LLM completion API with a prompt as string \n\n| ).

    TRY.
        llm_result = ai_api-&gt;execute_for_string( `Tell me a joke.` ).
        llm_answer = llm_result-&gt;get_completion( ).
        out-&gt;write( data = llm_answer name = `llm_answer` ).
      CATCH cx_aic_completion_api INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    out-&gt;write( |\n| ).
    out-&gt;write( repeat( val = `_` occ = 70 ) ).
    out-&gt;write( |\n| ).

*&amp;---------------------------------------------------------------------*
*&amp; Setting parameters
*&amp;---------------------------------------------------------------------*

    out-&gt;write( |3) Setting parameters \n\n| ).

    TRY.
        &quot;Creating another instance of the ISLM completion API
        ai_api = cl_aic_islm_compl_api_factory=&gt;get( )-&gt;create_instance( islm_scenario ).

        &quot;Setting parameters such as maximum tokens or temperature
        FINAL(params) = ai_api-&gt;get_parameter_setter( ).
        params-&gt;set_maximum_tokens( 500 ).
        params-&gt;set_temperature( &#x27;0.5&#x27; ). &quot;The value must be between 0 and 1

        llm_result = ai_api-&gt;execute_for_string( `What is ABAP?` ).
        llm_answer = llm_result-&gt;get_completion( ).
        out-&gt;write( data = llm_answer name = `llm_answer` ).
      CATCH cx_aic_api_factory cx_aic_completion_api INTO error.
        error_text = error-&gt;get_text( ).
        out-&gt;write( error_text ).
    ENDTRY.

    out-&gt;write( |\n| ).
    out-&gt;write( repeat( val = `_` occ = 70 ) ).
    out-&gt;write( |\n| ).

*&amp;---------------------------------------------------------------------*
*&amp; Retrieving information regarding the result
*&amp;---------------------------------------------------------------------*

    out-&gt;write( |4) Retrieving information regarding the result \n\n| ).

    TRY.
        &quot;Creating another instance of the ISLM completion API
        ai_api = cl_aic_islm_compl_api_factory=&gt;get( )-&gt;create_instance( islm_scenario ).
        llm_result = ai_api-&gt;execute_for_string( `What is an internal table in ABAP?` ).
        llm_answer = llm_result-&gt;get_completion( ).

        &quot;Number of tokens of the LLM output
        FINAL(completion_token_count) = llm_result-&gt;get_completion_token_count( ).
        &quot;Number of tokens of the LLM input
        FINAL(prompt_token_count) = llm_result-&gt;get_prompt_token_count( ).
        &quot;Number of tokens of the LLM input and output
        FINAL(total_token_count) = llm_result-&gt;get_total_token_count( ).
        &quot;Runtime of LLM call in milliseconds
        FINAL(runtime) = llm_result-&gt;get_runtime_ms( ).

        out-&gt;write( data = llm_answer name = `llm_answer` ).
        out-&gt;write( |\n| ).
        out-&gt;write( data = completion_token_count name = `completion_token_count` ).
        out-&gt;write( data = prompt_token_count name = `prompt_token_count` ).
        out-&gt;write( data = total_token_count name = `total_token_count` ).
        out-&gt;write( data = runtime name = `runtime` ).

      CATCH cx_aic_api_factory cx_aic_completion_api INTO error.
        error_text = error-&gt;get_text( ).
        out-&gt;write( error_text ).
    ENDTRY.

    out-&gt;write( |\n| ).
    out-&gt;write( repeat( val = `_` occ = 70 ) ).
    out-&gt;write( |\n| ).

*&amp;---------------------------------------------------------------------*
*&amp; Calling the LLM completion API with a prompt as message list
*&amp;---------------------------------------------------------------------*

    out-&gt;write( |5) Calling the LLM completion API with a prompt as message list \n\n| ).

    TRY.
        &quot;Creating another instance of the ISLM completion API
        ai_api = cl_aic_islm_compl_api_factory=&gt;get( )-&gt;create_instance( islm_scenario ).

        &quot;Creating a message container instance and adding messages
        FINAL(message_container) = ai_api-&gt;create_message_container( ).
        message_container-&gt;set_system_role( `You are a professional translator` ).
        message_container-&gt;add_user_message( `Can you translate German into English?` ).
        message_container-&gt;add_assistant_message( `Yes` ).
        message_container-&gt;add_user_message( `Translate the following German sentence into English: &quot;Entschuldigung, wie komme ich zum Bahnhof?&quot;` ).
        llm_answer = ai_api-&gt;execute_for_messages( message_container )-&gt;get_completion( ).

        &quot;Retrieving the messages added to the message container
        FINAL(messages) = message_container-&gt;get_messages( ).
        out-&gt;write( data = llm_answer name = `llm_answer` ).
        out-&gt;write( |\n| ).
        out-&gt;write( data = messages name = `messages` ).
      CATCH cx_aic_api_factory cx_aic_completion_api INTO error.
        error_text = error-&gt;get_text( ).
        out-&gt;write( error_text ).
    ENDTRY.

    out-&gt;write( |\n| ).
    out-&gt;write( repeat( val = `_` occ = 70 ) ).
    out-&gt;write( |\n| ).

*&amp;---------------------------------------------------------------------*
*&amp; Calling the prompt library API to use prompt templates
*&amp;---------------------------------------------------------------------*

    out-&gt;write( |6) Calling the prompt library API to use prompt templates \n\n| ).

    TRY.
        &quot;Creating another instance of the ISLM completion API
        ai_api = cl_aic_islm_compl_api_factory=&gt;get( )-&gt;create_instance( islm_scenario ).

        FINAL(prompt_temp) = cl_aic_islm_prompt_tpl_factory=&gt;get( )-&gt;create_instance( islm_scenario = islm_scenario
                                                                                      template_id   = prompt_template ).

        &quot;Retrieving the prompt from the template
        &quot;Note that, depending on the prompt template setup, there may be input parameters
        &quot;(&#x27;parameters&#x27; importing parameter) in the following method call to be assigned.
        FINAL(prompt) = prompt_temp-&gt;get_prompt( ).
        FINAL(msg_container) = ai_api-&gt;create_message_container( ).
        msg_container-&gt;set_system_role( prompt ).
        msg_container-&gt;add_user_message( `... some user message ...` ).
        llm_answer = ai_api-&gt;execute_for_messages( msg_container )-&gt;get_completion( ).

        out-&gt;write( data = llm_answer name = `llm_answer` ).
      CATCH cx_aic_api_factory cx_aic_completion_api cx_aic_prompt_template INTO error.
        error_text = error-&gt;get_text( ).
        out-&gt;write( error_text ).
    ENDTRY.
  ENDMETHOD.
ENDCLASS.</code></pre>
</body>
</html>