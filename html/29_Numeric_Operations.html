<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>29 Numeric Operations - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>Numeric Operations in ABAP</h1>
<ul>
<li><a href="#numeric-operations-in-abap">Numeric Operations in ABAP</a></li>
<li><a href="#excursion-numeric-data-types">Excursion: Numeric Data Types</a></li>
<li><a href="#numeric-values-in-character-like-fields">Numeric Values in Character-Like Fields</a></li>
<li><a href="#arithmetic-expressions-and-operators">Arithmetic Expressions and Operators</a></li>
<li><a href="#abap-calculator-example">ABAP Calculator Example</a></li>
<li><a href="#calculation-type">Calculation Type</a></li>
<li><a href="#calculation-assignments">Calculation Assignments</a></li>
<li><a href="#lossless-calculations">Lossless Calculations</a></li>
<li><a href="#numeric-functions">Numeric Functions</a></li>
<li><a href="#calculations-with-system-classes">Calculations with System Classes</a></li>
<li><a href="#calculations-with-date-time-and-time-stamp-values">Calculations with Date, Time and Time Stamp Values</a></li>
<li><a href="#arithmetic-expressions-and-built-in-functions-in-abap-sql-and-abap-cds">Arithmetic Expressions and Built-in Functions in ABAP SQL and ABAP CDS</a></li>
<li><a href="#executable-example">Executable Example</a></li>
</ul>
<p>This cheat sheet explores various aspects of numeric operations and calculations in ABAP, covering miscellaneous topics and code snippets.</p>
<div class="note">[!NOTE] - Several topics and similar or the same code snippets in this cheat sheet are also found in other cheat sheets. For example, date and time calculations appear in the [Date, Time, and Time Stamp](23_Date_and_Time.md) cheat sheet. They are included here in the context of calculations in ABAP. - Find more information in the [ABAP Keyword Documentation](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENCOMPUTE_EXPRESSIONS.html).</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursion: Numeric Data Types</h2>
<ul>
<li>Calculations use numeric data objects or those convertible to numeric types.</li>
<li>ABAP supports specific elementary numeric data types.</li>
<li>These types have distinct characteristics and value ranges, allowing the use for various purposes.</li>
<li>This cheat sheet covers only <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbuiltin_abap_type_glosry.htm">built-in ABAP types</a>. <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbuiltin_ddic_type_glosry.htm">Built-in DDIC types</a> are also available for defining types in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_dictionary_glosry.htm">ABAP Dictionary (DDIC)</a> and ABAP CDS. These types cannot be used directly in ABAP programs except for typed literals. They are mapped to built-in ABAP types; for example, the DDIC type <code>INT4</code> is mapped to type <code>i</code>, and <code>DECFLOAT16</code> is mapped to the built-in ABAP type <code>decfloat16</code>.</li>
<li>Most types&#x27; values (except type <code>i</code>) cannot be directly specified in the program and must be represented by character literals interpretable as the respective type.</li>
</ul>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Built-in ABAP type &lt;/td&gt; &lt;td&gt; Notes &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>i</code> and <code>int8</code> for integers</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to store <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninteger_number_glosry.htm">integer numbers</a> of various value ranges.</li>
<li>The type <code>i</code> can hold 4-byte integers, typically used for counters or indexes (like <code>sy-index</code> or <code>sy-tabix</code>), quantities, and more.</li>
<li><code>int8</code> holds 8-byte integers and offers a larger value range than <code>i</code>.</li>
<li>You can specify integer numbers with type <code>i</code> directly in ABAP programs as <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abennumeric_literal_glosry.htm">numeric literals</a>.</li>
<li>You might encounter the types <code>b</code> (1-byte integers) and <code>s</code> (2-byte integers) for short integers. They cannot be directly specified in ABAP programs, but you can use the DDIC types <code>INT1</code> and <code>INT2</code>.</li>
<li>If these integer types do not meet your value range needs, consider using type <code>p</code> without decimal places or floating point types for larger ranges. However, calculations will be slower then.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>p</code> for packed numbers</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to store fixed point numbers with a fixed number of decimal places after a period as the decimal separator.</li>
<li>The number of decimal places is specified as a property of the data type.</li>
<li>Packed numbers are typically used to represent lengths, weights, or currency values.</li>
<li>If the value range of packed numbers is insufficient for your needs, you can use floating point types for greater ranges.</li>
<li>The value range depends on the length and number of decimal places specified with the type declaration.</li>
<li>Length: Ranges from 1 to 16 bytes, with two places packed per byte. The <code>+</code> and <code>-</code> signs are in the last byte. The packed number consists of the length multiplied by 2 minus 1 digits.</li>
<li>Decimals: Up to 14 decimal places are possible; the decimal specification should not exceed the number of digits.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>f</code> for binary floating point numbers</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to store binary floating point numbers, which are binary representations of numbers with mantissas and exponents.</li>
<li>Can represent decimal numbers with up to 17 decimal places in the mantissa (one before the decimal point, 16 after), and allow a three-digit exponent.</li>
<li>Expect a scientific notation for the numeric value assigned, or interpretable it as such.</li>
<li>Represented internally as binary fractions, so not every decimal number can be represented exactly.</li>
<li>The type may be used in exceptional cases when precision is less important and fast calculations are crucial. Comparisons and calculations with this type and other numeric types might yield unexpected results due to imprecision and possible rounding effects. Consider decimal floating point types for greater precision.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>decfloat16</code> and <code>decfloat34</code> for decimal floating point numbers</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Can hold values with a maximum precision of 16 or 34 decimal places.</li>
<li>Typically used when you require a high precision in decimal places and a wide value range. The types <code>p</code> and <code>f</code> only address one of these aspects.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<div class="note">[!NOTE] - The built-in type `n` for numeric text fields is a character-like, not a numeric type, even though it only contains digits. It is not recommended for calculations. Use type `n` for purposes not involving calculations, like ID values or article numbers. - Generic types such as `numeric`, `p` and `decfloat` can be specified as types of field symbols and formal parameters of procedures such as methods of classes.</div>
<p>The following code snippet explores:</p>
<ul>
<li>Various options of how to create data objects with numeric types and assign values</li>
<li>Generic numeric types</li>
<li>Two system classes that deal with numeric values: <code>cl_abap_math</code> (mathematical utility library) and <code>cl_abap_random*</code> (computing random numeric values)</li>
</ul>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Integer types: i, int8
*&amp;---------------------------------------------------------------------*

DATA i_a TYPE i.
i_a = 2.
DATA i_b TYPE i VALUE 1.
&quot;Inline declaration of the elementary type i
&quot;The VALUE operator only works for elementary types to create initial values.
&quot;So, the following example basically corresponds to the declaration of i_a.
DATA(i_c) = VALUE i( ).
&quot;Integer numbers of type i can be specified directly in the program as numeric literals
&quot;i_d is automatically typed with type i.
DATA(i_d) = 3.
&quot;Immutable variable
FINAL(i_e) = 4.
&quot;Using the CONV operator to explicitly convert a value to the type specified.
&quot;Note: If &#x27;4&#x27; is not a character literal but a numeric literal like 4 (a numeric literal
&quot;of type i), a syntax warning will appear because the conversion is unnecessary.
DATA(i_f) = CONV i( &#x27;4&#x27; ).

DATA int8_a TYPE int8.
DATA int8_b TYPE int8 VALUE 1.
DATA(int8_c) = VALUE int8( ).
DATA(int8_d) = CONV int8( 2 ).

*&amp;---------------------------------------------------------------------*
*&amp; Packed numbers of type p
*&amp;---------------------------------------------------------------------*

&quot;The values cannot be specified directly. Character literals must be used whose content
&quot;can be interpreted as a packed number.

DATA p_d0 TYPE p LENGTH 16 VALUE &#x27;9639093946237986229144508806129-&#x27;.
DATA p_d1 TYPE p LENGTH 16 DECIMALS 1 VALUE &#x27;90854421485579917885733186954.8-&#x27;.
DATA p_d2 TYPE p LENGTH 16 DECIMALS 2 VALUE &#x27;8915050542191264935993694231.83-&#x27;.
DATA p_d3 TYPE p LENGTH 16 DECIMALS 3 VALUE &#x27;9506074954243163776128965613.326&#x27;.
DATA p_d4 TYPE p LENGTH 16 DECIMALS 4 VALUE &#x27;160413057636273347593653626.2245-&#x27;.
DATA p_d5 TYPE p LENGTH 16 DECIMALS 5 VALUE &#x27;40186196674188340559227708.43703-&#x27;.
DATA p_d6 TYPE p LENGTH 16 DECIMALS 6 VALUE &#x27;4561675521638812947212485.501355-&#x27;.
DATA p_d7 TYPE p LENGTH 16 DECIMALS 7 VALUE &#x27;814721091997334159140156.5732143&#x27;.
DATA p_d8 TYPE p LENGTH 16 DECIMALS 8 VALUE &#x27;22204917587256970330176.81515431-&#x27;.
DATA p_d9 TYPE p LENGTH 16 DECIMALS 9 VALUE &#x27;5185489996588937351174.098863656&#x27;.
DATA p_d10 TYPE p LENGTH 16 DECIMALS 10 VALUE &#x27;446453616374633295560.0486661184-&#x27;.
DATA p_d11 TYPE p LENGTH 16 DECIMALS 11 VALUE &#x27;54292568700375612882.85671756677&#x27;.
DATA p_d12 TYPE p LENGTH 16 DECIMALS 12 VALUE &#x27;6692515911726313748.455325155554&#x27;.
DATA p_d13 TYPE p LENGTH 16 DECIMALS 13 VALUE &#x27;278093280456662008.1493227920861&#x27;.
DATA p_d14 TYPE p LENGTH 16 DECIMALS 14 VALUE &#x27;62092769140182925.23049016628459&#x27;.

&quot;Note: The length specification means data objects of type p can be 1 to 16 bytes long.
&quot;Two places are packed in each byte. The last byte is packed with the plus/minus sign.
TYPES ty_p_l8d2 TYPE p LENGTH 8 DECIMALS 2.
DATA p_l8d2_a TYPE ty_p_l8d2.

&quot;A packed number consists of the length multiplied by 2 minus 1 digits
&quot;I.e., in the case of the example, 15 digits, of which 2 are reserved for the decimal
&quot;places, are possible.
p_l8d2_a = &#x27;1234567890123.99-&#x27;.
p_l8d2_a = &#x27;1234567890123.99+&#x27;.
p_l8d2_a = &#x27;1234567890123&#x27;.
p_l8d2_a = &#x27;1234567890123.00&#x27;.
p_l8d2_a = &#x27;1234567890123.00+&#x27;.
&quot;The following statements will show a syntax warning as the values are
&quot;non-admissible.
&quot;14 places before the period
&quot;p_l8d2_a = &#x27;12345678901234&#x27;.
&quot;More than two decimal places
&quot;p_l8d2_a = &#x27;1.234&#x27;.

DATA(p_l8d2_b) = VALUE ty_p_l8d2( ).
DATA(p_l8d2_c) = CONV ty_p_l8d2( &#x27;1.23&#x27; ).

*&amp;---------------------------------------------------------------------*
*&amp; Decimal floating point numbers of the types decfloat16 and decfloat34
*&amp;---------------------------------------------------------------------*

&quot;16 decimal places
DATA dec16 TYPE decfloat16 VALUE &#x27;0.7805874561940696&#x27;.

&quot;34 decimal places
DATA dec34 TYPE decfloat34 VALUE &#x27;0.0587780463975441530508753423121495&#x27;.

*&amp;---------------------------------------------------------------------*
*&amp; Binary floating point numbers of type f
*&amp;---------------------------------------------------------------------*

DATA float TYPE f VALUE &#x27;2.1643779466775481E-01&#x27;.

&quot;Note: Internally, type f is represented as binary fractions and a dual exponent.
&quot;As a consequence, not every decimal number that is in the type&#x27;s value range can
&quot;be exactly represented. As a consequence, this may lead to unexpected results
&quot;particulary in calculations and conversions.
float = CONV f( &#x27;0.815&#x27; ). &quot;8.1499999999999995E-01
DATA(dec34_b) = CONV decfloat34( &#x27;0.815&#x27; ). &quot;0.815

ASSERT float &lt;&gt; dec34_b.

*&amp;---------------------------------------------------------------------*
*&amp; Generic numeric types
*&amp;---------------------------------------------------------------------*

&quot;Generic types can be specified as types of field symbols and formal
&quot;parameters of procedures such as methods of classes.
&quot;The following examples use field symbols.

&quot;Generic type &#x27;numeric&#x27;
&quot;It can accept all of the types above.
FIELD-SYMBOLS &lt;number&gt; TYPE numeric.
ASSIGN i_a TO &lt;number&gt;.
ASSIGN int8_a TO &lt;number&gt;.
ASSIGN p_d0 TO &lt;number&gt;.
ASSIGN p_d14 TO &lt;number&gt;.
ASSIGN dec16 TO &lt;number&gt;.
ASSIGN dec34 TO &lt;number&gt;.
ASSIGN float TO &lt;number&gt;.

&quot;Generic type &#x27;p&#x27;
&quot;It can accept all packed number types.
FIELD-SYMBOLS &lt;p&gt; TYPE p.
ASSIGN p_d0 TO &lt;p&gt;.
ASSIGN p_d1 TO &lt;p&gt;.
ASSIGN p_d7 TO &lt;p&gt;.
ASSIGN p_d14 TO &lt;p&gt;.

&quot;Generic type &#x27;decfloat&#x27;
&quot;It can accept the types decfloat16 and decfloat34.
FIELD-SYMBOLS &lt;dec&gt; TYPE decfloat.
ASSIGN dec16 TO &lt;dec&gt;.
ASSIGN dec34 TO &lt;dec&gt;.

*&amp;---------------------------------------------------------------------*
*&amp; Excursion: System classes
*&amp;---------------------------------------------------------------------*

&quot;Various system classes are available that deal with numeric types
&quot;The following examples demonstrate:
&quot;- cl_abap_math: Mathematical utility library
&quot;- cl_abap_random*: Compute random numeric values
&quot;Check the class documentation and the methods available. The example only
&quot;covers a selection.

&quot;cl_abap_math: Among others, the class offers attributes to retrieve
&quot;the maximum and minimum possible values of different numeric types
DATA(min_int4) = cl_abap_math=&gt;min_int4.
DATA(max_int4) = cl_abap_math=&gt;max_int4.
DATA(max_int1) = cl_abap_math=&gt;max_int1.
DATA(min_int1) = cl_abap_math=&gt;min_int1.
DATA(max_int2) = cl_abap_math=&gt;max_int2.
DATA(min_int2) = cl_abap_math=&gt;min_int2.
DATA(max_int8) = cl_abap_math=&gt;max_int8.
DATA(min_int8) = cl_abap_math=&gt;min_int8.
DATA(max_decfloat16) = cl_abap_math=&gt;max_decfloat16.
DATA(min_decfloat16) = cl_abap_math=&gt;min_decfloat16.
DATA(max_decfloat34) = cl_abap_math=&gt;max_decfloat34.
DATA(min_decfloat34) = cl_abap_math=&gt;min_decfloat34.

&quot;cl_abap_random*
&quot;The following examples show random numeric value generation and value assignments.
&quot;Note that some &#x27;create&#x27; methods also offer to specify minimum and maximum values for
&quot;the random number&#x27;s value range.

DATA(random_i) = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                             min = 1
                                             max = 100 )-&gt;get_next( ).
DATA(random_int8) = cl_abap_random_int8=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_dec16) = cl_abap_random_decfloat16=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_dec34) = cl_abap_random_decfloat34=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_f) = cl_abap_random_float=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p) = cl_abap_random_packed=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d1) = cl_abap_random_packed_dec1=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d2) = cl_abap_random_packed_dec2=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d3) = cl_abap_random_packed_dec3=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d4) = cl_abap_random_packed_dec4=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d5) = cl_abap_random_packed_dec5=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d6) = cl_abap_random_packed_dec6=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d7) = cl_abap_random_packed_dec7=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d8) = cl_abap_random_packed_dec8=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d9) = cl_abap_random_packed_dec9=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d10) = cl_abap_random_packed_dec10=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d11) = cl_abap_random_packed_dec11=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d12) = cl_abap_random_packed_dec12=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d13) = cl_abap_random_packed_dec13=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).
DATA(random_p_d14) = cl_abap_random_packed_dec14=&gt;create( seed = cl_abap_random=&gt;seed( ) )-&gt;get_next( ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Numeric Values in Character-Like Fields</h3>
<ul>
<li>Most numeric types (except type <code>i</code>) cannot be directly specified in the program. These values use character literals that can be interpreted as their respective types.</li>
<li>The following code snippet demonstrates mathematical, commercial, and scientific notations. Not all notations are possible for each numeric type.</li>
<li>In this context, you can also check the <a href="07_String_Processing.md#formatting-options-in-string-templates">Formatting Options in String Templates</a> in the <em>String Processing</em> cheat sheet.</li>
</ul>
<pre><code>&quot;Mathematical notations
&quot;String of digits, a maximum of one period as decimal separator,
&quot;optional + or - sign on the left (which can be separated by the digits by blanks)
DATA(math_notation) = `- 123.45`.

&quot;Commercial notation
&quot;String of digits, a maximum of one period as decimal separator,
&quot;optional + or - sign on the right (which can be separated by the digits by blanks)
DATA(comm_notation) = `123.45-`.

&quot;Scientific notation
&quot;String of digits, mantissa can include an optional + or -, a period as decimal separator,
&quot;character e or E, and an exponent (which can optionally include a + or - sign and further digits)
DATA(sci_notation) = `-1.23456E03`.

&quot;It depends on the type, which notation is possible.
DATA dec16_n TYPE decfloat16.
DATA dec34_n TYPE decfloat34.
DATA i_n TYPE i.
DATA int8_n TYPE int8.
DATA f_n TYPE f.
DATA p_l16dec14 TYPE p LENGTH 16 DECIMALS 14.

&quot;decfloat16/decfloat34: All are possible
dec16_n = math_notation.
dec16_n = comm_notation.
dec16_n = sci_notation.
dec34_n = math_notation.
dec34_n = comm_notation.
dec34_n = sci_notation.

&quot;f: All are possible (but the signs must not be separated by blanks)
TRY.
    f_n = math_notation.
  CATCH cx_sy_conversion_no_number INTO DATA(e_f_math_notation).
    DATA(e_f_math_notation_text) = e_f_math_notation-&gt;get_text( ).
ENDTRY.

DATA(math_notation_no_blanks) = `-123.45`.
f_n = math_notation_no_blanks.

f_n = comm_notation.
f_n = sci_notation.

&quot;p, i, int8: Only mathematical and commercial notatations are allowed
i_n = math_notation.
i_n = comm_notation.

TRY.
    i_n = sci_notation.
  CATCH cx_sy_conversion_no_number INTO DATA(e_i_sci_notation).
    DATA(e_i_sci_notation_text) = e_i_sci_notation-&gt;get_text( ).
ENDTRY.

int8_n = math_notation.
int8_n = comm_notation.

TRY.
    int8_n = sci_notation.
  CATCH cx_sy_conversion_no_number INTO DATA(e_int8_sci_notation).
    DATA(e_int8_sci_notation_text) = e_int8_sci_notation-&gt;get_text( ).
ENDTRY.

p_l16dec14 = math_notation.
p_l16dec14 = comm_notation.

TRY.
    p_l16dec14 = sci_notation.
  CATCH cx_sy_conversion_no_number INTO DATA(e_p_sci_notation).
    DATA(e_p_sci_notation_text) = e_p_sci_notation-&gt;get_text( ).
ENDTRY.

&quot;Using the EXACT operator to check if there is a lossless assignment.
&quot;The example shows assignments to a data object of type decfloat34
&quot;that accepts all notation variants.
dec34_n = EXACT #( `9                      ` ).
dec34_n = EXACT #( `9999                   ` ).
dec34_n = EXACT #( `-9                     ` ).
dec34_n = EXACT #( `- 99                   ` ).
dec34_n = EXACT #( `-9999                  ` ).
dec34_n = EXACT #( `9-                     ` ).
dec34_n = EXACT #( `9999-                  ` ).
dec34_n = EXACT #( `9999999 -              ` ).
dec34_n = EXACT #( `9.99                   ` ).
dec34_n = EXACT #( `.99                    ` ).
dec34_n = EXACT #( `-.99                   ` ).
dec34_n = EXACT #( `0.99999-               ` ).
dec34_n = EXACT #( `9E3                    ` ).
dec34_n = EXACT #( `-9E-3                  ` ).
dec34_n = EXACT #( `.9E-3                  ` ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Arithmetic Expressions and Operators</h2>
<ul>
<li>Numeric calculations can be performed using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenarithmetic_expression_glosry.htm">arithmetic expressions</a> with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenarithmetic_operator_glosry.htm">arithmetic operators</a>.</li>
<li>Typically, the calculations are performed in operand positions and on the right side of assignments using the assignment operator <code>=</code>. Though more positions are possible, this cheat sheet focuses on simple assignments with <code>=</code>.</li>
<li>In arithmetic expressions in ABAP, data objects can include:</li>
<li>Data objects of numeric data types (named and unnamed data objects)</li>
<li>Data objects whose content can be converted to numeric data types</li>
<li>Numeric functions returning a numeric value</li>
<li>Functional method calls returning a numeric value</li>
<li>ABAP supports these arithmetic operators: <code>+</code>, <code>-</code>, <code><em></code>, <code>/</code>, <code>DIV</code>, <code>MOD</code>, and <code></em>*</code>. Notes:</li>
<li><code>DIV</code>: Returns the integer part of the division of the left operand by the right, with a positive remainder.</li>
<li><code>MOD</code>: Returns a positive remainder of the division of the left operand by the right.</li>
<li><code>**</code>: Raises the left operand to the power of the right (Note: Can produce type <code>f</code>; consider using the <code>ipow</code> function as an alternative if you want to avoid the type <code>f</code>.)</li>
<li>Parentheses in arithmetic expressions prioritize the calculations inside them.</li>
<li>You can combine one or more operands to perform calculations.</li>
<li>You can prefix operands with <code>+</code> or <code>-</code> signs.</li>
<li>Note that calculations can trigger exceptions. If not caught, these result in runtime errors.</li>
<li>Examples: Division by zero and overflows (if values exceed the supported range).</li>
<li>The result of arithmetic expressions is a numeric value. Note the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencalculation_type_glosry.htm">calculation type</a> as described below.</li>
</ul>
<div class="note">[!NOTE] - If the first operand is also 0 in a zero division, an exception is not raised. Instead, the result is set to 0. - For more information on the `DIV` and `MOD` operators, refer to [this blog](https://community.sap.com/t5/technology-blogs-by-sap/understanding-div-and-mod-in-abap-and-beyond/ba-p/14015181).</div>
<p>The following code snippet explores:</p>
<ul>
<li>A selection of (im)possible data objects in arithmetic expressions</li>
<li>Arithmetic expressions with all arithmetic operators</li>
<li>Using parentheses</li>
<li>Calculations raising exceptions</li>
<li>Pitfalls when performing calculations regarding numeric types and conversion/rounding</li>
</ul>
<pre><code>
*&amp;---------------------------------------------------------------------*
*&amp; Data objects in arithmetic expressions
*&amp;---------------------------------------------------------------------*

DATA result TYPE i.

&quot;Numeric literals (of type i)
result = 1 + 1.
ASSERT result = 2.

&quot;Literals/unnamed data objects (here, it is a character literal of
&quot;type c length 1, which has a numeric value and can be converted)
result = 1 + &#x27;2&#x27;.
ASSERT result = 3.

&quot;Named data objects
DATA num TYPE i VALUE 3.
&quot;Character-like data object whose content can be converted to a numeric value
DATA str TYPE string VALUE `7      `.

result = num + str.
ASSERT result = 10.

&quot;Numeric functions
result = 1 + ipow( base = 2 exp = 3 ).
ASSERT result = 9.

&quot;Numeric result of functional method call
&quot;The method call returns a random integer from 1 - 10
result = 1 + cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                         min  = 1
                                         max  = 10 )-&gt;get_next( ).
ASSERT result BETWEEN 2 AND 11.

&quot;Content of data object not convertible to numeric value
TRY.
    result = 1 + &#x27;#&#x27;.
  CATCH cx_sy_conversion_no_number INTO DATA(error).
    DATA(error_text) = error-&gt;get_text( ).
ENDTRY.

&quot;Avoid non-convertible operands
&quot;The following statement raises a shortdump
*    DATA dref TYPE REF TO data.
*    dref = NEW utclong( utclong_current( ) ).
*
*    result = 1 + dref-&gt;*.

*&amp;---------------------------------------------------------------------*
*&amp; Arithmetic expressions
*&amp;---------------------------------------------------------------------*

&quot;The following code examples demonstrate basic arithmetic expressions
&quot;where all operands and the result are of type i.

DATA res TYPE i.

&quot;Basic calculations using the operators +, -, *, /
res = 1 + 2.
res = 10 - 8.
res = 5 * -5.
res = 18 / 2.

&quot;DIV operator
&quot;Integer part of the division of the left operand by the right, with positive remainder
res = 7 DIV 3. &quot;2 (meaning 2 as integer part of 2.333...)
res = 4 DIV 5. &quot;0
res = 5 DIV 5. &quot;1

&quot;MOD operator
&quot;Positive remainder of the division of the left operand by the right
res = 7 MOD 3. &quot;1
res = 4 MOD 5. &quot;4
res = 5 MOD 5. &quot;0

&quot;** operator: Left operand raised to the power of the right
&quot;Here, special rules apply concerning the calculation type. The following
&quot;examples use inline declarations for the result. That means, the resulting data object
&quot;receives the calculation type. In this case, the type is decfloat34 if one type involved is
&quot;decfloat34. In other cases, it is type f. The example includes RTTI methods to retrieve
&quot;type information. Refer to the cl_abap_typedescr class for the constant values. E.g., &#x27;F&#x27;
&quot;stands for floating point numbers.
&quot;Note: As an alternative, you can use the ipow function to avoid the creation of the type f.
DATA(power_result1) = 2 ** 5.
ASSERT cl_abap_typedescr=&gt;describe_by_data( power_result1 )-&gt;type_kind = &#x27;F&#x27;.
DATA(power_result2) = CONV decfloat34( &#x27;2&#x27; ) ** 5.
ASSERT cl_abap_typedescr=&gt;describe_by_data( power_result2 )-&gt;type_kind = &#x27;e&#x27;.

&quot;Multiple operands specified on the right side
&quot;Note that calculation rules apply regarding prioritized calculations.
res = 9 + 8 - 7. &quot;10
res = 1 + 1 * 3. &quot;4
res = 2 + 2 - -2 * 2 / 2 + 4. &quot;10

&quot;Using parentheses
&quot;Arithmetic expressions in parentheses are calculated first
res = 3 + 3 / 3. &quot;4
res = ( 3 + 3 ) / 3. &quot;2

&quot;Calculations raising exceptions
&quot;The following examples show a selection of potential calculation errors
&quot;such as zero division and overflow (i.e. if the resulting value is beyond the
&quot;value range of a type).

&quot;Zero division
TRY.
    res = 1 / 0.
  CATCH cx_sy_zerodivide INTO DATA(e1).
    DATA(e1_text) = e1-&gt;get_text( ).
ENDTRY.

&quot;Note: Division by 0 does not raise an exception if the dividend is also 0.
&quot;Then, the result is 0.
TRY.
    res = 0 / 0.
  CATCH cx_sy_zerodivide INTO DATA(e2).
    DATA(e2_text) = e2-&gt;get_text( ).
ENDTRY.
ASSERT e2 IS INITIAL.

&quot;Overflow
&quot;The example uses the maximum value of type i, and tries to add 1 to this
&quot;value.
TRY.
    res = 2147483647 + 1.
  CATCH cx_sy_arithmetic_overflow INTO DATA(e3).
    DATA(e3_text) = e3-&gt;get_text( ).
ENDTRY.

TRY.
    res = 999999 * 999999.
  CATCH cx_sy_arithmetic_overflow INTO DATA(e4).
    DATA(e4_text) = e4-&gt;get_text( ).
ENDTRY.

&quot;Note the pitfalls when performing calculations regarding numeric types and
&quot;conversion/rounding.
&quot;The examples use integer values.
res = 1 / 3.
ASSERT res = 0.

res = 2 / 3.
ASSERT res = 1.

res = 3 * ( 1 / 3 ).
ASSERT res = 0.

res = 3 * ( 2 / 3 ).
ASSERT res = 3.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>ABAP Calculator Example</h3>
<ul>
<li>The following example class demonstrates calculator functionality with the basic arithmetic operators <code>+</code>, <code>-</code>, <code>*</code>, and <code>/</code>.</li>
<li>The class contains four methods, each requiring two numbers and an operator (represented as an enumerated type). The methods illustrate: Calculations using data objects of types <code>i</code> and <code>decfloat34</code>, as well as calculations with any numeric type (the method signature includes the generic type <code>numeric</code>). Additionally, each method features a different control structure, such as <code>IF</code> and <code>CASE</code> statements, and <code>COND</code> and <code>SWITCH</code> operators.</li>
<li>To try the example out, create a demo class named <code>zcl_some_class</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console.</li>
</ul>
<pre><code>CLASS zcl_some_class DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
    TYPES: BEGIN OF ENUM enum_operator,
             add,
             subtract,
             multiply,
             divide,
           END OF ENUM enum_operator.

    METHODS calculate_i IMPORTING num1          TYPE i
                                  operator      TYPE enum_operator
                                  num2          TYPE i
                        RETURNING VALUE(result) TYPE i
                        RAISING   cx_sy_arithmetic_error.

    METHODS calculate_decfloat34 IMPORTING num1          TYPE decfloat34
                                           operator      TYPE enum_operator
                                           num2          TYPE decfloat34
                                 RETURNING VALUE(result) TYPE decfloat34
                                 RAISING   cx_sy_arithmetic_error.

    METHODS calculate_numeric_cond IMPORTING num1          TYPE numeric
                                             operator      TYPE enum_operator
                                             num2          TYPE numeric
                                   RETURNING VALUE(result) TYPE decfloat34
                                   RAISING   cx_sy_arithmetic_error.

    METHODS calculate_numeric_switch IMPORTING num1          TYPE REF TO data
                                               operator      TYPE enum_operator
                                               num2          TYPE REF TO data
                                     RETURNING VALUE(result) TYPE decfloat34
                                     RAISING   cx_sy_arithmetic_error.

ENDCLASS.


CLASS zcl_some_class IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

*&amp;---------------------------------------------------------------------*
*&amp; Calculations with type i
*&amp; Method implementation using IF statements
*&amp;---------------------------------------------------------------------*

    TYPES: BEGIN OF s_int,
             num1     TYPE i,
             operator TYPE enum_operator,
             num2     TYPE i,
           END OF s_int,
           t_int TYPE TABLE OF s_int WITH EMPTY KEY.

    DATA(i_calculation_tab) = VALUE t_int(
      ( num1 = 1 operator = add num2 = 5 )
      ( num1 = -10 operator = add num2 = 7 )
      ( num1 = 8 operator = subtract num2 = 20 )
      ( num1 = 1000 operator = subtract num2 = 999 )
      ( num1 = 5 operator = multiply num2 = 5 )
      ( num1 = 10 operator = multiply num2 = 10 )
      ( num1 = 10 operator = divide num2 = 5 )
      ( num1 = 3 operator = divide num2 = 2 )
      &quot;Failing calculations
      ( num1 = 1 operator = divide num2 = 0 )
      ( num1 = 0 operator = divide num2 = 0 )
      ( num1 = 2147483647 operator = add num2 = 1 )
      ( num1 = 99999999 operator = multiply num2 = 99999999 ) ).

    DATA calc_results TYPE string_table.

    LOOP AT i_calculation_tab INTO DATA(cal_i).
      TRY.
          DATA(res_i) = calculate_i( num1 = cal_i-num1 operator = cal_i-operator num2 = cal_i-num2 ).
          APPEND |{ cal_i-num1 } { SWITCH #( cal_i-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_i-num2 } = { res_i STYLE = SIMPLE }| TO calc_results.
        CATCH cx_sy_arithmetic_error INTO DATA(error_cal_i).
          APPEND |{ cal_i-num1 } { SWITCH #( cal_i-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_i-num2 } = ERROR! { error_cal_i-&gt;get_text( ) }| TO calc_results.
      ENDTRY.
    ENDLOOP.

    out-&gt;write( |----------- Calculations with type i -----------| ).
    out-&gt;write( calc_results ).
    CLEAR calc_results.

*&amp;---------------------------------------------------------------------*
*&amp; Calculations with type decfloat34
*&amp; Method implementation using CASE statements
*&amp;---------------------------------------------------------------------*

    TYPES: BEGIN OF s_decfloat34,
             num1     TYPE decfloat34,
             operator TYPE enum_operator,
             num2     TYPE decfloat34,
           END OF s_decfloat34,
           t_decfloat34 TYPE TABLE OF s_decfloat34 WITH EMPTY KEY.

    DATA(decfloat34_calculation_tab) = VALUE t_decfloat34(
      ( num1 = CONV decfloat34( &#x27;1.4&#x27; ) operator = add num2 = CONV decfloat34( &#x27;0.7&#x27; ) )
      ( num1 = CONV decfloat34( &#x27;0.576&#x27; ) operator = subtract num2 = CONV decfloat34( &#x27;0.924&#x27; ) )
      ( num1 = CONV decfloat34( &#x27;9283658473562&#x27; ) operator = multiply num2 = CONV decfloat34( &#x27;23423&#x27; ) )
      ( num1 = CONV decfloat34( &#x27;984.2435&#x27; ) operator = divide num2 = CONV decfloat34( &#x27;468.2346&#x27; ) )
      &quot;Failing calculations
      ( num1 = CONV decfloat34( &#x27;1.0&#x27; ) operator = divide num2 = CONV decfloat34( &#x27;0.0&#x27; ) )
      ( num1 = CONV decfloat34( &#x27;0&#x27; ) operator = divide num2 = CONV decfloat34( &#x27;0&#x27; ) )
      ( num1 = cl_abap_math=&gt;max_decfloat34 operator = multiply num2 = CONV decfloat34( &#x27;2&#x27; ) ) ).

    LOOP AT decfloat34_calculation_tab INTO DATA(cal_decfloat34).
      TRY.
          DATA(res_decfloat34) = calculate_decfloat34( num1 = cal_decfloat34-num1 operator = cal_decfloat34-operator num2 = cal_decfloat34-num2 ).
          APPEND |{ cal_decfloat34-num1 } { SWITCH #( cal_decfloat34-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_decfloat34-num2 } = { res_decfloat34 STYLE = SIMPLE }| TO calc_results.
        CATCH cx_sy_arithmetic_error INTO DATA(error_cal_decfloat34).
          APPEND |{ cal_decfloat34-num1 } { SWITCH #( cal_decfloat34-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_decfloat34-num2 } = ERROR! { error_cal_decfloat34-&gt;get_text( ) }| TO calc_results.
      ENDTRY.
    ENDLOOP.

    out-&gt;write( |\n| ).
    out-&gt;write( |----------- Calculations with type decfloat34 -----------| ).
    out-&gt;write( calc_results ).
    CLEAR calc_results.

*&amp;---------------------------------------------------------------------*
*&amp; Calculations with random numeric types
*&amp; Method implementation using the COND operator
*&amp;---------------------------------------------------------------------*

    TYPES: BEGIN OF s_numbers,
             num1     TYPE REF TO data,
             operator TYPE enum_operator,
             num2     TYPE REF TO data,
           END OF s_numbers,
           t_numbers TYPE TABLE OF s_numbers WITH EMPTY KEY.

    TYPES p_d3 TYPE p LENGTH 16 DECIMALS 3.

    DATA(numeric_calculation_tab) = VALUE t_numbers(
       ( num1 = NEW decfloat34( &#x27;1.4&#x27; ) operator = add num2 = NEW i( 7 ) )
       ( num1 = NEW decfloat16( &#x27;0.123454&#x27; ) operator = add num2 = NEW decfloat34( &#x27;0.7483265&#x27; ) )
       ( num1 = NEW p_d3( &#x27;123.456&#x27; ) operator = subtract num2 = NEW i( 5 ) )
       ( num1 = NEW f( &#x27;2.1643779466775481E-01&#x27; ) operator = subtract num2 = NEW decfloat16( &#x27;1.23&#x27; ) )
       ( num1 = NEW i( 100 ) operator = multiply num2 = NEW i( 50 ) )
       ( num1 = NEW f( &#x27;1.2&#x27; ) operator = multiply num2 = NEW i( 5 ) )
       ( num1 = NEW decfloat34( &#x27;5.4&#x27; ) operator = divide num2 = NEW p_d3( &#x27;1.123&#x27; ) )
       ( num1 = NEW i( 999 ) operator = divide num2 = NEW decfloat16( &#x27;111&#x27; ) )
       &quot;Failing calculations
       ( num1 = NEW i( 1 ) operator = divide num2 = NEW decfloat34( &#x27;0&#x27; ) )
       ( num1 = NEW p_d3( &#x27;0&#x27; ) operator = divide num2 = NEW p_d3( &#x27;0&#x27; ) )
       ( num1 = NEW decfloat34( cl_abap_math=&gt;max_decfloat34 ) operator = multiply num2 = NEW decfloat34( &#x27;2&#x27; ) ) ).

    LOOP AT numeric_calculation_tab INTO DATA(cal_numeric).
      TRY.
          DATA(res_numeric) = calculate_numeric_cond( num1 = cal_numeric-num1-&gt;* operator = cal_numeric-operator num2 = cal_numeric-num2-&gt;* ).
          APPEND |{ cal_numeric-num1-&gt;* } { SWITCH #( cal_numeric-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_numeric-num2-&gt;* } = { res_numeric STYLE = SIMPLE }| TO calc_results.
        CATCH cx_sy_arithmetic_error INTO DATA(error_cal_numeric).
          APPEND |{ cal_numeric-num1-&gt;* } { SWITCH #( cal_numeric-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_numeric-num2-&gt;* } = ERROR! { error_cal_numeric-&gt;get_text( ) }| TO calc_results.
      ENDTRY.
    ENDLOOP.

    out-&gt;write( |\n| ).
    out-&gt;write( |----------- Calculations with random numeric types (COND operator) -----------| ).
    out-&gt;write( calc_results ).
    CLEAR calc_results.

*&amp;---------------------------------------------------------------------*
*&amp; Calculations with random numeric types
*&amp; Method implementation using the SWITCH operator
*&amp;---------------------------------------------------------------------*

    numeric_calculation_tab = VALUE #(
       ( num1 = NEW decfloat34( &#x27;0.4&#x27; ) operator = add num2 = NEW i( 20 ) )
       ( num1 = NEW decfloat16( &#x27;0.827123454&#x27; ) operator = subtract num2 = NEW decfloat34( &#x27;1.89632&#x27; ) )
       ( num1 = NEW p_d3( &#x27;1.5&#x27; ) operator = multiply num2 = NEW f( &#x27;3.123&#x27; ) )
       ( num1 = NEW i( 4 ) operator = divide num2 = NEW p_d3( &#x27;1.2&#x27; ) )
       &quot;Failing calculations
       ( num1 = NEW i( 1 ) operator = divide num2 = NEW decfloat34( &#x27;0&#x27; ) )
       ( num1 = NEW p_d3( &#x27;0&#x27; ) operator = divide num2 = NEW p_d3( &#x27;0&#x27; ) )
       ( num1 = NEW decfloat34( cl_abap_math=&gt;max_decfloat34 ) operator = multiply num2 = NEW decfloat34( &#x27;2&#x27; ) ) ).

    LOOP AT numeric_calculation_tab INTO cal_numeric.
      TRY.
          res_numeric = calculate_numeric_cond( num1 = cal_numeric-num1-&gt;* operator = cal_numeric-operator num2 = cal_numeric-num2-&gt;* ).
          APPEND |{ cal_numeric-num1-&gt;* } { SWITCH #( cal_numeric-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_numeric-num2-&gt;* } = { res_numeric STYLE = SIMPLE }| TO calc_results.
        CATCH cx_sy_arithmetic_error INTO error_cal_numeric.
          APPEND |{ cal_numeric-num1-&gt;* } { SWITCH #( cal_numeric-operator WHEN add THEN &#x27;+&#x27; WHEN subtract THEN &#x27;-&#x27; WHEN multiply THEN &#x27;*&#x27; WHEN divide THEN &#x27;/&#x27; ) } { cal_numeric-num2-&gt;* } = ERROR! { error_cal_numeric-&gt;get_text( ) }| TO calc_results.
      ENDTRY.
    ENDLOOP.

    out-&gt;write( |\n| ).
    out-&gt;write( |----------- Calculations with random numeric types (SWITCH operator) -----------| ).
    out-&gt;write( calc_results ).

  ENDMETHOD.

  METHOD calculate_i.
    IF operator = add.
      result = num1 + num2.
    ELSEIF operator = subtract.
      result = num1 - num2.
    ELSEIF operator = multiply.
      result = num1 * num2.
    ELSEIF operator = divide.
      IF num1 = 0 AND num2 = 0.
        RAISE EXCEPTION TYPE cx_sy_zerodivide.
      ELSE.
        result = num1 / num2.
      ENDIF.
    ENDIF.
  ENDMETHOD.

  METHOD calculate_decfloat34.
    CASE operator.
      WHEN add.
        result = num1 + num2.
      WHEN subtract.
        result = num1 - num2.
      WHEN multiply.
        result = num1 * num2.
      WHEN divide.
        CASE num1.
          WHEN 0.
            CASE num2.
              WHEN 0.
                RAISE EXCEPTION TYPE cx_sy_zerodivide.
            ENDCASE.
        ENDCASE.
        result = num1 / num2.
    ENDCASE.
  ENDMETHOD.

  METHOD calculate_numeric_cond.
    result = COND #( WHEN operator = add
                     THEN num1 + num2
                     WHEN operator = subtract
                     THEN num1 - num2
                     WHEN operator = multiply
                     THEN num1 * num2
                     WHEN operator = divide AND num2 = 0 THEN THROW cx_sy_zerodivide(  )
                     WHEN operator = divide AND num2 &lt;&gt; 0 THEN num1 / num2 ).
  ENDMETHOD.

  METHOD calculate_numeric_switch.
    result = SWITCH #( operator
                       WHEN add THEN num1-&gt;* + num2-&gt;*
                       WHEN subtract THEN num1-&gt;* - num2-&gt;*
                       WHEN multiply THEN num1-&gt;* * num2-&gt;*
                       WHEN divide THEN SWITCH #( num1-&gt;* WHEN 0 THEN SWITCH #( num2-&gt;* WHEN 0 THEN THROW cx_sy_zerodivide(  ) ) ELSE num1-&gt;* / num2-&gt;* ) ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Calculation Type</h3>
<ul>
<li>Arithmetic expressions have a calculation type that defines the calculation rules (particularly with regard to rounding of the types used).</li>
<li>This type is determined at runtime from the identifiable data types involved (i.e., those known statically).</li>
<li>The calculation type can influence the result before it is assigned to a data object, such as rounding.</li>
<li>An inline-declared target data object receives the calculation type as its data type. Note: In these cases, type <code>p</code> (unless overridden by another rule, as shown in the example below) results in type <code>p</code> with length 8 and no decimal places.</li>
<li>Generally, the largest data type involved determines the calculation type, with exceptions like type <code>f</code> in expressions with <code>**</code>.</li>
<li>Note the conversion rules for elementary, non-numeric data types:</li>
<li><code>d</code>, <code>t</code>, <code>x</code>, <code>xstring</code> are converted to <code>i</code></li>
<li><code>c</code>, <code>n</code>, <code>string</code> are converted to <code>p</code></li>
<li><code>utclong</code> is not supported</li>
<li>It is advisable to ensure all operands and the target data object share the same numeric type to avoid unnecessary conversions (this is intentionally not taken into consideration in many of the examples).</li>
<li>The <code>CONV</code> operator can be used to adjust the calculation type on the right side of an assignment.</li>
<li>Find more information <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENARITH_TYPE.html">here</a>.</li>
</ul>
<p>Examples</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Calculation type
*&amp;---------------------------------------------------------------------*

&quot;The following code snippets deal with the calculation type, which can be regarded as
&quot;property of arithmetic expressions and depends on the numeric data types
&quot;involved in the calculation.
&quot;The calculation type is determined at runtime.
&quot;Specific rules are determined by the calculation type.
&quot;If the numeric types of the operands of the arithmetic expression can be
&quot;determined statically, the calculation type is determined on the basis of
&quot;specific rules. The examples use inline declarations for the result.
&quot;In such cases, the target data object&#x27;s type is derived from the calculation type.
&quot;The rules apply in the following decreasing priority order.
&quot;Many examples use RTTI methods to retrieve type kind information to check the 
&quot;calculation type. For the type kind values, check the constants in class cl_abap_typedescr. 

&quot;---- 1 ----
&quot;The calculation type is a decfloat type if one of the types involved is decfloat34
&quot;or decfloat16. If decfloat34 is involved, it is decfloat34.

&quot;i + decfloat34
DATA(res_dec_a) = 1 + CONV decfloat34( &#x27;1.1&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_a )-&gt;type_kind = &#x27;e&#x27;.

&quot;decfloat16 + decfloat34
DATA(res_dec_b) = CONV decfloat16( &#x27;2.2&#x27; ) + CONV decfloat34( &#x27;1.1&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_b )-&gt;type_kind = &#x27;e&#x27;.

&quot;decfloat16 + i
DATA(res_dec_c) = CONV decfloat16( &#x27;2.2&#x27; ) + 5.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_c )-&gt;type_kind = &#x27;a&#x27;.

&quot;decfloat16 + int8
DATA(res_dec_d) = CONV decfloat16( &#x27;2.2&#x27; ) + CONV int8( 2 ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_d )-&gt;type_kind = &#x27;a&#x27;.

&quot;decfloat34 + p
DATA pd3 TYPE p LENGTH 16 DECIMALS 3 VALUE &#x27;1.326&#x27;.
DATA(res_dec_e) = CONV decfloat34( &#x27;2.2&#x27; ) + pd3.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_e )-&gt;type_kind = &#x27;e&#x27;.

&quot;decfloat16 + p
DATA(res_dec_f) = CONV decfloat16( &#x27;2.2&#x27; ) + pd3.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_f )-&gt;type_kind = &#x27;a&#x27;.

&quot;decfloat34 + f
DATA(res_dec_g) = CONV decfloat34( &#x27;2.2&#x27; ) + CONV f( &#x27;2.23645E-01&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_g )-&gt;type_kind = &#x27;e&#x27;.

&quot;decfloat16 + f
DATA(res_dec_h) = CONV decfloat16( &#x27;2.2&#x27; ) + CONV f( &#x27;2.23645E-01&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_h )-&gt;type_kind = &#x27;a&#x27;.

&quot;---- 2 ----
&quot;The calculation type is f if one of the types involved is f.

&quot;f + i
DATA(res_f_a) = 1 + CONV f( &#x27;2.23645E-01&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_f_a )-&gt;type_kind = &#x27;F&#x27;.
&quot;f + int8
DATA(res_f_b) = CONV f( &#x27;2.23645E-01&#x27; ) + CONV int8( 2 ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_f_b )-&gt;type_kind = &#x27;F&#x27;.
&quot;f + p
DATA(res_f_c) = 1 + CONV f( &#x27;2.23645E-01&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_f_c )-&gt;type_kind = &#x27;F&#x27;.

&quot;f + decfloat34
&quot;Note: Due to the higher priority of decfloat34, the calculation type and the
&quot;result are of type decfloat34.
DATA(res_f_d) = CONV decfloat34( &#x27;1.1&#x27; ) + CONV f( &#x27;2.23645E-01&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_dec_e )-&gt;type_kind = &#x27;e&#x27;.

&quot;f + decfloat16
DATA(res_f_e) = CONV decfloat16( &#x27;1.1&#x27; ) + CONV f( &#x27;2.23645E-01&#x27; ).
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_f_e )-&gt;type_kind = &#x27;a&#x27;.

&quot;---- 3 ----
&quot;The calculation type is p if one of the data types involved is p.
DATA p_l16d3 TYPE p LENGTH 16 DECIMALS 3 VALUE &#x27;1.234&#x27;.

&quot;Note: For the result of a computation with type p, the type p length 8 with 0 decimals is used
&quot;implicitly. It is not possible to determine the required length and decimal places.
&quot;Therefore, inline declarations should be avoided for such calculations.
&quot;Specifying ##TYPE suppresses a syntax warning.

&quot;p + i
DATA(res_p_a) = p_l16d3 + 1 ##TYPE.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_p_a )-&gt;type_kind = &#x27;P&#x27;.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_p_a )-&gt;length = 8.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_p_a )-&gt;decimals = 0.

&quot;p + int8
DATA(res_p_b) = p_l16d3 + CONV int8( 1 ) ##TYPE.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_p_b )-&gt;type_kind = &#x27;P&#x27;.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_p_b )-&gt;length = 8.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_p_b )-&gt;decimals = 0.

&quot;---- 4 ----
&quot;The calculation type is int8 if one of the data types involved is int8.

DATA(res_int8_a) = CONV int8( 2 ) + 2.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_int8_a )-&gt;type_kind = &#x27;8&#x27;.

&quot;---- 5 ----
&quot;The calculation type is i if one of the data types involved is i.

DATA(res_i_a) = 2 + 5.
ASSERT cl_abap_typedescr=&gt;describe_by_data( res_i_a )-&gt;type_kind = &#x27;I&#x27;.

&quot;The standard type is used in case of generally typed operands
FIELD-SYMBOLS &lt;num&gt; TYPE numeric.
ASSIGN pd3 TO &lt;num&gt;.
DATA(res_gen_a) = &lt;num&gt; + 1.
DATA(t_kind_a) = cl_abap_typedescr=&gt;describe_by_data( res_gen_a )-&gt;type_kind.

ASSIGN res_i_a TO &lt;num&gt;.
DATA(res_gen_b) = &lt;num&gt; + 1.
DATA(t_kind_b) = cl_abap_typedescr=&gt;describe_by_data( res_gen_b )-&gt;type_kind.

ASSIGN res_f_a TO &lt;num&gt;.
DATA(res_gen_c) = &lt;num&gt; + 1.
DATA(t_kind_c) = cl_abap_typedescr=&gt;describe_by_data( res_gen_c )-&gt;type_kind.

ASSERT t_kind_a = t_kind_b.
ASSERT t_kind_b = t_kind_c.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Calculation Assignments</h3>
<pre><code>  a = a + ( b ). 
  a = a - ( b ). 
  a = a * ( b ). 
  a = a / ( b ).

  &quot;Using calculation assignments
  a += b.
  a -= b.
  a *= b.
  a /= b.</code></pre>
<ul>
<li>The following calculation assignments are supported in ABAP: <code>+=</code>, <code>-=</code>, <code>*=</code>, <code>/=</code></li>
<li>They make code more concise and reflect the following patterns:</li>
</ul>
<p>Example:</p>
<pre><code>DATA int_calc TYPE i.

int_calc = 1.
int_calc = int_calc + 1. &quot;2
int_calc += 1. &quot;3

int_calc = 10.
int_calc = int_calc - 1. &quot;9
int_calc -= 1. &quot;8

int_calc = 5.
int_calc = int_calc * 2. &quot;10
int_calc *= 3. &quot;30

int_calc = 100.
int_calc = int_calc / 2. &quot;50
int_calc /= 2. &quot;25</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Lossless Calculations</h3>
<ul>
<li>You can use the <code>EXACT</code> operator to verify lossless assignments and calculations.</li>
<li>It is suitable for assignments between incompatible data objects, meaning those without matching type specifications.</li>
<li>After an assignment with <code>EXACT</code>, the target data object will hold a valid value; otherwise, it raises an exception.</li>
<li>Forced rounding during an assignment indicates it is not lossless. In the context of arithmetic expressions, a lossless calculation is performed.</li>
</ul>
<pre><code>&quot;Lossless assignments
TYPES ty_pl8d3 TYPE p LENGTH 8 DECIMALS 3.

&quot;The following examples show character literals whose content
&quot;is assigned to the target data objects. Using the EXACT operator,
&quot;it is checked if there is a lossless assignment. The example
&quot;values are ok, and an exact move is possible.
DATA(p_a) = EXACT ty_pl8d3( &#x27;123.987&#x27; ).
DATA(p_b) = EXACT ty_pl8d3( &#x27;123.98&#x27; ).

&quot;The following example raises an exception. The example character literal
&quot;has four decimal places. Thus, a rounding is necessary, which means that
&quot;a lossless assignment is not possible. Catching the CX_SY_CONVERSION_ROUNDING
&quot;exception is necessary.
&quot;DATA(p_c) = EXACT ty_p_l8d3( &#x27;123.9876&#x27; ).

TRY.
    DATA(p_d) = EXACT ty_pl8d3( &#x27;123.9876&#x27; ).
  CATCH cx_sy_conversion_rounding INTO DATA(error_p_rounding).
    DATA(error_p_rounding_text) = error_p_rounding-&gt;get_text( ).
ENDTRY.

&quot;Lossless calculations using arithmetic expressions
&quot;The first statement works, whereas the second statement raises an exception.
&quot;A rounding to two decimal places is required.
TYPES ty_pl8d2 TYPE p LENGTH 8 DECIMALS 2.
DATA(p_e) = EXACT ty_pl8d2( 1 / 4 ).
&quot;DATA(p_f) = EXACT ty_pl8d2( 1 / 3 ).

&quot;Catching exceptions when rounding in lossless calculations
TRY.
    DATA(p_g) = EXACT ty_pl8d2( 1 / 3 ).
  CATCH cx_sy_conversion_rounding INTO error_p_rounding.
    error_p_rounding_text = error_p_rounding-&gt;get_text( ).
ENDTRY.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Numeric Functions</h2>
<p>Various built-in functions are available. The following code snippet shows a selection of numeric built-in functions.</p>
<pre><code>&quot;----------- abs: Returning the absolute value -----------
&quot;4.756
DATA(abs1) = abs( CONV decfloat34( &#x27;-4.756&#x27; ) ).

&quot;4
DATA(abs2) = abs( -4 ).

&quot;----------- sign: Evaluating the sign -----------
&quot;-1 if negative, 0 if 0, 1 if positive
&quot;-1
DATA(sign1) = sign( -789 ).

&quot;0
DATA(sign2) = sign( 5 - 5 ).

&quot;1
DATA(sign3) = sign( -5 * -5 ).

&quot;----- ceil: smallest integer not less than the value specified -----
&quot;5
DATA(ceil1) = ceil( CONV decfloat34( &#x27;4.999&#x27; ) ).

&quot;5
DATA(ceil2) = ceil( CONV decfloat34( &#x27;4.001&#x27; ) ).

&quot;-4
DATA(ceil3) = ceil( CONV decfloat34( &#x27;-4.999&#x27; ) ).

&quot;-4
DATA(ceil4) = ceil( CONV decfloat34( &#x27;-4.001&#x27; ) ).

&quot;----- floor: largest integer not less than the value specified -----
&quot;4
DATA(floor1) = floor( CONV decfloat34( &#x27;4.999&#x27; ) ).

&quot;4
DATA(floor2) = floor( CONV decfloat34( &#x27;4.001&#x27; ) ).

&quot;-5
DATA(floor3) = floor( CONV decfloat34( &#x27;-4.999&#x27; ) ).

&quot;-5
DATA(floor4) = floor( CONV decfloat34( &#x27;-4.001&#x27; ) ).

&quot;------------- trunc: integer part -------------
&quot;4
DATA(trunc1) = trunc( CONV decfloat34( &#x27;4.999&#x27; ) ).

&quot;4
DATA(trunc2) = trunc( CONV decfloat34( &#x27;4.001&#x27; ) ).

&quot;-4
DATA(trunc3) = trunc( CONV decfloat34( &#x27;-4.999&#x27; ) ).

&quot;-4
DATA(trunc4) = trunc( CONV decfloat34( &#x27;-4.001&#x27; ) ).

&quot;------------- frac: decimal places -------------
&quot;0.999
DATA(frac1) = frac( CONV decfloat34( &#x27;4.999&#x27; ) ).

&quot;0.001
DATA(frac2) = frac( CONV decfloat34( &#x27;4.001&#x27; ) ).

&quot;-0.999
DATA(frac3) = frac( CONV decfloat34( &#x27;-4.999&#x27; ) ).

&quot;-0.001
DATA(frac4) = frac( CONV decfloat34( &#x27;-4.001&#x27; ) ).

&quot;------------- ipow: Calculalting the power -------------
&quot;8
DATA(ipow1) = ipow( base = 2 exp = 3 ).

&quot;1
DATA(ipow2) = ipow( base = 10 exp = 0 ).

&quot;Exception is raised
TRY.
    DATA(ipow3) = ipow( base = 10 exp = 100 ).
  CATCH cx_sy_arithmetic_overflow.
ENDTRY.

&quot;Numeric extremum functions that return the value of the largest
&quot;or smallest of the passed arguments.

&quot;A minimum of two, and a maximum of 9 arguments can be specified.
&quot;Numeric data objects and numeric expressions are possible
&quot;0.999
DATA(nmin) =  nmin( val1 = CONV decfloat34( &#x27;1.34&#x27; )
                     val2 = CONV decfloat34( &#x27;56.7&#x27; )
                     val3 = CONV decfloat34( &#x27;890.123&#x27; )
                     val4 = CONV decfloat34( &#x27;0.999&#x27; ) ).

&quot;890.123
DATA(nmax) =  nmax( val1 = CONV decfloat34( &#x27;1.34&#x27; )
                     val2 = CONV decfloat34( &#x27;56.7&#x27; )
                     val3 = CONV decfloat34( &#x27;890.123&#x27; )
                     val4 = CONV decfloat34( &#x27;0.999&#x27; ) ).

&quot;--- acos, asin, atan, cos, sin, tan, cosh, sinh, tanh, exp, log, log10, sqrt ---
&quot;The following examples cover a selection.

&quot;Calculating the square root
&quot;3
DATA(sqrt1) = sqrt( CONV decfloat34( &#x27;9&#x27; ) ).

&quot;6.4
DATA(sqrt2) = sqrt( CONV decfloat34( &#x27;40.96&#x27; ) ).

&quot;Calculating the logarithm to base 10
&quot;3
DATA(log10) = log10( CONV decfloat34( &#x27;1000&#x27; ) ).

&quot;-0.988031...
DATA(sine) = sin( &#x27;30&#x27; ).

&quot;0.525321...
DATA(cosine) = cos( &#x27;45&#x27; ).

&quot;-1.9952...
DATA(tangent) = tan( &#x27;90&#x27; ).

&quot;------------- round -------------
&quot;Rounding functions expect a decimal floating point number as argument.
&quot;The return value is of type decfloat34. The functions can be used to round
&quot;to decimal places and precisions. In addition, rounding rules can be specified.

&quot;Rounding to decimal places
&quot;1.24
DATA(round1) = round( val = CONV decfloat34( &#x27;1.2374&#x27; ) dec = 2 ).

&quot;1.237
DATA(round2) = round( val = CONV decfloat34( &#x27;1.2374&#x27; ) dec = 3 ).

&quot;Rounding to precision
&quot;1.234567890E+12
DATA(round3) = round( val = CONV decfloat34( &#x27;1234567890123&#x27; ) prec = 10 ).

&quot;1.23E+3
DATA(round4) = round( val = CONV decfloat34( &#x27;1234&#x27; ) prec = 3 ).

&quot;------------- rescale -------------
&quot;Similar to the round function, the dec (for scaling) or prec (for precision)
&quot;parameters must be specified. The input is rounded if required.
&quot;1235
DATA(rescale1) = rescale( val = CONV decfloat34( &#x27;1234.56789&#x27; ) dec = 0 ).

&quot;1234.6
DATA(rescale2) = rescale( val = CONV decfloat34( &#x27;1234.56789&#x27; ) dec = 1 ).

&quot;1.23E+3
DATA(rescale3) = rescale( val = CONV decfloat34( &#x27;1234.56789&#x27; ) prec = 3 ).

&quot;1234.567890
DATA(rescale4) = rescale( val = CONV decfloat34( &#x27;1234.56789&#x27; ) prec = 10 ).

&quot;------------- factorial -------------
TYPES: BEGIN OF demo_fact_struc,
         n             TYPE i,
         res        TYPE string,
         res_simple TYPE string,
       END OF demo_fact_struc.
DATA factorial_tab TYPE TABLE OF demo_fact_struc WITH EMPTY KEY.
DATA count TYPE i.
DO 26 TIMES.
  DATA(result) = factorial( count ).
  APPEND VALUE #( n           = count
                  res        = result
                  res_simple = |{ result STYLE = SIMPLE }| ) TO factorial_tab.
  count += 1.
ENDDO.

&quot;------------- binomial -------------
TYPES: BEGIN OF demo_bin_struc,
         n             TYPE i,
         k             TYPE i,
         res        TYPE string,
         res_simple TYPE string,
       END OF demo_bin_struc.
DATA bin_tab TYPE TABLE OF demo_bin_struc WITH EMPTY KEY.
DATA n_val TYPE i VALUE 21.
DATA k_val TYPE i VALUE 11.
DO 21 TIMES.
  n_val -= 1.
  k_val -= 1.
  DATA(bin_coeff) = binomial( n = n_val k = k_val ).
  APPEND VALUE #( n          = n_val
                  k          = k_val
                  res        = bin_coeff
                  res_simple = |{ bin_coeff STYLE = SIMPLE }| ) TO bin_tab.
ENDDO.
</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Calculations with System Classes</h2>
<ul>
<li>This section covers the <code>cl_abap_bigint</code> and <code>cl_abap_rational</code> classes.</li>
<li>Example use cases for these classes include:</li>
<li>Handling large numbers with many digits and high precision</li>
<li>Calculating very small or large numbers</li>
<li>Avoiding the risk of overflows</li>
<li>Performing basic calculations and beyond</li>
<li>With <code>cl_abap_rational</code>, you can manage rational numbers to preserve fractions and ensure high precision.</li>
</ul>
<p>The following code snippet explores a selection of various methods the classes offer. Refer to the class documentation for more information. More methods are available.</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; cl_abap_bigint
*&amp;---------------------------------------------------------------------*

&quot;Factories
DATA(bigint_int4) = cl_abap_bigint=&gt;factory_from_int4( 10 ).
DATA(bigint_int4c) = cl_abap_bigint=&gt;factory_from_string( `283469208407283452340` ).
DATA(bigint_int4d) = cl_abap_bigint=&gt;factory_from_int8( CONV int8( 1234567890123456 ) ).

DATA(a1) = cl_abap_bigint=&gt;factory_from_int4( -10 )-&gt;to_external( ).
DATA(a2) = cl_abap_bigint=&gt;factory_from_int4( -10 )-&gt;to_external( iv_flg_minus_in_front = abap_true ).
DATA(a3) = cl_abap_bigint=&gt;factory_from_int4( 100 )-&gt;to_utf8( ).
DATA(a4) = cl_abap_bigint=&gt;factory_from_string( `123` )-&gt;to_df34( ).
DATA(a5) = cl_abap_bigint=&gt;factory_from_int4( -10 )-&gt;to_string( ).
DATA(a6) = cl_abap_bigint=&gt;factory_from_int4( 4 )-&gt;add( bigint_int4 )-&gt;to_string( ).
DATA(a7) = cl_abap_bigint=&gt;factory_from_int4( 7 )-&gt;add_int4( 2 )-&gt;to_string( ).
DATA(a8) = cl_abap_bigint=&gt;factory_from_int4( -10 )-&gt;abs( )-&gt;to_string( ).
DATA(a9) = cl_abap_bigint=&gt;factory_from_int4( 19 )-&gt;compare_int4( 20 ).
DATA(a10) = cl_abap_bigint=&gt;factory_from_int4( 100 )-&gt;compare( bigint_int4 ).
DATA(a11) = cl_abap_bigint=&gt;factory_from_int4( 20 )-&gt;div( bigint_int4 ).
DATA(a12) = a11-quotient-&gt;to_string( ).
DATA(a13) = a11-remainder-&gt;to_string( ).
DATA(a14) = cl_abap_bigint=&gt;factory_from_int4( 10 )-&gt;div_int4( 3 ).
DATA(a15) = a14-quotient-&gt;to_string( ).
DATA(a16) = a14-remainder.
DATA(a17) = cl_abap_bigint=&gt;factory_from_int4( 10 )-&gt;div_by_two_power( CONV int8( 2 ) )-&gt;to_string( ).
DATA(a18) = cl_abap_bigint=&gt;factory_from_int4( 5 )-&gt;div_to_df34( bigint_int4 ).
DATA(a19) = cl_abap_bigint=&gt;factory_from_int4( 50 )-&gt;gcd( bigint_int4 )-&gt;to_string( ).
DATA(a20) = cl_abap_bigint=&gt;factory_from_int4( 1000 )-&gt;get_number_of_bits( ).
DATA(a21) = cl_abap_bigint=&gt;factory_from_int4( 10 )-&gt;is_equal( bigint_int4 ).

cl_abap_bigint=&gt;factory_from_string( `123` )-&gt;is_int4(
  IMPORTING
    ev_int4_value  = DATA(a22)
  RECEIVING
    rv_flg_is_int4 = DATA(a23)
).
DATA(a24) = cl_abap_bigint=&gt;factory_from_int4( 11 )-&gt;is_larger( bigint_int4 ).
DATA(a25) = cl_abap_bigint=&gt;factory_from_int4( 10 )-&gt;is_larger_or_equal( bigint_int4 ).
DATA(a26) = cl_abap_bigint=&gt;factory_from_int4( -10 )-&gt;is_negative( ).
DATA(a27) = cl_abap_bigint=&gt;factory_from_int4( 0 )-&gt;is_zero( ).
DATA(a28) = cl_abap_bigint=&gt;factory_from_int4( 123 )-&gt;mod( bigint_int4 )-&gt;to_string( ).
DATA(a29) = cl_abap_bigint=&gt;factory_from_int4( 10 )-&gt;mod_int4( 3 ).
DATA(a30) = cl_abap_bigint=&gt;factory_from_int4( 10 )-&gt;mul( bigint_int4 )-&gt;to_string( ).
DATA(a31) = cl_abap_bigint=&gt;factory_from_int4( 5 )-&gt;mul_by_two_power( 2 )-&gt;to_string( ).
DATA(a32) = cl_abap_bigint=&gt;factory_from_int4( 2 )-&gt;mul_int4( 5 )-&gt;to_string( ).
DATA(a33) = cl_abap_bigint=&gt;factory_from_int4( 8 )-&gt;pow( 2 )-&gt;to_string( ).
DATA(a34) = cl_abap_bigint=&gt;factory_from_int4( 9 )-&gt;sqrt( )-&gt;to_string( ).
DATA(a35) = cl_abap_bigint=&gt;factory_from_int4( 18 )-&gt;sub( bigint_int4 )-&gt;to_string( ).
DATA(a36) = cl_abap_bigint=&gt;factory_from_int4( 15 )-&gt;sub_int4( 9 )-&gt;to_string( ).
&quot;Cloning
DATA(a37) = cl_abap_bigint=&gt;factory_from_int4( 15 ).
DATA(a38) = cl_abap_bigint=&gt;factory_from_int4( 5 ).
&quot;Adding a number to another number to not get a new instance but the original instance
DATA(a39) = a37-&gt;add( a38 ).
ASSERT a39 = a37.
DATA(a40) = a37-&gt;to_string( ).
DATA(a41) = a39-&gt;to_string( ).
DATA(a42) = cl_abap_bigint=&gt;factory_from_int4( 15 ).
DATA(a43) = cl_abap_bigint=&gt;factory_from_int4( 5 ).

DATA(a44) = a42-&gt;clone( )-&gt;add( a43 ).
ASSERT a44 &lt;&gt; a42.
DATA(a45) = a42-&gt;to_string( ).
DATA(a46) = a44-&gt;to_string( ).
DATA(a47) = cl_abap_bigint=&gt;factory_from_int4( 15 )-&gt;sub_int4( 9 )-&gt;clone( )-&gt;to_string( ).

*&amp;---------------------------------------------------------------------*
*&amp; cl_abap_rational
*&amp;---------------------------------------------------------------------*

&quot;Factories
DATA(r1) = cl_abap_rational=&gt;factory_from_string( `-1/3` ).
DATA(r2) = cl_abap_rational=&gt;factory_from_bigint( cl_abap_bigint=&gt;factory_from_int4( 11 ) ).

TYPES p_l16d5 TYPE p LENGTH 16 DECIMALS 5.
DATA(r3) = cl_abap_rational=&gt;factory_from_dec( CONV p_l16d5( &#x27;123456.789&#x27; ) ).
DATA(r4) = cl_abap_rational=&gt;factory_from_decimal_string( `1.234567890` ).
DATA(r5) = cl_abap_rational=&gt;factory_from_df34( `1.4` ).
DATA(r6) = cl_abap_rational=&gt;factory_from_int4( 100 ).
DATA(r7) = cl_abap_rational=&gt;factory_from_int8( CONV int8( 123 ) ).
DATA(r8) = cl_abap_rational=&gt;factory_from_string( `-2/3` )-&gt;add_int4( 1 )-&gt;to_df34( ).
DATA(r9) = cl_abap_rational=&gt;factory_from_string( `-2/3` )-&gt;add_int4( 1 )-&gt;to_string( ).
DATA r10 TYPE p_l16d5.
cl_abap_rational=&gt;factory_from_string( `-2/3` )-&gt;add_int4( 1 )-&gt;to_dec( IMPORTING ev_decimal = r10 ).
&quot;Methods that include cl_abap_bigint instances
DATA(r11) = cl_abap_rational=&gt;factory_from_string( `-2/3` )-&gt;add_bigint( cl_abap_bigint=&gt;factory_from_int4( 1 ) )-&gt;to_string( ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Calculations with Date, Time and Time Stamp Values</h2>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Date calculations
*&amp;---------------------------------------------------------------------*

DATA date_calc_1 TYPE d VALUE &#x27;20240101&#x27;.
DATA date_calc_2 TYPE d VALUE &#x27;20231227&#x27;.
DATA date_calc_3 TYPE d VALUE &#x27;20231230&#x27;.
DATA date_calc_4 TYPE d VALUE &#x27;20240220&#x27;.

DATA(days_diff_1) = date_calc_1 - date_calc_2. &quot;5
DATA(days_diff_2) = date_calc_1 - date_calc_3. &quot;2
DATA(days_since_01_01_0001) = CONV i( date_calc_1 ). &quot;738887

&quot;------------ Performing date additions and subtractions using the XCO library ------------

&quot;Adding days to the current date using the &#x27;add&#x27; method
&quot;e.g. 2024-03-16 (if the current date is 2024-03-11)
DATA(xco_date_add_5days) = xco_cp=&gt;sy-&gt;date( )-&gt;add( iv_day = 5
                                             )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                             )-&gt;value.

&quot;The &#x27;add&#x27; method has various parameters, adding 1 day/month/year
&quot;e.g. 2025-04-12 (if the current date is 2024-03-11)
DATA(xco_date_add_1_mult) = xco_cp=&gt;sy-&gt;date( )-&gt;add( iv_day = 1 iv_month = 1 iv_year = 1
                                              )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                              )-&gt;value.

DATA(xco_yesterday) = xco_cp=&gt;sy-&gt;date( )-&gt;subtract( iv_day = 1
                                        )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                        )-&gt;value.

&quot;Addition with a custom date
&quot;2024-02-29
DATA(xco_date_add_1day_custom) = xco_cp_time=&gt;date( iv_year = &#x27;2024&#x27; iv_month = &#x27;02&#x27; iv_day = &#x27;28&#x27;
                                                  )-&gt;add( iv_day = 1
                                                  )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                                  )-&gt;value.

&quot;Subtraction with a custom date
DATA(xco_date_subtract_misc) = xco_cp_time=&gt;date( iv_year = &#x27;2024&#x27; iv_month = &#x27;02&#x27; iv_day = &#x27;28&#x27;
                                                )-&gt;add( iv_day = 1 iv_month = 1 iv_year = 1
                                                )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                                )-&gt;value.

&quot;Optional parameter io_calculation
&quot;io_calculation parameter: xco_cp_time=&gt;date_calculation-&gt;preserving,
&quot;i.e. the date is calculated mathmatically and preserved. It is the
&quot;default.
&quot;In case of an invalid resulting date, an exception is raised.
TRY.
    DATA(inv_date_a) = xco_cp_time=&gt;date( iv_year  = &#x27;2024&#x27;
                                          iv_month = &#x27;08&#x27;
                                          iv_day   = &#x27;31&#x27;
                                        )-&gt;add( iv_month = 1
                                                io_calculation = xco_cp_time=&gt;date_calculation-&gt;preserving
                                        )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                        )-&gt;value.
  CATCH cx_root INTO DATA(error_inv_date1).
    DATA(error_inv_date1_text) = error_inv_date1-&gt;get_text( ).
ENDTRY.

&quot;io_calculation parameter: xco_cp_time=&gt;date_calculation-&gt;ultimo,
&quot;Here, the actual last day of the month is considered. If the calculated
&quot;date is invalid, the ultimo is automatically used. In the example, one month is added to
&quot;the date. However, September does not have 31 days, so the result is adjusted to the actual
&quot;last day of September, which is 30 in this case.
&quot;Result: 2024-09-30
TRY.
    DATA(inv_date_b) = xco_cp_time=&gt;date( iv_year  = &#x27;2024&#x27;
                                          iv_month = &#x27;08&#x27;
                                          iv_day   = &#x27;31&#x27;
                                        )-&gt;add( iv_month = 1
                                                io_calculation = xco_cp_time=&gt;date_calculation-&gt;ultimo
                                        )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                        )-&gt;value.
  CATCH cx_root INTO DATA(error_inv_date2).
    DATA(error_inv_date2_text) = error_inv_date2-&gt;get_text( ).
ENDTRY.

*&amp;---------------------------------------------------------------------*
*&amp; Time calculations
*&amp;---------------------------------------------------------------------*

&quot;------------ Calculating the time delta between two time values ------------

DATA: time1 TYPE t VALUE &#x27;210000&#x27;,
      time2 TYPE t VALUE &#x27;040000&#x27;.

&quot;Calculating the time difference by subtracting
&quot;Note that the resulting data object is of type i.
&quot;The time values are automatically converted to type i.
&quot;Following the conversion rule of type t to i, the resulting value
&quot;is calculated as follows: format hhmmss of type t -&gt; integer values
&quot;as a result of the calculation hh * 3600 + mm * 60 + ss.

                                                            &quot;-61200
DATA(time_diff) = time2 - time1.

&quot;75600
DATA(time1_conv2i) = CONV i( time1 ).
ASSERT time1_conv2i = ( 21 * 3600 ) + ( 00 * 60 ) + 00.

&quot;14400
DATA(time2_conv2i) = CONV i( time2 ).
ASSERT time2_conv2i = ( 04 * 3600 ) + ( 00 * 60 ) + 00.
ASSERT time2_conv2i - time1_conv2i = time_diff.

&quot;---- Calculating the total values of the time difference in seconds, minutes and hours ----
&quot;The MOD operator is used and works in a way that the positive remainder
&quot;of the division of the left operand by the right is returned. Therefore,
&quot;it is irrelevant whether the time difference value is either positive or
&quot;negative.
&quot;The value 86400 is used as right operand, representing the number of seconds
&quot;per day/24 hours.
&quot;25200
DATA(time_diff_seconds_total) = ( time2 - time1 ) MOD 86400.

&quot;120.05
DATA(time_diff_minutes_total) = CONV decfloat34( ( ( time2 - time1 ) MOD 86400 ) / 60 ).

&quot;2.000833333333333333333333333333333
DATA(time_diff_hours_total) = CONV decfloat34( ( ( ( time2 - time1 ) MOD 86400 ) / 3600 ) ).

&quot;---- Representing the time difference in a data object of type t ----
DATA diff_time TYPE t.

DATA(diff) = ( time2 - time1 ) MOD 86400.

&quot;The following calculations use the DIV operator
&quot;This operator returns the integer part of the division of the left operand
&quot;by the right
DATA(hours) = diff DIV 3600.
diff = diff MOD 3600.
DATA(minutes) = diff DIV 60.
DATA(seconds) = diff MOD 60.

&quot;diff_time: &#x27;070000&#x27;
diff_time(2) = hours.
diff_time+2(2) = minutes.
diff_time+4(2) = seconds.

&quot;More examples
time1 = &#x27;225958&#x27;.
time2 = &#x27;010001&#x27;.

&quot;diff_time: &#x27;020003&#x27;
diff_time(2) = ( ( time2 - time1 ) MOD 86400 ) DIV 3600.
diff_time+2(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) DIV 60.
diff_time+4(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) MOD 60.

time1 = &#x27;010001&#x27;.
time2 = &#x27;225958&#x27;.

&quot;diff_time: &#x27;215957&#x27;
diff_time(2) = ( ( time2 - time1 ) MOD 86400 ) DIV 3600.
diff_time+2(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) DIV 60.
diff_time+4(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) MOD 60.

&quot;diff_time: &#x27;154342&#x27;
time1 = &#x27;132415&#x27;.
time2 = &#x27;050757&#x27;.
diff_time(2) = ( ( time2 - time1 ) MOD 86400 ) DIV 3600.
diff_time+2(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) DIV 60.
diff_time+4(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) MOD 60.

time1 = &#x27;050757&#x27;.
time2 = &#x27;132415&#x27;.

&quot;diff_time: &#x27;081618&#x27;
diff_time(2) = ( ( time2 - time1 ) MOD 86400 ) DIV 3600.
diff_time+2(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) DIV 60.
diff_time+4(2) = ( ( ( time2 - time1 ) MOD 86400 ) MOD 3600 ) MOD 60.

&quot;---- Excursion: / and DIV operators (and why DIV is used above) ----
time1 = &#x27;132415&#x27;.
time2 = &#x27;050757&#x27;.

&quot;Compare the result of the following statements that use different operators
&quot;15
DATA(hours_w_div) = ( ( time2 - time1 ) MOD 86400 ) DIV 3600.

&quot;16 (result of type i, result is rounded)
DATA(hours_no_div) = ( ( time2 - time1 ) MOD 86400 ) / 3600.
&quot;15.72833333333333333333333333333333
DATA(hours_no_div_dec) = CONV decfloat34( ( ( time2 - time1 ) MOD 86400 ) / 3600 ).

&quot;Using the XCO library
&quot;See the snippet above in the date section as well as the class documentation.
&quot;Adding
&quot;e.g. 16:27:34 (if the current time is 15:26:33)
DATA(time_xco_add) = xco_cp=&gt;sy-&gt;time( xco_cp_time=&gt;time_zone-&gt;user
                                      )-&gt;add( iv_hour = 1 iv_minute = 1 iv_second = 1
                                      )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                      )-&gt;value.
&quot;Subtracting
&quot;e.g. 14:27:45 (if the current time is 15:28:46)
DATA(time_xco_subtr) = xco_cp=&gt;sy-&gt;time( xco_cp_time=&gt;time_zone-&gt;user
                                       )-&gt;subtract( iv_hour = 1 iv_minute = 1 iv_second = 1
                                       )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                       )-&gt;value.

&quot;Addition with a custom time
&quot;13:36:59
DATA(time_xco_cust_add) = xco_cp_time=&gt;time( iv_hour   = &#x27;12&#x27;
                                             iv_minute = &#x27;34&#x27;
                                             iv_second = &#x27;56&#x27;
                                           )-&gt;add( iv_hour   = 1
                                                   iv_minute = 2
                                                   iv_second = 3
                                           )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                           )-&gt;value.

&quot;Subtraction with a custom time
&quot;11:32:53
DATA(time_xco_cust_subtr) = xco_cp_time=&gt;time( iv_hour   = &#x27;12&#x27;
                                               iv_minute = &#x27;34&#x27;
                                               iv_second = &#x27;56&#x27;
                                             )-&gt;subtract( iv_hour   = 1
                                                          iv_minute = 2
                                                          iv_second = 3
                                             )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                             )-&gt;value.

*&amp;---------------------------------------------------------------------*
*&amp; Time stamp calculations
*&amp;---------------------------------------------------------------------*

&quot;Time stamp calculations with the built-in function utclong_add
&quot;With the built-in function utclong_add, at least one parameter must be specified
&quot;besides &#x27;val&#x27;.
&quot;Note that there are no parameters for years and months.
DATA(utc4calc) = CONV utclong( &#x27;2024-01-01 15:55:14.1173220&#x27; ).

&quot;Adding one hour
&quot;e.g. 2024-01-01 16:55:14.1173220
DATA(ts1) = utclong_add( val = utc4calc
                         hours = 1 ).

&quot;Subtracting one hour by passing a negative integer value (no separate substract
&quot;function available)
&quot;e.g. 2024-01-01 14:55:14.1173220
DATA(ts3) = utclong_add( val = utc4calc
                         hours = -1 ).

&quot;Using all parameters
&quot;e.g. 2024-01-02 18:09:07.2373220
DATA(ts4) = utclong_add( val = utc4calc
                         days = 1 &quot;type i
                         hours = 2 &quot;type i
                         minutes = CONV int8( &#x27;13&#x27; )
                         seconds = CONV decfloat34( &#x27;53.12&#x27; ) ).

&quot;Using XCO
&quot;Addition with current time stamp
DATA(xco_ts_add) = xco_cp=&gt;sy-&gt;moment( xco_cp_time=&gt;time_zone-&gt;user
                                     )-&gt;add( iv_year        = 1
                                             iv_month       = 2
                                             iv_day         = 3
                                             iv_hour        = 4
                                             iv_minute      = 5
                                             iv_second      = 6
                                     )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                     )-&gt;value.

&quot;Subtraction with current time stamp
DATA(xco_ts_subtr) = xco_cp=&gt;sy-&gt;moment( xco_cp_time=&gt;time_zone-&gt;user
                                       )-&gt;subtract( iv_year        = 1
                                                    iv_month       = 2
                                                    iv_day         = 3
                                                    iv_hour        = 4
                                                    iv_minute      = 5
                                                    iv_second      = 6
                                       )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                       )-&gt;value.

&quot;Addition with a custom time stamp
&quot;2026-03-18T16:40:02
DATA(xco_cust_ts_add) = xco_cp_time=&gt;moment( iv_year   = &#x27;2025&#x27;
                                             iv_month  = &#x27;01&#x27;
                                             iv_day    = &#x27;15&#x27;
                                             iv_hour   = &#x27;12&#x27;
                                             iv_minute = &#x27;34&#x27;
                                             iv_second = &#x27;56&#x27;
                                           )-&gt;add( iv_year        = 1
                                                   iv_month       = 2
                                                   iv_day         = 3
                                                   iv_hour        = 4
                                                   iv_minute      = 5
                                                   iv_second      = 6
                                           )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                           )-&gt;value.

&quot;Subtraction with a custom time stamp
&quot;2023-11-12T08:29:50
DATA(xco_cust_ts_subtr) = xco_cp_time=&gt;moment( iv_year   = &#x27;2025&#x27;
                                               iv_month  = &#x27;01&#x27;
                                               iv_day    = &#x27;15&#x27;
                                               iv_hour   = &#x27;12&#x27;
                                               iv_minute = &#x27;34&#x27;
                                               iv_second = &#x27;56&#x27;
                                             )-&gt;subtract( iv_year        = 1
                                                          iv_month       = 2
                                                          iv_day         = 3
                                                          iv_hour        = 4
                                                          iv_minute      = 5
                                                          iv_second      = 6
                                             )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                             )-&gt;value.

&quot;Interval calculations
DATA(xco_moment1) = xco_cp_time=&gt;moment( iv_year       = &#x27;2025&#x27;
                                         iv_month  = &#x27;01&#x27;
                                         iv_day    = &#x27;15&#x27;
                                         iv_hour   = &#x27;12&#x27;
                                         iv_minute = &#x27;34&#x27;
                                         iv_second = &#x27;56&#x27; ).

DATA(xco_moment2) = xco_cp_time=&gt;moment( iv_year   = &#x27;2026&#x27;
                                         iv_month  = &#x27;02&#x27;
                                         iv_day    = &#x27;16&#x27;
                                         iv_hour   = &#x27;16&#x27;
                                         iv_minute = &#x27;38&#x27;
                                         iv_second = &#x27;52&#x27; ).

DATA(xco_ts_interval) = xco_moment1-&gt;interval_to( xco_moment2 ).
DATA(ts_interval_low) = xco_ts_interval-&gt;lower_bound-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended )-&gt;value.
DATA(ts_interval_high) = xco_ts_interval-&gt;upper_bound-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended )-&gt;value.

&quot;Calculating time stamp differences using the built-in function utclong_diff

DATA(ts5) = CONV utclong( &#x27;2024-01-01 05:30:00&#x27; ).
DATA(ts6) = CONV utclong( &#x27;2024-01-01 06:30:00&#x27; ).
&quot;The return value has the type decfloat34. It contains the exact difference in seconds.

DATA(ts_diff1) = utclong_diff( high = ts6
                               low = ts5 ). &quot;3600

DATA(ts_diff2) = utclong_diff( high = ts5
                               low = ts6 ). &quot;-3600

DATA(low_timestamp) = CONV utclong( &#x27;2024-01-01 05:30:00&#x27; ).
DATA(high_timestamp) = CONV utclong( &#x27;2024-01-03 10:35:12&#x27; ).

&quot;&#x27;diff&#x27; method: Calculating time differences
cl_abap_utclong=&gt;diff( EXPORTING high    = high_timestamp
                                 low     = low_timestamp
                       IMPORTING days    = DATA(diff_days) &quot;2
                                 hours   = DATA(diff_hours) &quot;5
                                 minutes = DATA(diff_minutes) &quot;5
                                 seconds = DATA(diff_seconds) ). &quot;12

&quot;CL_ABAP_TSTMP: Calculating and converting time stamps in packed numbers

&quot;Creating a time stamp of type timestamp
&quot;e.g. 20240101131220
GET TIME STAMP FIELD DATA(tsa).

&quot;Calculations for time stamps in packed numbers
&quot;Adding 1 hour
&quot;e.g. 20240101141220.0000000
DATA(tsb) = cl_abap_tstmp=&gt;add( tstmp = tsa
                                secs  = 3600 ).

&quot;Subtracting 2 hours
&quot;e.g. 20240101111220.0000000
DATA(tsc) = cl_abap_tstmp=&gt;subtractsecs( tstmp = tsa
                                         secs  = 7200 ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Arithmetic Expressions and Built-in Functions in ABAP SQL and ABAP CDS</h2>
<p>More information:</p>
<ul>
<li>ABAP SQL: <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENSQL_ARITH.html">Arithmetic expressions</a>, <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENSQL_ARITH_FUNC.html">Numeric functions</a></li>
<li>ABAP CDS: <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENCDS_ARITHMETIC_EXPRESSION_V2.html">Arithmetic expressions</a>, <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENCDS_SQL_FUNCTIONS_NUMERIC_V2.html">Numeric functions</a></li>
</ul>
<p>The following example code snippet includes an ABAP SQL <code>SELECT</code> statement with a selection of arithmetic expressions and numeric functions. You can also check <a href="03_ABAP_SQL.md#aggregate-expressions">aggregate expressions</a> that perform calculations as described in the <em>ABAP SQL</em> cheat sheet.</p>
<pre><code>SELECT SINGLE
   &quot;---- Arithmethic expressions ----
   1 + 2 AS add, &quot;3
   10 - -8 AS subtract, &quot;18
   3 * 5 AS multiply, &quot;15
   &quot;The / operator is not allowed in integer expressions.
   &quot;4 / 2 AS divide_nope,
   &quot;The following example uses typed literals to use decimal
   &quot;floating point numbers.
   d34n`4.4` / d34n`2.2` AS divide1, &quot;2
   &quot;The following example uses cast expressions to convert integer values
   &quot;so that a division can be performed.
   CAST( 1 AS D34N ) / CAST( 2 AS D34N ) AS divide2, &quot;0.5
   &quot;Using parentheses
   ( 1 + 2 ) * 3 AS parentheses, &quot;9
   &quot;---- Functions for numeric values ----
   &quot;Division, result rounded to an integer
   div( 4, 2 ) AS div, &quot;2
   &quot;Division, 3rd argument: result is rounded to the specified
   &quot;number of decimals
   division( 1, 3, 2 ) AS division, &quot;0.33
   &quot;Result is rounded to first greater integer
         ceil( decfloat34`1.333` ) AS ceil, &quot;2
   &quot;Result is the remainder of division
   mod( 3, 2 ) AS mod, &quot;1
   &quot;Largest integer value not greater than the specified value
   floor( decfloat34`1.333` ) AS floor, &quot;1
   &quot;Returns the absolute number
   abs( int4`-2` ) AS abs, &quot;2
   &quot;Result is rounded to the specified position after the decimal separator
   round( decfloat34`1.337`, 2 ) AS round &quot;1.34
 FROM i_timezone &quot;Released API (not relevant in the example)
 INTO @DATA(some_data).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;⬆️ back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Executable Example</h2>
<p><a href="./src/zcl_demo_abap_numeric_op.clas.abap">zcl_demo_abap_numeric_op</a></p>
<div class="note">[!NOTE] - The steps to import and run the code are outlined [here](README.md#-getting-started-with-the-examples). - [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
</body>
</html>