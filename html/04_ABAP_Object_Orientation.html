<!DOCTYPE html>
<html lang="fr">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>04 ABAP Object Orientation - ABAP Cheat Sheet</title>
    <style>

        * {
            box-sizing: border-box;
        }
        body {
            font-family: 'Segoe UI', Tahoma, Geneva, Verdana, sans-serif;
            line-height: 1.6;
            max-width: 1200px;
            margin: 0 auto;
            padding: 20px;
            background-color: #f5f5f5;
            color: #333;
        }
        h1 {
            color: #2c3e50;
            border-bottom: 3px solid #3498db;
            padding-bottom: 10px;
            margin-top: 20px;
        }
        h2 {
            color: #2c3e50;
            border-bottom: 2px solid #95a5a6;
            padding-bottom: 8px;
            margin-top: 30px;
        }
        h3 {
            color: #34495e;
            margin-top: 25px;
        }
        h4, h5, h6 {
            color: #34495e;
            margin-top: 20px;
        }
        p {
            margin: 10px 0;
        }
        code {
            background-color: #f4f4f4;
            padding: 2px 6px;
            border-radius: 3px;
            font-family: 'Courier New', Consolas, Monaco, monospace;
            color: #c7254e;
            font-size: 0.9em;
        }
        pre {
            background-color: #282c34;
            color: #abb2bf;
            padding: 15px;
            border-radius: 5px;
            overflow-x: auto;
            margin: 15px 0;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        pre code {
            background-color: transparent;
            color: inherit;
            padding: 0;
            font-size: 0.9em;
            line-height: 1.5;
        }
        table {
            border-collapse: collapse;
            width: 100%;
            margin: 20px 0;
            background-color: white;
            box-shadow: 0 2px 4px rgba(0,0,0,0.1);
        }
        th, td {
            border: 1px solid #ddd;
            padding: 12px;
            text-align: left;
        }
        th {
            background-color: #3498db;
            color: white;
            font-weight: bold;
        }
        tr:nth-child(even) {
            background-color: #f9f9f9;
        }
        tr:hover {
            background-color: #f5f5f5;
        }
        ul, ol {
            margin: 10px 0;
            padding-left: 30px;
        }
        li {
            margin: 5px 0;
        }
        a {
            color: #3498db;
            text-decoration: none;
        }
        a:hover {
            text-decoration: underline;
            color: #2980b9;
        }
        hr {
            border: none;
            border-top: 2px solid #bdc3c7;
            margin: 30px 0;
        }
        .note {
            background-color: #d4edda;
            border-left: 4px solid #28a745;
            padding: 12px;
            margin: 15px 0;
            border-radius: 4px;
        }
        blockquote {
            border-left: 4px solid #3498db;
            padding-left: 15px;
            margin: 15px 0;
            color: #555;
            font-style: italic;
        }
        @media (max-width: 768px) {
            body {
                padding: 10px;
            }
            table {
                font-size: 0.9em;
            }
            th, td {
                padding: 8px;
            }
        }
    
    </style>
</head>
<body>
<p>&lt;a name=&quot;top&quot;&gt;&lt;/a&gt;</p>
<h1>ABAP Object Orientation</h1>
<ul>
<li><a href="#abap-object-orientation">ABAP Object Orientation</a></li>
<li><a href="#classes-and-objects">Classes and Objects</a></li>
<li><a href="#creating-classes">Creating Classes</a></li>
<li><a href="#creating-a-global-class">Creating a Global Class</a></li>
<li><a href="#creating-a-local-class">Creating a Local Class</a></li>
<li><a href="#additions-in-the-class-declaration-part">Additions in the Class Declaration Part</a></li>
<li><a href="#excursion-class-pool-and-include-programs">Excursion: Class Pool and Include Programs</a></li>
<li><a href="#visibility-of-components">Visibility of Components</a></li>
<li><a href="#creating-the-visibility-sections">Creating the Visibility Sections</a></li>
<li><a href="#defining-components">Defining Components</a></li>
<li><a href="#class-attributes">Class Attributes</a></li>
<li><a href="#methods">Methods</a></li>
<li><a href="#parameter-interface">Parameter Interface</a></li>
<li><a href="#formal-and-actual-parameters">Formal and Actual Parameters</a></li>
<li><a href="#complete-typing-of-formal-parameters">Complete Typing of Formal Parameters</a></li>
<li><a href="#generic-typing-of-formal-parameters">Generic Typing of Formal Parameters</a></li>
<li><a href="#defining-parameters-as-optional">Defining Parameters as Optional</a></li>
<li><a href="#defining-input-parameters-as-preferred">Defining Input Parameters as Preferred</a></li>
<li><a href="#constructors">Constructors</a></li>
<li><a href="#working-with-objects-and-components">Working with Objects and Components</a></li>
<li><a href="#declaring-object-reference-variables">Declaring Object Reference Variables</a></li>
<li><a href="#creating-objects">Creating Objects</a></li>
<li><a href="#working-with-reference-variables">Working with Reference Variables</a></li>
<li><a href="#accessing-attributes">Accessing Attributes</a></li>
<li><a href="#calling-methods">Calling Methods</a></li>
<li><a href="#excursion-inline-declarations-returning-parameters">Excursion: Inline Declarations, Returning Parameters</a></li>
<li><a href="#self-reference-me">Self-Reference me</a></li>
<li><a href="#method-chaining-and-chained-attribute-access">Method Chaining and Chained Attribute Access</a></li>
<li><a href="#excursion-example-class">Excursion: Example Class</a></li>
<li><a href="#inheritance">Inheritance</a></li>
<li><a href="#additions-related-to-inheritance-and-instantiation">Additions Related to Inheritance and Instantiation</a></li>
<li><a href="#excursion-inheritance-example">Excursion: Inheritance Example</a></li>
<li><a href="#polymorphism-and-casting-upcastdowncast">Polymorphism and Casting (Upcast/Downcast)</a></li>
<li><a href="#example-with-local-classes-demonstrating-upcasts-and-downcasts">Example with Local Classes Demonstrating Upcasts and Downcasts</a></li>
<li><a href="#upcasts-and-downcasts-using-the-rtts-inheritance-tree">Upcasts and Downcasts Using the RTTS Inheritance Tree</a></li>
<li><a href="#checking-the-dynamic-type-of-object-reference-variables">Checking the Dynamic Type of Object Reference Variables</a></li>
<li><a href="#interfaces">Interfaces</a></li>
<li><a href="#defining-interfaces">Defining Interfaces</a></li>
<li><a href="#implementing-interfaces">Implementing Interfaces</a></li>
<li><a href="#additions-related-to-interface-implementations">Additions Related to Interface Implementations</a></li>
<li><a href="#interface-reference-variables-accessing-objects-and-components">Interface Reference Variables, Accessing Objects and Components</a></li>
<li><a href="#excursion-example-interface">Excursion: Example Interface</a></li>
<li><a href="#friendship">Friendship</a></li>
<li><a href="#friendship-between-global-and-local-classes">Friendship between Global and Local Classes</a></li>
<li><a href="#events">Events</a></li>
<li><a href="#excursions">Excursions</a></li>
<li><a href="#abap-examples-of-design-patterns-in-object-oriented-programming">ABAP Examples of Design Patterns in Object-Oriented Programming</a></li>
<li><a href="#class-based-and-classic-exceptions">Class-Based and Classic Exceptions</a></li>
<li><a href="#abap-unit-tests">ABAP Unit Tests</a></li>
<li><a href="#abap-doc-comments">ABAP Doc Comments</a></li>
<li><a href="#escape-character">Escape Character</a></li>
<li><a href="#more-information">More Information</a></li>
<li><a href="#executable-examples">Executable Examples</a></li>
</ul>
<p>This ABAP cheat sheet provides an overview on selected syntax options and concepts related to ABAP object orientation.</p>
<div class="note">[!NOTE] - The cheat sheet is supported by code snippets and an executable example. They are **not** suitable as role models for object-oriented design. Their primary focus is on the syntax and functionality. - For more details, refer to the respective topics in the ABAP Keyword Documentation. Find an overview in the topic [ABAP Objects - Overview](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_objects_oview.htm). - The [executable examples](#executable-examples) reflect several points and code snippets covered in the cheat sheet.</div>
<h2>Classes and Objects</h2>
<p>Object-oriented programming in ABAP means dealing with <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclass_glosry.htm">classes</a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_glosry.htm">objects</a>.</p>
<p>Objects ...</p>
<ul>
<li>are</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninstance_glosry.htm">instances</a> of a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentype_glosry.htm">type</a>. In this context, they are instances of a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclass_glosry.htm">class</a>. The terms <em>object</em> and <em>instance</em> are used synonymously.</p>
<ul>
<li>exist in the [internal</li>
</ul>
<p>session](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninternal_session_glosry.htm &quot;Glossary Entry&quot;) of an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_program_glosry.htm">ABAP program</a>.</p>
<p>Classes ...</p>
<ul>
<li>are templates for objects, i. e. they determine how</li>
</ul>
<p>all instances of a class are set up. All instances are created (i.e they are <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninstantiation_glosry.htm">instantiated</a>) based on this template and, thus, have the same setup.</p>
<ul>
<li>To give an example: If, for example, a vehicle represents a class, then the</li>
</ul>
<p>instances of the class <code>vehicle</code> have the same setup. That means they all share the same kind of components like a brand, model and color or the same functionality like the acceleration or braking distance. However, the values of these components are different from instance to instance. For example, one instance is a red sedan of brand A having a certain acceleration; another instance is a black SUV of brand B and so on. You can create an object (or instance respectively) that stands for an actual vehicle which you can work with. You might create any number of objects that are based on such a class - if instantiation is allowed.</p>
<ul>
<li>contain</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencomponent_glosry.htm">components</a>:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenattribute_glosry.htm">Attributes</a></li>
</ul>
<p>of the objects (the data object declarations)</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenmethod_glosry.htm">Methods</a></li>
</ul>
<p>that determine the behavior of an object</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenevent_glosry.htm">Events</a> to trigger the processing of ABAP code</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Creating Classes</h3>
<p>You can either create local or global classes:</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt;&lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenlocal_class_glosry.htm&quot;&gt;Local classes&lt;/a&gt;&lt;/td&gt; &lt;td&gt;&lt;ul&gt;&lt;li&gt;can be defined within an &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_program_glosry.htm&quot;&gt;ABAP program&lt;/a&gt; such as in include programs of global classes (e.g. the &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenccimp_glosry.htm&quot;&gt;CCIMP include&lt;/a&gt;, &lt;i&gt;Local Types&lt;/i&gt; table in ADT) or in executable programs (&quot;reports&quot;; in &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstandard_abap_glosry.htm&quot;&gt;Standard ABAP&lt;/a&gt; only)&lt;/li&gt;&lt;li&gt;can only be used in the ABAP program in which the class is defined&lt;/li&gt;&lt;/ul&gt;&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt;&lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenglobal_class_glosry.htm&quot;&gt;Global classes&lt;/a&gt;&lt;/td&gt; &lt;td&gt;&lt;ul&gt;&lt;li&gt;are defined as &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenglobal_type_glosry.htm&quot;&gt;global types&lt;/a&gt;, i. e. they are visible as a repository object - in contrast to local classes. As a global type, they can be used - as the name implies - globally in other ABAP programs or global classes&lt;/li&gt;&lt;li&gt;are declared in &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclass_pool_glosry.htm&quot;&gt;class pools&lt;/a&gt; that contain a &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenccimp_glosry.htm&quot;&gt;CCIMP include&lt;/a&gt; and other &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninclude_program_glosry.htm&quot;&gt;include programs&lt;/a&gt;&lt;/li&gt;&lt;/ul&gt; &lt;/td&gt; &lt;/tr&gt; &lt;/table&gt;</p>
<div class="note">[!NOTE] - If a class is only used in one ABAP program, creating a local class is enough. However, if you choose to create a global class, you must bear in mind that such a class can be used everywhere. Consider the impact on the users of the global class when you change, for example, the visibility section of a component or you delete it. - Apart from ADT, global classes can also be created in the ABAP Workbench (`SE80`) or with transaction `SE24` in [classic ABAP](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclassic_abap_glosry.htm).</div>
<p>Basic structure of classes:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendeclaration_part_glosry.htm">Declaration part</a> that includes declarations of the class components.</li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenimplementation_part_glosry.htm">Implementation part</a> that includes method implementations.</li>
<li>Both are introduced by <code>CLASS</code> and ended by <code>ENDCLASS</code>.</li>
</ul>
<h4>Creating a Global Class</h4>
<p>The code snippet shows a basic skeleton of a global class. There are <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclass_options.htm">further additions</a> possible for the declaration part.</p>
<pre><code>&quot;Declaration part
CLASS global_class DEFINITION
  PUBLIC                       &quot;Makes the class a global class in the class library.
  FINAL                        &quot;Means that no subclasses can be derived from this class.
  CREATE PUBLIC.               &quot;This class can be instantiated anywhere it is visible.

    ... &quot;Here go the declarations for all components and visibility sections.

ENDCLASS.

&quot;Implementation part
CLASS global_class IMPLEMENTATION.

    ... &quot;Here go the method implementations.
        &quot;Only required if you declare methods in the DEFINITION part.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Creating a Local Class</h4>
<ul>
<li>You can create local classes, for example, in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenccimp_glosry.htm">CCIMP include</a> (<em>Local Types</em> tab in ADT) of a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclass_pool_glosry.htm">class pool</a>.</li>
<li>Local classes are used in their own ABAP program. While dynamic access beyond program boundaries is possible, it is not recommended.</li>
<li>In the cheat sheet, local classes are used in several sections, particularly for self-contained examples that require multiple classes.</li>
</ul>
<p>The following snippet shows the skeleton of local class declaration.</p>
<ul>
<li>The <code>PUBLIC</code> addition makes a class a global class in the class library. This is not possible for local classes.</li>
<li>It does not specify <code>CREATE ...</code>. Note that <code>CREATE PUBLIC</code> is the default, which means that not specifying any <code>CREATE ...</code> addition makes the class implicitly specified with <code>CREATE PUBLIC</code>.</li>
<li>More additions are possible. They are covered in the following sections.</li>
</ul>
<pre><code>&quot;Declaration part
CLASS local_class DEFINITION.

    ... &quot;Here go the declarations for all components and visibility sections.
        &quot;You should place the declarations at the beginning of the program.

ENDCLASS.

&quot;Implementation part
CLASS local_class IMPLEMENTATION.

    ...  &quot;Here go the method implementations.
         &quot;Only required if you declare methods in the declaration part.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Additions in the Class Declaration Part</h4>
<p>This section covers a selection of additions to declare classes. They are also covered in other sections below, e.g. <a href="#additions-related-to-inheritance-and-instantiation">Additions Related to Inheritance and Instantiation</a>. Find more information on the additions in the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclass_options.htm">ABAP Keyword Documentation</a>. The additions assume dealing with global classes, however, many of the additions are also possible for local classes.</p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Addition &lt;/td&gt; &lt;td&gt; Notes &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>PUBLIC</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Creates a global class</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>FINAL</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The class does not allow inheritance.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>CREATE PUBLIC</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The class is instantiable anywhere. Note that not specifying a <code>CREATE ...</code> addition means the class specifies  <code>CREATE PUBLIC</code> by default.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>CREATE PROTECTED</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The class can only be instantiated in methods of its <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensubclass_glosry.htm">subclasses</a>, of the class itself, and of its <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfriend_glosry.htm">friends</a>.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>CREATE PRIVATE</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>The class can only be instantiated in methods of the class itself or of its friends. Hence, it cannot be instantiated as an inherited component of subclasses.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>INHERITING FROM superclass</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>As the name implies, it is used to inherit from a visible <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensuperclass_glosry.htm">superclass</a>. If the addition is not specified, the created class implicitly inherits from the predefined empty, abstract class <code>object</code> (the root object).</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>ABSTRACT</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>To define <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabstract_glosry.htm">abstract</a> classes. These classes cannot be instantiated. They can contain both abstract methods and non-abstract methods. Abstract methods can only be implemented in <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensubclass_glosry.htm">subclasses</a> by redefinition. See a simple implementation example <a href="#excursion-example-interface">here</a>.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>[GLOBAL|LOCAL] FRIENDS class</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to define <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfriend_glosry.htm">friendships</a> (also possible for interfaces). Friends of a class have unrestricted access to all components of that class.</li>
<li><code>GLOBAL FRIENDS</code>: Used in global classes (together with the <code>PUBLIC</code> addition) to grant friendship to other global classes and interfaces</li>
<li><code>FRIENDS</code>: For local classes, e.g. local classes granting friendship to other local classes or the global class of the class pool</li>
<li><code>LOCAL FRIENDS</code>: Used for global classes to grant friendship to local classes and interfaces in its own class pool. However, it is a dedicated statement, as shown in the <a href="#friendship">Friendship</a> section.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>FOR TESTING</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>For <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_unit_glosry.htm">ABAP Unit</a> tests. Find more information in the <a href="14_ABAP_Unit_Tests.md">ABAP Unit Tests</a> cheat sheet.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>FOR BEHAVIOR OF</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>To define <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbehavior_pool_glosry.htm">ABAP behavior pools</a>. Find more information in the <a href="08_EML_ABAP_for_RAP.md">ABAP for RAP: Entity Manipulation Language (ABAP EML)</a> cheat sheet.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>DEFINITION DEFERRED</code></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Makes a local class known in a program before the actual class definition. It is typically used in test classes of ABAP Unit. Find more information <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclass_deferred.htm">here</a>.</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Excursion: Class Pool and Include Programs</h4>
<ul>
<li>A class pool is an ABAP program containing the definition of one global class (<em>Global Class</em> tab in ADT)</li>
<li>Global classes are marked as such with the <code>PUBLIC</code> addition to the <code>CLASS</code> statement: <code>CLASS zcl_demo_abap DEFINITION PUBLIC ...</code></li>
<li>Additionally, a class pool can also contain local classes that are defined in dedicated include programs (CCDEF and the other include names are internal names the include programs end with):</li>
<li>CCDEF include (<em>Class-relevant Local Types</em> tab in ADT): Is included in front of the declaration part of the global class</li>
<li>CCIMP include (<em>Local Types</em> tab in ADT): Is included behind the declaration part and in front of the implementation part of the global class</li>
<li>CCAU include (<em>Test classes</em> tab in ADT): Test include; contains ABAP Unit test classes</li>
</ul>
<p>The following simplified example demonstrates include programs.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more details&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>Global class:</p>
<ul>
<li>Create a new global class (the example uses the name <code>zcl_demo_abap</code>) and copy and paste the following code in the <em>Global Class</em> tab in ADT.</li>
<li>The method in the private section makes use of local types that are defined in the CCDEF include. In the example the method is deliberately included in the private visibility section. Having it like this in the public section is not possible due to the use of local types.</li>
<li>The <code>if_oo_adt_classrun~main</code> implementation contains method calls to this method. It also contains method calls to a method implemented in a local class in the CCIMP include.</li>
<li>As a prerequisite to activate the class, you also need to copy and paste the code snippets further down for the CCDEF and CCIMP include. Once you have pasted the code, the syntax errors in the global class will disappear.</li>
<li>You can run the class using F9. Some output is displayed.</li>
<li>When you have copied and pasted the CCAU code snippet for the simple ABAP Unit test, and activated the class, you can choose <em>CTRL+Shift+F10</em> in ADT to run the ABAP Unit test. Alternatively, you can make a right click in the class code, choose <em>Run As</em> and <em>4 ABAP Unit Test</em>.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

  PROTECTED SECTION.
  PRIVATE SECTION.
    &quot;This method uses types (data type c1 and the local exception class)
    &quot;defined in the CCDEF include
    METHODS calculate IMPORTING num1          TYPE i
                                operator      TYPE c1
                                num2          TYPE i
                      RETURNING VALUE(result) TYPE i
                      RAISING   lcx_wrong_operator.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;The method called has formal parameters using type declarations from the CCDEF include
    TRY.
        DATA(result1) = calculate( num1 = 10 operator = &#x27;+&#x27; num2 = 4 ).
        out-&gt;write( data = result1 name = `result1` ).
      CATCH lcx_wrong_operator.
        out-&gt;write( `Operator not allowed` ).
    ENDTRY.

    TRY.
        DATA(result2) = calculate( num1 = 10 operator = &#x27;-&#x27; num2 = 4 ).
        out-&gt;write( data = result2 name = `result2` ).
      CATCH lcx_wrong_operator.
        out-&gt;write( `Operator not allowed` ).
    ENDTRY.

    TRY.
        DATA(result3) = calculate( num1 = 10 operator = &#x27;*&#x27; num2 = 4 ).
        out-&gt;write( data = result3 name = `result3` ).
      CATCH lcx_wrong_operator.
        out-&gt;write( `Operator not allowed` ).
    ENDTRY.

    &quot;Using local class implemented in the CCIMP include
    DATA(hi1) = lcl_demo=&gt;say_hello( ).
    out-&gt;write( data = hi1 name = `hi1` ).
    DATA(hi2) = lcl_demo=&gt;say_hello( xco_cp=&gt;sy-&gt;user( )-&gt;name ).
    out-&gt;write( data = hi2 name = `hi2` ).

    &quot;Test include (CCAU)
    &quot;For running the ABAP Unit test, choose CTRL+Shift+F10 in ADT.
    &quot;Or you can make a right click in the class code, choose
    &quot;&#x27;Run As&#x27; and &#x27;4 ABAP Unit Test&#x27;.
  ENDMETHOD.

  METHOD calculate.
    result = SWITCH #( operator
                       WHEN &#x27;+&#x27; THEN num1 + num2
                       WHEN &#x27;-&#x27; THEN num1 - num2
                       ELSE THROW lcx_wrong_operator( ) ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<pre><code>CLASS lcx_wrong_operator DEFINITION INHERITING FROM cx_static_check.
ENDCLASS.

TYPES c1 TYPE c LENGTH 1.</code></pre>
<p>Code snippet for the CCDEF include:</p>
<pre><code>CLASS lcl_demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: say_hello IMPORTING name TYPE string optional
                             RETURNING VALUE(hi) type string.

ENDCLASS.

CLASS lcl_demo IMPLEMENTATION.

  METHOD say_hello.
    hi = |Hallo{ COND #( WHEN name IS SUPPLIED THEN ` ` &amp;&amp; name ) }!|.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>Code snippet for the CCIMP include:</p>
<pre><code>CLASS ltc_test DEFINITION DEFERRED.
CLASS zcl_demo_abap DEFINITION LOCAL FRIENDS ltc_test.

CLASS ltc_test DEFINITION FOR TESTING
RISK LEVEL HARMLESS
DURATION SHORT.

  PRIVATE SECTION.
    METHODS test_calculate FOR TESTING.

ENDCLASS.

CLASS ltc_test IMPLEMENTATION.

  METHOD test_calculate.
    &quot;Creating an object of the class under test
    DATA(ref_cut) = NEW zcl_demo_abap( ).

    &quot;Calling method that is to be tested
    TRY.
        DATA(result1) = ref_cut-&gt;calculate( num1 = 10 operator = &#x27;+&#x27; num2 = 4 ).
      CATCH lcx_wrong_operator.
    ENDTRY.

    TRY.
        DATA(result2) = ref_cut-&gt;calculate( num1 = 10 operator = &#x27;-&#x27; num2 = 4 ).
      CATCH lcx_wrong_operator.
    ENDTRY.

    &quot;Assertions
    cl_abap_unit_assert=&gt;assert_equals(
          act = result1
          exp = 14
          quit = if_abap_unit_constant=&gt;quit-no ).

    cl_abap_unit_assert=&gt;assert_equals(
          act = result2
          exp = 6
          quit = if_abap_unit_constant=&gt;quit-no ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>Code snippet for the test include (CCAU): &lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Visibility of Components</h3>
<p>In the class declaration part, you specify three <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenvisibility_section_glosry.htm">visibility sections</a> and include class components to define their visibility. These visibility sections serve the purpose of encapsulation in ABAP Objects. For example, you do not want to make certain components publicly available for all users. The visibility sections are as follows:</p>
<p>&lt;table&gt; &lt;tr&gt; &lt;td&gt;&lt;pre&gt;PUBLIC SECTION.&lt;/pre&gt;&lt;/td&gt; &lt;td&gt;Components declared in this section can be accessed from within the class and from all users of the class.&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt;&lt;pre&gt;PROTECTED SECTION.&lt;/pre&gt;&lt;/td&gt; &lt;td&gt;Components declared in this section can be accessed from within the class and &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensubclass_glosry.htm&quot;&gt;subclasses&lt;/a&gt; as well as &lt;a href=&quot;https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfriend_glosry.htm&quot;&gt;friends&lt;/a&gt;.&lt;/td&gt; &lt;/tr&gt; &lt;tr&gt; &lt;td&gt;&lt;pre&gt;PRIVATE SECTION.&lt;/pre&gt;&lt;/td&gt; &lt;td&gt;Components declared in this section can only be accessed from within the class in which they are declared and its friends.&lt;/td&gt; &lt;/tr&gt; &lt;/table&gt;</p>
<p>Summary:</p>
<table>
<thead><tr>
<th>Visible for</th>
<th>PUBLIC SECTION</th>
<th>PROTECTED SECTION</th>
<th>PRIVATE SECTION</th>
</tr></thead>
<tbody>
<tr>
<td>Same class and its friends</td>
<td>X</td>
<td>X</td>
<td>X</td>
</tr>
<tr>
<td>Any subclasses</td>
<td>X</td>
<td>X</td>
<td>-</td>
</tr>
<tr>
<td>Any repository objects</td>
<td>X</td>
<td>-</td>
<td>-</td>
</tr>
</tbody>
</table>
<h4>Creating the Visibility Sections</h4>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC                       
  FINAL                        
  CREATE PUBLIC.  

    PUBLIC SECTION.
      &quot;Here go the components.
    PROTECTED SECTION.
      &quot;Here go the components.
    PRIVATE SECTION.
      &quot;Here go the components.
ENDCLASS.

...</code></pre>
<p>At least one section must be specified.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Defining Components</h3>
<p>All components, i. e.</p>
<ul>
<li>attributes (using <code>TYPES</code>, <code>DATA</code>, <code>CLASS-DATA</code>, and <code>CONSTANTS</code> for data types and data</li>
</ul>
<p>objects),</p>
<ul>
<li>methods (using <code>METHODS</code> and <code>CLASS-METHODS</code>),</li>
<li>events (using <code>EVENTS</code> and <code>CLASS-EVENTS</code>) as well as</li>
<li>interfaces (only in the public visibility section),</li>
</ul>
<p>are declared in the declaration part of the class. There, they must be assigned to a visibility section.</p>
<p>Two kinds of components are to be distinguished when, for example, looking at declarations using <code>DATA</code> and <code>CLASS-DATA</code> having a preceding <code>CLASS-</code>:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninstance_component_glosry.htm">Instance components</a>:</li>
</ul>
<p>Components that exist separately for each instance and can only be accessed in instances of a class.</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstatic_component_glosry.htm">Static components</a> (the declarations with <code>CLASS-</code>):</li>
</ul>
<p>Components that exist only once per class. They do no not exclusively exist for specific instances. They can be addressed using the name of the class.</p>
<h4>Class Attributes</h4>
<ul>
<li>The attributes of a class (or interface) mean the data objects declared within a</li>
</ul>
<p>class (or interface).</p>
<ul>
<li>[Instance</li>
</ul>
<p>attributes](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninstance_attribute_glosry.htm &quot;Glossary Entry&quot;) (<code>DATA</code>): Determine the state of objects of a class. The data is only valid in the context of an instance. As shown further down, instance attributes can only be accessed via an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_refer_variable_glosry.htm">object reference variable</a> (or <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninterface_ref_variable_glosry.htm">interface reference variable</a>).</p>
<ul>
<li>[Static</li>
</ul>
<p>attributes](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstatic_attribute_glosry.htm &quot;Glossary Entry&quot;) (<code>CLASS-DATA</code>): Their content is independent of instances of a class and, thus, valid for all instances.  That means that if you change such a static attribute, the change is visible in all instances. As shown further down, static attributes can be accessed by both using an object reference variable (however, this access is not recommended) and using the class name without a prior creation of an instance.</p>
<div class="note">[!NOTE] - You can declare constant data objects that should not be</div>
<div class="note">- The addition</div>
<div class="note">- Can be read from outside of the class - Cannot be changed from outside - Can only be changed using methods of the class or its subclasses - Note that when creating attributes in the public visibility section, they are globally visible and can therefore be globally used. Note the consequences on the users when changing attributes in the public visibility section (e.g. making an attribute read-only at a later point in time when, for example, other classes use the attribute).</div>
<p>changed using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapconstants.htm"><code>CONSTANTS</code></a> statements. You specify the values for the constants (which are also static attributes) when you declare them in the declaration part of a class. <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapdata_options.htm#!ABAP_ADDITION_2@2@"><code>READ-ONLY</code></a> can be used in the public visibility section. Effect:</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC                       
  FINAL                        
  CREATE PUBLIC.  

    PUBLIC SECTION.
      TYPES some_type TYPE c LENGTH 3.               &quot;Type declaration

      DATA: inst_number TYPE i,                      &quot;Instance attributes
            inst_string TYPE string,
            dobj_r_only TYPE c LENGTH 5 READ-ONLY.   &quot;Read-only attribute

      CLASS-DATA: stat_number TYPE i,                &quot;Static attributes
                  stat_char   TYPE c LENGTH 3.

      CONSTANTS const_num TYPE i VALUE 123.          &quot;Non-changeable constant

    PROTECTED SECTION.
      &quot;Here go more attributes if needed.

    PRIVATE SECTION.
      &quot;Here go more attributes if needed.

ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.

      ... &quot;Here go all method implementations.

ENDCLASS.</code></pre>
<p>Declaring attributes in visibility sections. In the code snippet below, all attributes are declared in the public section.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Methods</h4>
<ul>
<li>Are internal</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenprocedure_glosry.htm">procedures</a> determining the behavior of the class.</p>
<ul>
<li>Can access all of the attributes of a class and, if not defined</li>
</ul>
<p>otherwise, change their content.</p>
<ul>
<li>Have a [parameter</li>
</ul>
<p>interface](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenparameter_interface_glosry.htm &quot;Glossary Entry&quot;) (also known as <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensignature_glosry.htm">signature</a>) with which methods can get values to work with when being called and pass values back to the caller (see the notes on formal and actual parameters below).</p>
<ul>
<li>[Static</li>
</ul>
<p>methods](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstatic_method_glosry.htm &quot;Glossary Entry&quot;) can only access static attributes of a class and trigger static events. You declare them using <code>CLASS-METHODS</code> statements in a visibility section.</p>
<ul>
<li>[Instance</li>
</ul>
<p>methods](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninstance_method_glosry.htm &quot;Glossary Entry&quot;) can access all of the attributes of a class and trigger all events. You declare them using <code>METHODS</code> statements in a visibility section. Note that you must create an instance of a class first before using instance methods. Called within the same class, an instance is not required.</p>
<ul>
<li><code>CLASS-METHODS</code> and <code>METHODS</code> can be followed by a colon to list one or more methods, separated by commas, or without a colon to declare a single method.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC                       
  FINAL                        
  CREATE PUBLIC. 

    PUBLIC SECTION.
      METHODS: inst_meth1,                                      &quot;instance methods

               inst_meth2 IMPORTING a TYPE string,

               inst_meth3 IMPORTING b TYPE i
                          EXPORTING c TYPE i,

               inst_meth4 IMPORTING d TYPE string
                          RETURNING VALUE(e) TYPE string,

               &quot;Note that method declarations should be done with care. An example
               &quot;as follows may not be advisable, e.g. specifying multiple output
               &quot;parameters (both exporting and returning parameters for a method).
               &quot;As is valid for all examples in the cheat sheet, the focus is on
               &quot;syntax options.
               inst_meth5 IMPORTING f TYPE i
                          EXPORTING g TYPE i
                          CHANGING  h TYPE string
                          RETURNING VALUE(i) TYPE i
                          RAISING   cx_sy_zerodivide,

              constructor IMPORTING j TYPE i.                   &quot;instance constructor with importing parameter

      CLASS-METHODS: stat_meth1,                                &quot;static methods  

                     stat_meth2 IMPORTING k TYPE i              
                                EXPORTING l TYPE i,

                     class_constructor,                         &quot;static constructor

                     &quot;Options of formal parameter definitions
                     stat_meth3 IMPORTING VALUE(m) TYPE i,       &quot;pass by value
                     stat_meth4 IMPORTING REFERENCE(n) TYPE i,   &quot;pass by reference
                     stat_meth5 IMPORTING o TYPE i,              &quot;same as n; the specification of REFERENCE(...) is optional
                     stat_meth6 RETURNING VALUE(p) TYPE i,       &quot;pass by value once more (note: it&#x27;s the only option for returning parameters)

                     &quot;OPTIONAL/DEFAULT additions
                     stat_meth7 IMPORTING q TYPE i DEFAULT 123
                                          r TYPE i OPTIONAL,

                     &quot;The examples above use a complete type for 
                     &quot;the parameter specification. Generic types
                     &quot;are possible.
                     stat_meth8 IMPORTING s TYPE any           &quot;Any data type
                                          t TYPE any table     &quot;Any internal table type                 
                                          u TYPE clike.        &quot;Character-like types (c, n, string, d, t and character-like flat structures)

ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
   METHOD inst_meth1.
      ...
   ENDMETHOD.

  ... &quot;Further method implementations. Note that all declared methods must go here.
ENDCLASS.</code></pre>
<p>The following code snippet (which anticipates aspects described in the following sections, such as specifying the method signature, constructors etc.) shows multiple method definitions in the public section of a global class. Most of the formal parameters of the demo methods below are defined by just using the parameter name. This means passing by reference (returning parameters require to be passed by value).</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Parameter Interface</h4>
<p>In the simplest form, methods can have no parameter at all. Apart from that, methods can be defined with the following parameters:</p>
<table>
<thead><tr>
<th>Addition</th>
<th>Details</th>
</tr></thead>
<tbody>
<tr>
<td><code>IMPORTING</code></td>
<td>Defines one or more input parameters to be imported by the method.</td>
</tr>
<tr>
<td><code>EXPORTING</code></td>
<td>Defines one or more output parameters to be exported by the method.</td>
</tr>
<tr>
<td><code>CHANGING</code></td>
<td>Defines one or more input or output parameters, i. e. that can be both imported and exported.</td>
</tr>
<tr>
<td><code>RETURNING</code></td>
<td>For <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfunctional_method_glosry.htm">functional methods</a>, i. e. such methods have only one <code>RETURNING</code> parameter that can be defined. As an output parameter like the <code>EXPORTING</code> parameter, <code>RETURNING</code> parameters pass back values (note that the formal parameters of returning parameters must be passed by value as covered below; the parameter must be <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencomplete_typing_glosry.htm">completely typed</a>). In contrast to <code>EXPORTING</code> for which multiple parameters can be specified, only one <code>RETURNING</code> parameter can be specified in a method. If you only need one output parameter, you can benefit from using a <code>RETURNING</code> parameter by shortening the method call and enabling method chaining. Another big plus is that such functional methods can, for example, be used in expressions. In case of standalone method calls, the returned value can be accessed using the addition <code>RECEIVING</code>.</td>
</tr>
<tr>
<td><code>RAISING</code></td>
<td>Used to declare the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclass_based_exception_glosry.htm">class-based exceptions</a> that can be propagated from the method to the caller. It can also be specified with the addition <code>RESUMABLE</code> for <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenresumable_exception_glosry.htm">resumable exceptions</a>. Find more information in the <a href="27_Exceptions.md">Exceptions and Runtime Errors</a> cheat sheet.</td>
</tr>
</tbody>
</table>
<div class="note">[!NOTE] - It is advisable to avoid specifying multiple different output parameters (exporting, returning, changing) in a signature to reduce complexity. - Find more information [here](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_general.htm). - You may find the addition `EXCEPTIONS` especially in definitions of older classes. They are for non-class-based exceptions. This addition should not be used in ABAP for Cloud Development. See the section [Class-Based Exceptions](#class-based-exceptions), and the section [Classic Exceptions](27_Exceptions.md#classic-exceptions) in the [Exceptions and Runtime Errors](27_Exceptions.md) cheat sheet.</div>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Formal and Actual Parameters</h4>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenformal_parameter_glosry.htm">Formal parameters</a>: You define method parameters by specifying a name with a type which can be a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abengeneric_data_type_glosry.htm">generic</a> or  <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencomplete_data_type_glosry.htm">complete</a></li>
</ul>
<p>type.</p>
<ul>
<li>Examples:</li>
<li><code>fp</code> is the formal parameter that has a complete type: <code>... meth IMPORTING fp TYPE string ...</code></li>
<li><code>gen</code> is the formal parameter that has a generic type: <code>... meth IMPORTING gen TYPE any ...</code></li>
<li>Find more information about generic types also in the <a href="16_Data_Types_and_Objects.md#generic-types">Data Types and Data Objects</a> cheat sheet.</li>
<li>This formal parameter includes the specification of how the value passing should happen. Parameters can be passed by ...</li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenpass_by_reference_glosry.htm">reference</a>: <code>... REFERENCE(param) ...</code>; note that just specifying the parameter name <code>... param ...</code> - as a shorter syntax - means passing by reference by default)</li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenpass_by_value_glosry.htm">value</a>: <code>... VALUE(param) ...</code></li>
<li>An <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenactual_parameter_glosry.htm">actual parameter</a> represents the data object whose content is passed to or copied from a formal parameter as an argument when a procedure is called.</li>
<li>If passing by reference is used, a local data object is not created for the actual parameter. Instead, the procedure is given a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenreference_glosry.htm">reference</a> to the actual parameter during the call and works with the actual parameter itself.</li>
<li>Note that parameters that are input and passed by reference cannot be modified in the procedure. However, the use of a reference is beneficial regarding the performance compared to creating a local data object.</li>
</ul>
<p>The following example (which anticipates aspects described in the following sections, such as calling methods) shows a class with a simple method demonstrating the syntax for formal parameter specifications. Complete types are used.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

    &quot;Passing by reference and value
    METHODS: meth IMPORTING i_a            TYPE i
                            REFERENCE(i_b) TYPE string
                            VALUE(i_c)     TYPE i
                  RETURNING VALUE(r_a)     TYPE string.


  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;The values 1, hello, and 2 are actual parameters supplied when 
    &quot;the method is called.
    DATA(result) = meth( i_a = 1
                         i_b = `hello`
                         i_c = 2 ).

  ENDMETHOD.

  METHOD meth.
    ... &quot;Method implementation

    &quot;Input parameters passed by reference cannot be changed in the method implementation.
    &quot;i_a += 1.
    &quot;i_b &amp;&amp;= ` world`.

    &quot;Input parameters passed by value can be changed in the method implementation.
    i_c += 1.

    r_a = `ABAP`.

  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Complete Typing of Formal Parameters</h4>
<p>Syntax for <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencomplete_data_type_glosry.htm">completely</a> typing a formal parameter:</p>
<ul>
<li><code>TYPE complete_type</code></li>
<li><code>TYPE LINE OF complete_type</code></li>
<li><code>TYPE REF TO type</code></li>
<li><code>LIKE dobj</code></li>
<li><code>LIKE LINE OF dobj</code></li>
<li><code>LIKE REF TO dobj</code></li>
</ul>
<div class="note">[!NOTE] - `complete_type`: Stands for a non-generic built-in ABAP, ABAP DDIC, ABAP CDS, a public data type from a global class or interface, or a local type declared with `TYPES` - `REF TO` types as a reference variable. A generic type cannot be specified after `REF TO`. A typing with `TYPE REF TO data` and `TYPE REF TO object` is considered as completely typing a formal parameter. - Enumerated types can also be used to type the formal parameter. - The considerations also apply to the typing of field symbols.</div>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

    &quot;Local types and data objects used in the example
    TYPES c3 TYPE c LENGTH 3.
    TYPES der_type TYPE TABLE FOR CREATE zdemo_abap_rap_ro_m.
    DATA int TYPE i.
    DATA itab TYPE TABLE OF zdemo_abap_fli_ve WITH EMPTY KEY.

    &quot;Various syntax options for completely typing formal parameters
    &quot;Note: The example parameters are all specified for passing
    &quot;actual parameters by reference.
    METHODS: meth IMPORTING
                    &quot;Non-generic built-in ABAP types
                    i_a TYPE i
                    i_b TYPE string
                    &quot;ABAP DDIC types
                    i_c TYPE land1           &quot;elementary type
                    i_d TYPE timestampl      &quot;elementary type
                    i_e TYPE zdemo_abap_fli &quot;structured type based on DDIC database table
                    i_f TYPE string_hashed_table &quot;table type
                    &quot;ABAP CDS types (all of the examples are structured types)
                    i_g TYPE zdemo_abap_fli_ve &quot;CDS view entity
                    i_h TYPE zdemo_abap_abstract_ent &quot;CDS abstract entity
                    i_i TYPE zdemo_abap_table_function &quot;CDS table function
                    &quot;Data types declared in public section of a class
                    i_j TYPE zcl_demo_abap_dtype_dobj=&gt;t_pub_text_c30 &quot;elementary type
                    i_k TYPE zcl_demo_abap_amdp=&gt;carr_fli_struc &quot;structured type
                    i_l TYPE zcl_demo_abap_amdp=&gt;carr_fli_tab &quot;table type
                    &quot;Data types declared in an interface
                    i_m TYPE zdemo_abap_get_data_itf=&gt;occ_rate &quot;elementary type
                    i_n TYPE zdemo_abap_get_data_itf=&gt;carr_tab &quot;table type
                    &quot;Local types
                    i_o TYPE c3 &quot;elementary type
                    i_p TYPE der_type &quot;table type (BDEF derived type)
                    &quot;Note: Examples such as the following are not allowed type specifications of formal parameters.
                    &quot;In the following cases, extra (local) type declarations with TYPES are required before the
                    &quot;method declaration to type the formal parameters.
                    &quot;i_no1 TYPE c LENGTH 3
                    &quot;i_no2 TYPE TABLE OF zdemo_abap_fli WITH EMPTY KEY
                    &quot;Reference types
                    i_q TYPE REF TO i &quot;Data reference
                    i_r TYPE REF TO zdemo_abap_carr &quot;Data reference
                    i_s TYPE REF TO zcl_demo_abap_unit_test &quot;Object reference
                    i_t TYPE REF TO data &quot;Data reference (considered as complete typing, too)
                    i_u TYPE REF TO object &quot;Object reference (considered as complete typing, too)
                    &quot;TYPE LINE OF addition (structured type based on a table type)
                    i_v TYPE LINE OF zcl_demo_abap_amdp=&gt;carr_fli_tab
                    i_w TYPE LINE OF der_type
                    &quot;LIKE addition (types based on existing data objects)
                    i_x LIKE int &quot;Local data object
                    i_y LIKE zcl_demo_abap_dtype_dobj=&gt;comma &quot;Constant specified in a class
                    i_z LIKE zdemo_abap_objects_interface=&gt;stat_str &quot;Data object specified in an interface
                    &quot;LIKE LINE OF addition (types based on existing internal tables)
                    i_1 LIKE LINE OF itab &quot;Local internal table
                    &quot;LIKE REF TO addition (reference types based on existing data object)
                    i_2 LIKE REF TO int &quot;Local elementary data object
                    i_3 LIKE REF TO itab &quot;Local internal table
                  .

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;Calling methods and providing actual parameters
*    meth(
*      i_a = 1
*      i_b = `hello`
*      ... ).

  ENDMETHOD.

  METHOD meth.
    ... &quot;Method implementation
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Generic Typing of Formal Parameters</h4>
<p>Find more information on generic types in the <a href="16_Data_Types_and_Objects.md#generic-types">Data Types and Data Objects</a> cheat sheet. The following code snippet anticipates aspects described in the following sections, such as calling methods.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

    &quot;Example method demonstrating the generic typing of formal parameters
    METHODS: meth IMPORTING
                    &quot;Any data type
                    i_data           TYPE data
                    i_any            TYPE any

                    &quot;Character-like types
                    i_c              TYPE c         &quot;Text field with a generic length
                    i_clike          TYPE clike     &quot;Character-like (c, n, string, d, t, and character-like flat structures)
                    i_csequence      TYPE csequence &quot;Text-like (c, string)
                    i_n              TYPE n         &quot;Numeric text with generic length
                    i_x              TYPE x         &quot;Byte field with generic length
                    i_xsequence      TYPE xsequence &quot;Byte-like (x, xstring)

                    &quot;Numeric types
                    i_decfloat       TYPE decfloat &quot;decfloat16 decfloat34
                    i_numeric        TYPE numeric  &quot;Numeric (i, int8, p, decfloat16, decfloat34, f, (b, s))
                    i_p              TYPE p        &quot;Packed number (generic length and number of decimal places)

                    &quot;Internal table types
                    i_any_table      TYPE ANY TABLE      &quot;Internal table with any table type
                    i_hashed_table   TYPE HASHED TABLE
                    i_index_table    TYPE INDEX TABLE
                    i_sorted_table   TYPE SORTED TABLE
                    i_standard_table TYPE STANDARD TABLE
                    i_table          TYPE table          &quot;Standard table

                    &quot;Other types
                    i_simple         TYPE simple &quot;Elementary data type including enumerated types and
                    &quot;structured types with exclusively character-like flat components
                  .
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;Structure including various components of specific types
    &quot;They represent actual parameters in the method call below
    DATA: BEGIN OF s,
            c3        TYPE c LENGTH 3,
            c10       TYPE c LENGTH 10,
            n4        TYPE n LENGTH 4,
            str       TYPE string,
            time      TYPE t,
            date      TYPE d,
            dec16     TYPE decfloat16,
            dec34     TYPE decfloat34,
            int       TYPE i,
            pl4d2     TYPE p LENGTH 4 DECIMALS 2,
            tab_std   TYPE STANDARD TABLE OF string WITH EMPTY KEY,
            tab_so    TYPE SORTED TABLE OF string WITH NON-UNIQUE KEY table_line,
            tab_ha    TYPE HASHED TABLE OF string WITH UNIQUE KEY table_line,
            xl1       TYPE x LENGTH 1,
            xstr      TYPE xstring,
            structure TYPE zdemo_abap_carr, &quot;character-like flat structure
          END OF s.

    &quot;The following method call specifies various actual parameters for the
    &quot;generic formal parameters.
    &quot;Note the comments for allowed and not allowed example assignments of
    &quot;actual parameters.
    meth(
      &quot;------------- Any data type -------------
      &quot;--- data/any: Allowed (examples) ---
      i_data = s-c3
      &quot;i_data = s-time
      &quot;i_data = s-tab_std
      &quot;i_data = s-xstr

      i_any = s-c3
      &quot;i_any = s-time
      &quot;i_any = s-tab_std
      &quot;i_any = s-xstr

      &quot;------------- Character-like types -------------
      &quot;--- c: Allowed (examples) ---
      i_c = s-c3
      &quot;i_c = s-c10
      &quot;--- c: Not allowed (examples) ---
      &quot;i_c = s-str
      &quot;i_c = s-n4

      &quot;--- clike: Allowed (examples) ---
      i_clike = s-c3
      &quot;i_clike = s-c10
      &quot;i_clike = s-str
      &quot;i_clike = s-structure
      &quot;i_clike = s-time
      &quot;i_clike = s-date
      &quot;i_clike = s-n4
      &quot;--- clike: Not allowed (examples) ---
      &quot;i_clike = s-xstr
      &quot;i_clike = s-xl1
      &quot;i_clike = s-pl4d2

      &quot;--- csequence: Allowed (examples) ---
      i_csequence  = s-c3
      &quot;i_csequence  = s-c10
      &quot;i_csequence  = s-str
      &quot;--- csequence: Not allowed (examples) ---
      &quot;i_csequence  = s-time
      &quot;i_csequence  = s-date
      &quot;i_csequence  = s-structure

      &quot;--- n: Allowed ---
      i_n = s-n4
      &quot;--- n: Not allowed (examples) ---
      &quot;i_n = s-c3
      &quot;i_n = s-int

      &quot;--- x: Allowed ---
      i_x = s-xl1
      &quot;--- x: Not allowed (examples) ---
      &quot;i_x = s-xstr
      &quot;i_x = s-c3

      &quot;--- xsequence: Allowed ---
      i_xsequence = s-xstr
      &quot;i_xsequence = s-xl1
      &quot;--- xsequence: Not allowed (examples) ---
      &quot;i_xsequence = s-c3
      &quot;i_xsequence = s-str

      &quot;--- decfloat: Allowed ---
      i_decfloat = s-dec16
      &quot;i_decfloat = s-dec34
      &quot;--- decfloat: Not allowed (examples) ---
      &quot;i_decfloat = s-int
      &quot;i_decfloat = s-pl4d2

      &quot;--- numeric: Allowed (examples) ---
      i_numeric  = s-int
      &quot;i_numeric = s-dec16
      &quot;i_numeric = s-dec34
      &quot;i_numeric = s-pl4d2
      &quot;--- numeric: Not allowed (examples) ---
      &quot;i_numeric = s-n4
      &quot;i_numeric = s-date

      &quot;--- p: Allowed ---
      i_p = s-pl4d2
      &quot;--- p: Not allowed (examples) ---
      &quot;i_p = s-dec16
      &quot;i_p = s-dec34

      &quot;--- any table: Allowed ---
      i_any_table = s-tab_std
      &quot;i_any_table = s-tab_ha
      &quot;i_any_table = s-tab_so
      &quot;--- any table: Not allowed (examples) ---
      &quot;i_any_table = s-structure
      &quot;i_any_table = s-c3

      &quot;--- hashed table: Allowed ---
      i_hashed_table = s-tab_ha
      &quot;--- hashed table: Not allowed ---
      &quot;i_hashed_table = s-tab_std
      &quot;i_hashed_table = s-tab_so

      &quot;--- index table: Allowed ---
      i_index_table = s-tab_std
      &quot;i_index_table = s-tab_so
      &quot;--- index table: Not allowed ---
      &quot;i_index_table = s-tab_ha

      &quot;--- sorted table: Allowed ---
      i_sorted_table = s-tab_so
      &quot;--- sorted table: Not allowed ---
      &quot;i_sorted_table = s-tab_std
      &quot;i_sorted_table = s-tab_ha

      &quot;--- standard table/table: Allowed ---
      i_standard_table = s-tab_std
      i_table = s-tab_std
      &quot;--- standard table/table: Not allowed ---
      &quot;i_standard_table = s-tab_so
      &quot;i_standard_table = s-tab_ha
      &quot;i_table = s-tab_so
      &quot;i_table = s-tab_ha

     &quot;--- simple: Allowed (examples) ---
      i_simple = s-structure
      &quot;i_simple = s-c3
      &quot;i_simple = s-n4
      &quot;i_simple = s-int
      &quot;i_simple = s-pl4d2
      &quot;i_simple = s-xstr
      &quot;i_simple = s-str
      &quot;--- simple: Not allowed (examples) ---
      &quot;i_simple = s-tab_ha
      &quot;i_simple = s-tab_so

       ).

  ENDMETHOD.

  METHOD meth.
    ... &quot;Method implementation
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Defining Parameters as Optional</h4>
<ul>
<li>Parameters specified after <code>IMPORTING</code> and <code>CHANGING</code> can be defined as optional using the <code>OPTIONAL</code> and <code>DEFAULT</code> additions:</li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_parameters.htm#!ABAP_ONE_ADD@1@"><code>OPTIONAL</code></a>: It is then not mandatory to pass an actual</li>
</ul>
<p>parameter.</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_parameters.htm#!ABAP_ONE_ADD@1@"><code>DEFAULT</code></a>: Also makes the passing of an actual parameter optional. However, when using this addition, as the name implies, a default value is set.</li>
<li>In the method implementations you may want to check whether an actual parameter was passed. You can use predicate expressions using <code>IS SUPPLIED</code>. See the example further down.</li>
</ul>
<p>The following example (which anticipates aspects described in the following sections, such as calling methods) includes three methods that specify optional parameters. The methods are called with and without providing actual parameters. The method implementations include the use of the predicate expression <code>IS SUPPLIED</code> with <code>IF</code> statements and the <code>COND</code> operator. The console output of the example, run with F9, is as follows:</p>
<pre><code>meth1_result_a                                    
The parameter is not supplied. Initial value: &quot;0&quot;.
meth1_result_b                        
The parameter is supplied. Value: &quot;2&quot;.
meth2_result_a                                    
The parameter is not supplied. Default value: &quot;1&quot;.
meth2_result_b                        
The parameter is supplied. Value: &quot;3&quot;.
meth3_result_b                                                                                      
num1: &quot;4&quot; / num2 (is not supplied; initial value): &quot;0&quot; / num3 (is not supplied; default value): &quot;1&quot; 
meth3_result_c                                                                   
num1: &quot;5&quot; / num2 (is supplied): &quot;6&quot; / num3 (is not supplied; default value): &quot;1&quot; 
meth3_result_d                                                                   
num1: &quot;7&quot; / num2 (is not supplied; initial value): &quot;0&quot; / num3 (is supplied): &quot;8&quot; </code></pre>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    METHODS meth1 IMPORTING num        TYPE i OPTIONAL
                  RETURNING VALUE(str) TYPE string.
    METHODS meth2 IMPORTING num        TYPE i DEFAULT 1
                  RETURNING VALUE(str) TYPE string.
    METHODS meth3 IMPORTING num1       TYPE i
                            num2       TYPE i OPTIONAL
                            num3       TYPE i DEFAULT 1
                  RETURNING VALUE(str) TYPE string.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA(meth1_result_a) = meth1( ).
    DATA(meth1_result_b) = meth1( 2 ).

    DATA(meth2_result_a) = meth2( ).
    DATA(meth2_result_b) = meth2( 3 ).

    &quot;The commented out statement is not possible as there is one
    &quot;non-optional parameter.
    &quot;DATA(meth3_result_a) = meth3( ).
    DATA(meth3_result_b) = meth3( 4 ).
    DATA(meth3_result_c) = meth3( num1 = 5 num2 = 6 ).
    DATA(meth3_result_d) = meth3( num1 = 7 num3 = 8 ).

    out-&gt;write( data = meth1_result_a name = `meth1_result_a` ).
    out-&gt;write( data = meth1_result_b name = `meth1_result_b` ).
    out-&gt;write( data = meth2_result_a name = `meth2_result_a` ).
    out-&gt;write( data = meth2_result_b name = `meth2_result_b` ).
    out-&gt;write( data = meth3_result_b name = `meth3_result_b` ).
    out-&gt;write( data = meth3_result_c name = `meth3_result_c` ).
    out-&gt;write( data = meth3_result_d name = `meth3_result_d` ).

  ENDMETHOD.

  METHOD meth1.
    IF num IS SUPPLIED.
      str = |The parameter is supplied. Value: &quot;{ num }&quot;.|.
    ELSE.
      str = |The parameter is not supplied. Initial value: &quot;{ num }&quot;.|.
    ENDIF.
  ENDMETHOD.

  METHOD meth2.
    str = COND #( WHEN num IS SUPPLIED THEN |The parameter is supplied. Value: &quot;{ num }&quot;.|
                  ELSE |The parameter is not supplied. Default value: &quot;{ num }&quot;.|  ).
  ENDMETHOD.

  METHOD meth3.
    str = |num1: &quot;{ num1 }&quot; / |.

    str &amp;&amp;= |{ COND #( WHEN num2 IS SUPPLIED THEN |num2 (is supplied): &quot;{ num2 }&quot;|
                       ELSE |num2 (is not supplied; initial value): &quot;{ num2 }&quot;| ) } / |.

    str &amp;&amp;= |{ COND #( WHEN num3 IS SUPPLIED THEN |num3 (is supplied): &quot;{ num3 }&quot;|
                       ELSE |num3 (is not supplied; default value): &quot;{ num3 }&quot;| ) } |.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Defining Input Parameters as Preferred</h4>
<ul>
<li>Using the <code>PREFERRED PARAMETER</code> addition, you can flag an input parameter from the list as preferred.</li>
<li>All parameters must be optional (i.e. specified with <code>OPTIONAL</code> or <code>DEFAULT</code>).</li>
<li>The preferred parameter is implicitly optional, but you should explicitly specify it as <code>OPTIONAL</code> or <code>DEFAULT</code>, or a warning will be displayed.</li>
<li>When you call a method and specify a single actual parameter without specifying the name of the formal parameter in an assignment, the actual parameter is automatically assigned to the preferred parameter.</li>
</ul>
<p>The following example (which anticipates aspects described in the following sections, such as calling methods) shows a simple method with input parameters of type <code>i</code> (an addition is performed using the actual parameter values), where one parameter is preferred. The <code>IS SUPPLIED</code> addition in <code>COND</code> statements checks whether parameters are supplied. The final output shows the preferred parameter assigned automatically when the formal parameter is not specified explicitly. To try the example out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console. The example should display the following:</p>
<pre><code>IS SUPPLIED: num1 &quot;X&quot;, num2 &quot;X&quot;, num3 &quot;X&quot; / Addition result &quot;9&quot;
IS SUPPLIED: num1 &quot;X&quot;, num2 &quot;X&quot;, num3 &quot;&quot; / Addition result &quot;7&quot;
IS SUPPLIED: num1 &quot;&quot;, num2 &quot;X&quot;, num3 &quot;X&quot; / Addition result &quot;6&quot;
IS SUPPLIED: num1 &quot;X&quot;, num2 &quot;&quot;, num3 &quot;X&quot; / Addition result &quot;6&quot;
IS SUPPLIED: num1 &quot;&quot;, num2 &quot;X&quot;, num3 &quot;&quot; / Addition result &quot;4&quot;
IS SUPPLIED: num1 &quot;&quot;, num2 &quot;&quot;, num3 &quot;X&quot; / Addition result &quot;3&quot;
IS SUPPLIED: num1 &quot;&quot;, num2 &quot;&quot;, num3 &quot;&quot; / Addition result &quot;1&quot;
IS SUPPLIED: num1 &quot;X&quot;, num2 &quot;&quot;, num3 &quot;&quot; / Addition result &quot;4&quot;</code></pre>
<p>Example code:</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

    CLASS-METHODS meth
      IMPORTING num1        TYPE i OPTIONAL
                num2        TYPE i OPTIONAL
                num3        TYPE i DEFAULT 1
                PREFERRED PARAMETER num1
      RETURNING VALUE(text) TYPE string.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA(text1) = meth( num1 = 3 num2 = 3 num3 = 3 ).
    out-&gt;write( text1 ).

    DATA(text2) = meth( num1 = 3 num2 = 3 ).
    out-&gt;write( text2 ).

    DATA(text3) = meth( num2 = 3 num3 = 3 ).
    out-&gt;write( text3 ).

    DATA(text4) = meth( num1 = 3 num3 = 3 ).
    out-&gt;write( text4 ).

    DATA(text5) = meth( num2 = 3 ).
    out-&gt;write( text5 ).

    DATA(text6) = meth( num3 = 3 ).
    out-&gt;write( text6 ).

    DATA(text7) = meth( ).
    out-&gt;write( text7 ).

    &quot;Not specifying the name of the formal parameter. The
    &quot;actual parameter is assigned to the preferred input
    &quot;parameter.
    DATA(text8) = meth( 3 ).
    out-&gt;write( text8 ).
  ENDMETHOD.

  METHOD meth.
    DATA(addition) = num1 + num2 + num3.
    text = |IS SUPPLIED: num1 &quot;{ COND #( WHEN num1 IS SUPPLIED THEN &#x27;X&#x27; ELSE &#x27;&#x27; ) }&quot;, | &amp;&amp;
           |num2 &quot;{ COND #( WHEN num2 IS SUPPLIED THEN &#x27;X&#x27; ELSE &#x27;&#x27; ) }&quot;, | &amp;&amp;
           |num3 &quot;{ COND #( WHEN num3 IS SUPPLIED THEN &#x27;X&#x27; ELSE &#x27;&#x27; ) }&quot; / | &amp;&amp;
           |Addition result &quot;{ addition }&quot;|.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Constructors</h4>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_glosry.htm">Constructors</a></li>
</ul>
<p>are special methods that are usually used for setting a defined initial value for attributes of the class or its objects.</p>
<ul>
<li>A class has exactly one [instance</li>
</ul>
<p>constructor](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninstance_constructor_glosry.htm &quot;Glossary Entry&quot;) and one <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstatic_constructor_glosry.htm">static constructor</a>.</p>
<ul>
<li>The declaration and use of constructors is optional.</li>
<li>Static constructor:</li>
<li>Declared using the predefined name <code>class_constructor</code> as part of a</li>
</ul>
<p><code>CLASS-METHODS</code> statement in the public visibility section.</p>
<ul>
<li>Has no parameters.</li>
<li>Automatically and immediately called once for each class when calling a class for</li>
</ul>
<p>the first time in an internal session, i. e. when, for example, an instance of a class is created or a component is used. Note: If it is not explicitly declared and implemented, it is merely an empty method.</p>
<ul>
<li>Instance constructor:</li>
<li>Declared using the predefined name <code>constructor</code> as part of a</li>
</ul>
<p><code>METHODS</code> statement. In case of global classes, it can only be declared in the public visibility section.</p>
<ul>
<li>Automatically called when a class is</li>
</ul>
<p>instantiated and an instance is created.</p>
<ul>
<li>Can have <code>IMPORTING</code> parameters and raise exceptions.</li>
</ul>
<p>The following example (which anticipates aspects described in the following sections, such as creating instances of classes) demonstrates static and instance constructors. To try the example out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console.</p>
<p>Notes:</p>
<ul>
<li>The example class defines the instance and static constructors.</li>
<li>The instance constructor includes an optional importing parameter, which static constructors do not allow. If the parameter were not optional, the class would not run with F9, as it could not pass an actual parameter. In the example, the actual parameter indicates the name of the instance created for output purposes.</li>
<li>The example class creates multiple instances.</li>
<li>Both instance and static attributes of each instance are accessed and added to an internal table, which is then output.</li>
<li>The constructor implementations include:</li>
<li>Instance constructor:</li>
<li>Stores the current timestamp in an instance attribute.</li>
<li>Maintains a call counter for the number of times the instance constructor is called, stored in a static attribute to reflect the count per internal session.</li>
<li>Stores the manually provided instance name in an instance attribute.</li>
<li>Static constructor:</li>
<li>Stores the current timestamp in a static attribute.</li>
<li>Maintains a call counter for static constructor calls, stored in a static attribute.</li>
<li>The constructors demonstrate:</li>
<li>The static constructor is called only once, even when multiple class instances are created, leading to a constant <code>static_timestamp</code> value and <code>stat_constr_call_count</code> value, which remains 1.</li>
<li>The <code>instance_timestamp</code> attribute shows different timestamps for each created instance.</li>
<li>The static attribute <code>instance_constr_call_count</code> increases with each instance. Note that running the class with F9 in ADT also calls the instance and static constructors. Thus, the final <code>instance_constr_call_count</code> totals the number of <code>DO</code> loop passes plus 1, starting with 2 for <code>inst1</code> instead of 1.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    METHODS constructor IMPORTING text TYPE string OPTIONAL.
    CLASS-METHODS class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
    DATA instance_timestamp TYPE utclong.
    CLASS-DATA static_timestamp TYPE utclong.
    DATA instance_name TYPE string.
    CLASS-DATA stat_constr_call_count TYPE i.
    CLASS-DATA instance_constr_call_count TYPE i.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA itab TYPE string_table.

    DO 5 TIMES.
      DATA(inst) = NEW zcl_demo_abap( |inst{ sy-index }| ).
      APPEND |-------------- Instance &quot;{ inst-&gt;instance_name }&quot; --------------| TO itab.
      APPEND |instance_timestamp: { inst-&gt;instance_timestamp }| TO itab.
      APPEND |static_timestamp: { inst-&gt;static_timestamp }| TO itab.
      APPEND |instance_constr_call_count: { inst-&gt;instance_constr_call_count }| TO itab.
      APPEND |stat_constr_call_count: { inst-&gt;stat_constr_call_count }| TO itab.
      APPEND INITIAL LINE TO itab.
    ENDDO.

    out-&gt;write( itab ).
  ENDMETHOD.

  METHOD class_constructor.
    static_timestamp = utclong_current( ).
    stat_constr_call_count += 1.
  ENDMETHOD.

  METHOD constructor.
    instance_timestamp = utclong_current( ).
    instance_constr_call_count += 1.

    IF text IS SUPPLIED AND text IS NOT INITIAL.
      instance_name = text.
    ENDIF.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Working with Objects and Components</h2>
<h3>Declaring Object Reference Variables</h3>
<ul>
<li>To create an object, an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_refer_variable_glosry.htm">object reference variable</a> must be declared.</li>
<li>It is also necessary for accessing objects and their components. That means objects are not directly accessed but only via references that point to</li>
</ul>
<p>those objects.</p>
<ul>
<li>This object reference variable contains the reference to the object - after assigning the reference to the object (see further down).</li>
</ul>
<pre><code>&quot;Declaring object reference variables
DATA: ref1 TYPE REF TO local_class,
      ref2 TYPE REF TO global_class,
      ref3 LIKE ref1.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Creating Objects</h3>
<ul>
<li>Using the instance operator</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_expression_new.htm"><code>NEW</code></a>, you can create objects of a class (and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenanonymous_data_object_glosry.htm">anonymous data objects</a>, too, that are not dealt with here). As a result, you get a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenreference_variable_glosry.htm">reference variable</a> that points to the created object.</p>
<ul>
<li>Regarding the type specifications before and parameters within the</li>
</ul>
<p>parentheses:</p>
<ul>
<li>Right before the first parenthesis after <code>NEW</code>, the type, i. e. the class, must be specified. The <code>#</code> character - instead of the class name -</li>
</ul>
<p>means that the type (<code>TYPE REF TO ...</code>) can be derived from the context (in this case from the type of the reference variable). You can also omit the explicit declaration of a reference variable by declaring a new reference variable <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendata_inline.htm">inline</a>, for example, using <code>DATA</code>. In this case, the name of the class must be placed after <code>NEW</code> and before the first parenthesis.</p>
<ul>
<li>No parameter specified within the parentheses: No values are</li>
</ul>
<p>passed to the instance constructor of an object. However, non-optional input parameters of the instance constructor of the instantiated class must be filled. No parameters are passed for a class without an explicitly declared instance constructor. See more information: <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abennew_constructor_params_class.htm">here</a>.</p>
<ul>
<li>The operator</li>
</ul>
<p>basically replaces the syntax <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcreate_object.htm"><code>CREATE OBJECT</code></a> you might stumble on. However, <code>CREATE OBJECT</code> statements are still required (i.e. they are the only option, <code>NEW</code> is not possible for them) for creating objects dynamically. For more information, see the <a href="06_Dynamic_Programming.md">Dynamic Programming</a> cheat sheet.</p>
<pre><code>&quot;Declaring object reference variable
DATA: ref1 TYPE REF TO some_class.

&quot;Creating objects
ref1 = NEW #( ).                     &quot;Type derived from already declared ref1

DATA(ref2) = NEW some_class( ).      &quot;Reference variable declared inline, explicit type
                                     &quot;(class) specification
&quot;The assumption is that this class has no mandatory importing parameters for the 
&quot;instance constructor. If a class has, actual parameters must be provided.                                      
DATA(ref_mand_param) = NEW another_class( ip1 = ... ip2 = ... ).

&quot;Older syntax, replaced by NEW operator 
&quot;However, CREATE OBJECT is required in dynamic object creation.
CREATE OBJECT ref3.                  &quot;Type derived from already declared ref3
CREATE OBJECT ref4 TYPE some_class.  &quot;Corresponds to the result of the expression above</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Working with Reference Variables</h3>
<p>This section covers some aspects of working with reference variables. Find a copyable example demonstrating the aspects in the next section.</p>
<p><strong>Assigning Reference Variables</strong></p>
<p>To assign or copy reference variables, use the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenassignment_operator_glosry.htm">assignment operator</a> <code>=</code>. In the example below, both object reference variables have the same type. Note the concepts of polymorphism, upcasts and downcasts when assigning reference variables covered further down.</p>
<pre><code>DATA: ref1 TYPE REF TO some_class,
      ref2 TYPE REF TO some_class.

ref1 = NEW #( ).

&quot;Assigning existing reference
ref2 = ref1.</code></pre>
<pre><code>DATA ref TYPE REF TO some_class.

&quot;Creating a new object
&quot;The example assumes there is a mandatory importing parameter for the 
&quot;instance constructor.
ref = NEW #( 1 ).

&quot;Creating another object using the same object reference variable
&quot;The existing reference is overwritten.
ref = NEW #( 1 ).</code></pre>
<p><strong>Overwriting reference variables</strong>: An <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_reference_glosry.htm">object reference</a> is overwritten when a new object is created with a reference variable already pointing to an instance.</p>
<p><strong>Retaining object references</strong>:</p>
<pre><code>DATA: ref TYPE REF TO some_class,
      itab TYPE TABLE OF REF TO some_class.

DO 3 TIMES.
  ref = NEW #( ).
  itab = VALUE #( BASE itab ( ref ) ).   &quot;Adding the reference to itab
ENDDO.</code></pre>
<ul>
<li>If your use case is to retain the object references, for example, if you create multiple objects using the same object reference variable, you can put the reference variables in internal tables that are declared using <code>... TYPE TABLE OF REF TO ...</code>.</li>
<li>The following code snippet just visualizes that the object references are not overwritten. Three objects are created with the same reference variable. The internal table includes all object references and, thus, their values are retained.</li>
</ul>
<pre><code>CLEAR ref.</code></pre>
<p><strong>Clearing object references</strong>: You can use <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclear.htm"><code>CLEAR</code></a> statements to explicitly clear a reference variable.</p>
<div class="note">[!NOTE] Objects use up space in the memory and should therefore be</div>
<p>cleared if they are no longer needed. However, the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abengarbage_collector_glosry.htm">garbage collector</a> is called periodically and automatically by the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_runtime_frmwk_glosry.htm">ABAP runtime framework</a> and clears all objects without any reference.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Accessing Attributes</h3>
<ul>
<li>Instance attributes: Accessed using</li>
</ul>
<p>the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenobject_component_select_glosry.htm">object component selector</a> <code>-&gt;</code> via a reference variable.</p>
<ul>
<li>Static attributes: Accessed (if the attributes are visible) using the [class component</li>
</ul>
<p>selector](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenclass_component_select_glosry.htm &quot;Glossary Entry&quot;) <code>=&gt;</code> via the class name. Static attributes can but should not be addressed via a reference variable.</p>
<pre><code>&quot;Accessing instance attribute via an object reference variable
... ref-&gt;some_attribute ...

&quot;Accessing static attributes via the class name
... some_class=&gt;static_attribute ...

&quot;Without the class name only within the class itself
... static_attribute ...

&quot;Type and data object declarations
TYPES some_type LIKE some_class=&gt;some_static_attribute.
DATA dobj1      TYPE some_class=&gt;some_type.
DATA dobj2      LIKE some_class=&gt;some_static_attribute.</code></pre>
<p>The following executable example addresses aspects of this section and the previous one:</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    METHODS constructor IMPORTING ts TYPE utclong OPTIONAL.
    DATA timestamp TYPE utclong.
    DATA instance_number TYPE i.
    CLASS-DATA static_number TYPE i.
    TYPES chars TYPE c LENGTH 4.
    CONSTANTS text TYPE chars VALUE `ABAP`.
  PROTECTED SECTION.
  PRIVATE SECTION.
    CLASS-METHODS test_static_method.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    DATA oref TYPE REF TO zcl_demo_abap.
    DATA oref2 TYPE REF TO zcl_demo_abap.

*&amp;---------------------------------------------------------------------*
*&amp; Assigning object reference variables
*&amp;---------------------------------------------------------------------*

    oref = NEW #( utclong_current( ) ).

    &quot;Accessing instance attribute
    DATA(ts1) = oref-&gt;timestamp.
    out-&gt;write( data = ts1 name = `ts1` ).

    &quot;Assigning reference to existing reference variable
    oref2 = oref.
    DATA(ts2) = oref2-&gt;timestamp.
    out-&gt;write( data = ts2 name = `ts2` ).

*&amp;---------------------------------------------------------------------*
*&amp; Overwriting a reference variable
*&amp;---------------------------------------------------------------------*

    oref = NEW #( utclong_current( ) ).
    DATA(ts3) = oref-&gt;timestamp.
    out-&gt;write( data = ts3 name = `ts3` ).

*&amp;---------------------------------------------------------------------*
*&amp; Retaining object references
*&amp;---------------------------------------------------------------------*

    DATA oref_tab TYPE TABLE OF REF TO zcl_demo_abap.

    DO 3 TIMES.
      oref = NEW #( utclong_current( ) ).
      oref-&gt;instance_number = sy-index.
      oref-&gt;static_number = sy-index.
      oref_tab = VALUE #( BASE oref_tab ( oref ) ).
    ENDDO.

    out-&gt;write( data = oref_tab name = `oref_tab` ).

*&amp;---------------------------------------------------------------------*
*&amp; Clearing object references
*&amp;---------------------------------------------------------------------*

    DATA oref_generic TYPE REF TO object.
    oref_generic = NEW zcl_demo_abap( utclong_current( ) ).

    IF oref_generic IS INITIAL.
      out-&gt;write( `oref_generic is initial` ).
    ELSE.
      out-&gt;write( `oref_generic is not initial` ).
    ENDIF.

    IF oref_generic IS INSTANCE OF zcl_demo_abap.
      out-&gt;write( `oref_generic is an instance of zcl_demo_abap` ).
    ELSE.
      out-&gt;write( `oref_generic is not an instance of zcl_demo_abap` ).
    ENDIF.

    CLEAR oref_generic.

    IF oref_generic IS INITIAL.
      out-&gt;write( `oref_generic is initial` ).
    ELSE.
      out-&gt;write( `oref_generic is not initial` ).
    ENDIF.

    IF oref_generic IS INSTANCE OF zcl_demo_abap.
      out-&gt;write( `oref_generic is an instance of zcl_demo_abap` ).
    ELSE.
      out-&gt;write( `oref_generic is not an instance of zcl_demo_abap` ).
    ENDIF.

*&amp;---------------------------------------------------------------------*
*&amp; Accessing class attributes
*&amp;---------------------------------------------------------------------*

    &quot;Accessing instance attribute via an object reference variable
    oref = NEW #( utclong_current( ) ).
    &quot;Read access
    DATA(ts4) = oref-&gt;timestamp.
    &quot;Write access
    oref-&gt;instance_number = 123.

    &quot;Accessing static attributes via the class name
    &quot;Write access
    zcl_demo_abap=&gt;static_number = 456.
    &quot;Read access
    DATA(static_attr_access) = zcl_demo_abap=&gt;static_number.
    &quot;Read access (constant)
    DATA(const_value) = zcl_demo_abap=&gt;text.

    &quot;Accessing static attributes within the class without specifying the class name
    &quot;See the test_static_method method for restrictions regarding instance attributes.
    &quot;Write access
    static_number = 789.
    &quot;Read access
    static_attr_access = static_number.
    &quot;Read access (constant)
    const_value = text.

    &quot;Type and data object declarations based on class attributes
    TYPES type1 TYPE zcl_demo_abap=&gt;chars.
    TYPES type2 LIKE zcl_demo_abap=&gt;static_number.
    TYPES type3 LIKE zcl_demo_abap=&gt;text.
    DATA dobj1 TYPE zcl_demo_abap=&gt;chars.
    DATA dobj2 LIKE zcl_demo_abap=&gt;static_number.
    DATA dobj3 LIKE zcl_demo_abap=&gt;text.

    &quot;Without class name specification within the class itself
    TYPES type4 TYPE chars.
    TYPES type5 LIKE static_number.
    TYPES type6 LIKE text.
    DATA dobj4 TYPE chars.
    DATA dobj5 LIKE static_number.
    DATA dobj6 LIKE text.

    &quot;Using instance attributes
    &quot;Note: You can access static attributes via object reference variables.
    &quot;However, it is advisable to access static attributes as shown above.
    DATA(oref3) = NEW zcl_demo_abap( utclong_current( ) ).
    TYPES type7 TYPE oref3-&gt;chars.
    TYPES type8 LIKE oref3-&gt;instance_number.

    DATA dobj7 TYPE oref3-&gt;chars.
    DATA dobj8 LIKE oref3-&gt;timestamp.
  ENDMETHOD.

  METHOD constructor.
    timestamp = ts.
  ENDMETHOD.

  METHOD test_static_method.
    &quot;Accessing static attributes
    zcl_demo_abap=&gt;static_number = 987.
    &quot;Read access
    DATA(static_attr_access) = zcl_demo_abap=&gt;static_number.
    &quot;Read access (constant)
    DATA(const_value) = zcl_demo_abap=&gt;text.

    &quot;Accessing attributes within the class without specifying the class name
    &quot;Write access
    static_number = 654.
    &quot;Read access
    static_attr_access = static_number.
    &quot;Read access (constant)
    const_value = text.

    &quot;Access to instance attributes in static methods like above is not possible
    DATA inst_attr_access TYPE utclong.
    &quot;inst_attr_access = timestamp.
    &quot;Instance attribute access via object reference variable
    DATA(oref) = NEW zcl_demo_abap( utclong_current( ) ).
    inst_attr_access = oref-&gt;timestamp.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Calling Methods</h3>
<ul>
<li>Similar to accessing attributes, instance</li>
</ul>
<p>methods are called using <code>-&gt;</code> via a reference variable.</p>
<ul>
<li>Static</li>
</ul>
<p>methods are called using <code>=&gt;</code> via the class name. When used within the class in which it is declared, the static method can also be called without <code>class_name=&gt;...</code>.</p>
<ul>
<li>Static methods can but should not be called via reference variable (&lt;code&gt;oref-&gt;some_static_method( ).&lt;/code&gt;).</li>
<li>When methods are called, the (non-optional) parameters must be specified within parentheses.</li>
<li>You might also stumble on method calls with the older <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcall_method_static.htm"><code>CALL METHOD</code></a></li>
</ul>
<p>statements. It is recommended to use the new syntax in new developments. Note that <code>CALL METHOD</code> statements are still required in the context of <a href="06_Dynamic_Programming.md">dynamic programming</a>. Therefore, <code>CALL METHOD</code> statements should be reserved for dynamic method calls.</p>
<ul>
<li>Find an example class demonstrating various method calls in section <a href="#excursion-example-class">Excursion: Example Class</a>.</li>
<li>When calling methods that declare importing and/or exporting parameters, keep the following in mind: For methods with importing parameters, you can or must (if other parameters are available and specified in the method call) precede the parameters and their assignments with <code>EXPORTING</code>. For methods with exporting parameters, you can or must use <code>IMPORTING</code> before the parameters and their assignments.</li>
</ul>
<pre><code>&quot;Calling instance methods via reference variable;
&quot;within the parentheses, the parameters must be specified and assigned - if required

ref-&gt;inst_meth( ... ).

&quot;Calling static methods via/without the class name

class_name=&gt;stat_meth( ... ).

&quot;Only within the program in which it is declared.
stat_meth( ... ).

&quot;Calling (static) method having no parameter

class_name=&gt;stat_meth( ).

&quot;Calling (static) methods having a single importing parameter:

&quot;Note that in the method call, the caller exports values to the
&quot;method having importing parameters defined; hence, the addition
&quot;EXPORTING is relevant for the caller. The following three method calls are the same

&quot;Explicit use of EXPORTING.
class_name=&gt;meth( EXPORTING a = b ).

&quot;Only importing parameters in the method signature: explicit EXPORTING not needed

class_name=&gt;meth( a = b ).

&quot;If only a single value must be passed:
&quot;the formal parameter name (a) and EXPORTING not needed

stat_meth( b ).

&quot;Calling (static) methods having importing/exporting parameters
&quot;Parameters must be specified if they are not marked as optional

class_name=&gt;meth( EXPORTING a = b c = d     &quot;a/c: importing parameters in the method signature
                  IMPORTING e = f ).        &quot;e: exporting parameter in the method signature

&quot;To store the value of the parameter, you may also declare it inline.

class_name=&gt;meth( EXPORTING a = b c = d
                  IMPORTING e = DATA(z) ).  

&quot;Calling (static) methods having a changing parameter;
&quot;should be reserved for changing an existing local variable and value

DATA h TYPE i VALUE 123.
class_name=&gt;meth( CHANGING g = h ).

&quot;Calling (static) methods having a returning parameter.
&quot;Basically, they do the same as methods with exporting parameters
&quot;but they are way more versatile, and you can save lines of code.

&quot;They do not need temporary variables.
&quot;In the example, the return value is stored in a variable declared inline.

&quot;i and k are importing parameters
DATA(result) = class_name=&gt;meth( i = j k = l ).

&quot;They can be used with other statements, e. g. logical expressions.
&quot;In the example below, the assumption is that the returning parameter is of type i.
IF class_name=&gt;meth( i = j k = l ) &gt; 100.
  ...
ENDIF.

&quot;They enable method chaining.
&quot;The example shows a method to create random integer values.
&quot;The methods have a returning parameter.
DATA(random_no) = cl_abap_random_int=&gt;create( )-&gt;get_next( ).

&quot;RECEIVING parameter: Available in methods defined with a returning parameter;
&quot;used in standalone method calls only.
&quot;In the snippet, m is the returning parameter; n stores the result.
class_name=&gt;meth( EXPORTING i = j k = l RECEIVING m = DATA(n) ).</code></pre>
<p>Examples for instance method calls and static method calls:</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h4>Excursion: Inline Declarations, Returning Parameters</h4>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more information and example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>The example code below highlights the convenience of inline declarations when defining target data objects for output parameters.</li>
<li>This approach allows you to create data objects on the spot, eliminating the need for additional helper variables.</li>
<li>It also mitigates the risk of type mismatches.</li>
<li>However, when declaring a method with both exporting and returning parameters, it is not possible to specify target data objects for both inline at the same time in case of functional method calls.</li>
<li>Additional code snippets illustrate various ways to use methods declared with returning parameters in expression positions.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    CLASS-METHODS meth1 IMPORTING i_str        TYPE string
                                  i_tab        TYPE string_table OPTIONAL
                        EXPORTING e_dec        TYPE decfloat34
                                  e_tab        TYPE string_table
                        RETURNING VALUE(r_int) TYPE i.
    CLASS-METHODS meth2 RETURNING VALUE(r_tab) TYPE string_table.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.    
    &quot;Note: Calling the method in the same class means specifying &#x27;zcl_demo_abap=&gt;&#x27; is optional here.

    &quot;Standalone method call
    &quot;Specifying target data objects for all output parameters
    &quot;Inline declarations are handy because you can create an
    &quot;appropriately typed data object in place. No need to
    &quot;create an extra variable, check on the type etc.
    zcl_demo_abap=&gt;meth1(
      EXPORTING
        i_str = `ABAP`
      IMPORTING
        e_dec = DATA(a)
        e_tab = DATA(b)
      RECEIVING
        r_int = DATA(c)
    ).

    &quot;Functional method call
    &quot;The target data object of the returning parameter is specified on the left side of an assignment.
    &quot;Note: In this case, you cannot specify inline declarations for the exporting parameters.
    DATA e TYPE decfloat34.
    DATA f TYPE string_table.
    DATA(g) = zcl_demo_abap=&gt;meth1(
      EXPORTING
        i_str = `ABAP`
      IMPORTING
        &quot;e_dec = DATA(h)
        &quot;e_tab = DATA(i)
        e_dec = e
        e_tab = f
    ).

    &quot;Benefits of returning parameters: They can, for example, be used in expressions
    &quot;The following snippets show a selection (and ignore the available exporting
    &quot;parameters).

    CASE zcl_demo_abap=&gt;meth1( i_str = `ABAP` ).
      WHEN 0. ...
      WHEN 1. ...
      WHEN OTHERS. ...
    ENDCASE.

    IF zcl_demo_abap=&gt;meth1( i_str = `ABAP` ) &gt; 5.
      ...
    ELSE.
      ...
    ENDIF.

    &quot;IF used with a predicative method call
    &quot;The result of the relational expression is true if the result of the functional
    &quot;method call is not initial and false if it is initial. The data type of the result
    &quot;of the functional meth1od call, i. e. the return value of the called functional method,
    &quot;is arbitrary. A check is made for the type-dependent initial value.
    IF zcl_demo_abap=&gt;meth1( i_str = `ABAP` ).
      ...
    ELSE.
      ...
    ENDIF.

    DO zcl_demo_abap=&gt;meth1( i_str = `ABAP` ) TIMES.
      ...
    ENDDO.

    &quot;Method call result as actual parameter
    DATA(j) = zcl_demo_abap=&gt;meth1( i_str = `ABAP` i_tab = zcl_demo_abap=&gt;meth2( ) ).

    &quot;Examples of returning parameters typed with a table type
    LOOP AT zcl_demo_abap=&gt;meth2( ) INTO DATA(wa1).
      ...
    ENDLOOP.

    READ TABLE zcl_demo_abap=&gt;meth2( ) INTO DATA(wa2) INDEX 1.

  ENDMETHOD.
  
  METHOD meth1.
    ... &quot;Method implementation
  ENDMETHOD.

  METHOD meth2.
    ... &quot;Method implementation
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Self-Reference me</h3>
<pre><code>... some_method( ... ) ...

... me-&gt;some_method( ... ) ...</code></pre>
<p>When implementing instance methods, you can optionally make use of the implicitly available object reference variable <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenme.htm"><code>me</code></a> which is always available at runtime and points to the respective object itself. You can use it, for example, to refer to components of the instance of a particular class:</p>
<p>You can also address the entire object, which is illustrated in the example of section <a href="#method-chaining-and-chained-attribute-access">Method Chaining and Chained Attribute Access</a>. The following code snippet declares a local data object in a method. It has the same name as a data object declared in the private visibility section. <code>me</code> is used to access the non-local data object.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
    DATA str TYPE string.
    METHODS meth RETURNING VALUE(text) TYPE string.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    str = `AP`.
    DATA(text) = meth( ).
    ASSERT text = `ABAP`.

  ENDMETHOD.

  METHOD meth.

    &quot;Declaring a local data object having the same
    &quot;name as a data object declared in a visibility section
    DATA str TYPE string VALUE `AB`.

    &quot;Addressing locally declared data object
    DATA(local_string) = str.

    &quot;Addressing data object declared in private visibility section
    DATA(other_string) = me-&gt;str.

    text = local_string &amp;&amp; other_string.

  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Method Chaining and Chained Attribute Access</h3>
<ul>
<li>As shown in a previous example, method chaining is possible for functional method calls (i.e. methods that have exactly one return value declared with <code>RETURNING</code>) at appropriate read positions.</li>
<li>In this method design, the return values are reference variables that point to objects with the next method in question. The methods in the example class below assign the objects to the returning parameter using the self-reference <code>me</code>.</li>
<li>The method&#x27;s return value is then used as an ABAP operand.</li>
<li>A chained method call can consist of multiple functional methods that are linked using component selectors <code>-&gt;</code>.</li>
<li>Class attributes can also be added to the chain.</li>
<li>In the context of method chaining, constructor expressions (e.g. with <code>NEW</code>) come in handy. The example class below shows functional method calls (i.e. the return value of the method is used as an ABAP operand), and a standalone statement.</li>
</ul>
<p>The following example illustrates method chaining. Find a self-contained example class further down.</p>
<pre><code>&quot;The following class creates random integers. Find more information in the
&quot;class documentation.
&quot;Both methods have returning parameters specified.
DATA(some_int1) = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                              min  = 1
                                              max  = 10 )-&gt;get_next( ).

&quot;Getting to the result as above - not using method chaining and inline declarations.
DATA some_int2 TYPE i.
DATA dref TYPE REF TO cl_abap_random_int.

dref = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                   min  = 1
                                   max  = 10 ).

some_int2 = dref-&gt;get_next( ).

&quot;Using the RECEIVING parameter in a standalone method call
DATA some_int3 TYPE i.
dref-&gt;get_next( RECEIVING value = some_int3 ).

&quot;IF statement that uses the return value in a read position
IF cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                               min  = 1
                               max  = 10 )-&gt;get_next( ) &lt; 5.
  ... &quot;The random number is lower than 5.
ELSE.
  ... &quot;The random number is equal or greater than 5.
ENDIF.

&quot;Examples using classes of the XCO library (see more information in the
&quot;ABAP for Cloud Development and Released ABAP Classes cheat sheets), in which
&quot;multiple chained method calls can be specified. Each of the methods
&quot;has a returning parameter specified.

&quot;In the following example, 1 hour is added to the current time.
DATA(add1hour) = xco_cp=&gt;sy-&gt;time( xco_cp_time=&gt;time_zone-&gt;user )-&gt;add( iv_hour = 1 )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended )-&gt;value.

&quot;In the following example, a string is converted to xstring using a codepage
DATA(xstr) = xco_cp=&gt;string( `Some string` )-&gt;as_xstring( xco_cp_character=&gt;code_page-&gt;utf_8 )-&gt;value.

&quot;In the following example, JSON data is created. First, a JSON data builder
&quot;is created. Then, using different methods, JSON data is added. Finally,
&quot;the JSON data is turned to a string.
DATA(json) = xco_cp_json=&gt;data-&gt;builder( )-&gt;begin_object(
  )-&gt;add_member( &#x27;CarrierId&#x27; )-&gt;add_string( &#x27;DL&#x27;
  )-&gt;add_member( &#x27;ConnectionId&#x27; )-&gt;add_string( &#x27;1984&#x27;
  )-&gt;add_member( &#x27;CityFrom&#x27; )-&gt;add_string( &#x27;San Francisco&#x27;
  )-&gt;add_member( &#x27;CityTo&#x27; )-&gt;add_string( &#x27;New York&#x27;
  )-&gt;end_object( )-&gt;get_data( )-&gt;to_string( ).</code></pre>
<p>Self-contained example class demonstrating chained method calls with a functional method call and a standalone statement:</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    METHODS add_text IMPORTING str        TYPE string
                     RETURNING VALUE(ref) TYPE REF TO zcl_demo_abap.
    METHODS add_space RETURNING VALUE(ref) TYPE REF TO zcl_demo_abap.
    METHODS add_period RETURNING VALUE(ref) TYPE REF TO zcl_demo_abap.
    METHODS return_text RETURNING VALUE(str) TYPE string.
    METHODS display_text IMPORTING cl_run_ref TYPE REF TO if_oo_adt_classrun_out.
    DATA text TYPE string.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

*&amp;---------------------------------------------------------------------*
*&amp; Method chaining with a functional method call
*&amp;---------------------------------------------------------------------*

    &quot;Hallo NAME. This is an example of method chaining.
    DATA(text1) = NEW zcl_demo_abap(
                      )-&gt;add_text( `Hallo`
                      )-&gt;add_space(
                      )-&gt;add_text( xco_cp=&gt;sy-&gt;user( )-&gt;name
                      )-&gt;add_period(
                      )-&gt;add_space(
                      )-&gt;add_text( `This`
                      )-&gt;add_space(
                      )-&gt;add_text( `is`
                      )-&gt;add_space(
                      )-&gt;add_text( `an`
                      )-&gt;add_space(
                      )-&gt;add_text( `example`
                      )-&gt;add_space(
                      )-&gt;add_text( `of`
                      )-&gt;add_space(
                      )-&gt;add_text( `method`
                      )-&gt;add_space(
                      )-&gt;add_text( `chaining`
                      )-&gt;add_period(
                      )-&gt;return_text( ).

    out-&gt;write( text1 ).

    &quot;The following example chained method call includes a chained attribute
    &quot;access at the end so that the target variable contains the content of
    &quot;the attribute.

    &quot;Example result: Today is 2025-03-05. It&#x27;s 14:30:38. Have a nice day.
    DATA(text2) = NEW zcl_demo_abap(
                      )-&gt;add_text( `Today`
                      )-&gt;add_space(
                      )-&gt;add_text( `is`
                      )-&gt;add_space(
                      )-&gt;add_text( xco_cp=&gt;sy-&gt;date( )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended )-&gt;value
                      )-&gt;add_period(
                      )-&gt;add_space(
                      )-&gt;add_text( `It&#x27;s`
                      )-&gt;add_space(
                      )-&gt;add_text( xco_cp=&gt;sy-&gt;time( xco_cp_time=&gt;time_zone-&gt;user
                                    )-&gt;as( xco_cp_time=&gt;format-&gt;iso_8601_extended
                                    )-&gt;value
                      )-&gt;add_period(
                      )-&gt;add_space(
                      )-&gt;add_text( `Have`
                      )-&gt;add_space(
                      )-&gt;add_text( `a`
                      )-&gt;add_space(
                      )-&gt;add_text( `nice`
                      )-&gt;add_space(
                      )-&gt;add_text( `day`
                      )-&gt;add_period(
                      )-&gt;text.

    out-&gt;write( text2 ).

*&amp;---------------------------------------------------------------------*
*&amp; Method chaining with a standalone statement
*&amp;---------------------------------------------------------------------*

    &quot;In the example, the final method call in the chain receives
    &quot;the classrun instance available in the implementation of the
    &quot;if_oo_adt_classrun~main method. The method implementation
    &quot;includes the writing to the console.

    &quot;Console output: Lorem ipsum dolor sit amet
    NEW zcl_demo_abap( )-&gt;add_text( `Lorem`
                        )-&gt;add_space(
                        )-&gt;add_text( `ipsum`
                        )-&gt;add_space(
                        )-&gt;add_text( `dolor`
                        )-&gt;add_space(
                        )-&gt;add_text( `sit`
                        )-&gt;add_space(
                        )-&gt;add_text( `amet`
                        )-&gt;display_text( out ).

  ENDMETHOD.

  METHOD add_text.
    text &amp;&amp;= str.
    ref = me.
  ENDMETHOD.

  METHOD add_space.
    text &amp;&amp;= ` `.
    ref = me.
  ENDMETHOD.

  METHOD display_text.
    cl_run_ref-&gt;write( text ).
  ENDMETHOD.

  METHOD add_period.
    text &amp;&amp;= `.`.
    ref = me.
  ENDMETHOD.

  METHOD return_text.
    str = me-&gt;text.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Excursion: Example Class</h3>
<p>Expand the following collapsible section for an example class. The commented example class explores various aspects covered in the previous sections. You can create a demo class called <code>zcl_demo_abap</code> and copy and paste the following code. Once activated, you can choose <em>F9</em> in ADT to run the class. The example is designed to display output in the console that shows the result of calling different methods.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

*&amp;---------------------------------------------------------------------*
*&amp; Attributes
*&amp;---------------------------------------------------------------------*

    &quot;Instance attributes
    DATA: inst_attr   TYPE utclong,
          inst_string TYPE string.

    &quot;Static attribute
    CLASS-DATA stat_attr TYPE utclong.

    &quot;Attribute with the READ-ONLY addition. It is also possible for instance
    &quot;attributes. Such an attribute can only be read from outside the class,
    &quot;not changed. Inside the class, it can be changed. This also applies to
    &quot;subclasses (note that the example class does not allow inheritance).
    CLASS-DATA read_only_attr TYPE string VALUE `read only` READ-ONLY.

*&amp;---------------------------------------------------------------------*
*&amp; Methods
*&amp;---------------------------------------------------------------------*
    
    &quot;The parameter interfaces (signatures) of the methods are intended to
    &quot;demonstrate various syntax options described in the cheat sheet.

    &quot;Instance methods
    METHODS:
      &quot;No parameter specified
      inst_meth1,

      &quot;Single importing parameter
      inst_meth2 IMPORTING ip TYPE string,

      &quot;Importing and exporting parameters
      inst_meth3 IMPORTING ip1 TYPE i
                           ip2 TYPE i
                 EXPORTING ep  TYPE i,

      &quot;Returning parameters
      inst_meth4 RETURNING VALUE(ret) TYPE string,

      inst_meth5 IMPORTING ip1        TYPE string
                           ip2        TYPE string
                 EXPORTING ep         TYPE string
                 RETURNING VALUE(ret) TYPE string,

      &quot;Changing parameter
      inst_meth6 CHANGING chg TYPE string,

      &quot;Raising exceptions
      inst_meth7 IMPORTING ip1        TYPE i
                           ip2        TYPE i
                 RETURNING VALUE(ret) TYPE i
                 RAISING   cx_sy_arithmetic_overflow,

      inst_meth8 IMPORTING ip1        TYPE i
                 RETURNING VALUE(ret) TYPE string
                 RAISING   cx_uuid_error,

      &quot;Instance constructor
      &quot;Instance constructors can optionally have importing parameters
      &quot;and raise exceptions.
      constructor.

    &quot;Static methods
    CLASS-METHODS:
      &quot;Options of formal parameter definitions
      stat_meth1 IMPORTING REFERENCE(ip1) TYPE string  &quot;pass by reference (specifying REFERENCE(...) is optional)
                           ip2            TYPE string  &quot;pass by reference
                           VALUE(ip3)     TYPE string  &quot;pass by value
                 RETURNING VALUE(ret)     TYPE string, &quot;pass by value (mandatory for returning parameters)

      &quot;OPTIONAL/DEFAULT additions
      &quot;Both additions denote that passing values is optional. DEFAULT: If not supplied,
      &quot;the default value specified is used.
      stat_meth2 IMPORTING ip1        TYPE string DEFAULT `ABAP`
                           ip2        TYPE string OPTIONAL
                 RETURNING VALUE(ret) TYPE string_table,

      &quot;Generic types are possible for field symbols and method parameters
      &quot;All of the previous formal parameters are typed with complete types (e.g. type i).
      &quot;The following method includes several formal parameters typed with generic
      &quot;types. Find more information in the &#x27;Data Types and Objects&#x27; cheat sheet.
      &quot;Note: Returning parameters must be completely typed.
      stat_meth3 IMPORTING ip_data      TYPE data      &quot;Any data type
                           ip_clike     TYPE clike     &quot;Character-like data type (such as c, n, string, etc.)
                           ip_numeric   TYPE numeric   &quot;Numeric type (such as i, p, decfloat34 etc.)
                           ip_any_table TYPE ANY TABLE &quot;Any table type (standard, sorted, hashed)
                 RETURNING VALUE(ret)   TYPE string,   &quot;No generic type possible for returning parameters

      &quot;Static constructor
      &quot;No parameters possible
      class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;Note:
    &quot;This is a self-contained example. Usually, you must create an instance
    &quot;of a class first before using instance methods - when calling methods
    &quot;from outside, e.g. from another class. Within the class itself, you can
    &quot;indeed call the instance methods without a prior instance creation.
    &quot;To demonstrate &#x27;calls from external&#x27;, an instance of the class is
    &quot;nevertheless created in the example.

*&amp;---------------------------------------------------------------------*
*&amp; Constructors
*&amp;---------------------------------------------------------------------*

    &quot;Instance constructor: Automatically called when a class is instantiated
    &quot;and an instance is created.
    &quot;Creating an instance of a class and accessing an instance attribute
    &quot;using the object component selector -&gt;.
    DATA(oref) = NEW zcl_demo_abap( ).
    out-&gt;write( data = oref-&gt;inst_attr name = `oref-&gt;inst_attr` ).

    &quot;Creating more instances
    &quot;The example is implemented in a way that an instance attribute
    &quot;is assigned a time stamp when the instance constructor is called.
    &quot;As instance constructors are called once for each instance, the
    &quot;inst_attr values differ.
    DATA(oref2) = NEW zcl_demo_abap( ).
    out-&gt;write( data = oref2-&gt;inst_attr name = `oref2-&gt;inst_attr` ).

    DATA(oref3) = NEW zcl_demo_abap( ).
    out-&gt;write( data = oref3-&gt;inst_attr name = `oref3-&gt;inst_attr` ).

    &quot;Static constructor: Automatically and immediately called once for each class
    &quot;when calling a class for the first time in an internal session (e.g. an instance
    &quot;is created or a component is used).
    &quot;The example is implemented in a way that a static attribute is assigned a time
    &quot;stamp when the static constructor is called. The class was called with creating
    &quot;an instance above, and so the static constructor was called once. There is no new
    &quot;time stamp assigment, no new calls of the static constructor in this internal session.
    &quot;Therefore, the value of stat_attr is the same.
    &quot;The access is done using the class name, the class component selector =&gt;, and
    &quot;the attribute name.
    out-&gt;write( data = zcl_demo_abap=&gt;stat_attr name = `zcl_demo_abap=&gt;stat_attr` ).

    &quot;Static attributes are also accessible via the object reference variable
    out-&gt;write( data = oref-&gt;stat_attr name = `oref-&gt;stat_attr` ).

    &quot;Checking that the values of the instance attribute that is modified when calling
    &quot;the instance constructor differ from instance to instance.
    IF ( oref3-&gt;inst_attr &gt; oref2-&gt;inst_attr )
    AND ( oref2-&gt;inst_attr &gt; oref-&gt;inst_attr ).
      out-&gt;write( `Instance attribute check: All values differ.` ).
    ELSE.
      out-&gt;write( `Instance attribute check: At least one check is not successful.` ).
    ENDIF.

    &quot;Checking that the value of the static attribute that is modified when calling
    &quot;the static constructor is identical from instance to instance (it is only changed
    &quot;when the class is called for the first time).
    IF ( oref3-&gt;stat_attr = oref2-&gt;stat_attr )
    AND ( oref2-&gt;stat_attr = oref-&gt;stat_attr )
    AND ( oref-&gt;stat_attr = zcl_demo_abap=&gt;stat_attr ).
      out-&gt;write( `Static attribute check: All values are identical.` ).
    ELSE.
      out-&gt;write( `Static attribute check: At least one check is not successful.` ).
    ENDIF.

    &quot;Excursion: See the note above. In the self-contained example, in this class itself, the
    &quot;components can be called without reference variable or providing the class name. The
    &quot;assumption in the following snippets of the example is that the class is called
    &quot;from outside and instances are created outside of the class, so the variable/class
    &quot;name are provided.
    &quot;The following access is possible in the same class (not outside of this class):
    DATA(a) = inst_attr.
    DATA(b) = stat_attr.

    &quot;The following is also possible inside the class (it is the only option outside of
    &quot;this class):
    DATA(c) = oref-&gt;inst_attr.
    DATA(d) = zcl_demo_abap=&gt;stat_attr.
    DATA(e) = oref-&gt;stat_attr.

*&amp;---------------------------------------------------------------------*
*&amp; Method calls
*&amp;---------------------------------------------------------------------*

    &quot;Calling instance methods
    &quot;Instance methods are called using the object component selector -&gt;
    &quot;via reference variables.

    &quot;--- Method without parameters ---
    oref-&gt;inst_meth1( ).

    &quot;To show an effect of the method call in the example, the method implementation
    &quot;simply includes the change of an instance attribute value.
    out-&gt;write( data = oref-&gt;inst_string name = `oref-&gt;inst_string` ).

    &quot;Notes:
    &quot;- See the method implementation of inst_meth1. It shows that
    &quot;  instance methods can access both static and instance attributes.
    &quot;- As mentioned above regarding the attributes, in the same class and
    &quot;  in this example, you can call the methods directly (without using
    &quot;  a reference variable).
    inst_meth1( ).

    &quot;--- Methods that specify importing parameters ---
    &quot;Similar to the inst_meth1 method, the implementation includes the
    &quot;change of an instance attribute.
    &quot;Notes:
    &quot;- In the method call, the caller exports values to the
    &quot;  method having importing parameters defined. Therefore, the addition
    &quot;  EXPORTING is relevant for the caller.
    &quot;- If a method only specifies importing parameters, specifying EXPORTING
    &quot;  is optional.
    &quot;- If a method only specifies a single importing parameter, specifying
    &quot;  EXPORTING and the formal parameter name is optional.

    &quot;Method with a single importing parameter (the following three method
    &quot;calls are basically the same)
    &quot;Method call that specifies EXPORTING and the formal parameter name
    oref-&gt;inst_meth2( EXPORTING ip = `hello` ).

    out-&gt;write( data = oref-&gt;inst_string name = `oref-&gt;inst_string` ).

    &quot;Method call that specifies the formal parameter, without EXPORTING
    oref-&gt;inst_meth2( ip = `world` ).

    out-&gt;write( data = oref-&gt;inst_string name = `oref-&gt;inst_string` ).

    &quot;Method call that only includes the actual parameter
    oref-&gt;inst_meth2( `ABAP` ).

    out-&gt;write( data = oref-&gt;inst_string name = `oref-&gt;inst_string` ).

    &quot;--- Methods that specify exporting parameters ---
    &quot;For the method call, specify EXPORTING for importing parameters,
    &quot;and IMPORTING for exporting parameters.
    &quot;The method implementation performs a calculation. The calculation
    &quot;result is assigned to the exporting parameter. You can assign the
    &quot;value to a suitable data object.
    DATA calc_result1 TYPE i.
    oref-&gt;inst_meth3( EXPORTING ip1 = 5
                                ip2 = 3
                      IMPORTING ep = calc_result1 ).

    out-&gt;write( data = calc_result1 name = `calc_result1` ).

    &quot;Inline declaration is also possible.
    oref-&gt;inst_meth3( EXPORTING ip1 = 2
                                ip2 = 4
                      IMPORTING ep = DATA(calc_result2) ).

    out-&gt;write( data = calc_result2 name = `calc_result2` ).

    &quot;--- Methods that specify returning parameters ---
    DATA(result1) = oref-&gt;inst_meth4( ).
    out-&gt;write( data = result1 name = `result1` ).

    &quot;The RECEIVING addition is available in standalone method
    &quot;calls only if methods are defined with a returning parameter.
    &quot;Inline declaration is also possible here.
    oref-&gt;inst_meth4( RECEIVING ret = DATA(result2) ).
    out-&gt;write( data = result2 name = `result2` ).

    &quot;The following example method specifies multiple parameters,
    &quot;among them 2 output parameters (exporting and reporting).

    &quot;Functional method call
    &quot;In a functional method call, inline declaration is not possible
    &quot;for &#x27;ep&#x27;.
    DATA str1 TYPE string.
    DATA(str2) = oref-&gt;inst_meth5( EXPORTING ip1 = `AB`
                                             ip2 = `AP`
                                   IMPORTING ep = str1 ).

    out-&gt;write( data = str1 name = `str1` ).
    out-&gt;write( data = str2 name = `str2` ).

    &quot;Standalone method call (including inline declarations)
    oref-&gt;inst_meth5( EXPORTING ip1 = `AB`
                                ip2 = `AP`
                      IMPORTING ep = DATA(str3)
                      RECEIVING ret = DATA(str4) ).

    out-&gt;write( data = str3 name = `str3` ).
    out-&gt;write( data = str4 name = `str4` ).

    &quot;Returning parameters enable ...
    &quot;... the use of method calls in other statements, for example,
    &quot;in logical expressions, instead of storing the method call
    &quot;result in an extra variable.
    IF oref-&gt;inst_meth4( ) IS INITIAL.
      out-&gt;write( `Initial` ).
    ELSE.
      out-&gt;write( `Not initial` ).
    ENDIF.

    &quot;... method chaining to write more concise code and avoid
    &quot;declaring helper variables.
    &quot;The following example uses a class that creates random integers.
    &quot;The &#x27;create&#x27; method has a returning parameter. It returns an
    &quot;instance of the class (an object reference) based on which
    &quot;more methods can be called. Using method chaining, you can
    &quot;do the following in one go.
    DATA(inst) = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                             min  = 1
                                             max  = 10 ).
    DATA(random_integer1) = inst-&gt;get_next( ).

    DATA(random_integer2) = cl_abap_random_int=&gt;create( seed = cl_abap_random=&gt;seed( )
                                                        min  = 1
                                                        max  = 10 )-&gt;get_next( ).

    out-&gt;write( data = random_integer1 name = `random_integer1` ).
    out-&gt;write( data = random_integer2 name = `random_integer2` ).

    &quot;--- Method that specifies a changing parameter ---
    &quot;Changing parameters should be reserved for changing an existing
    &quot;local variable and value.
    &quot;The method implementation includes the demonstration of the
    &quot;self-reference &#x27;me&#x27;. For this purpose, a data object is created
    &quot;in the method implementation that has the same name as a data
    &quot;object declared in the class&#x27;s declaration part.

    &quot;Changing the value of the instance attribute with the same name
    &quot;as the local data object in the method implementation.
    oref-&gt;inst_string = `TEST`.

    DATA local_var TYPE string VALUE `hello`.
    oref-&gt;inst_meth6( CHANGING chg = local_var ).

    out-&gt;write( data = local_var name = `local_var` ).

    &quot;--- Methods that specify RAISING ---
    &quot;The following example method declares an exception of the category
    &quot;CX_DYNAMIC_CHECK. That is, the check is not performed until runtime.
    &quot;There is no syntax warning shown at compile time.
    &quot;The ipow function is included in the method implementation. The
    &quot;second example raises the exception.

    &quot;No TRY control structure, no syntax warning at compile time. However,
    &quot;the calculation works.
    DATA(power_res1) = oref-&gt;inst_meth7( ip1 = 5 ip2 = 2 ).
    out-&gt;write( data = power_res1 name = `power_res1` ).

    &quot;The example raises the exception because the resulting number is too high.
    &quot;Not catching the exception results in a runtime error.
    TRY.
        DATA(power_res2) = oref-&gt;inst_meth7( ip1 = 10 ip2 = 10 ).
        out-&gt;write( data = power_res2 name = `power_res2` ).
      CATCH cx_sy_arithmetic_overflow.
        out-&gt;write( `cx_sy_arithmetic_overflow was raised` ).
    ENDTRY.

    &quot;The following example method declares an exception of the category
    &quot;CX_STATIC_CHECK. That is, it is statically checked. Without the
    &quot;TRY control structure and catching the exception, a syntax warning
    &quot;is displayed. And in the case of the example implementation,
    &quot;the exception is indeed raised. Not catching the exception results
    &quot;in a runtime error.
    TRY.
        DATA(test) = oref-&gt;inst_meth8( ip1 = 1 ).
        out-&gt;write( data = test name = `test` ).
      CATCH cx_uuid_error.
        out-&gt;write( `cx_uuid_error was raised` ).
    ENDTRY.

    &quot;A syntax warning is displayed for the following method call.
    &quot;DATA(test2) = oref-&gt;inst_meth8( ip1 = 1 ).

    &quot;Calling static methods
    &quot;The method definitions/implementations cover further aspects.

    &quot;--- Pass by reference/value ---
    &quot;The following example method emphasizes pass by reference and
    &quot;by value.
    &quot;Notes:
    &quot;- Returning parameters can only be declared with VALUE(...)
    &quot;- When passing by reference, the content of the data objects
    &quot;  cannot be changed in the method implementation.
    &quot;- The method implementation includes that ...
    &quot;  - attributes declared with READ-ONLY can be changed within
    &quot;    the class they are declared.
    &quot;  - static methods can only access static attributes.
    DATA(res) = zcl_demo_abap=&gt;stat_meth1( ip1 = `a` &quot;pass by reference (declaration with REFERENCE(...))
                                           ip2 = `b` &quot;pass by reference (without REFERENCE(...))
                                           ip3 = `c` &quot;pass by value (declaration with VALUE(...))
                                         ).
    out-&gt;write( data = res name = `res` ).

    &quot;--- OPTIONAL/DEFAULT additions ---
    &quot;The method implementation includes the predicate expression IS SUPPLIED
    &quot;that checks whether a formal parameter is populated.

    DATA(res_tab1) = zcl_demo_abap=&gt;stat_meth2( ip1 = `aaa` ip2 = `bbb` ).
    out-&gt;write( data = res_tab1 name = `res_tab1` ).

    DATA(res_tab2) = zcl_demo_abap=&gt;stat_meth2( ip1 = `ccc` ).
    out-&gt;write( data = res_tab2 name = `res_tab2` ).

    DATA(res_tab3) = zcl_demo_abap=&gt;stat_meth2( ip2 = `ddd` ).
    out-&gt;write( data = res_tab3 name = `res_tab3` ).

    DATA(res_tab4) = zcl_demo_abap=&gt;stat_meth2( ).
    out-&gt;write( data = res_tab4 name = `res_tab4` ).

    &quot;--- Generically typed formal parameters ---
    &quot;In the following method calls, various data objects are
    &quot;assigned to formal parameters. Note that returning parameters
    &quot;must be completely typed.
    &quot;In the example, the value passed for parameter ip_clike (which is
    &quot;convertible to type string) is returned.
    DATA(res_gen1) = zcl_demo_abap=&gt;stat_meth3( ip_data = VALUE string_table( ( `hi` ) ) &quot;any data type
                                                ip_clike = abap_true &quot;Character-like data type (such as c, n, string, etc.)
                                                ip_numeric = 1 &quot;Numeric type (such as i, p, decfloat34 etc.)
                                                ip_any_table = VALUE string_table( ( `ABAP` ) ) &quot;Any table type (standard, sorted, hashed)
                                              ).

    out-&gt;write( data = res_gen1 name = `res_gen1` ).

    DATA(res_gen2) = zcl_demo_abap=&gt;stat_meth3( ip_data = 123
                                                ip_clike = &#x27;ABAP&#x27;
                                                ip_numeric = CONV decfloat34( &#x27;1.23&#x27; )
                                                ip_any_table = VALUE string_hashed_table( ( `hello` ) ( `world` ) )
                                              ).

    out-&gt;write( data = res_gen2 name = `res_gen2` ).

  ENDMETHOD.

  METHOD class_constructor.
    &quot;Assigning the current UTC timestamp to a static attribute
    stat_attr = utclong_current( ).
  ENDMETHOD.

  METHOD constructor.
    &quot;Assigning the current UTC timestamp to an instance attribute
    inst_attr = utclong_current( ).
  ENDMETHOD.

  METHOD inst_meth1.
    inst_string = `Changed in inst_meth1`.

    &quot;Instance methods can access both static and instance attributes.
    DATA(a) = inst_attr.
    DATA(b) = stat_attr.
  ENDMETHOD.

  METHOD inst_meth2.
    inst_string = |Changed in inst_meth2. Content of passed string: { ip }|.
  ENDMETHOD.

  METHOD inst_meth3.
    ep = ip1 + ip2.
  ENDMETHOD.

  METHOD inst_meth4.
    ret = |This string was assigned to the returning parameter at { utclong_current( ) }|.
  ENDMETHOD.

  METHOD inst_meth5.
    ep = |Strings &#x27;{ ip1 }&#x27; and &#x27;{ ip2 }&#x27; were assigned to the exporting parameter at { utclong_current( ) }|.
    ret = |Strings &#x27;{ ip1 }&#x27; and &#x27;{ ip2 }&#x27; were assigned to the returning parameter at { utclong_current( ) }|.
  ENDMETHOD.

  METHOD inst_meth6.
    &quot;Demonstrating the self-reference &#x27;me&#x27;
    &quot;Its use is optional. The following data object intentionally
    &quot;has the same name as an instance attribute specified in the
    &quot;class&#x27;s declaration part.
    DATA inst_string TYPE string.
    inst_string = `Local string`.

    chg = |The local data object was changed: &#x27;{ to_upper( chg ) }&#x27;\nChecking the self-reference me:\ninst_string: &#x27;{ inst_string }&#x27;\nme-&gt;inst_string: &#x27;{ me-&gt;inst_string }&#x27;|.
  ENDMETHOD.

  METHOD inst_meth7.
    ret = ipow( base = ip1 exp = ip2 ).
  ENDMETHOD.

  METHOD inst_meth8.
    IF ip1 = 1.
      RAISE EXCEPTION TYPE cx_uuid_error.
    ELSE.
      ret = `No exception was raised.`.
    ENDIF.
  ENDMETHOD.

  METHOD stat_meth1.
    &quot;Static methods can only access static attributes.
    &quot;DATA(test1) = inst_attr.
    DATA(test2) = stat_attr.

    &quot;Only read access possible when parameters are passed by value
    DATA(a) = ip1.
    DATA(b) = ip2.

    &quot;Write access is not possible (however, if you need to work with them
    &quot;and change the content, you can create copies as above)
    &quot;ip1 = `y`.
    &quot;ip2 = `z`.

    &quot;Pass by value, modification is possible
    ip3 = to_upper( ip3 ) &amp;&amp; `##`.

    &quot;Modifying a read only attribute is possible in the class
    &quot;in which it is declared.
    read_only_attr = `Read-only attribute was modified`.

    ret = |ip1 = &#x27;{ ip1 }&#x27;, ip2 = &#x27;{ ip2 }&#x27;, ip3 (modified) = &#x27;{ ip3 }&#x27; / read_only_attr = &#x27;{ read_only_attr }&#x27;|.
  ENDMETHOD.

  METHOD stat_meth2.
    &quot;Using IS SUPPLIED in an IF control structure
    IF ip1 IS SUPPLIED.
      APPEND |ip1 is supplied, value: &#x27;{ ip1 }&#x27;| TO ret.
    ELSE.
      APPEND |ip1 is not supplied, value: &#x27;{ ip1 }&#x27;| TO ret.
    ENDIF.

    &quot;Using IS SUPPLIED with the COND operator
    APPEND COND #( WHEN ip2 IS SUPPLIED
                   THEN |ip2 is supplied, value: &#x27;{ ip2 }&#x27;|
                   ELSE |ip2 is not supplied, value: &#x27;{ ip2 }&#x27;| ) TO ret.
  ENDMETHOD.

  METHOD stat_meth3.
    &quot;You may check the content in the debugger.
    DATA(a) = REF #( ip_data ).
    DATA(b) = REF #( ip_clike ).
    DATA(c) = REF #( ip_numeric ).
    DATA(d) = REF #( ip_any_table ).

    ret = ip_clike.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Inheritance</h2>
<p>The following table covers a selection of aspects about inheritance. The code snippets demonstrate local classes.</p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Aspect &lt;/td&gt; &lt;td&gt; Notes/Code examples &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Superclasses and subclasses</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Inheritance means deriving any number of new classes (<a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensubclass_glosry.htm">subclasses</a>) from an existing class (<a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abensuperclass_glosry.htm">superclass</a>).</li>
<li>This derivation establishes a hierarchical relationship between superclasses and subclasses, forming an inheritance tree. A class can have multiple subclasses but only one direct superclass.</li>
<li>A subclass can also be a superclass to multiple direct subclasses but still has only one direct superclass.</li>
</ul>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>The inheritance tree of the example classes is as follows: <code>LCL1</code> is a superclass with direct subclasses <code>LCL2</code> and <code>LCL3</code>. <code>LCL3</code> is also a superclass from which more subclasses are derived. Similarly, <code>LCL5</code> is a superclass with <code>LCL6</code> as a direct subclass.</p>
<pre><code>LCL1
  |
  |--LCL2
  |
  |--LCL3
  |   |
  |   |--LCL4
  |   |
  |   |--LCL5
  |   |   |
  |   |   |--LCL6</code></pre>
<p>The classes are without any component specifications for demonstration purposes.</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
ENDCLASS.

CLASS lcl4 DEFINITION INHERITING FROM lcl3.
  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
ENDCLASS.

CLASS lcl5 DEFINITION INHERITING FROM lcl3.
  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl5 IMPLEMENTATION.
ENDCLASS.

CLASS lcl6 DEFINITION INHERITING FROM lcl5.
  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl6 IMPLEMENTATION.
ENDCLASS.</code></pre>
<p>&lt;/details&gt; &lt;br&gt;</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Components specified in superclasses</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Subclasses inherit and therefore adopt all components, such as attributes or methods, from superclasses.</li>
<li>Subclasses can use these components and add new ones.</li>
<li>Subclasses know about superclass components, but superclasses do not know about subclass components, unless a friendship relationgship is defined (see further down). Generally, superclasses are unaware of their subclasses.</li>
<li>By adding new components - and redefining methods, as covered below - subclasses become more specific, while superclasses remain more generic. This distinction is important for polymorphism and casting as outlined below.</li>
<li>If a subclass has no additional components, it contains only the components of the superclass, except for those in the private visibility section (unless friendship is granted).</li>
<li>Changes or additions to attributes in subclasses are not visible to superclasses, making these changes relevant only to the subclass and its subclasses.</li>
</ul>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>The inheritance tree of the following example classes is as follows:</p>
<pre><code>LCL1
  |
  |--LCL2
  |   |
  |   |--LCL3</code></pre>
<p>The code snippet includes various data object declarations and value assignments by exploring:</p>
<ul>
<li>Subclasses adopt all components from the public and protected sections. The example does not cover redefining instance methods.</li>
<li>Adding new components.</li>
<li>Superclasses are unaware of their subclasses and their components.</li>
<li>Instance attributes are not accessible in static methods.</li>
</ul>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    DATA pub_inst_str_lcl1 TYPE string.
    CLASS-DATA pub_stat_str_lcl1 TYPE string.
    METHODS inst_meth_lcl1.
    CLASS-METHODS stat_meth_lcl1.
  PROTECTED SECTION.
    DATA prot_inst_str_lcl1 TYPE string.
    CLASS-DATA prot_stat_str_lcl1 TYPE string.
  PRIVATE SECTION.
    DATA priv_inst_str_lcl1 TYPE string.
    CLASS-DATA priv_stat_str_lcl1 TYPE string.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD inst_meth_lcl1.
    &quot;Accessing attributes of the class itself
    pub_inst_str_lcl1 = `a`.
    pub_stat_str_lcl1 = `b`.

    prot_inst_str_lcl1 = `c`.
    prot_stat_str_lcl1 = `d`.

    priv_inst_str_lcl1 = `e`.
    priv_stat_str_lcl1 = `f`.

    &quot;Superclasses do not know of the components of their subclasses.
    &quot;Visible static, public components can be accessed using the class name
    &quot;followed by =&gt; and the component name. In case of this particular
    &quot;local class example, the definition part of lcl2 must be specified before
    &quot;the implementation part of lcl1 for the lcl2=&gt;pub_stat_str_lcl2 ...
    &quot;statement to work.
    &quot;pub_inst_str_lcl2 = `g`.
    &quot;pub_stat_str_lcl2 = `h`.
    &quot;prot_inst_str_lcl2 = `i`.
    &quot;prot_stat_str_lcl2 = `j`.
    &quot;priv_inst_str_lcl2 = `k`.
    &quot;priv_stat_str_lcl2 = `l`.
    &quot;lcl2=&gt;pub_stat_str_lcl2 = `m`.
  ENDMETHOD.

  METHOD stat_meth_lcl1.
    &quot;Instance attributes are not accessible in static methods.

    &quot;pub_inst_str_lcl1 = `g`.
    pub_stat_str_lcl1 = `h`.

    &quot;prot_inst_str_lcl1 = `i`.
    prot_stat_str_lcl1 = `j`.

    &quot;priv_inst_str_lcl1 = `k`.
    priv_stat_str_lcl1 = `l`.
  ENDMETHOD.

ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    DATA pub_inst_str_lcl2 TYPE string.
    CLASS-DATA pub_stat_str_lcl2 TYPE string.
    DATA added_pub_inst_str_lcl2 TYPE string.
    CLASS-DATA added_pub_stat_str_lcl2 TYPE string.
    METHODS inst_meth_lcl2.
    CLASS-METHODS stat_meth_lcl2.
  PROTECTED SECTION.
    DATA prot_inst_str_lcl2 TYPE string.
    CLASS-DATA prot_stat_str_lcl2 TYPE string.
    DATA added_prot_inst_str_lcl2 TYPE string.
    CLASS-DATA added_prot_stat_str_lcl2 TYPE string.
  PRIVATE SECTION.
    DATA priv_inst_str_lcl2 TYPE string.
    CLASS-DATA priv_stat_str_lcl2 TYPE string.
    DATA added_priv_inst_str_lcl2 TYPE string.
    CLASS-DATA added_priv_stat_str_lcl2 TYPE string.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD inst_meth_lcl2.
    &quot;Accessing attributes of the class itself
    pub_inst_str_lcl2 = `a`.
    pub_stat_str_lcl2 = `b`.
    added_pub_inst_str_lcl2 = `c`.
    added_pub_stat_str_lcl2 = `d`.

    prot_inst_str_lcl2 = `e`.
    prot_stat_str_lcl2 = `f`.
    added_prot_inst_str_lcl2 = `g`.
    added_prot_stat_str_lcl2 = `h`.

    priv_inst_str_lcl2 = `i`.
    priv_stat_str_lcl2 = `j`.
    added_priv_inst_str_lcl2 = `k`.
    added_priv_stat_str_lcl2 = `l`.

    &quot;Accessing attributes of the superclass
    pub_inst_str_lcl1 = `m`.
    pub_stat_str_lcl1 = `n`.

    prot_inst_str_lcl1 = `o`.
    prot_stat_str_lcl1 = `p`.

    &quot;Private components of the superclass cannot be accessed
    &quot;priv_inst_str_lcl1 = `q`.
    &quot;priv_stat_str_lcl1 = `r`.

    &quot;The following statement shows a syntax warning as an identically named
    &quot;attribute as in the superclass is created.
    &quot;DATA pub_inst_str_lcl1 TYPE string.
    &quot;However, the following declaration using an identical name as one of the
    &quot;superclass attributes is possible because the private component of the
    &quot;superclass cannot be accessed.
    DATA priv_inst_str_lcl1 TYPE string.
  ENDMETHOD.

  METHOD stat_meth_lcl2.
    &quot;Instance attributes are not accessible in static methods.

    &quot;Accessing attributes of the class itself
    &quot;pub_inst_str_lcl2 = `a`.
    pub_stat_str_lcl2 = `b`.
    &quot;added_pub_inst_str_lcl2 = `c`.
    added_pub_stat_str_lcl2 = `d`.

    &quot;prot_inst_str_lcl2 = `e`.
    prot_stat_str_lcl2 = `f`.
    &quot;added_prot_inst_str_lcl2 = `g`.
    added_prot_stat_str_lcl2 = `h`.

    &quot;priv_inst_str_lcl2 = `i`.
    priv_stat_str_lcl2 = `j`.
    &quot;added_priv_inst_str_lcl2 = `k`.
    added_priv_stat_str_lcl2 = `l`.

    &quot;Accessing attributes of the superclass
    &quot;pub_inst_str_lcl1 = `m`.
    pub_stat_str_lcl1 = `n`.

    &quot;prot_inst_str_lcl1 = `o`.
    prot_stat_str_lcl1 = `p`.

    &quot;Private components of the superclass cannot be accessed
    &quot;priv_inst_str_lcl1 = `q`.
    &quot;priv_stat_str_lcl1 = `r`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl2.
  PUBLIC SECTION.
    DATA pub_inst_str_lcl3 TYPE string.
    CLASS-DATA pub_stat_str_lcl3 TYPE string.
    METHODS inst_meth_lcl3.
    CLASS-METHODS stat_meth_lcl3.
  PROTECTED SECTION.
    DATA prot_inst_str_lcl3 TYPE string.
    CLASS-DATA prot_stat_str_lcl3 TYPE string.
  PRIVATE SECTION.
    DATA priv_inst_str_lcl3 TYPE string.
    CLASS-DATA priv_stat_str_lcl3 TYPE string.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD inst_meth_lcl3.
    &quot;Accessing attributes of the class itself
    pub_inst_str_lcl3 = `a`.
    pub_stat_str_lcl3 = `b`.

    prot_inst_str_lcl3 = `c`.
    prot_stat_str_lcl3 = `d`.

    priv_inst_str_lcl3 = `e`.
    priv_stat_str_lcl3 = `f`.

    &quot;Accessing attributes of the superclasses
    pub_inst_str_lcl1 = `g`.
    pub_stat_str_lcl1 = `h`.

    prot_inst_str_lcl1 = `i`.
    prot_stat_str_lcl1 = `j`.

    pub_inst_str_lcl2 = `k`.
    pub_stat_str_lcl2 = `l`.
    added_pub_inst_str_lcl2 = `m`.
    added_pub_stat_str_lcl2 = `n`.

    prot_inst_str_lcl2 = `o`.
    prot_stat_str_lcl2 = `p`.
    added_prot_inst_str_lcl2 = `q`.
    added_prot_stat_str_lcl2 = `r`.

    &quot;Private components of the superclasses cannot be accessed
    &quot;priv_inst_str_lcl1 = `s`.
    &quot;priv_stat_str_lcl1 = `t`.

    &quot;priv_inst_str_lcl2 = `u`.
    &quot;priv_stat_str_lcl2 = `v`.
    &quot;added_priv_inst_str_lcl2 = `w`.
    &quot;added_priv_stat_str_lcl2 = `x`.
  ENDMETHOD.

  METHOD stat_meth_lcl3.
    &quot;Instance attributes are not accessible in static methods.

    &quot;Accessing attributes of the class itself
    &quot;pub_inst_str_lcl3 = `a`.
    pub_stat_str_lcl3 = `b`.

    &quot;prot_inst_str_lcl3 = `c`.
    prot_stat_str_lcl3 = `d`.

    &quot;priv_inst_str_lcl3 = `e`.
    priv_stat_str_lcl3 = `f`.

    &quot;Accessing attributes of the superclasses
    &quot;pub_inst_str_lcl1 = `g`.
    pub_stat_str_lcl1 = `h`.

    &quot;prot_inst_str_lcl1 = `i`.
    prot_stat_str_lcl1 = `j`.

    &quot;pub_inst_str_lcl2 = `k`.
    pub_stat_str_lcl2 = `l`.
    &quot;added_pub_inst_str_lcl2 = `m`.
    added_pub_stat_str_lcl2 = `n`.

    &quot;prot_inst_str_lcl2 = `o`.
    prot_stat_str_lcl2 = `p`.
    &quot;added_prot_inst_str_lcl2 = `q`.
    added_prot_stat_str_lcl2 = `r`.

    &quot;Private components of the superclasses cannot be accessed
    &quot;priv_inst_str_lcl1 = `s`.
    &quot;priv_stat_str_lcl1 = `t`.

    &quot;priv_inst_str_lcl2 = `u`.
    &quot;priv_stat_str_lcl2 = `v`.
    &quot;added_priv_inst_str_lcl2 = `w`.
    &quot;added_priv_stat_str_lcl2 = `x`.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt; &lt;br&gt;</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Redefinition of superclass methods</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can (but need not) reimplement public and protected instance methods of all preceding superclasses in subclasses through redefinition (<code>REDEFINITION</code> addition), making subclasses more specific.</li>
<li>Reimplementing methods allows you to reuse the parameter interface without changes.</li>
<li>You can use the pseudo-reference <code>super-&gt;...</code> to call direct superclass methods while implementing redefined instance methods, which is useful for taking over, overriding, or extending implementations.</li>
<li>Static methods can be accessed but not redefined.</li>
<li>Changes or additions of methods in subclasses are not visible to superclasses, affecting only the class itself and its subclasses.</li>
</ul>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>The inheritance tree of the following example classes looks as follows:</p>
<pre><code>LCL1
  |
  |--LCL2
  |   |
  |   |--LCL3</code></pre>
<p>The example explores the redefinition of methods.</p>
<p>Global class to run the example and visualize the implementations of the local classes in the CCIMP include. The output should be as follows:</p>
<pre><code>A
A1
B
A12
B3
C</code></pre>
<p>&lt;br&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA(str1) = NEW lcl1( )-&gt;meth_lcl1( ).
    out-&gt;write( str1 ).
    DATA(str2) = NEW lcl2( )-&gt;meth_lcl1( ).
    out-&gt;write( str2 ).
    DATA(str3) = NEW lcl2( )-&gt;meth_lcl2( ).
    out-&gt;write( str3 ).
    DATA(str4) = NEW lcl3( )-&gt;meth_lcl1( ).
    out-&gt;write( str4 ).
    DATA(str5) = NEW lcl3( )-&gt;meth_lcl2( ).
    out-&gt;write( str5 ).
    DATA(str6) = NEW lcl3( )-&gt;meth_lcl3( ).
    out-&gt;write( str6 ).

  ENDMETHOD.
ENDCLASS.</code></pre>
<p>Local classes in the CCIMP include:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth_lcl1 RETURNING VALUE(str1) TYPE string.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth_lcl1.
    str1 = `A`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth_lcl2 RETURNING VALUE(str2) TYPE string.
    METHODS meth_lcl1 REDEFINITION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth_lcl2.
    str2 = `B`.
  ENDMETHOD.

  METHOD meth_lcl1.
    DATA(super_str_lcl1) = super-&gt;meth_lcl1( ).
    str1 = super_str_lcl1 &amp;&amp; `1`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl2.
  PUBLIC SECTION.
    METHODS meth_lcl3 RETURNING VALUE(str3) TYPE string.
    METHODS meth_lcl2 REDEFINITION.
    METHODS meth_lcl1 REDEFINITION.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth_lcl3.
    str3 = `C`.
  ENDMETHOD.
  METHOD meth_lcl1.
    DATA(super_str_lcl1) = super-&gt;meth_lcl1( ).
    str1 = super_str_lcl1 &amp;&amp; `2`.
  ENDMETHOD.

  METHOD meth_lcl2.
    DATA(super_str_lcl2) = super-&gt;meth_lcl2( ).
    str2 = super_str_lcl2 &amp;&amp; `3`.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt; &lt;br&gt; &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Visibility of components</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Components in the public visibility section are accessible from anywhere (where visible).</li>
<li>Protected components are accessible by subclasses and friends.</li>
<li>Private components are not accessible by subclasses unless friendship is granted.</li>
<li>See <em>Components specified in superclasses</em> for an example.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Additions impacting inheritance</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>You can use the <code>FINAL</code> addition to prevent classes from being inherited.</li>
<li>You can also apply the <code>FINAL</code> addition to methods to prevent them from being redefined in subclasses.</li>
<li>Refer to the next section for notes on these topics and other inheritance and instantiation features.</li>
</ul>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Constructors</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Instance constructors (<code>constructor</code>):</li>
<li>Subclasses cannot redefine the instance constructors of superclasses.</li>
<li>These constructors are called automatically when creating an object; they cannot be called explicitly.</li>
<li>In inheritance, a subclass&#x27;s constructor must call all its superclasses&#x27; constructors. When you implement the instance constructor in subclasses, you must use a <code>super-&gt;constructor( )</code> call to call the direct superclass&#x27;s constructor, even if the superclass does not explicitly declare it. Similarly, even if a subclass does not define and implement its constructor, the superclass&#x27;s constructor will be called.</li>
<li>It is required to fill any non-optional importing parameters.</li>
<li>Class instantiation is controlled by specific additions (<code>CREATE PUBLIC/PROTECTED/PRIVATE</code>), affecting the ability to call constructors. The constructor&#x27;s visibility cannot be more specific than the instance creator&#x27;s visibility. For example, if a class is declared using <code>CREATE PUBLIC</code>, the constructor must be in the public section. If a subclass implicitly specifies <code>CREATE PUBLIC</code>, its constructor must also be public. If it specifies <code>CREATE PROTECTED</code>, the constructor can be in the public or protected section, but not private. Subclasses can specify their instantiability independently from the superclass. In classes declared using <code>CREATE PRIVATE</code>, the constructor is only visible within the class itself, unless friendship is granted. Consider making such classes final to prevent derivation.</li>
<li>Static constructors (<code>class_constructor</code>):</li>
<li>These constructors are implicitly available in all classes, whether declared or not.</li>
<li>They are called when creating a class instance for the first time in an ABAP program or when accessing a static component, except for types and constants.</li>
<li>In inheritance, static constructors in the entire inheritance tree are called first.</li>
<li>A static constructor is called only once during program runtime.</li>
<li>A static constructor is always public.</li>
</ul>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Example 1&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>The inheritance tree of the following example classes is as follows:</p>
<pre><code>LCL1
  |
  |--LCL2
  |
  |--LCL3
  |   |
  |   |--LCL4</code></pre>
<p>This example explores how instance and static constructors can be specified. It shows that subclasses can determine their own instantiation independently of the superclass. The visibility of the constructor cannot be more specific than that of the <code>CREATE ...</code> specification.</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD class_constructor.
  ENDMETHOD.

  METHOD constructor.
  ENDMETHOD.
ENDCLASS.

&quot;Implicit CREATE PUBLIC
CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD class_constructor.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
  ENDMETHOD.
ENDCLASS.

&quot;CREATE PROTECTED
CLASS lcl3 DEFINITION INHERITING FROM lcl1 CREATE PROTECTED.
  PUBLIC SECTION.
    CLASS-METHODS class_constructor.
    &quot;METHODS constructor.
  PROTECTED SECTION.
    METHODS constructor.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD class_constructor.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
  ENDMETHOD.
ENDCLASS.

&quot;CREATE PRIVATE
CLASS lcl4 DEFINITION INHERITING FROM lcl3 CREATE PRIVATE FINAL.
  PUBLIC SECTION.
    CLASS-METHODS class_constructor.
    METHODS constructor.
  PROTECTED SECTION.
    &quot;METHODS constructor.
  PRIVATE SECTION.
    &quot;METHODS constructor.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD class_constructor.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
  ENDMETHOD.
ENDCLASS.

&quot;No instances of the subclass can be created. unless friendship is created
*CLASS lcl5 DEFINITION INHERITING FROM lcl4.
*...</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;br&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Example 2&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>The inheritance tree of the following example classes is as follows:</p>
<pre><code>LCL1
  |
  |--LCL2
  |   |
  |   |--LCL3
  |   |   |
  |   |   |--LCL4</code></pre>
<p>This example explores the order of instance and static constructor calls during object creation. The constructors add strings to a string table. To visualize the output, run the global class, which includes object creations. Refer to the notes on the example output below.</p>
<p>Global class:</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.
    DATA(oref1) = NEW lcl1( ).
    out-&gt;write( lcl1=&gt;tab ).
    out-&gt;write( repeat( val = `_` occ = 30 ) &amp;&amp; |\n\n| ).

    CLEAR lcl1=&gt;tab.

    DATA(oref2) = NEW lcl2( ).
    out-&gt;write( lcl1=&gt;tab ).
    out-&gt;write( repeat( val = `_` occ = 30 ) &amp;&amp; |\n\n| ).

    CLEAR lcl1=&gt;tab.

    DATA(oref3) = NEW lcl3( ).
    out-&gt;write( lcl1=&gt;tab ).
    out-&gt;write( repeat( val = `_` occ = 30 ) &amp;&amp; |\n\n| ).

    CLEAR lcl1=&gt;tab.

    DATA(oref4) = NEW lcl4( ).
    out-&gt;write( lcl1=&gt;tab ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>Local classes in the CCIMP include:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
    CLASS-DATA tab TYPE string_table.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD class_constructor.
    APPEND `lcl1 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.
    APPEND `lcl1 constructor` TO tab.
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD class_constructor.
    APPEND `lcl2 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    APPEND `lcl2 constructor` TO tab.
  ENDMETHOD.

ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl2.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD class_constructor.
    APPEND `lcl3 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.

    super-&gt;constructor( ).
    APPEND `lcl3 constructor` TO tab.
  ENDMETHOD.

ENDCLASS.

CLASS lcl4 DEFINITION INHERITING FROM lcl3.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD class_constructor.
    APPEND `lcl4 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    APPEND `lcl4 constructor` TO tab.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>Notes on the output:</p>
<ul>
<li>When an object is created, static constructors are called first, following the inheritance tree top down. Then, instance constructors are called, also top down.</li>
<li>Note that when the class runs, the example is executed in a single internal session. The static constructor of a class is called only once. Therefore, you will not see <code>lcl1 class_constructor</code> in the output when creating an object of <code>lcl2</code>, and so on.</li>
</ul>
<pre><code>    lcl1 class_constructor
    lcl1 constructor      
    ______________________________

    lcl2 class_constructor
    lcl1 constructor      
    lcl2 constructor     
    ...</code></pre>
<p>If you comment out <code>DATA(oref1) = NEW lcl1( ).</code> and <code>out-&gt;write( lcl1=&gt;tab ).</code>, which are the statements accessing <code>lcl1</code> for the first time, and then run the class, you will see in the output (or while debugging) that the static constructor is indeed called. The same applies to the static constructor of <code>lcl2</code>, which is not called when creating an object for <code>lcl3</code>, and so on.</p>
<pre><code>    lcl1 class_constructor
    lcl2 class_constructor
    lcl1 constructor      
    lcl2 constructor      
    ______________________________

    ...</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;br&gt;</p>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><code>object</code> as root node of inheritance trees</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>In inheritance trees, the root node is the predefined <code>object</code> class, representing an empty, abstract class.</li>
<li>If a class does not use the <code>INHERITING FROM</code> addition, it is implicitly a subclass of <code>object</code>.</li>
<li>The following statement uses a released ABAP class and RTTI (see the [Dynamic Programming]() cheat sheet) to determine that the root class of a class that does not explicitly inherit from another class is <code>object</code>.</li>
</ul>
<pre><code>    DATA(superclass) = cast cl_abap_classdescr( cl_abap_typedescr=&gt;describe_by_name( &#x27;CL_SYSTEM_UUID&#x27; ) )-&gt;get_super_class_type( )-&gt;get_relative_name( ).
    ASSERT superclass = `OBJECT`.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Additions Related to Inheritance and Instantiation</h3>
<p>The table below includes selected syntax related to inheritance in class and method declarations. It also includes additions related to instantiation.</p>
<div class="note">[!NOTE] - Some of the syntax options have already been mentioned previously. This is to summarize. - The code examples show local classes and interfaces declared, for example, in the [CCIMP include](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenccimp_glosry.htm) of a class pool. - The snippets provided do not represent all possible syntax combinations. For the complete picture, refer to the ABAP Keyword Documentation. Additional syntax options are available in the context of friendship (`GLOBAL FRIENDS/FRIENDS`), testing (`FOR TESTING`), [RAP](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenarap_glosry.htm) (`FOR BEHAVIOR OF`; to declare [ABAP behavior pools](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbehavior_pool_glosry.htm)), and more. - The order of the additions can vary.</div>
<p><strong>Declarations in Classes</strong></p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Syntax Example &lt;/td&gt; &lt;td&gt; Details &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>CLASS zcl_demo DEFINITION     
  PUBLIC
  FINAL
  CREATE PUBLIC .</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li><code>... PUBLIC ...</code>: Specifies that the class is a global class, available globally within the class library. The cheat sheet also includes example classes that do not specify  <code>PUBLIC</code>, declaring these classes as local classes.</li>
<li><code>... FINAL ...</code>: Specifies that the class cannot have any subclasses, effectively prohibiting inheritance. This addition seals off a branch of the inheritance tree. In final classes, all methods are automatically final.</li>
<li><code>... CREATE PUBLIC ...</code>: Specifies that the class can be instantiated wherever it is visible. Not specifying the addition <code>CREATE ...</code> means <code>CREATE PUBLIC</code> by default.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes (no <code>... PUBLIC ...</code> addition is used):</p>
<pre><code>CLASS lcl1 DEFINITION FINAL CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
   &quot;Creating an instance of lcl1 within the class itself.
   DATA(oref1) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION FINAL CREATE PUBLIC.
  PUBLIC SECTION.
    CLASS-METHODS meth2.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth2.
   &quot;Creating an instance of lcl1 wherever visible.
   DATA(oref2) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Inheriting from the class is not possible
*CLASS lcl3 DEFINITION INHERITING FROM lcl1.
*  PUBLIC SECTION.
*ENDCLASS.
*
*CLASS lcl3 IMPLEMENTATION.
*ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>CLASS zcl_demo DEFINITION   
  PUBLIC
  CREATE PUBLIC .</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<p>This class permits inheritance because it does not include the <code>FINAL</code> addition. Subclasses can be derived from this superclass.</p>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    &quot;Creating an instance of lcl1 within the class itself.
    DATA(oref1) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION FINAL CREATE PUBLIC.
  PUBLIC SECTION.
    CLASS-METHODS meth2.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth2.
    &quot;Creating an instance of lcl1 wherever visible.
    DATA(oref2) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Inheriting from the class
CLASS lcl3 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth1.
    super-&gt;meth1( ).
    &quot;Example instantiations (all classes allow instantation)
    &quot;Note: lcl3 does not specify CREATE PUBLIC explicitly. It is
    &quot;specified implicitly.
    DATA(oref3) = NEW lcl1( ).
    DATA(oref4) = NEW lcl2( ).
    DATA(oref5) = NEW lcl3( ).
  ENDMETHOD.
ENDCLASS.

CLASS lcl4 DEFINITION.
  PUBLIC SECTION.
    METHODS meth3.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD meth3.
    DATA(oref6) = NEW lcl3( ).
    DATA(oref7) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>CLASS zcl_demo DEFINITION    
  PUBLIC
  CREATE PROTECTED .</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>This class permits inheritance because it does not include the <code>FINAL</code> addition.</li>
<li><code>... CREATE PROTECTED ...</code>: Specifies that the class can only be instantiated within its own methods, its subclasses&#x27; methods, or those of its friends.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PROTECTED.
  PUBLIC SECTION.
    METHODS meth1.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    &quot;Creating an instance of lcl1 within the class itself.
    DATA(oref1) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION FINAL CREATE PUBLIC.
  PUBLIC SECTION.
    CLASS-METHODS meth2.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth2.
    &quot;Creating an instance of lcl1 is not possible as this class is not
    &quot;a subclass or friend
    &quot;DATA(oref2) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Inheriting from the class
CLASS lcl3 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth1.
    super-&gt;meth1( ).

    &quot;Creating an instance is possible as lcl3 is a subclass of lcl1
    DATA(oref3) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

**********************************************************************

&quot;Creating instances is allowed for friends
&quot;Note the DEFINITION DEFERRED additions for lcl5. It is used to make the class known in the program
&quot;before its actual definition. Such statements are particularly necessary in local classes, and if a
&quot;reference to a local class is made before it is defined.
&quot;A method of lcl5 includes the creation of an instance of lcl4, demonstrating that friends can
&quot;indeed create the instances. You can comment out FRIENDS lcl5 in the class declaration part of
&quot;lcl4. Consequently, a syntax error is displayed in lcl5 for the instance creation.
CLASS lcl5 DEFINITION DEFERRED.
CLASS lcl4 DEFINITION CREATE PROTECTED FRIENDS lcl5.
  PUBLIC SECTION.
    METHODS meth3.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD meth3.
  ENDMETHOD.
ENDCLASS.

CLASS lcl5 DEFINITION CREATE PROTECTED.
  PUBLIC SECTION.
    METHODS meth4.
ENDCLASS.

CLASS lcl5 IMPLEMENTATION.
  METHOD meth4.
    DATA(oref_friend) = NEW lcl4( ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>CLASS zcl_demo DEFINITION    
  FINAL
  CREATE PRIVATE .</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>This class does not permit inheritance because it includes the <code>FINAL</code> addition.</li>
<li><code>... CREATE PRIVATE ...</code>: Specifies that the class can only be instantiated within its own methods or those of its friends. It cannot be instantiated as a component of (not befriended) subclasses.</li>
<li>Consider the implications of defining a superclass in this manner:</li>
<li>External users cannot instantiate a subclass.</li>
<li>If inheritance is allowed, subclasses cannot instantiate themselves though. This is because they cannot access the superclass&#x27;s instance constructor, preventing the creation of subclass instances. However, this can be altered if the subclass is a friend of the superclass.</li>
<li>Similarly, subclass objects cannot be created within their superclass if declared using <code>CREATE PROTECTED</code> or <code>CREATE PRIVATE</code>. This is only possible if the superclasses are friends with their subclasses.</li>
<li>The <code>FINAL</code> addition can be beneficial with <code>CREATE PRIVATE</code> to prevent the derivation of subclasses.</li>
<li>Note: In each class, the <code>CREATE PUBLIC</code>, <code>CREATE PROTECTED</code>, and <code>CREATE PRIVATE</code> additions of the <code>CLASS</code> statement control who can create an instance of the class and who can call its instance constructor.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PRIVATE.
  PUBLIC SECTION.
    METHODS meth1.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    &quot;Creating an instance of lcl1 within the class itself.
    DATA(oref1) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Creating instances is not possible outside the class
CLASS lcl2 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth2.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth2.
    &quot;Creating an instance is not possible
    &quot;DATA(oref2) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Note: lcl1 is not defined as FINAL, so derivation is not explicitly
&quot;disallowed. The following class definition shows a syntax warning as
&quot;instantiation is not allowed.
*CLASS lcl3 DEFINITION INHERITING FROM lcl1.
*  PUBLIC SECTION.
*ENDCLASS.
*
*CLASS lcl3 IMPLEMENTATION.
*ENDCLASS.

**********************************************************************

&quot;Instantiation is only possible for befriended classes
&quot;Defining another class with CREATE PRIVATE, FINAL also specified
&quot;See the notes on DEFINITION DEFERRED above.
CLASS lcl5 DEFINITION DEFERRED.
CLASS lcl4 DEFINITION FINAL CREATE PRIVATE FRIENDS lcl5.
  PUBLIC SECTION.
    METHODS meth3.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD meth3.
    &quot;Creating an instance of lcl4 in the class itself.
    DATA(oref3) = NEW lcl4( ).

    &quot;Creating an instance of lcl1 not possible outside of the class.
    DATA(oref4) = NEW lcl2( ).
  ENDMETHOD.
ENDCLASS.

CLASS lcl5 DEFINITION.
  PUBLIC SECTION.
    METHODS meth4.
ENDCLASS.

CLASS lcl5 IMPLEMENTATION.
  METHOD meth4.
    &quot;Creating instances of lcl4 is allowed as lcl5 is befriended.
    DATA(oref_friend) = NEW lcl4( ).
  ENDMETHOD.
ENDCLASS.

&quot;Another class with CREATE PRIVATE, FINAL not specified
CLASS lcl7 DEFINITION DEFERRED.
CLASS lcl6 DEFINITION CREATE PRIVATE FRIENDS lcl7.
  PUBLIC SECTION.
    METHODS meth5.
ENDCLASS.

CLASS lcl6 IMPLEMENTATION.
  METHOD meth5.
    &quot;Creating an instance of lcl6 in the class itself.
    DATA(oref5) = NEW lcl6( ).
  ENDMETHOD.
ENDCLASS.

&quot;Creating a subclass of lcl6, which is befriended
CLASS lcl7 DEFINITION INHERITING FROM lcl6.
  PUBLIC SECTION.
    METHODS meth5 REDEFINITION.
ENDCLASS.

CLASS lcl7 IMPLEMENTATION.
  METHOD meth5.
    &quot;Creating instances of lcl6 is allowed as lcl7 is befriended.
    DATA(oref_friend) = NEW lcl6( ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>CLASS zcl_demo DEFINITION     
  PUBLIC
  ABSTRACT
  CREATE ...</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<ul>
<li><code>... ABSTRACT ...</code>:</li>
<li>Defines abstract classes</li>
<li>Abstract classes cannot be instantiated.</li>
<li>To use the instance components of an abstract class, you can instantiate a subclass of that class.</li>
<li>Abstract classes may contain both abstract and concrete instance methods. However, abstract methods are not implemented within abstract classes.</li>
<li>By adding the <code>FINAL</code> addition, abstract classes can be made final. In these cases, only static components are usable. While instance components may be declared, they are not usable.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION ABSTRACT CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
    DATA inst_attr TYPE i.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    &quot;Creating an instance of lcl1 within the class itself
    &quot;is not possible. Generally, abstract classes cannot be
    &quot;instantiated.
    &quot;DATA(oref1) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Creating instances is not possible outside the class
CLASS lcl2 DEFINITION CREATE PUBLIC.
PUBLIC SECTION.
  METHODS meth2.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth2.
    &quot;Creating an instance is not possible
    &quot;DATA(oref2) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.

&quot;Using instance components of abstract classes only via subclasses
CLASS lcl3 DEFINITION INHERITING FROM lcl1.
PUBLIC SECTION.
  METHODS meth1 REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth1.
    ...
    DATA(oref3) = NEW lcl3( ).
    oref3-&gt;inst_attr = 1.

    &quot;Creating instances of abstract classes not possible
    &quot;DATA(oref4) = NEW lcl1( ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>CLASS zcl_sub DEFINITION       
  INHERITING FROM zcl_demo       
  ...</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<ul>
<li><code>... INHERITING FROM ...</code>:</li>
<li>Can be specified in subclasses inheriting from visible superclasses</li>
<li>If not specified, the class implicitly inherits from the predefined, empty, abstract class <code>OBJECT</code> (the root object).</li>
<li>A subclass inherits all components of the superclasses.</li>
<li>For example, if an internal table <code>itab</code> is declared as a static component in superclass <code>zcl_super</code>, subclasses can refer to <code>itab</code> directly, not necessarily by specifying the class name as with <code>zcl_super=&gt;itab</code> (which is also possible).</li>
<li>If the superclass defines a type, subclasses cannot define a type with the same name.</li>
<li>The visibility of the components remains unchanged.</li>
<li>Only the public and protected components of the superclass are visible in the subclass.</li>
<li>Private components of superclasses are inaccessible in subclasses.</li>
<li>The properties of inherited components are immutable. However, subclasses can declare additional components (with unique names) and redefine inherited methods without altering the interface.</li>
<li>Upon instantiation of a subclass, all superclasses are also instantiated, ensuring the initialization of superclass attributes through calling superclass constructors.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
    DATA num1 TYPE i.
    CLASS-DATA str TYPE string.
  PROTECTED SECTION.
    DATA num2 TYPE i.
  PRIVATE SECTION.
    DATA num3 TYPE i.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.
ENDCLASS.

&quot;Subclasses inheriting from lcl1
CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
    &quot;Data object with the same name as specified in the superclass
    &quot;is not possible in subclasses
    &quot;DATA num1 TYPE i.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth1.
    ...
    &quot;Only public and protected components can be accessed in subclasses.
    DATA(oref) = NEW lcl2( ).
    oref-&gt;num1 = 1.
    oref-&gt;num2 = 2.
    &quot;Private components are not accessible
    &quot;oref-&gt;num3 = 3.

    &quot;Static attribute accessible with the name directly without class=&gt; (which is also possible)
    str = `hello`.
    lcl1=&gt;str = `hi`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p><strong>Declarations in Instance Methods</strong></p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Syntax Example &lt;/td&gt; &lt;td&gt; Details &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>METHODS some_meth FINAL ...    </code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>Declares final methods</li>
<li>These methods cannot be overridden in subclasses.</li>
<li>Note: In final classes, all methods are inherently final. Therefore, the <code>FINAL</code> addition cannot be specified. Instance constructors are always final, but the use of the <code>FINAL</code> addition is optional.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
    METHODS meth2 FINAL.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.
  
  METHOD meth2.
    ...
  ENDMETHOD.  
ENDCLASS.

&quot;Subclass inheriting from lcl1
CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
    &quot;meth2 is a final method and cannot be redefined
    &quot;METHODS meth2 REDEFINITION.    
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>METHODS some_meth ABSTRACT ...    </code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>Declares abstract methods</li>
<li>Can only be used in abstract classes</li>
<li>You can implement these methods in a subclass (by redefining using the <code>REDEFINITION</code>addition), not in the abstract class itself. When declared, there is no implementation part in the abstract class.</li>
<li>All instance methods can be declared as abstract, except for instance constructors.</li>
<li>Private methods cannot be redefined and can therefore not be declared as abstract.</li>
<li>If abstract methods are declared in classes that are both abstract and final, they cannot be implemented. Therefore, the methods are not usable.</li>
<li>In interfaces, methods are implicitly abstract as interfaces do not contain method implementations.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION ABSTRACT CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
    METHODS meth2 ABSTRACT.
    DATA num TYPE i.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  &quot;meth1 is a nonabstract meth
  METHOD meth1.
    ...
    num = 1.
  ENDMETHOD.

  &quot;meth2 is defined as abstract and cannot be implemented
  &quot;in the abstract class
*  METHOD meth2.
*    ...
*  ENDMETHOD.
ENDCLASS.

&quot;Subclass inheriting from lcl1
CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
    &quot;The abstract method meth2 must be implemented in subclasses.
    METHODS meth2 REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth1.
    ...
    super-&gt;meth1( ).
    DATA(result) = num + 1.
  ENDMETHOD.

  METHOD meth2.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>&quot;Instance constructor
METHODS constructor. 
&quot;Static constructor
CLASS-METHODS class_constructor.</code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<p>Find information on constructors in the previous section.</p>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates constructors. When the constructors are called, a string is added to an internal table (a static attribute). You can try out the example and explore the calling of constructors as follows:</p>
<ol>
<li>Create a global class that implements the class run, and add the following codes. Local classes, implemented in the CCIMP include, are called. The example is designed to write the content of the internal table to the console.</li>
</ol>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.
    DATA(oref1) = NEW lcl2( ).
    DATA(tab) = lcl1=&gt;tab.
    out-&gt;write( tab ).
    out-&gt;write( |\n| ).
    DATA(oref2) = NEW lcl3( ).
    tab = lcl1=&gt;tab.
    out-&gt;write( tab ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<ol>
<li>Add the following code to the CCIMP include (<em>Local Types</em> tab in ADT).</li>
</ol>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
    CLASS-DATA tab TYPE string_table.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.

  METHOD class_constructor.
    APPEND `lcl1 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.
    APPEND `lcl1 constructor` TO tab.
  ENDMETHOD.

ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD class_constructor.
    APPEND `lcl2 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    APPEND `lcl2 constructor` TO tab.
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl2.
  PUBLIC SECTION.
    METHODS constructor.
    CLASS-METHODS class_constructor.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD class_constructor.
    APPEND `lcl3 class_constructor` TO tab.
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    APPEND `lcl3 constructor` TO tab.
  ENDMETHOD.
ENDCLASS.</code></pre>
<ol>
<li>Activate and go back to the global class. Choose F9 to run the example. The internal table content that is output demonstrates the sequence of constructor calls of the simple example.</li>
</ol>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<pre><code>METHODS some_meth REDEFINITION.           
METHODS another_meth FINAL REDEFINITION.      </code></pre>
<p>&lt;/td&gt; &lt;td&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>Specified in subclasses to redefine inherited methods from superclasses.</li>
<li>The method&#x27;s implementation is expected to reimplement the inherited method. However, the subclass&#x27;s new implementation conceals the superclass&#x27;s implementation.</li>
<li>The redefined method accesses the private components of its class, not any similarly named private components in the superclass.</li>
<li>The superclass&#x27;s implementation can be called in the redefined method using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenpseudo_reference_glosry.htm">pseudo reference</a> <code>super-&gt;meth( ).</code>. Note that non-optional importing parameters must be filled.</li>
<li>The redefinition is valid for subclasses until the method is redefined again.</li>
<li>The <code>FINAL</code> addition can be specified, preventing further redefinition of the method in other subclasses.</li>
</ul>
<p>&lt;br&gt;</p>
<p>The following code example demonstrates local classes:</p>
<pre><code>CLASS lcl1 DEFINITION CREATE PUBLIC.
  PUBLIC SECTION.
    METHODS meth1.
    METHODS meth2.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.

  METHOD meth2.
    ...
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
    METHODS meth2 FINAL REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.

  METHOD meth2.
    ...
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl2.
  PUBLIC SECTION.
    METHODS meth1 REDEFINITION.
    &quot;meth2 cannot be further redefined as it is defined as FINAL
    &quot;in the class&#x27;s superclass
    &quot;METHODS meth2 REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth1.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Excursion: Inheritance Example</h3>
<div class="note">[!NOTE] This example is also included in the ABAP cheat sheet repository: [zcl_demo_abap_oo_inheritance_1](./src/zcl_demo_abap_oo_inheritance_1.clas.abap)</div>
<p>Expand the following collapsible section for example classes. The example classes explore inheritance and demonstrate a selection of the inheritance-related syntax described above. The inheritance tree consists of four example classes. The base class includes the implementation of the classrun interface. The example is designed to output information to the console. So, you can execute this class using F9 in ADT. The purpose of the example and information output is to visualize and explore concepts and syntax related to inheritance, checking out when and how methods are called, redefining methods, abstract and final classes and methods.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more information and example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>The inheritance tree of the example classes is as follows:</p>
<pre><code>ZCL_DEMO_ABAP_OO_INHERITANCE_1
  |
  |--ZCL_DEMO_ABAP_OO_INHERITANCE_2
  |   |
  |   |--ZCL_DEMO_ABAP_OO_INHERITANCE_3
  |   |   |
  |   |   |--ZCL_DEMO_ABAP_OO_INHERITANCE_4</code></pre>
<p>Notes on the example:</p>
<ul>
<li>All classes include instance and static constructor declarations.</li>
<li>Many instance methods are declared in all classes to demonstrate inheritance. However, there is no meaningful implementation in these methods in all classes. All instance methods include the same code.</li>
<li>The purpose of the code in the method implementations is to add a line to a log table (which is output to the console) with various pieces of information:</li>
<li>Name of the method that is called</li>
<li>In which class the method is implemented when it is called</li>
<li>From which class the method is called</li>
<li>Whether the method is inherited, redefined, final, or a static method</li>
<li>Visibility of the method</li>
<li>Time stamp of when the method is called</li>
<li>The information retrieval is implemented in a static method in the <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code> class by getting callstack information to determine which method in which class was called by whom. Based on the retrieved class and method names, RTTI is used to get detailed information about the methods.</li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code></li>
<li>Allows inheritance and represents the root class of the inheritance hierarchy</li>
<li>Declares several instance methods in each visibility section</li>
<li>One of them is declared with <code>FINAL</code>, so no redefinition is possible in subclasses.</li>
<li>Includes the implementation of the classrun interface meaning this class is executable using F9 in ADT.</li>
<li>The class includes an internal table that represents a log table and that is output to the console as described above.</li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_2</code></li>
<li>Inherits from <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code> and allows inheritance itself</li>
<li>Specifies <code>CREATE PROTECTED</code>, so the class can only be instantiated in methods of its subclasses, of the class itself, and of its friends</li>
<li>You may want to try to create an instance of the class in <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code> like this <code>DATA(oref) = NEW zcl_demo_abap_oo_inheritance_2( ).</code> It is not possible. In <code>ZCL_DEMO_ABAP_OO_INHERITANCE_3</code> and <code>ZCL_DEMO_ABAP_OO_INHERITANCE_4</code>, for example, it is possible.</li>
<li>Declares several instance methods</li>
<li>One of them is declared with <code>FINAL</code>, so no redefinition is possible in subclasses</li>
<li>Instance methods of the direct superclass are redefined</li>
<li>Note: Private methods of superclasses cannot be redefined. You cannot specify abstract methods, which is only possible in abstract classes. Abstract methods are generally not possible in the private visibility section since they cannot be redefined.</li>
<li>Declares a static method to delegate method calls of this class</li>
<li>The implementation includes the creation of an instance of the class and instance method calls (including redefined methods).</li>
<li>It is called in the classrun implementation in <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code>.</li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_3</code></li>
<li>Inherits from <code>ZCL_DEMO_ABAP_OO_INHERITANCE_2</code> and thus from <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code></li>
<li>Declared as abstract class using the <code>ABSTRACT</code> addition, so no instances can be created from the class</li>
<li>Declares several instance methods</li>
<li>Two abstract methods are included using the <code>ABSTRACT</code> addition, so they can only be implemented in subclasses (there is no implementation of these methods in the class)</li>
<li>Instance methods of the direct superclass are redefined as well as methods from two levels up the inheritance hierarchy</li>
<li>One redefined method specifies <code>FINAL REDEFINITION</code>, so a further redefinition in subclasses is not possible.</li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_4</code></li>
<li>Inherits from <code>ZCL_DEMO_ABAP_OO_INHERITANCE_3</code> and thus from <code>ZCL_DEMO_ABAP_OO_INHERITANCE_2</code> and <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code></li>
<li>Specifies <code>FINAL</code> and so does not allow inheritance</li>
<li>Declares several instance methods</li>
<li>Instance methods of the direct superclass, which is an abstract class, are redefined. It is mandatory to redefine the abstract methods.</li>
<li>Other instance methods from further levels up the inheritance hierarchy are redefined (except one method that is declared with <code>FINAL REDEFINITION</code> in <code>ZCL_DEMO_ABAP_OO_INHERITANCE_3</code>)</li>
<li>For demonstration purposes, instance methods implemented in the abstract direct superclass (instances of abstract classes cannot be created) are called in the respective redefined methods by referring to the direct superclass using the syntax <code>super-&gt;...</code>.</li>
<li>Declares a static method to delegate method calls of this class</li>
</ul>
<p>To try the example classes out, create four demo classes named</p>
<ul>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code></li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_2</code></li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_3</code></li>
<li><code>ZCL_DEMO_ABAP_OO_INHERITANCE_4</code></li>
</ul>
<p>and paste the code into it. After activation, choose <em>F9</em> in ADT to execute the class <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code>. The example is set up to display output in the console.</p>
<p>&lt;br&gt;</p>
<p>Class <code>ZCL_DEMO_ABAP_OO_INHERITANCE_1</code></p>
<pre><code>CLASS zcl_demo_abap_oo_inheritance_1 DEFINITION
  PUBLIC
  CREATE PUBLIC .

  PUBLIC SECTION.
    &quot;Classrun interface
    INTERFACES if_oo_adt_classrun.

    &quot;Instance/static constructor declarations
    METHODS constructor.
    CLASS-METHODS class_constructor.

    &quot;Instance method declarations
    METHODS meth_public_1.
    &quot;Final method
    METHODS meth_public_1_final FINAL.

    &quot;Components used for logging information about method calls
    TYPES: BEGIN OF s_log,
             method            TYPE string,
             implemented_where TYPE string,
             called_from       TYPE syrepid,
             is_inherited      TYPE abap_boolean,
             is_redefined      TYPE abap_boolean,
             is_final          TYPE abap_boolean,
             visibility        TYPE abap_visibility,
             is_static_method  TYPE abap_boolean,
             called_at         TYPE utclong,
           END OF s_log,
           t_log TYPE TABLE OF s_log WITH EMPTY KEY.

    CLASS-DATA log_tab TYPE t_log.
    CLASS-METHODS get_method_info RETURNING VALUE(info) TYPE s_log.

  PROTECTED SECTION.
    METHODS meth_protected_1.

  PRIVATE SECTION.
    METHODS meth_private_1.
ENDCLASS.



CLASS zcl_demo_abap_oo_inheritance_1 IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;----- First level in the inheritance hierarchy ----
    &quot;Creating an instance of the class
    DATA(oref_super) = NEW zcl_demo_abap_oo_inheritance_1( ).

    &quot;Calling methods of the class
    oref_super-&gt;meth_public_1( ).
    oref_super-&gt;meth_public_1_final( ).
    oref_super-&gt;meth_protected_1( ).
    oref_super-&gt;meth_private_1( ).

    &quot;----- Second level in the inheritance hierarchy ----
    &quot;The instance creation and method calling is delegated to
    &quot;a static method in the class
    zcl_demo_abap_oo_inheritance_2=&gt;perform_meth_calls_2( ).

    &quot;----- Third level in the inheritance hierarchy ----
    &quot;Note: The class zcl_demo_abap_oo_inheritance_3 is abstract and contains
    &quot;both non-abstract and abstract instance methods. Instances of abstract
    &quot;classes cannot be created. So, the following statement is not possible.
    &quot;DATA(oref_3) = NEW zcl_demo_abap_oo_inheritance_3( ).

    &quot;Instance components of an abstract class can be accessed via its subclasses.
    &quot;zcl_demo_abap_oo_inheritance_4 inherits from zcl_demo_abap_oo_inheritance_3 and
    &quot;redefines methods of zcl_demo_abap_oo_inheritance_3. Both abstract methods (which
    &quot;are mandatory to implement) and non-abstract methods are redefined. To also access
    &quot;the method implementations of the non-abstract instance methods of
    &quot;zcl_demo_abap_oo_inheritance_3, the respective implementations of the redefined
    &quot;methods in zcl_demo_abap_oo_inheritance_4 include method calls to the direct
    &quot;superclass using the syntax super-&gt;meth( ).. The instance methods of
    &quot;zcl_demo_abap_oo_inheritance_3 are called in the context of the static method call
    &quot;via zcl_demo_abap_oo_inheritance_4 below.

    &quot;----- Fourth level in the inheritance hierarchy ----
    &quot;As above, the instance creation and method calling is delegated to
    &quot;a static method in the class. This method call includes method calls to
    &quot;non-abstract instance methods implemented in zcl_demo_abap_oo_inheritance_3.
    zcl_demo_abap_oo_inheritance_4=&gt;perform_meth_calls_4( ).

    &quot;Writing the log table to the console
    out-&gt;write( data = log_tab name = `log_tab` ).

    &quot;Excursion: Using RTTI to retrieve the name of the superclass
    &quot;As this class starts an inheritance hierarchy, the superclass of this class
    &quot;is the root class OBJECT.
    DATA(tdo_cl) = CAST cl_abap_classdescr( cl_abap_typedescr=&gt;describe_by_name( &#x27;ZCL_DEMO_ABAP_OO_INHERITANCE_1&#x27; ) ).
    DATA(superclass) = tdo_cl-&gt;get_super_class_type( )-&gt;get_relative_name( ).
    out-&gt;write( |\n\n| ).
    out-&gt;write( data = superclass name = `superclass` ).

  ENDMETHOD.

  METHOD class_constructor.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD constructor.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_private_1.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_1.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_1.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_1_final.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD get_method_info.
    &quot;This method retrieves callstack information to determine which method in which
    &quot;class was called by whom.
    &quot;Based on the retrieved class and method names, RTTI is used to get detailed
    &quot;information about methods (such as the visibility or whether the method is
    &quot;inherited, redefined, final, and a static method).

    &quot;Getting callstack information
    DATA(call_stack_tab) = xco_cp=&gt;current-&gt;call_stack-&gt;full( )-&gt;from-&gt;position( 2
                            )-&gt;to-&gt;position( 2 )-&gt;as_text( xco_cp_call_stack=&gt;format-&gt;adt( )
                            )-&gt;get_lines( )-&gt;value.

    IF lines( call_stack_tab ) &lt; 2.
      RETURN.
    ENDIF.

    LOOP AT call_stack_tab INTO DATA(wa) TO 2.
      DATA(tabix) = sy-tabix.
      SPLIT wa AT ` ` INTO TABLE DATA(entry).
      DELETE entry WHERE table_line IS INITIAL.

      DATA(class_name) = condense( val = entry[ 1 ] to = `` ).

      IF tabix = 1.
        info-implemented_where = class_name.

        DATA(meth_name) = condense( val = to_upper( entry[ 2 ] ) to = `` ).
        info-method = meth_name.

        IF class_name IS NOT INITIAL AND meth_name IS NOT INITIAL.
          DATA(tdo_cl) = CAST cl_abap_classdescr( cl_abap_typedescr=&gt;describe_by_name( class_name ) ).
          DATA(methods_cl) = tdo_cl-&gt;methods.
          DATA(meth_info) = VALUE #( methods_cl[ name = meth_name ] OPTIONAL ).
          IF meth_info IS NOT INITIAL.
            info-is_inherited = meth_info-is_inherited.
            info-is_redefined = meth_info-is_redefined.
            info-is_final = meth_info-is_final.
            info-visibility = meth_info-visibility.
            info-is_static_method = meth_info-is_class.
          ENDIF.
        ENDIF.

      ELSE.
        info-called_from = class_name.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.

ENDCLASS.</code></pre>
<p>Class <code>ZCL_DEMO_ABAP_OO_INHERITANCE_2</code></p>
<pre><code>CLASS zcl_demo_abap_oo_inheritance_2 DEFINITION
  INHERITING FROM zcl_demo_abap_oo_inheritance_1
  PUBLIC
  CREATE PROTECTED .

  PUBLIC SECTION.
    &quot;Instance/static constructor declarations
    METHODS constructor.
    CLASS-METHODS class_constructor.

    &quot;Instance method declarations
    METHODS meth_public_2.
    METHODS meth_public_2_final FINAL.

    &quot;Static method declaration for display purposes
    CLASS-METHODS perform_meth_calls_2.

    &quot;Redefining method from the class one level up in the inheritance hierarchy
    &quot;(i.e. the direct superclass)
    METHODS meth_public_1 REDEFINITION.

    &quot;Excursions:
    &quot;- Redefining the final public method of the superclass is not possible.
    &quot;- The same applies to constructors.
    &quot;- The following type has the same name as a type in the superclass. Since components
    &quot;  are inherited, the following declaration is not possible.
    &quot;TYPES t_log TYPE string_table.
    &quot;- Similary, the log table, which is a static component in the superclass, can be
    &quot;  referenced in the method implementations using &#x27;log_table&#x27;. Using the class name
    &quot;  and =&gt; is also possible, but not required.
    &quot;INSERT VALUE #( ) INTO TABLE zcl_demo_abap_oo_inheritance_1=&gt;log_tab.

  PROTECTED SECTION.
    &quot;Instance method declaration
    METHODS meth_protected_2.

    &quot;Redefining method from the class one level up in the inheritance hierarchy
    &quot;(i.e. the direct superclass)
    METHODS meth_protected_1 REDEFINITION.

  PRIVATE SECTION.
    &quot;Ecursion:
    &quot;- The following declarations are not possible.
    &quot;- Private methods cannot be redefined.
    &quot;METHODS meth_private_1 REDEFINITION.
    &quot;- Abstract methods can only be declared in abstract classes. And, since
    &quot;  private methods cannot be redefined, abstract private methods are not
    &quot;  possible.
    &quot;METHODS meth_private_2_abstract ABSTRACT.
ENDCLASS.



CLASS zcl_demo_abap_oo_inheritance_2 IMPLEMENTATION.
  METHOD class_constructor.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_2.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_2_final.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_2.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_1.
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_1.
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD perform_meth_calls_2.
    &quot;Method of this class
    &quot;Creating an instance of the class
    DATA(oref_2) = NEW zcl_demo_abap_oo_inheritance_2( ).

    &quot;Calling methods of this class
    oref_2-&gt;meth_public_2( ).
    oref_2-&gt;meth_public_2_final( ).
    oref_2-&gt;meth_protected_2( ).
    &quot;Calling redefined methods from the class one level up in the inheritance hierarchy (direct superclass)
    oref_2-&gt;meth_protected_1( ).
    oref_2-&gt;meth_public_1( ).

  ENDMETHOD.

ENDCLASS.</code></pre>
<p>Class <code>ZCL_DEMO_ABAP_OO_INHERITANCE_3</code></p>
<pre><code>CLASS zcl_demo_abap_oo_inheritance_3 DEFINITION
  INHERITING FROM zcl_demo_abap_oo_inheritance_2
  PUBLIC
  ABSTRACT
  CREATE PUBLIC .

  PUBLIC SECTION.
    &quot;Instance/static constructor declarations
    METHODS constructor.
    CLASS-METHODS class_constructor.

    &quot;Instance method declarations
    METHODS meth_public_3.
    &quot;Abstract method
    METHODS meth_public_3_abstract ABSTRACT.

    &quot;Redefining methods from the class ...
    &quot;... one level up in the inheritance hierarchy (i.e. the direct superclass)
    METHODS meth_public_2  REDEFINITION.
    &quot;... two levels up in the inheritance hierarchy
    METHODS meth_public_1 REDEFINITION.

  PROTECTED SECTION.
    &quot;Instance method declarations
    METHODS meth_protected_3.
    &quot;Abstract method
    METHODS meth_protected_3_abstract ABSTRACT.

    &quot;Redefining methods from the class ...
    &quot;... one level up in the inheritance hierarchy (i.e. the direct superclass)
    METHODS meth_protected_2 REDEFINITION.
    &quot;... two levels up in the inheritance hierarchy
    &quot;Specifying the FINAL addition
    METHODS meth_protected_1 FINAL REDEFINITION.

  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap_oo_inheritance_3 IMPLEMENTATION.

  METHOD class_constructor.
     INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_3.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_3.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_2.
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_2.
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_1.
    &quot;Reimplementing a method from the class two levels up in the inheritance hierarchy
    &quot;Note that the method is specified with FINAL REDEFINITION. So, a further redefinition in subclasses is not possible.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_1.
    &quot;Reimplementing a method from the class two levels up in the inheritance hierarchy
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>Class <code>ZCL_DEMO_ABAP_OO_INHERITANCE_4</code></p>
<pre><code>CLASS zcl_demo_abap_oo_inheritance_4 DEFINITION
  INHERITING FROM zcl_demo_abap_oo_inheritance_3
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    &quot;Instance/static constructor declarations
    METHODS constructor.
    CLASS-METHODS class_constructor.

    &quot;Instance method declaration
    METHODS meth_public_4.
    &quot;Static method declaration for display purposes
    CLASS-METHODS perform_meth_calls_4.

    &quot;Redefining methods from the class ...
    &quot;... one level up in the inheritance hierarchy (i.e. the direct superclass)
    METHODS meth_public_3 REDEFINITION.
    &quot;Note: Redefining the abstract method here is mandatory.
    METHODS meth_public_3_abstract REDEFINITION.
    &quot;... two levels up in the inheritance hierarchy
    METHODS meth_public_2 REDEFINITION.
    &quot;... three levels up in the inheritance hierarchy
    METHODS meth_public_1 REDEFINITION.

  PROTECTED SECTION.

    &quot;Instance method declaration
    METHODS meth_protected_4.

    &quot;Redefining methods from the class ...
    &quot;... one level up in the inheritance hierarchy (i.e. the direct superclass)
    METHODS meth_protected_3 REDEFINITION.
    &quot;Note: Redefining the abstract method here is mandatory.
    METHODS meth_protected_3_abstract REDEFINITION.
    &quot;... two levels up in the inheritance hierarchy
    METHODS meth_protected_2 REDEFINITION.
    &quot;... three levels up in the inheritance hierarchy
    &quot;The meth_protected_1 method is specified with FINAL REDEFINITION in the
    &quot;direct superclass. Therefore, a further redefinition is not possible.
    &quot;The following statement is not possible.
    &quot;METHODS meth_protected_1 REDEFINITION.

  PRIVATE SECTION.
ENDCLASS.



CLASS zcl_demo_abap_oo_inheritance_4 IMPLEMENTATION.

  METHOD class_constructor.
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD constructor.
    super-&gt;constructor( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_4.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_4.
    &quot;Method of this class
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_3.
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    &quot;Calling this instance method that is redefined in the abstract direct superclass (instances of abstract classes cannot be created)
    &quot;by referring to the direct superclass using the syntax super-&gt;...
    super-&gt;meth_public_3( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_3.
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    &quot;This method is a non-abstract instance method of the abstract direct superclass. Instances of abstract classes
    &quot;cannot be created. The syntax super-&gt;meth is used to also call instance methods of the abstract direct superclass
    &quot;for output purposes.
    super-&gt;meth_protected_3( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_3_abstract.
    &quot;Implementating abstract methods are only possible in subclasses of abstract classes
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_3_abstract.
    &quot;Implementating abstract methods are only possible in subclasses of abstract classes
    &quot;Reimplementing a method from the class one level up in the inheritance hierarchy (direct superclass)
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_2.
    &quot;Reimplementing a method from the class two levels up in the inheritance hierarchy
    &quot;Calling this instance method that is redefined in the abstract direct superclass (instances of abstract
    &quot;classes cannot be created) by referring to the direct superclass using the syntax super-&gt;....
    super-&gt;meth_public_2( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_protected_2.
    &quot;Reimplementing a method from the class two levels up in the inheritance hierarchy
    &quot;Calling this instance method that is redefined in the abstract direct superclass (instances of abstract
    &quot;classes cannot be created) by referring to the direct superclass using the syntax super-&gt;....
    super-&gt;meth_protected_2( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD meth_public_1.
    &quot;Reimplementing a method from the class three levels up in the inheritance hierarchy
    &quot;Calling this instance method that is redefined in the abstract direct superclass (instances of abstract
    &quot;classes cannot be created) by referring to the direct superclass using the syntax super-&gt;....
    super-&gt;meth_public_1( ).
    INSERT VALUE #( called_at = utclong_current( ) ) INTO TABLE log_tab ASSIGNING FIELD-SYMBOL(&lt;fs&gt;).
    &lt;fs&gt; = CORRESPONDING #( BASE ( &lt;fs&gt; ) get_method_info( ) EXCEPT called_at ).
  ENDMETHOD.

  METHOD perform_meth_calls_4.
    &quot;Method of this class
    &quot;Creating an instance of the class
    DATA(oref_4) = NEW zcl_demo_abap_oo_inheritance_4( ).

    &quot;Calling methods of this class
    oref_4-&gt;meth_public_4( ).
    oref_4-&gt;meth_protected_4( ).
    &quot;Calling redefined methods from the class ...
    &quot;... one level up in the inheritance hierarchy (direct superclass)
    &quot;Among them are abstract methods that can only be implemented in subclasses.
    &quot;Note that the implementations of the non-abstract, redefined methods in this
    &quot;class includes method calls of the abstract direct superclass so that
    &quot;also these implementations are called for output purposes.
    oref_4-&gt;meth_public_3( ).
    oref_4-&gt;meth_public_3_abstract( ).
    oref_4-&gt;meth_protected_3( ).
    oref_4-&gt;meth_protected_3_abstract( ).
    &quot;... two levels up in the inheritance hierarchy
    oref_4-&gt;meth_public_2( ).
    oref_4-&gt;meth_protected_2( ).
    &quot;... three levels up in the inheritance hierarchy
    oref_4-&gt;meth_public_1( ).
    &quot;Note: The following method call calls the method implementation in
    &quot;class zcl_demo_abap_oo_inheritance_3. The method is specified with FINAL
    &quot;REDEFINITION. So, it cannot be redefined in this class inheriting from
    &quot;zcl_demo_abap_oo_inheritance_3.
    oref_4-&gt;meth_protected_1( ).
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Polymorphism and Casting (Upcast/Downcast)</h2>
<p>The object orientation concept <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenpolymorphism_glosry.htm">polymorphism</a> means you can address differently implemented methods belonging to different objects of different classes using one and the same reference variable, for example, object reference variables pointing to a superclass can point to objects of a subclass.</p>
<p>Note the concept of static and dynamic type in this context:</p>
<ul>
<li>Object reference variables (and also interface reference variables) have both a</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenstatic_type_glosry.htm">static</a> and a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendynamic_type_glosry.htm">dynamic type</a>.</p>
<ul>
<li>When declaring an object reference variable, e. g. <code>DATA oref TYPE REF TO cl</code>, you determine the static type, i. e.</li>
</ul>
<p><code>cl</code> - a class - is used to declare the reference variable that is statically defined in the code. This is the class of an object to which the reference variable points to.</p>
<ul>
<li>Similarly, the dynamic type also defines the class of an object which the reference variable points to. However, the dynamic type is determined at runtime, i. e. the class of an object which the reference variable points to can change.</li>
<li>Relevant for? This differentiation enters the picture in polymorphism when a reference variable typed with reference to a subclass can always be assigned to reference variables typed with reference to one of its superclasses or their interfaces. That&#x27;s what is called <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenup_cast_glosry.htm">upcast</a> (or widening cast). Or the assignment is done the other way round. That&#x27;s what is called <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendown_cast_glosry.htm">downcast</a> (or narrowing cast).</li>
</ul>
<div class="note">[!TIP] - The following basic rule applies: The static type is always more general than or the same as the dynamic type. The other way round: The dynamic type is always more special than or equal to the static type. - That means: - If the static type is a class, the dynamic type must be the same class or one of its subclasses. - If the static type is an interface, the dynamic type must implement the interface.</div>
<ul>
<li>Regarding assignments: If it can be statically checked that an assignment is possible</li>
</ul>
<p>although the types are different, the assignment is done using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenassignment_operator_glosry.htm">assignment operator</a> <code>=</code> that triggers an upcast automatically.</p>
<ul>
<li>Otherwise, it is a</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abendown_cast_glosry.htm">downcast</a>. Here, the assignability is not checked until runtime. The downcast - in contrast to upcasts - must be triggered explicitly using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencasting_operator_glosry.htm">casting operator</a> <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconstructor_expression_cast.htm"><code>CAST</code></a>. You might see code using the older operator <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmove_cast.htm"><code>?=</code></a>.</p>
<ul>
<li>See more information in the topic [Assignment Rules for Reference</li>
</ul>
<p>Variables](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenconversion_references.htm).</p>
<p>As an example, assume there is an inheritance tree with <code>lcl_super</code> as the superclass and <code>lcl_sub</code> as a direct subclass. <code>lcl_sub2</code> is a direct subclass of <code>lcl_sub</code>.</p>
<p>In the following code snippet, the rule is met since the superclass is either the same as or more generic than the subclass (the subclass has, for example, redefined methods and is, thus, more specific). Hence, the assignment of an object reference variable pointing to the subclass to a variable pointing to a superclass works. An upcast is triggered. After this casting, the type of <code>oref_super</code> has changed and the methods of <code>lcl_sub</code> can be accessed via <code>oref_super</code>.</p>
<pre><code>&quot;Creating object references
DATA(oref_super) = NEW lcl_super( ).

DATA(oref_sub) = NEW lcl_sub( ).

&quot;Upcast
oref_super = oref_sub.

&quot;The casting might be done when creating the object.
DATA super_ref TYPE REF TO lcl_super.

super_ref = NEW lcl_sub( ).</code></pre>
<ul>
<li>As mentioned above, a downcast must be triggered</li>
</ul>
<p>manually. Just an assignment like <code>oref_sub = oref_super.</code> does not work. A syntax error occurs saying the right-hand variable&#x27;s type cannot be converted to the left-hand variable&#x27;s type.</p>
<ul>
<li>If you indeed want to carry out this casting, you must use</li>
</ul>
<p><code>CAST</code> (or you might see code using the older operator <code>?=</code>) to overcome this syntax error (but just the syntax error!). Note: You might also use these casting operators for the upcasts. That means <code>oref_super = oref_sub.</code> has the same effect as <code>oref_super = CAST #( oref_sub ).</code>. Using the casting operator for upcasts is usually not necessary.</p>
<ul>
<li>At runtime, the assignment is checked and if the conversion does not work, you face a (catchable) exception. Even more so, the assignment <code>oref_sub = CAST #( oref_super ).</code> does not throw a syntax error but it does not work in this example either because it violates the rule mentioned above (<code>oref_sub</code> is more specific than <code>oref_super</code>).</li>
<li>To check whether such an assignment is (not) possible</li>
</ul>
<p>on specific classes, you can use the predicate expression [<code>IS [NOT] INSTANCE OF</code>](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenlogexp_instance_of.htm) or the case distinction <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapcase_type.htm"><code>CASE TYPE OF</code></a>. Carrying out an upcast before the downcast ensures that the left-hand variable&#x27;s type is compatible to the right-hand variable&#x27;s type.</p>
<pre><code>DATA(oref_super) = NEW lcl_super( ).
DATA(oref_sub) = NEW lcl_sub( ).
DATA(oref_sub2) = NEW lcl_sub2( ).

&quot;Downcast impossible (oref_sub is more specific than oref_super);
&quot;the exception is caught here

TRY.
  oref_sub = CAST #( oref_super ).
  CATCH cx_sy_move_cast_error INTO DATA(e).
    ...
ENDTRY.

&quot;Working downcast with a prior upcast

oref_super = oref_sub2.

&quot;Due to the prior upcast, the following check is actually not necessary.

IF oref_super IS INSTANCE OF lcl_sub.
  oref_sub = CAST #( oref_super ).
  ...
ENDIF.

&quot;Excursion RTTI: Downcasts, CAST and method chaining
&quot;Downcasts particularly play, for example, a role in the context of
&quot;retrieving type information using RTTI. Method chaining is handy
&quot;because it reduces the lines of code in this case.
&quot;The example below shows the retrieval of type information
&quot;regarding the components of a structure. 
&quot;Due to the method chaining in the second example, the three
&quot;statements in the first example are reduced to one statement.

DATA struct4cast TYPE zdemo_abap_carr.

DATA(rtti_a) = cl_abap_typedescr=&gt;describe_by_data( struct4cast ).
DATA(rtti_b) = CAST cl_abap_structdescr( rtti_a ).
DATA(rtti_c) = rtti_b-&gt;components.

DATA(rtti_d) = CAST cl_abap_structdescr(
  cl_abap_typedescr=&gt;describe_by_data( struct4cast )
      )-&gt;components.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Example with Local Classes Demonstrating Upcasts and Downcasts</h3>
<p>The following example class demonstrates upcasts and downcasts using multiple local classes to avoid the extra creation of multiple global classes and to have a self-contained example. To try the example out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it (<em>Global Class</em> and <em>Local Types</em> tabs in ADT). After activation, choose <em>F9</em> in ADT to execute the class. The example is not set up to display output in the console, but it contains comments. You may want to set break points and walk through the demo assignments.</p>
<p>The inheritance tree of the local example classes is as follows:</p>
<pre><code>LCL1
  |
  |--LCL2
  |
  |--LCL3
  |   |
  |   |--LCL4</code></pre>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Class include &lt;/td&gt; &lt;td&gt; Code &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Global class</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;Object reference variables
    &quot;Create object references
    DATA: oref1 TYPE REF TO lcl1,
          oref2 TYPE REF TO lcl2,
          oref3 TYPE REF TO lcl3,
          oref4 TYPE REF TO lcl4.

    &quot;Creating objects
    oref1 = NEW #( ).
    oref2 = NEW #( ).
    oref3 = NEW #( ).
    oref4 = NEW #( ).

    &quot;Calling redefined methods
    DATA text TYPE string.

    &quot;A
    text = oref1-&gt;meth( ).
    &quot;B
    text = oref2-&gt;meth( ).
    &quot;C
    text = oref3-&gt;meth( ).
    &quot;D
    text = oref4-&gt;meth( ).

    &quot;Polymorphism is demonstrated as follows: A reference variable typed with
    &quot;reference to a subclass can always be assigned to reference variables typed
    &quot;with reference to one of its superclasses.
    &quot;The following statements show upcasts using the assignment operator =.
    &quot;The upcast works because the basic rule is met that the static types of the
    &quot;targets are more general or identical to the static type of the sources.
    &quot;Example:
    &quot;- oref1 is statically typed with the type ref to lcl1.
    &quot;- oref2 is statically typed with the type ref to lcl2.
    &quot;- In the assignment &#x27;oref1 = oref2.&#x27;, the rule is met as the target variable&#x27;s
    &quot;  static type of oref1 is more general (higher up in the inheritance tree).
    &quot;- For the assignment to finally work, the dynamic type (which is the actual
    &quot;  object the object reference points to at runtime) must be the same class or one
    &quot;  of its subclasses.

    &quot;lcl1 is the superclass of the other local classes
    oref1 = oref2.
    &quot;B
    text = oref1-&gt;meth( ).

    oref1 = oref3.
    &quot;C
    text = oref1-&gt;meth( ).

    &quot;lcl4 is a subclass of lcl3 (and thus also from lcl1)
    oref1 = oref4.
    &quot;D
    text = oref1-&gt;meth( ).

    oref3 = oref4.
    &quot;D
    text = oref3-&gt;meth( ).

    &quot;Upcasts also work if the static types are identical
    DATA(oref1b) = NEW lcl1( ).
    oref1 = oref1b.
    text = oref1-&gt;meth( ).

    &quot;Re-creating objects
    oref1 = NEW #( ).
    oref2 = NEW #( ).
    oref3 = NEW #( ).
    oref4 = NEW #( ).

    &quot;The following statement (basically moving down the inheritance tree) cannot
    &quot;be specified using the assignment operator as there is a type conflict.
    &quot;The right-hand variable&#x27;s type cannot be converted to the left-hand variable&#x27;s type.
    &quot;If you indeed want to cast, you must perform a downcast.
    &quot;oref2 = oref1.

    &quot;Downcasts
    &quot;Here, the static type of the target variables are more specific than the static types
    &quot;of the source variables. The downcast must be triggered explicitly, e.g. by the CAST or
    &quot;the older ?= operator.
    &quot;However, note that the assignability is not checked until runtime. Only the syntax error
    &quot;as in the previous statement is not displayed anymore.

    &quot;The following example triggers a downcast explicitly. So, the syntax error from above
    &quot;is not displayed. However, that does not mean that the assignment actually works.
    &quot;For the downcast to work, the dynamic type must be the same class or one of its subclasses.
    TRY.
        oref2 = CAST #( oref1 ).
      CATCH cx_sy_move_cast_error INTO DATA(error).
        text = error-&gt;get_text( ).
    ENDTRY.

    &quot;The following example performs an upcast so that the dynamic type of oref1 refers to
    &quot;one of the subclasses. However, the example downcast does not work either. lcl3 (static type
    &quot;of oref3) is not a subclass of lcl2 oref2 refers to.
    oref1 = oref3.

    TRY.
        oref2 = CAST #( oref1 ).
      CATCH cx_sy_move_cast_error INTO error.
        text = error-&gt;get_text( ).
    ENDTRY.

    &quot;The following downcasts work because the rule is met that the dynamic type must be the same
    &quot;class or one of its subclasses.
    &quot;Upcast before the downcast
    oref1 = oref2.
    &quot;Downcast (dynamic type is the same)
    oref2 = CAST #( oref1 ).
    &quot;B
    text = oref2-&gt;meth( ).

    &quot;Downcast (dynamic type is one of the subclasses)
    &quot;Re-creating objects
    oref1 = NEW #( ).
    oref4 = NEW #( ).

    &quot;lcl4 (oref4 points to) is a subclass of lcl3 (oref3 points to), and thus of lcl1
    &quot;Upcast before the downcast
    oref1 = oref4.
    &quot;Downcast (dynamic type is one of the subclasses)
    oref3 = CAST #( oref1 ).
    &quot;D
    text = oref3-&gt;meth( ).

    &quot;You can check whether downcasts are possible with IF and CASE statements
    &quot;Re-creating objects
    oref1 = NEW #( ).
    oref3 = NEW #( ).
    oref4 = NEW #( ).

    IF oref1 IS INSTANCE OF lcl3.
      oref3 = CAST #( oref1 ).
      text = oref3-&gt;meth( ).
    ELSE.
      &quot;This section is executed in the example.
      text = `...`.
    ENDIF.

    &quot;Upcast
    oref1 = oref4.

    CASE TYPE OF oref1.
      WHEN TYPE lcl3.
        &quot;This section is executed in the example.
        oref3 = CAST #( oref1 ).
        &quot;D
        text = oref3-&gt;meth( ).
      WHEN OTHERS.
        ...
    ENDCASE.

  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>CCIMP include (Local Types tab in ADT)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    METHODS meth RETURNING VALUE(text) TYPE string.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD meth.
    text = `A`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD meth.
    text = `B`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS meth REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD meth.
    text = `C`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl4 DEFINITION INHERITING FROM lcl3.
  PUBLIC SECTION.
    METHODS meth REDEFINITION.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD meth.
    text = `D`.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Example with local classes implementing a local interface&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<ul>
<li>This example, which anticipates interfaces, is similar to the previous one. Here, local classes implement a local interface.</li>
<li>The example emphasizes that interface reference variables can point to objects of classes that implement the same interface.</li>
</ul>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Class include &lt;/td&gt; &lt;td&gt; Code &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Global class</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    &quot;Object reference variables
    &quot;Creating object references
    DATA:
      &quot;lcl1 implements the interface lif
      &quot;Up to oref4, the classes are in an inheritance relationship
      oref1 TYPE REF TO lcl1,
      oref2 TYPE REF TO lcl2,
      oref3 TYPE REF TO lcl3,
      oref4 TYPE REF TO lcl4,
      &quot;Implements the interface lif but is not in the inheritance relationship above
      oref5 TYPE REF TO lcl5,
      &quot;Interface reference variable
      iref  TYPE REF TO lif.

    &quot;Creating objects
    oref1 = NEW #( ).
    oref2 = NEW #( ).
    oref3 = NEW #( ).
    oref4 = NEW #( ).
    oref5 = NEW #( ).

    &quot;Calling redefined methods
    DATA text TYPE string.

    &quot;A
    text = oref1-&gt;lif~meth( ).
    &quot;B
    text = oref2-&gt;lif~meth( ).
    &quot;C
    text = oref3-&gt;lif~meth( ).
    &quot;D
    text = oref4-&gt;lif~meth( ).
    &quot;E
    text = oref5-&gt;lif~meth( ).

    &quot;The static type can also refer to an interface.
    &quot;Assignments

    &quot;The static types of the example reference variables refer to classes
    &quot;that implement the interface and are in an inheritance relationship.
    iref = oref1.
    &quot;A
    text = iref-&gt;meth( ).

    iref = oref2.
    &quot;B
    text = iref-&gt;meth( ).

    iref = oref3.
    &quot;C
    text = iref-&gt;meth( ).

    iref = oref4.
    &quot;D
    text = iref-&gt;meth( ).

    &quot;Example class that implements the interface
    iref = oref5.
    &quot;E
    text = iref-&gt;meth( ).

    &quot;Both lcl1 (oref1) and lcl5 (oref5) implement the interface lif,
    &quot;so the assignment works as the static type of the interface reference
    &quot;variables is the same.
    DATA irefb TYPE REF TO lif.
    iref = oref1.
    irefb = oref5.
    iref = irefb.

    &quot;Downcasts
    &quot;Upcast before the downcast
    iref = oref4.
    &quot;The following statement triggers a sytax error
    &quot;oref3 = iref.
    oref3 = CAST #( iref ).
    &quot;D
    text = oref3-&gt;lif~meth( ).

    &quot;Downcast not possible because the dynamic type is not the same or a subclass
    TRY.
        iref = oref2.
        oref3 = CAST #( iref ).
      CATCH cx_sy_move_cast_error INTO DATA(error).
        text = error-&gt;get_text( ).
    ENDTRY.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>CCIMP include (Local Types tab in ADT)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth RETURNING VALUE(text) TYPE string.
ENDINTERFACE.

CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.
  METHOD lif~meth.
    text = `A`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS lif~meth REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD lif~meth.
    text = `B`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl3 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS lif~meth REDEFINITION.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.
  METHOD lif~meth.
    text = `C`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl4 DEFINITION INHERITING FROM lcl3.
  PUBLIC SECTION.
    METHODS lif~meth REDEFINITION.
ENDCLASS.

CLASS lcl4 IMPLEMENTATION.
  METHOD lif~meth.
    text = `D`.
  ENDMETHOD.
ENDCLASS.

CLASS lcl5 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

CLASS lcl5 IMPLEMENTATION.
  METHOD lif~meth.
    text = `E`.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Upcasts and Downcasts Using the RTTS Inheritance Tree</h3>
<p>The examples in the following code snippet use object reference variables to illustrate the class hierarchy of the <a href="#runtime-type-services-rtts">Runtime Type Services (RTTS)</a>, which is covered in more detail in the <a href="06_Dynamic_Programming.md">Dynamic Programming</a> cheat sheet.</p>
<p>Hierarchy tree of the classes:</p>
<pre><code>CL_ABAP_TYPEDESCR
  |
  |--CL_ABAP_DATADESCR
  |   |
  |   |--CL_ABAP_ELEMDESCR
  |   |   |
  |   |   |--CL_ABAP_ENUMDESCR
  |   |
  |   |--CL_ABAP_REFDESCR
  |   |--CL_ABAP_COMPLEXDESCR
  |       |
  |       |--CL_ABAP_STRUCTDESCR
  |       |--CL_ABAP_TABLEDESCR
  |
  |--CL_ABAP_OBJECTDESCR
     |
     |--CL_ABAP_CLASSDESCR
     |--CL_ABAP_INTFDESCR</code></pre>
<p>Examples:</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Object reference variables
*&amp;---------------------------------------------------------------------*

&quot;Static and dynamic types
&quot;Defining an object reference variable with a static type
DATA tdo TYPE REF TO cl_abap_typedescr.

&quot;Retrieving type information
&quot;The reference the reference variable points to is either cl_abap_elemdescr,
&quot;cl_abap_enumdescr, cl_abap_refdescr, cl_abap_structdescr, or cl_abap_tabledescr.
&quot;So, it points to one of the subclasses. The static type of tdo refers to
&quot;cl_abap_typedescr, however, the dynamic type is one of the subclasses mentioned.
&quot;in the case of the example, it is cl_abap_elemdescr. Check in the debugger.
DATA some_string TYPE string.
tdo = cl_abap_typedescr=&gt;describe_by_data( some_string ).

&quot;Some more object reference variables
DATA tdo_super TYPE REF TO cl_abap_typedescr.
DATA tdo_elem TYPE REF TO cl_abap_elemdescr.
DATA tdo_data TYPE REF TO cl_abap_datadescr.
DATA tdo_gen_obj TYPE REF TO object.

*&amp;---------------------------------------------------------------------*
*&amp; Upcasts
*&amp;---------------------------------------------------------------------*

&quot;Moving up the inheritance tree
&quot;Assignments:
&quot;- If the static type of target variable is less specific or the same, an assignment works.
&quot;- The target variable inherits the dynamic type of the source variable.

&quot;Static type of target variable is the same
tdo_super = tdo.

&quot;Examples for static types of target variables that are less specific
&quot;Target variable has the generic type object
tdo_gen_obj = tdo.

&quot;Target variable is less specific because the direct superclass of cl_abap_elemdescr
&quot;is cl_abap_datadescr
&quot;Note: In the following three assignments, the target variable remains initial 
&quot;since the source variables do not (yet) point to any object.
tdo_data = tdo_elem.

&quot;Target variable is less specific because the direct superclass of cl_abap_datadescr
&quot;is cl_abap_typedescr
tdo_super = tdo_data.

&quot;Target variable is less specific because the class cl_abap_typedescr is higher up in
&quot;the inheritance tree than cl_abap_elemdescr
tdo_super = tdo_elem.

&quot;The casting happens implicitly. You can also excplicitly cast and use
&quot;casting operators, but it is usually not required.
tdo_super = CAST #( tdo ).
tdo_super ?= tdo.

&quot;In combination with inline declarations, the CAST operator can be used to provide a
&quot;reference variable with a more general type.
DATA(tdo_inl_cast) = CAST cl_abap_typedescr( tdo_elem ).

CLEAR: tdo_super, tdo_elem, tdo_data, tdo_gen_obj.

*&amp;---------------------------------------------------------------------*
*&amp; Downcasts
*&amp;---------------------------------------------------------------------*

&quot;Moving down the inheritance tree
&quot;Assignments:
&quot;- If the static type of the target variable is more specific than the static type
&quot;  of the source variable, performing a check whether it is less specific or the same
&quot;  as the dynamic type of the source variable is required at runtime before the assignment
&quot;- The target variable inherits the dynamic type of the source variable, however, the target
&quot;  variable can accept fewer dynamic types than the source variable
&quot;- Downcasts are always performed explicitly using casting operators

&quot;Static type of the target is more specific
&quot;object -&gt; cl_abap_typedescr
tdo_super = CAST #( tdo_gen_obj ).
&quot;cl_abap_typedescr -&gt; cl_abap_datadescr
&quot;Note: Here, the dynamic type of the source variable is cl_abap_elemdescr.
tdo_data = CAST #( tdo ).
&quot;cl_abap_datadescr -&gt; cl_abap_elemdescr
tdo_elem = CAST #( tdo_data ).
&quot;cl_abap_typedescr -&gt; cl_abap_elemdescr
tdo_elem = CAST #( tdo_super ).</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Checking the Dynamic Type of Object Reference Variables</h3>
<ul>
<li>Special control structures using <code>IF ... IS INSTANCE OF ...</code> and <code>CASE TYPE OF</code> check the dynamic type of non-initial object reference variables and the static type of initial object reference variables.</li>
<li>The branch&#x27;s first statement block is executed when a specified class or interface is more general than or equal to the given type.</li>
<li>In doing so, you can especially check if assignments between object reference variable work, and you can prevent errors in downcasts.</li>
</ul>
<pre><code>DATA type_descr_obj TYPE REF TO cl_abap_typedescr.
DATA type_descr_obj_gen TYPE REF TO object.
DATA str TYPE string.

&quot;In the examples above, the assignments work. The following code snippets
&quot;deal with examples in which a downcast is not possible. An exception is
&quot;raised.
DATA str_table TYPE string_table.
DATA type_descr_obj_table TYPE REF TO cl_abap_tabledescr.

&quot;With the following method call, type_descr_obj points to an object with
&quot;reference to cl_abap_tabledescr.
type_descr_obj = cl_abap_typedescr=&gt;describe_by_data( str_table ).

&quot;Therefore, the following downcast works.
type_descr_obj_table = CAST #( type_descr_obj ).

&quot;You could also achieve the same in one statement and with inline
&quot;declaration.
DATA(type_descr_obj_table_2) = CAST cl_abap_tabledescr( cl_abap_typedescr=&gt;describe_by_data( str_table ) ).

&quot;Example for an impossible downcast
&quot;The generic object reference variable points to cl_abap_elemdescr after the following
&quot;assignment.
type_descr_obj_gen = cl_abap_typedescr=&gt;describe_by_data( str ).

&quot;Without catching the exception, the runtime error MOVE_CAST_ERROR
&quot;occurs. There is no syntax error at compile time. The static type of
&quot;type_descr_obj_gen is more generic than the static type of the target variable.
&quot;The error occurs when trying to downcast, and the dynamic type is used.
TRY.
    type_descr_obj_table = CAST #( type_descr_obj_gen ).
  CATCH cx_sy_move_cast_error.
ENDTRY.
&quot;Note: type_descr_obj_table sill points to the reference as assigned above after trying
&quot;to downcast in the TRY control structure.

&quot;Using CASE TYPE OF and IS INSTANCE OF statements, you can check if downcasts
&quot;are possible.
&quot;Note: In case of ...
&quot;- non-initial object reference variables, the dynamic type is checked.
&quot;- initial object reference variables, the static type is checked.

*&amp;---------------------------------------------------------------------*
*&amp; IS INSTANCE OF
*&amp;---------------------------------------------------------------------*

DATA some_type_descr_obj TYPE REF TO cl_abap_typedescr.
some_type_descr_obj = cl_abap_typedescr=&gt;describe_by_data( str_table ).

IF some_type_descr_obj IS INSTANCE OF cl_abap_elemdescr.
  DATA(type_descr_obj_a) = CAST cl_abap_elemdescr( some_type_descr_obj ).
ELSE.
  &quot;This branch is executed. The downcast is not possible.
  ...
ENDIF.

IF some_type_descr_obj IS INSTANCE OF cl_abap_elemdescr.
  DATA(type_descr_obj_b) = CAST cl_abap_elemdescr( some_type_descr_obj ).
ELSEIF some_type_descr_obj IS INSTANCE OF cl_abap_refdescr.
  DATA(type_descr_obj_c) = CAST cl_abap_refdescr( some_type_descr_obj ).
ELSEIF some_type_descr_obj IS INSTANCE OF cl_abap_structdescr.
  DATA(type_descr_obj_d) = CAST cl_abap_structdescr( some_type_descr_obj ).
ELSEIF some_type_descr_obj IS INSTANCE OF cl_abap_tabledescr.
  &quot;In this example, this branch is executed. With the check,
  &quot;you can make sure that the downcast is indeed possible.
  DATA(type_descr_obj_e) = CAST cl_abap_tabledescr( some_type_descr_obj ).
ELSE.
  ...
ENDIF.

DATA initial_type_descr_obj TYPE REF TO cl_abap_typedescr.

IF initial_type_descr_obj IS INSTANCE OF cl_abap_elemdescr.
  DATA(type_descr_obj_f) = CAST cl_abap_elemdescr( some_type_descr_obj ).
ELSEIF initial_type_descr_obj IS INSTANCE OF cl_abap_refdescr.
  DATA(type_descr_obj_g) = CAST cl_abap_refdescr( some_type_descr_obj ).
ELSEIF initial_type_descr_obj IS INSTANCE OF cl_abap_structdescr.
  DATA(type_descr_obj_h) = CAST cl_abap_structdescr( some_type_descr_obj ).
ELSEIF initial_type_descr_obj IS INSTANCE OF cl_abap_tabledescr.
  DATA(type_descr_obj_i) = CAST cl_abap_tabledescr( some_type_descr_obj ).
ELSE.
  &quot;In this example, this branch is executed. The static
  &quot;type of the initial object reference variable is used,
  &quot;which is cl_abap_typedescr here.
  ...
ENDIF.

*&amp;---------------------------------------------------------------------*
*&amp; CASE TYPE OF
*&amp;---------------------------------------------------------------------*

&quot;The examples are desinged similarly to the IS INSTANCE OF examples.

DATA(dref) = REF #( str_table ).
some_type_descr_obj = cl_abap_typedescr=&gt;describe_by_data( dref ).

CASE TYPE OF some_type_descr_obj.
  WHEN TYPE cl_abap_elemdescr.
    DATA(type_descr_obj_j) = CAST cl_abap_elemdescr( some_type_descr_obj ).
  WHEN TYPE cl_abap_refdescr.
    &quot;In this example, this branch is executed. With the check,
    &quot;you can make sure that the downcast is indeed possible.
    DATA(type_descr_obj_k) = CAST cl_abap_refdescr( some_type_descr_obj ).
  WHEN TYPE cl_abap_structdescr.
    DATA(type_descr_obj_l) = CAST cl_abap_structdescr( some_type_descr_obj ).
  WHEN TYPE cl_abap_tabledescr.
    DATA(type_descr_obj_m) = CAST cl_abap_tabledescr( some_type_descr_obj ).
  WHEN OTHERS.
    ...
ENDCASE.

&quot;Example with initial object reference variable
CASE TYPE OF initial_type_descr_obj.
  WHEN TYPE cl_abap_elemdescr.
    DATA(type_descr_obj_n) = CAST cl_abap_elemdescr( some_type_descr_obj ).
  WHEN TYPE cl_abap_refdescr.
    DATA(type_descr_obj_o) = CAST cl_abap_refdescr( some_type_descr_obj ).
  WHEN TYPE cl_abap_structdescr.
    DATA(type_descr_obj_p) = CAST cl_abap_structdescr( some_type_descr_obj ).
  WHEN TYPE cl_abap_tabledescr.
    DATA(type_descr_obj_q) = CAST cl_abap_tabledescr( some_type_descr_obj ).
  WHEN OTHERS.
    &quot;In this example, this branch is executed. The static
    &quot;type of the initial object reference variable is used,
    &quot;which is cl_abap_typedescr here.
    ...
ENDCASE.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Interfaces</h2>
<p>Interfaces ...</p>
<ul>
<li>represent a template for the components in the public visibility</li>
</ul>
<p>section of classes.</p>
<ul>
<li>enhance classes by adding interface components.</li>
<li>are possible as both</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenlocal_interface_glosry.htm">local</a> and <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenglobal_interface_glosry.htm">global interfaces</a>.</p>
<ul>
<li>support</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenpolymorphism_glosry.htm">polymorphism</a> in classes. Each class that implements an interface can implement its methods differently. <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninterface_ref_variable_glosry.htm">Interface reference variables</a> can point to objects of all classes that implement the associated interface.</p>
<ul>
<li>can be implemented by classes of an inheritance tree. It can be any number of interfaces. However, each interface can be implemented only once in an inheritance tree.</li>
<li>are different from classes in the following ways:</li>
<li>They only consist of a part declaring the components without an</li>
</ul>
<p>implementation part. The implementation is done in classes that use the interface.</p>
<ul>
<li>There are no visibility sections. All components of an interface are public.</li>
<li>No instances can be created from interfaces.</li>
<li>Declarations as mentioned for classes, e. g. <code>DATA</code>,</li>
</ul>
<p><code>CLASS-DATA</code>, <code>METHODS</code>, <code>CLASS-METHODS</code>, are possible. Constructors are not possible.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Defining Interfaces</h3>
<ul>
<li>You can define global and local interfaces.</li>
<li>Like global classes, global interfaces are defined using the <code>PUBLIC</code> addition.</li>
</ul>
<pre><code>&quot;The addition PUBLIC is for global interfaces
INTERFACE intf_global PUBLIC.
&quot;Local interface
&quot;INTERFACE intf_local.
        
    DATA ...
    CLASS-DATA ...
    METHODS ...
    CLASS-METHODS ...
    CONSTANTS ...
    TYPES ...
    

ENDINTERFACE.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Implementing Interfaces</h3>
<ul>
<li>A class can implement multiple interfaces.</li>
<li>Interfaces must be specified in the declaration part of a class using the statement</li>
</ul>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapinterfaces.htm"><code>INTERFACES</code></a>.</p>
<ul>
<li>Since all interface components are public, you must include this statement and the interfaces in the public visibility section of a class. When an interface is implemented in a class, all interface components are added to the other components of the class in the public visibility section.</li>
<li>Classes must implement the methods of all implemented interfaces in them unless ...</li>
<li>methods are flagged as abstract or final (see next section).</li>
<li>methods mark their implementation as optional using the additions <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_default.htm"><code>DEFAULT IGNORE</code></a> or <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_default.htm"><code>DEFAULT FAIL</code></a> (see next section).</li>
<li>Interface components can be addressed using the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninterface_comp_selector_glosry.htm">interface component selector</a>: <code>... intf~comp ...</code>.</li>
<li>You can also include other interfaces in interfaces.</li>
</ul>
<pre><code>&quot;Local interface in a CCIMP include
INTERFACE lif.

  DATA str TYPE string.
  CLASS-DATA number TYPE i.
  CONSTANTS const_number TYPE i VALUE 1.
  &quot;All of the following methods must be implemented in classes
  &quot;that implement the interface.
  METHODS inst_meth IMPORTING num TYPE i.
  CLASS-METHODS stat_meth RETURNING VALUE(result) TYPE string.

ENDINTERFACE.

&quot;Local class in a CCIMP include implementing the interface
CLASS lcl DEFINITION.
  PUBLIC SECTION.
    &quot;Multiple interface implementations are possible
    INTERFACES lif.
ENDCLASS.

CLASS lcl IMPLEMENTATION.

  METHOD lif~inst_meth.
    ...
    &quot;Assuming lif~number has been assigned a value somewhere.
    DATA(addition) = num + lif~number.
    ...
  ENDMETHOD.

  METHOD lif~stat_meth.
    ...
  ENDMETHOD.

ENDCLASS.

**********************************************************************
&quot;Including interfaces in other interfaces

INTERFACE lif2.
  METHODS meth_of_lif2.
  INTERFACES lif.
ENDINTERFACE.

CLASS lcl2 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif2.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD lif2~meth_of_lif2.
    ...
  ENDMETHOD.

  METHOD lif~inst_meth.
    ...
  ENDMETHOD.

  METHOD lif~stat_meth.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Additions Related to Interface Implementations</h3>
<div class="note">[!NOTE] The code examples show local classes and interfaces declared, for example, in the [CCIMP include](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenccimp_glosry.htm) of a class pool.</div>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Addition &lt;/td&gt; &lt;td&gt; Notes &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapaliases.htm"><code>ALIASES ... FOR ...</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Specifies alias names for the interface components. The components can then be addressed using the alias name.</p>
<p>&lt;br&gt;</p>
<pre><code>&quot;Local interface in a CCIMP include
INTERFACE lif.
  METHODS some_method.
  DATA some_string type string.
ENDINTERFACE.

&quot;Local class in a CCIMP include implementing the interface
CLASS lcl DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
    ALIASES meth FOR lif~some_method.
    ALIASES str FOR lif~some_string.
ENDCLASS.

CLASS lcl IMPLEMENTATION.
  METHOD meth.
    &quot;The following syntax is also possible: METHOD lif~some_method.
    ...
    DATA(string1) = str.
    &quot;The following sytanx is also possible. However, when you have already addressed the component
    &quot;with the alias as in the assignment above, the following statement shows a syntax warning.
    &quot;DATA(string2) = lif~some_string.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapinterfaces_class.htm"><code>ABSTRACT METHODS</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Specifies instance methods as abstract.</li>
<li>Multiple methods can be specified.</li>
<li>The addition can only be used when the whole class is defined as abstract.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1.
  METHODS meth2.
ENDINTERFACE.

&quot;Local abstract class 
&quot;meth1 is specified as abstract method, meth2 is not.
&quot;Therefore, only meth2 must be implemented. meth1 must
&quot;be implemented by the subclasses.
CLASS lcl1 DEFINITION ABSTRACT.
  PUBLIC SECTION.    
    INTERFACES lif ABSTRACT METHODS meth1.    
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.  
  METHOD lif~meth2.
    ...
  ENDMETHOD.
ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS lif~meth1 REDEFINITION. 
    METHODS lif~meth2 REDEFINITION.       
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD lif~meth1.
    ...
  ENDMETHOD.
  
  METHOD lif~meth2.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapinterfaces_class.htm"><code>ALL METHODS ABSTRACT</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>See above. With this addition, all methods are specified as abstract.</p>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1.
  METHODS meth2.
ENDINTERFACE.

&quot;Local abstract class
&quot;All methods are specified as abstract methods.
&quot;Therefore, all methods of the interface must be implemented
&quot;by the subclasses.
CLASS lcl1 DEFINITION ABSTRACT.
  PUBLIC SECTION.
    INTERFACES lif ALL METHODS ABSTRACT.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.

ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    METHODS lif~meth1 REDEFINITION.
    METHODS lif~meth2 REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.
  METHOD lif~meth1.
    ...
  ENDMETHOD.

  METHOD lif~meth2.
    ...
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapinterfaces_class.htm"><code>FINAL METHODS</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>Specifies methods as final so they cannot be further redefined. Multiple methods can be specified.</p>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1.
  METHODS meth2.
ENDINTERFACE.

&quot;meth1 is specified as final method, meth2 is not.
&quot;Therefore, only meth2 can be further redefined in subclasses.
CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif FINAL METHODS meth1.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.

  METHOD lif~meth1.
    ...
  ENDMETHOD.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    &quot;meth1 cannot be redefined as it is declared as final in the superclass.
    &quot;METHODS lif~meth1 REDEFINITION.
    METHODS lif~meth2 REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapinterfaces_class.htm"><code>ALL METHODS FINAL</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>See above. With this addition, all methods are specified as final.</p>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1.
  METHODS meth2.
ENDINTERFACE.

&quot;All methods are specified as final. Therefore, they
&quot;cannot be further redefined in subclasses.
CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif ALL METHODS FINAL.
    METHODS meth3.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.

  METHOD lif~meth1.
    ...
  ENDMETHOD.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

  METHOD meth3.
    ...
  ENDMETHOD.

ENDCLASS.

CLASS lcl2 DEFINITION INHERITING FROM lcl1.
  PUBLIC SECTION.
    &quot;meth1 and meth2 cannot be redefined.
    &quot;METHODS lif~meth1 REDEFINITION.
    &quot;METHODS lif~meth2 REDEFINITION.
    METHODS meth3 REDEFINITION.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.

  METHOD meth3.
    ...
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABAPINTERFACES_CLASS.html"><code>DATA VALUES</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Used to assign start values for attributes</li>
<li>Works in the style of <code>DATA ... VALUE ...</code> statements, e.g. <code>DATA number TYPE i VALUE 123</code>.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1 RETURNING VALUE(result) TYPE i.
  DATA num1 TYPE i.
  DATA num2 TYPE i.
  CLASS-DATA num3 TYPE i.
ENDINTERFACE.

CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif DATA VALUES num1 = 1 num2 = 3 num3 = 6.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.

  METHOD lif~meth1.
    result = lif~num1 + lif~num2 + lif~num3.
  ENDMETHOD.

ENDCLASS.

CLASS lcl3 DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS meth.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.

  METHOD meth.
    &quot;result: 10
    DATA(result) = NEW lcl1( )-&gt;lif~meth1( ).    
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABAPINTERFACES_PARTIALLY.html"><code>PARTIALLY IMPLEMENTED</code></a></p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Relevant only for test classes.</li>
<li>When you use the <code>PARTIALLY IMPLEMENTED</code> addition in test classes, you are not forced to implement all of the concrete non-optional methods.</li>
<li>It is particularly useful for interfaces to implement test doubles, and not all methods are necessary.</li>
<li>See the <a href="14_ABAP_Unit_Tests.md">ABAP Unit Tests</a> cheat sheet.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>&quot;Test double class in a test include
CLASS ltd_test_double DEFINITION FOR TESTING.
  PUBLIC SECTION.
    INTERFACES some_intf PARTIALLY IMPLEMENTED.    
ENDCLASS.

CLASS ltd_test_double IMPLEMENTATION.
  METHOD some_intf~some_meth.
    ...   
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_default.htm"><code>DEFAULT IGNORE</code></a></p>
<p>&lt;br&gt; (addition used in the interface definition)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Marks the implementation of methods as optional</li>
<li>Defines a default behavior when non-implemented methods are called. When a method with such a declaration is called without an implementation, it behaves as though no implementation exists.</li>
<li>Can only be used in interfaces for instance and static methods (except constructors and test methods)</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1 DEFAULT IGNORE.
  METHODS meth2.
ENDINTERFACE.

CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

&quot;The class implementation does not include the optional
&quot;implementation of lif~meth1.
CLASS lcl1 IMPLEMENTATION.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

ENDCLASS.

CLASS lcl2 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

&quot;The class implementation includes the optional
&quot;implementation of lif~meth1.
CLASS lcl2 IMPLEMENTATION.

  METHOD lif~meth1.
    ...
  ENDMETHOD.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

ENDCLASS.

CLASS lcl3 DEFINITION.
  PUBLIC SECTION.
    class-methods meth3.
ENDCLASS.

&quot;The class implementation includes the optional
&quot;implementation of lif~meth1.
CLASS lcl3 IMPLEMENTATION.

  METHOD meth3.
    DATA(oref1) = NEW lcl1( ).
    DATA(oref2) = NEW lcl2( ).
    
    &quot;Although not implemented, meth1 can be specified to be called. 
    &quot;In this case, it is just like calling a method with empty implementation.
    oref1-&gt;lif~meth1( ).
    oref1-&gt;lif~meth2( ).
    
    &quot;In this class, both methods are implemented.
    oref2-&gt;lif~meth1( ).
    oref2-&gt;lif~meth2( ).
        
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapmethods_default.htm"><code>DEFAULT FAIL</code></a></p>
<p>&lt;br&gt; (addition used in the interface definition)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<p>See above. The behavior with this addition is that when an unimplemented method is called, the <code>CX_SY_DYN_CALL_ILLEGAL_METHOD</code> exception is raised.</p>
<p>&lt;br&gt;</p>
<pre><code>INTERFACE lif.
  METHODS meth1 DEFAULT FAIL.
  METHODS meth2.
ENDINTERFACE.

CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

&quot;The class implementation does not include the optional
&quot;implementation of lif~meth1.
CLASS lcl1 IMPLEMENTATION.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

ENDCLASS.

CLASS lcl2 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

&quot;The class implementation includes the optional
&quot;implementation of lif~meth1.
CLASS lcl2 IMPLEMENTATION.

  METHOD lif~meth1.
    ...
  ENDMETHOD.

  METHOD lif~meth2.
    ...
  ENDMETHOD.

ENDCLASS.

CLASS lcl3 DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS meth3.
ENDCLASS.

CLASS lcl3 IMPLEMENTATION.

  METHOD meth3.
    DATA(oref1) = NEW lcl1( ).
    DATA(oref2) = NEW lcl2( ).

    &quot;Although not implemented, meth1 can be specified to be called.
    &quot;However, with the DEFAULT FAIL addition, an exception is
    &quot;raised.
    TRY.
        oref1-&gt;lif~meth1( ).
      CATCH cx_sy_dyn_call_illegal_method INTO DATA(error).
        DATA(error_text) = error-&gt;get_text( ).
    ENDTRY.
    oref1-&gt;lif~meth2( ).

    &quot;In this class, both methods are implemented.
    oref2-&gt;lif~meth1( ).
    oref2-&gt;lif~meth2( ).

  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Interface Reference Variables, Accessing Objects and Components</h3>
<ul>
<li>Addressing an object happens via an object reference variable with reference to a class.</li>
<li>An interface variable can contain references to objects of classes that implement the corresponding interface.</li>
<li>You create an interface reference variable like this: <code>DATA i_ref TYPE REF TO intf.</code></li>
</ul>
<p>Addressing interface components:</p>
<ul>
<li>Addressing instance components using interface reference variable</li>
<li>attribute: <code>i_ref-&gt;attr</code></li>
<li>instance method: <code>i_ref-&gt;meth( )</code></li>
<li>Addressing instance components using an object reference variable (Note: The type is a class that implements the interface) is also possible but it&#x27;s not the recommended way:</li>
<li>attribute: <code>cl_ref-&gt;intf~attr</code></li>
<li>instance method: <code>cl_ref-&gt;intf~meth</code></li>
<li>Addressing static components:</li>
<li>static attribute: <code>class=&gt;intf~attr</code>,</li>
<li>static method: <code>class=&gt;intf~meth( )</code></li>
<li>constant: <code>intf=&gt;const</code></li>
</ul>
<pre><code>&quot;----------------------- Syntax patterns -----------------------
&quot;Addressing instance interface components using interface reference variable
DATA i_ref TYPE REF TO intf.

DATA cl_ref TYPE REF TO class.

&quot;Creating an instance of a class that implements the interface intf
cl_ref = NEW #( ).

&quot;If the class class implements an interface intf,
&quot;the class reference variable cl_ref can be assigned
&quot;to the interface reference variable i_ref.
&quot;The reference in i_ref then points to the same object
&quot;as the reference in cl_ref.
i_ref = cl_ref.

&quot;Can also be done directly, i. e. directly creating an object to which the interface reference variable points
i_ref = NEW class( ).

&quot;Instance interface method via interface reference variable
... i_ref-&gt;inst_method( ... ) ...

&quot;Instance interface attribute via interface reference variable
... i_ref-&gt;inst_attr ...

&quot;Addressing instance components using the class reference variable
&quot;is also possible but it&#x27;s not the recommended way.
... cl_ref-&gt;intf~inst_method( ... ) ...
... cl_ref-&gt;intf~inst_attr ...

&quot;Addressing static interface components
&quot;class=&gt; can be dropped if the method is called in the same class that implements the interface
... class=&gt;intf~stat_method( ... ) ...
... class=&gt;intf~stat_attr ...

&quot;Just for the record: Static interface components can be called via reference variables, too.
... i_ref-&gt;stat_method( ... ) ...
... i_ref-&gt;stat_attr ...
... cl_ref-&gt;intf~stat_method( ... ) ...

&quot;Constants
&quot;A constant can be addressed using the options mentioned above.
&quot;Plus, it can be addressed using the following pattern
... intf=&gt;const ...</code></pre>
<p>Example using local interfaces and classes</p>
<pre><code>INTERFACE lif.
  METHODS inst_meth.
  CLASS-METHODS stat_meth.
  TYPES c1 TYPE c LENGTH 1.
  DATA inst_num TYPE i.
  CLASS-DATA stat_str TYPE string.
  CONSTANTS const TYPE string VALUE `ABAP`.
ENDINTERFACE.

CLASS lcl1 DEFINITION.
  PUBLIC SECTION.
    INTERFACES lif.
ENDCLASS.

CLASS lcl1 IMPLEMENTATION.

  METHOD lif~inst_meth.
    ...
  ENDMETHOD.

  METHOD lif~stat_meth.
    ...
  ENDMETHOD.

ENDCLASS.

&quot;This class demonstrates addressing interface components
CLASS lcl2 DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS meth.
    DATA flag TYPE lif=&gt;c1.
ENDCLASS.

CLASS lcl2 IMPLEMENTATION.

  METHOD meth.

    &quot;Addressing instance interface components using interface reference variable
    &quot;Interface reference variable
    DATA i_ref TYPE REF TO lif.
    &quot;Object reference variable
    DATA cl_ref TYPE REF TO lcl1.

    &quot;Creating an instance of a class that implements the interface lif
    cl_ref = NEW #( ).

    &quot;If the class lcl1 implements the interface lif,
    &quot;the class reference variable cl_ref can be assigned
    &quot;to the interface reference variable i_ref.
    &quot;The reference in i_ref then points to the same object
    &quot;as the reference in cl_ref.
    i_ref = cl_ref.

    &quot;This can also be done directly, i. e. directly creating an object to
    &quot;which the interface reference variable points
    DATA i_ref2 TYPE REF TO lif.
    i_ref2 = NEW lcl1( ).

    &quot;Instance interface method via interface reference variable
    i_ref-&gt;inst_meth( ).

    &quot;Instance interface attribute via interface reference variable
    DATA(a) = i_ref-&gt;inst_num.

    &quot;Addressing instance components using the class reference variable
    &quot;is also possible but it is not the recommended way.
    cl_ref-&gt;lif~inst_meth( ).
    DATA(b) = cl_ref-&gt;lif~inst_num.

    &quot;Addressing static interface components
    &quot;The class name and =&gt; can be dropped if the method is called in the
    &quot;same class that implements the interface.
    lcl1=&gt;lif~stat_meth( ).
    DATA(c) = lcl1=&gt;lif~stat_str.

    &quot;Note: Static interface components can be called via reference variables, too.
    i_ref-&gt;stat_meth( ).
    DATA(d) = i_ref-&gt;stat_str.
    cl_ref-&gt;lif~stat_meth( ).

    &quot;Constants
    &quot;A constant can be addressed using the options mentioned above.
    &quot;Plus, it can be addressed using the pattern intf=&gt;...
    DATA(e) = lif=&gt;const.
    DATA(f) = i_ref-&gt;const.
    DATA(g) = cl_ref-&gt;lif~const.

    &quot;Types
    DATA h TYPE lif=&gt;c1.
    &quot;Referring to attributes in the interface
    DATA i LIKE lif=&gt;const.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Excursion: Example Interface</h3>
<p>Expand the following collapsible section for example code. To try it out, create a demo interface named <code>zif_some_interface</code>, a class named <code>zcl_demo_abap</code>, and paste the code into the artifacts. After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more information and example code&lt;/summary&gt; &lt;!-- --&gt; &lt;br&gt;</p>
<pre><code>INTERFACE zif_some_interface
  PUBLIC .

  TYPES c3 type c length 3.
  DATA add_result TYPE i.
  CLASS-DATA: subtr_result TYPE i.
  METHODS addition IMPORTING num1          TYPE i
                             num2          TYPE i.
  CLASS-METHODS subtraction IMPORTING num1          TYPE i
                                      num2          TYPE i.

  METHODS meth_ignore DEFAULT IGNORE returning value(int) type i.
  METHODS meth_fail DEFAULT FAIL returning value(int) type i.

ENDINTERFACE.</code></pre>
<p>Example interface:</p>
<p>When you have activated the interface, create the class. The following steps comment on various things.</p>
<p>Example class implementations:</p>
<p>1)</p>
<ul>
<li>When you create the class, you can add <code>INTERFACES zif_some_interface.</code> to the public visibility section.</li>
<li>Using the quick fix in ADT, you can automatically add the method implementation skeletons of the interface methods.</li>
<li>Note that the interface methods declared with <code>DEFAULT IGNORE</code> and <code>DEFAULT FAIL</code> are not automatically added. If implementations are desired, you can manually add the implementations for these methods. See the following step.</li>
<li>The example class also implements the interface <code>if_oo_adt_classrun</code>.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    INTERFACES zif_some_interface.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.
  ENDMETHOD.

  METHOD zif_some_interface~addition.
  ENDMETHOD.

  METHOD zif_some_interface~subtraction.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>2)</p>
<ul>
<li>Adding the implementations for the interface methods declared with <code>DEFAULT IGNORE</code> and <code>DEFAULT FAIL</code>.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    INTERFACES zif_some_interface.
  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.
  ENDMETHOD.

  METHOD zif_some_interface~addition.
  ENDMETHOD.

  METHOD zif_some_interface~subtraction.
  ENDMETHOD.

  METHOD zif_some_interface~meth_fail.
  ENDMETHOD.

  METHOD zif_some_interface~meth_ignore.
  ENDMETHOD.

ENDCLASS.</code></pre>
<p>3)</p>
<ul>
<li>The following example class represents an executable example that displays output in the ADT console.</li>
<li>Apart from simple demo implementations, it includes alias names specified for interface components.</li>
<li>The interface methods declared with <code>DEFAULT IGNORE</code> and <code>DEFAULT FAIL</code> are intentionally not implemented, but the methods are nevertheless called.</li>
<li>Interface method declared with <code>DEFAULT IGNORE</code>: No implementation available, and no value is assigned for the returning parameter. Therefore, the value is initial.</li>
<li>Interface method declared with <code>DEFAULT FAIL</code>: If the exception is not caught, a runtime error occurs.</li>
<li>The example demonstrates method calls using object and interface reference variables.</li>
</ul>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    INTERFACES zif_some_interface.
    ALIASES res FOR zif_some_interface~add_result.
    ALIASES add FOR zif_some_interface~addition.
    ALIASES subtr FOR zif_some_interface~subtraction.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.


CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.

    &quot;Examples using an object reference variable
    DATA(oref) = NEW zcl_demo_abap( ).

    oref-&gt;add( num1 = 1 num2 = 2 ).
    DATA(res1) = oref-&gt;res.
    out-&gt;write( res1 ).

    oref-&gt;subtr( num1 = 1 num2 = 2 ).
    DATA(res2) = oref-&gt;zif_some_interface~subtr_result.
    out-&gt;write( res2 ).

    &quot;Referring to a type declared in the interface
    DATA char_a TYPE zif_some_interface~c3.
    DATA char_b TYPE zif_some_interface=&gt;c3.

    &quot;Calling non-implemented methods
    DATA(int_ig_a) = oref-&gt;zif_some_interface~meth_ignore( ).
    ASSERT int_ig_a = 0.

    TRY.
        DATA(int_fl_a) = oref-&gt;zif_some_interface~meth_fail( ).
      CATCH cx_sy_dyn_call_illegal_method INTO DATA(error).
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.

    &quot;Similar examples using an interface reference variable
    DATA iref TYPE REF TO zif_some_interface.
    iref = NEW zcl_demo_abap( ).

    iref-&gt;addition( num1 = 3 num2 = 5 ).
    DATA(res3) = iref-&gt;add_result.
    out-&gt;write( res3 ).

    iref-&gt;subtraction( num1 = 3 num2 = 5 ).
    DATA(res4) = iref-&gt;subtr_result.
    out-&gt;write( res4 ).

    &quot;Referring to a type declared in the interface
    DATA char_c TYPE iref-&gt;c3.

    &quot;Calling non-implemented methods
    DATA(int_ig_b) = iref-&gt;meth_ignore( ).
    ASSERT int_ig_b = 0.

    TRY.
        DATA(int_fl_b) = iref-&gt;meth_fail( ).
      CATCH cx_sy_dyn_call_illegal_method INTO error.
        out-&gt;write( error-&gt;get_text( ) ).
    ENDTRY.
  ENDMETHOD.

  METHOD add.
    res = num1 + num2.
  ENDMETHOD.

  METHOD subtr.
    zif_some_interface~subtr_result = num1 - num2.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Friendship</h2>
<ul>
<li>The concept of friendship enters the picture if your use case for your classes is to work together very closely. This is true, for example, for [unit</li>
</ul>
<p>tests](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenunit_test_glosry.htm &quot;Glossary Entry&quot;) if you want to test private methods.</p>
<ul>
<li>Classes can grant access to invisible components for their <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenfriend_glosry.htm">friends</a>.</li>
<li>The friends can be other classes and interfaces. In case of interfaces, friendship is granted to all classes that implement the interface.</li>
<li>Impact of friendship:</li>
<li>Access is granted to all components, regardless of the visibility section or the addition <code>READ-ONLY</code>.</li>
<li>Friends of a class can create instances of the class without restrictions.</li>
<li>Friendship is a one-way street, i. e. a class granting friendship to another class is not granted friendship the other way round. If class <code>a</code> grants friendship to class <code>b</code>, class <code>b</code> must also explicitly grant friendship to class <code>a</code> so that <code>a</code> can access the invisible components of class <code>b</code>.</li>
<li>Friendship and inheritance: Heirs of friends and interfaces that contain a friend as a component interface also become friends. However, granting friendship is not inherited, i. e. a friend of a superclass is not automatically a friend of its subclasses.</li>
<li>Additions in the context of granting friendship:</li>
<li><code>FRIENDS</code>: For local classes, e.g. local classes granting friendship to other local classes or the global class of the class pool</li>
<li><code>GLOBAL FRIENDS</code>: Used in global classes to grant friendship to other global classes and interfaces</li>
<li><code>LOCAL FRIENDS</code>: Used for global classes to grant friendship to local classes and interfaces in its own class pool; however, it is a dedicated statement, as shown in the example below (the declaration <code>CLASS zcl_demo_abap DEFINITION LOCAL FRIENDS local_class.</code> in the CCDEF include)</li>
</ul>
<pre><code>&quot;For local classes. Friendship can be granted to all classes/interfaces
&quot;of the same program and the class library.
&quot;Multiple classes can be specified as friends.
CLASS lo_class DEFINITION FRIENDS other_class ... .
...

CLASS lo_class DEFINITION CREATE PRIVATE FRIENDS other_class ... .

&quot;Addition GLOBAL only allowed for global classes, i. e. if the addition PUBLIC is also used
&quot;Other global classes and interfaces from the class library can be specified after GLOBAL FRIENDS.
CLASS global_class DEFINITION CREATE PUBLIC FRIENDS other_global_class ... .</code></pre>
<p>You specify the befriended class in the definition part using a <code>FRIENDS</code> addition:</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Friendship between Global and Local Classes</h3>
<p>Expand the following collapsible section for an example class. It demonstrates granting friendship between a global class and a local class (in the CCIMP include, <em>Local Types</em> tab in ADT). In the example, friendship is granted in both ways so that the global class can access private components of the local class, and the local class can access private components of the global class. For more information, see the following topics:</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclass_local_friends.htm"><code>LOCAL FRIENDS</code></a></li>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclass_deferred.htm"><code>DEFERRED</code></a></li>
</ul>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for more information and example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Class include &lt;/td&gt; &lt;td&gt; Code &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Global class</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Create a new global class (the example uses the name <code>zcl_demo_abap</code>) and copy and paste the following code in the <em>Global Class</em> tab in ADT.</li>
<li>The class has a type and method declaration in the private section. They are used in the local class.</li>
<li>Once activated (and the code of the other includes has been inserted), you can choose <em>F9</em> in ADT to run the class.</li>
<li>When running the class, a method of the local class that is declared in the private section there is called. As a result of this method call, a string is assigned to an attribute that is also declared in the private section of the local class. This attribute is accessed by the global class, and finally displayed in the ADT console.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
  PROTECTED SECTION.
  PRIVATE SECTION.
    TYPES str TYPE string.
    CLASS-METHODS get_hello RETURNING VALUE(hello) TYPE str.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.
    local_class=&gt;say_hello( ).
    DATA(hello) = local_class=&gt;hello.
    out-&gt;write( hello ).
  ENDMETHOD.
  METHOD get_hello.
    hello = `Hello`.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>CCDEF include (Class-relevant Local Types tab in ADT)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>Regarding the includes, see the information in section <a href="#excursion-class-pool-and-include-programs">Excursion: Class Pool and Include Programs</a></li>
<li>The <code>LOCAL FRIENDS</code> addition makes the local class a friend of the global class. The private components of the global class can then be accessed by the local class.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>CLASS local_class DEFINITION DEFERRED.
CLASS zcl_demo_abap DEFINITION LOCAL FRIENDS local_class.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>CCIMP include (Local Types tab in ADT)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<ul>
<li>The <code>FRIENDS</code> addition makes the global class a friend of the local class. The private components of the local class can then be accessed by the global class.</li>
<li>A type declared in the private section of the global class is used to type an attribute.</li>
<li>The method, which is also declared in the private section, includes a method call in the implementation. It is a method declared in the private section of the global class.</li>
</ul>
<p>&lt;br&gt;</p>
<pre><code>CLASS local_class DEFINITION FRIENDS zcl_demo_abap.

  PUBLIC SECTION.
  PROTECTED SECTION.
  PRIVATE SECTION.
    CLASS-DATA hello TYPE zcl_demo_abap=&gt;str.
    CLASS-METHODS say_hello.
    
ENDCLASS.

CLASS local_class IMPLEMENTATION.
  METHOD say_hello.
    hello = |{ zcl_demo_abap=&gt;get_hello( ) } { sy-uname }.|.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Events</h2>
<p>High-level steps involved:</p>
<ul>
<li>Declaring an event</li>
<li>Declaring an event handler</li>
<li>Registering the event handler so that it can handle the event</li>
<li>Raising an event</li>
</ul>
<p><strong>Events</strong></p>
<pre><code>    &quot;Declaration part of a class/interface
    &quot;Instance events
    EVENTS: i_evt1,

            &quot;Events can only have output parameters that are passed by value
            i_evt2 EXPORTING VALUE(num) TYPE i ...
    ...
    &quot;Static events
    CLASS-EVENTS: st_evt1,
                st_evt2 EXPORTING VALUE(num) TYPE i ...</code></pre>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenevent_glosry.htm">Events</a> can trigger the processing of <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenprocessing_block_glosry.htm">processing blocks</a>, meaning they can initiate the execution of event handlers (that is, other methods).</li>
<li>You can declare events in the visibility section of a class or interface declaration, for example:</li>
<li>as an instance event using an <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapevents.htm"><code>EVENTS</code></a> statement.</li>
<li>as a static event using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapclass-events.htm"><code>CLASS-EVENTS</code></a>.</li>
<li>The key difference is that instance events are bound to specific instances of classes and can only be raised by instance methods within the same class. In contrast, static events are independent of class instances and can be raised by any method in the class.</li>
<li>Parameter interface:</li>
<li>In the declaration, you can only specify output parameters (using <code>EXPORTING</code> and formal parameters specifying passing by value) for an event. When the event is raised, actual parameters are transferred to event handlers.</li>
<li>For instance events, the parameter interface includes an implicit output parameter called <code>sender</code>, which is a reference variable that automatically assigns the raising object.</li>
<li>Example declarations:</li>
</ul>
<p><strong>Event handlers</strong></p>
<pre><code>    &quot;Event handlers for instance events
    METHODS: handler_meth1 FOR EVENT i_evt1 OF some_class,

            &quot;Parameter names must be the same as declared;
            &quot;no further additions possible for the parameter (e.g. TYPE);
            &quot;the predefined, implicit parameter sender as another formal parameter is possible with instance events,
            &quot;it is typed as a reference variable, which itself has the class/interface as a static type,
            &quot;If the event handler is called by an instance event, it is passed a reference to the raising object in sender.
            handler_meth2 FOR EVENT i_evt2 OF some_class IMPORTING num sender,
    ...</code></pre>
<ul>
<li>An event is raised by a <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapraise_event.htm"><code>RAISE EVENT</code></a> statement in either another method or the same method.</li>
<li>Raising an event triggers the associated <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenevent_handler_glosry.htm">event handlers</a>, which are special methods that can handle the event.</li>
<li>Typically, event handlers are called indirectly using <code>RAISE EVENT</code> rather than through direct method calls.</li>
<li>To execute an event handler when an event is raised, you must register it with a <code>SET HANDLER</code> statement.</li>
<li>Both static and instance methods can function as event handlers. The main difference is that static event handlers can be called independently of class instances.</li>
<li>Use the <code>FOR EVENT some_event OF cl|intf</code> syntax for event handler declarations.</li>
<li>Pay attention to the visibility section of an event handler; it cannot be more general than the event&#x27;s visibility section.</li>
<li>Case: Instance method as event handler:</li>
<li>If <code>some_event</code> is an instance event, the event handler can manage events for all objects of class <code>cl</code>, its subclasses, or objects implementing the interface <code>intf</code>.</li>
<li>If <code>some_event</code> is a static event, the event handler can manage events for class <code>cl</code>, its subclasses, or any classes implementing the interface <code>intf</code>.</li>
<li>You can also specify additions like <code>ABSTRACT</code> and <code>FINAL</code> for instance methods, and <code>DEFAULT IGNORE</code> or <code>DEFAULT FAIL</code> for both instance and static methods.</li>
<li>Parameter interface:</li>
<li>You can use <code>IMPORTING</code> in the declaration to define input parameters.</li>
<li>You may only specify those formal parameters defined as output parameters with <code>EXPORTING</code> in the declared event.</li>
<li>Specify only the names of the formal parameters without modifiers like <code>TYPE</code>, <code>LIKE</code>, <code>OPTIONAL</code>, or <code>DEFAULT</code>. The properties will be inherited from the specified event parameters.</li>
<li>You do not need to specify all output parameters.</li>
<li>If <code>some_event</code> is an instance event, you can specify the <code>sender</code> formal parameter, which is an implicit output parameter for each instance event, providing a reference to the object that raised the event.</li>
<li>Example declarations:</li>
</ul>
<p><strong>Registering and deregistering event handlers</strong></p>
<ul>
<li>Two syntax forms are available for (de)registering:</li>
<li>For instance event handlers: <code>SET HANDLER handler1 handler2 ... FOR oref|ALL INSTANCES [ACTIVATION act].</code></li>
<li><code>handler1</code>, etc. can be specified as follows: <code>meth</code> (methods from the same or other classes defined as instance event handlers; event handlers for static events cannot be specified), <code>oref-&gt;meth</code>, <code>class=&gt;meth</code></li>
<li><code>FOR oref</code>: (De)registers event handlers for a single object.</li>
<li><code>FOR ALL INSTANCES</code>: (De)registers the event handlers for all instances.</li>
<li><code>ACTIVATION</code>: Used to (de)register event handlers; expects a single-character text field. The default value is <code>X</code> (indicating registration; so, the addition is optional for registration). A blank value means it is deregistered.</li>
<li>For static event handlers: <code>SET HANDLER handler1 handler2 ... [ACTIVATION act].</code></li>
<li>The statement (de)registers static event handlers (not instance events).</li>
<li>Note that the (de)registration is independent of class instances and applies globally to the current <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abeninternal_session_glosry.htm">internal session</a>.</li>
<li><code>SET HANDLER</code> statements set the system field <code>sy-subrc</code>. See the example.</li>
</ul>
<p><strong>Raising events</strong></p>
<ul>
<li>Events are raised using <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapraise_event.htm"><code>RAISE EVENT</code></a> statements, which can only occur within methods.</li>
<li>After raising the event, all registered event handlers are executed, and the program flow continues after the statement.</li>
<li>You can use the <code>EXPORTING</code> addition (<code>RAISE EVENT some_event EXPORTING a = b c = d ...</code>) to pass actual parameters to event handlers.</li>
<li>If the formal parameter <code>sender</code> is declared for an event handler, it automatically receives a reference to the raising object when instance events are triggered. However, <code>sender</code> cannot be explicitly specified or assigned here.</li>
</ul>
<div class="note">[!NOTE] In RAP, special [RAP business events](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenrap_entity_event_glosry.htm) are available. They can be raised in [ABAP behavior pools](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenbehavior_pool_glosry.htm) with [RAISE ENTITY EVENT](https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abapraise_entity_event.htm) statements. Find more information in the [ABAP for RAP: Entity Manipulation Language (ABAP EML)](08_EML_ABAP_for_RAP.md) cheat sheet.</div>
<p>Expand the following collapsible section for example code. To try it out, create a demo class named <code>zcl_demo_abap</code> and paste the code into it. Note that the example includes code in the global class and the CCIMP include (Local Types tab in ADT). After activation, choose <em>F9</em> in ADT to execute the class. The example is set up to display output in the console. For more information on the example, see the inline comments.</p>
<p>&lt;details&gt; &lt;summary&gt;üü¢ Click to expand for example code&lt;/summary&gt; &lt;!-- --&gt;</p>
<p>&lt;br&gt;</p>
<p>&lt;table&gt;</p>
<p>&lt;tr&gt; &lt;td&gt; Class include &lt;/td&gt; &lt;td&gt; Code &lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>Global class</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.
    CLASS-DATA event_log TYPE string_table.
  PROTECTED SECTION.
  PRIVATE SECTION.
    CLASS-METHODS display_log_table IMPORTING out TYPE REF TO if_oo_adt_classrun_out.
ENDCLASS.



CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

*&amp;-------------------------------------------------------------------------------------*
*&amp; 1) Basic events
*&amp;-------------------------------------------------------------------------------------*

    &quot;- The class lcl_greetings in the CCIMP include declares four instance events without parameters.
    &quot;- The same class also defines four event handler methods.
    &quot;- Additionally, a separate method implements the raising of the four events.
    &quot;- The event handlers are registered here using a SET HANDLER statement.
    &quot;- Based on the current time, a string (an instance attribute in the class) is populated with a
    &quot;  greeting appropriate for the time of day.

    out-&gt;write( |1) Basic events\n\n| ).

    DATA(ref_events) = NEW lcl_greetings( ).

    &quot;Registering event handler methods
    SET HANDLER: ref_events-&gt;morning_greets
                 ref_events-&gt;afternoon_greets
                 ref_events-&gt;evening_greets
                 ref_events-&gt;night_greets
                 FOR ref_events.

    &quot;Calling method that raises an event
    ref_events-&gt;greetings( ).

    out-&gt;write( data = ref_events-&gt;greets name = `ref_events-&gt;greets` ).
    out-&gt;write( |\n{ repeat( val = `*` occ = 75 ) }\n| ).

*&amp;-------------------------------------------------------------------------------------*
*&amp; 2) Various instance and static events
*&amp;-------------------------------------------------------------------------------------*

    &quot;- The following code snippets explore various instance and static events.
    &quot;- The class lcl_1 declares instance and static events, some with and some without
    &quot;  exporting parameters.
    &quot;- The class also declares event handlers. The evt_handler_c method intentionally omits the num2
    &quot;  formal parameter, demonstrating that it is optional to specify all parameters. Additionally,
    &quot;  explicitly specifying the sender parameter is also optional.
    &quot;- The example implementations populate a string table for visualization and display purposes.

    out-&gt;write( |2) Various instance and static events\n\n| ).

    DATA(oref_evt_1) = NEW lcl_1( ).
    oref_evt_1-&gt;some_text = `ABAP`.

    &quot;Registering event handler methods
    SET HANDLER: oref_evt_1-&gt;evt_handler_a
                 oref_evt_1-&gt;evt_handler_b
                 oref_evt_1-&gt;evt_handler_c
                 FOR oref_evt_1.

    SET HANDLER: lcl_1=&gt;evt_handler_d lcl_1=&gt;evt_handler_e.

    &quot;Calling method that raises an event
    DO 5 TIMES.
      oref_evt_1-&gt;raise_event( int = sy-index
                               txt = CONV #( sy-index ) ).
    ENDDO.

    out-&gt;write( data = lcl_1=&gt;event_log_lcl_1 name = `lcl_1=&gt;event_log_lcl_1` ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).

*&amp;-------------------------------------------------------------------------------------*
*&amp; SET HANDLER syntax options
*&amp; 3a) Registering events
*&amp;-------------------------------------------------------------------------------------*

    &quot;- The following code snippets demonstrate syntax options of the SET HANDLER statement.
    &quot;- The demo class declares two instance events and a static event. The class also
    &quot;  includes the event handlers. The raise_event method raises a specific event based
    &quot;  on the value of the importing parameter. Note that for simplicity of the example,
    &quot;  the instance method raise_event also raises the static event.

    out-&gt;write( |3) SET HANDLER syntax options\n| ).
    out-&gt;write( |3a) Registering events\n\n| ).

    DATA(oref_evt_2a) = NEW lcl_2( ).

    &quot;Registering instance events
    SET HANDLER oref_evt_2a-&gt;evt_handler_f FOR oref_evt_2a.
    &quot;Optional specification of the ACTIVATION addition. The default value is X.
    SET HANDLER oref_evt_2a-&gt;evt_handler_g FOR oref_evt_2a ACTIVATION &#x27;X&#x27;.
    &quot;Registering static event
    SET HANDLER lcl_2=&gt;evt_handler_h ACTIVATION &#x27;X&#x27;.

    DO 3 TIMES.
      oref_evt_2a-&gt;raise_event( num = sy-index ).
    ENDDO.

    out-&gt;write( data = lcl_2=&gt;event_log_lcl_2 name = `lcl_2=&gt;event_log_lcl_2` ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).
    CLEAR lcl_2=&gt;event_log_lcl_2.

*&amp;-------------------------------------------------------------------------------------*
*&amp; 3b) Deregistering events
*&amp;-------------------------------------------------------------------------------------*

    out-&gt;write( |3b) Deregistering events\n\n| ).

    DATA(oref_evt_2b) = NEW lcl_2( ).

    &quot;Registering and deregistering an instance event handler.
    &quot;Note that the handler for the static event is still registered from above, therefore it
    &quot;is called. In the loop it is explicitly deregistered. So the result will not show two
    &quot;entries for evt_handler_h.
    SET HANDLER oref_evt_2b-&gt;evt_handler_f FOR oref_evt_2b ACTIVATION &#x27;X&#x27;.
    SET HANDLER oref_evt_2b-&gt;evt_handler_g FOR oref_evt_2b ACTIVATION &#x27; &#x27;.

    DO 4 TIMES.
      IF sy-index &lt; 4.
        oref_evt_2b-&gt;raise_event( num = sy-index ).
      ELSE.
        SET HANDLER lcl_2=&gt;evt_handler_h ACTIVATION &#x27; &#x27;.
        oref_evt_2b-&gt;raise_event( num = 3 ).
      ENDIF.
    ENDDO.

    out-&gt;write( data = lcl_2=&gt;event_log_lcl_2 name = `lcl_2=&gt;event_log_lcl_2` ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).

    CLEAR lcl_2=&gt;event_log_lcl_2.

*&amp;-------------------------------------------------------------------------------------*
*&amp; 3c) sy-subrc value setting of SET HANDLER statements
*&amp;-------------------------------------------------------------------------------------*

    &quot;sy-subrc values set by SET HANDLER statements
    &quot;0: All specified handlers were successfully (de)registered.
    &quot;4: At least one specified handler was not registered because it was already registered for the same event.
    &quot;8: At least one specified handler was not deregistered because it was not registered for the current event.

    out-&gt;write( |3c) sy-subrc value setting of SET HANDLER statements\n\n| ).

    DATA(oref_evt_2c) = NEW lcl_2( ).

    SET HANDLER oref_evt_2c-&gt;evt_handler_f
                oref_evt_2c-&gt;evt_handler_g
                FOR oref_evt_2c ACTIVATION &#x27;X&#x27;.

    DATA(subrc) = sy-subrc.

    out-&gt;write( data = subrc name = `sy-subrc value (1)` ).
    out-&gt;write( |\n| ).

    SET HANDLER oref_evt_2c-&gt;evt_handler_f
                oref_evt_2c-&gt;evt_handler_g
                FOR oref_evt_2c ACTIVATION &#x27;X&#x27;.

    subrc = sy-subrc.

    out-&gt;write( data = subrc name = `sy-subrc value (2)` ).
    out-&gt;write( |\n| ).

    SET HANDLER lcl_2=&gt;evt_handler_h ACTIVATION &#x27; &#x27;.
    subrc = sy-subrc.

    out-&gt;write( data = subrc name = `sy-subrc value (3)` ).
    out-&gt;write( |\n| ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).
    CLEAR lcl_2=&gt;event_log_lcl_2.

*&amp;-------------------------------------------------------------------------------------*
*&amp; 3d) FOR ALL INSTANCES addition
*&amp;-------------------------------------------------------------------------------------*

    out-&gt;write( |3d) FOR ALL INSTANCES addition\n| ).

    DATA(oref_evt_2d) = NEW lcl_2( ).

    out-&gt;write( |Example 1\n\n| ).

    &quot;- evt_handler_f is registered for all instances
    &quot;- evt_handler_g and evt_handler_h are not regitered
    SET HANDLER oref_evt_2d-&gt;evt_handler_f FOR ALL INSTANCES.

    &quot;Creating some more instances
    DATA(oref_evt_2e) = NEW lcl_2( ).
    DATA(oref_evt_2f) = NEW lcl_2( ).

    DO 3 TIMES.
      CASE sy-index.
        WHEN 1.
          APPEND `-------------- oref_evt_2d --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2d-&gt;raise_event( num = sy-index ).
          ENDDO.
        WHEN 2.
          APPEND `-------------- oref_evt_2e --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2e-&gt;raise_event( num = sy-index ).
          ENDDO.
        WHEN 3.
          APPEND `-------------- oref_evt_2f --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2f-&gt;raise_event( num = sy-index ).
          ENDDO.
      ENDCASE.
    ENDDO.

    out-&gt;write( data = lcl_2=&gt;event_log_lcl_2 name = `lcl_2=&gt;event_log_lcl_2` ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).
    CLEAR lcl_2=&gt;event_log_lcl_2.

    out-&gt;write( |Example 2\n\n| ).

    &quot;- evt_handler_g is registered for all instances
    &quot;- evt_handler_f is still registered for all instances
    &quot;- evt_handler_h is not regitered
    SET HANDLER oref_evt_2d-&gt;evt_handler_g FOR ALL INSTANCES.

    DO 3 TIMES.
      CASE sy-index.
        WHEN 1.
          APPEND `-------------- oref_evt_2d --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2d-&gt;raise_event( num = sy-index ).
          ENDDO.
        WHEN 2.
          APPEND `-------------- oref_evt_2e --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2e-&gt;raise_event( num = sy-index ).
          ENDDO.
        WHEN 3.
          APPEND `-------------- oref_evt_2f --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2f-&gt;raise_event( num = sy-index ).
          ENDDO.
      ENDCASE.
    ENDDO.

    out-&gt;write( data = lcl_2=&gt;event_log_lcl_2 name = `lcl_2=&gt;event_log_lcl_2` ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).
    CLEAR lcl_2=&gt;event_log_lcl_2.

    out-&gt;write( |Example 3\n\n| ).

    &quot;- Deregistering evt_handler_g and evt_handler_h for all instances
    &quot;- evt_handler_h is not regitered
    SET HANDLER oref_evt_2d-&gt;evt_handler_f
                oref_evt_2d-&gt;evt_handler_g
                FOR ALL INSTANCES ACTIVATION &#x27; &#x27;.

    DO 3 TIMES.
      CASE sy-index.
        WHEN 1.
          APPEND `-------------- oref_evt_2d --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2d-&gt;raise_event( num = sy-index ).
          ENDDO.
        WHEN 2.
          APPEND `-------------- oref_evt_2e --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2e-&gt;raise_event( num = sy-index ).
          ENDDO.
        WHEN 3.
          APPEND `-------------- oref_evt_2f --------------` TO lcl_2=&gt;event_log_lcl_2.
          DO 3 TIMES.
            oref_evt_2f-&gt;raise_event( num = sy-index ).
          ENDDO.
      ENDCASE.
    ENDDO.

    out-&gt;write( data = lcl_2=&gt;event_log_lcl_2 name = `lcl_2=&gt;event_log_lcl_2` ).
    out-&gt;write( |\n{ repeat( val = `*` occ = 75 ) }\n| ).

*&amp;-------------------------------------------------------------------------------------*
*&amp; 4) Excursion: Events in inheritance
*&amp;-------------------------------------------------------------------------------------*

    out-&gt;write( |4) Excursion: Events in inheritance\n| ).
    out-&gt;write( |4a) Static events\n| ).

    &quot;Inheritance tree of the example
    &quot;LCL_A
    &quot;  |
    &quot;  |--LCL_B
    &quot;  |   |
    &quot;  |   |--LCL_C
    &quot;
    &quot;LCL_D: Class that implements event handlers

    &quot;- Static and instance events are declared in the superclass lcl_a.
    &quot;- The superclass and its subclasses contain a static and an instance method that raise the events.
    &quot;- The class lcl_d declares event handlers for the events in lcl_b, which are inherited from the superclass lcl_a.
    &quot;- Different options are used to access methods. A log table visualizes the method call flow.
    &quot;- The event handler stat_evt_handler in class lcl_b can only handle events raised within this class or its subclasses.
    &quot;- Events raised by the static method stat_meth_1 in lcl_a are not handled, regardless of the class name used for the call.
    &quot;- The event handler inst_evt_handler in class lcl_b can also only handle events raised within this class or its subclasses.
    &quot;- Unlike calling static methods, events raised in the inherited instance method inst_meth_1 of the classes lcl_b and lcl_c
    &quot;  are handled. The object&#x27;s class is always addressed when addressig an object using an object reference.

    SET HANDLER lcl_d=&gt;stat_evt_handler.

    lcl_a=&gt;stat_meth_1( ).

    out-&gt;write( |lcl_a=&gt;stat_meth_1( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    lcl_b=&gt;stat_meth_1( ).

    out-&gt;write( |lcl_b=&gt;stat_meth_1( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    lcl_c=&gt;stat_meth_1( ).

    out-&gt;write( |lcl_c=&gt;stat_meth_1( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    lcl_b=&gt;stat_meth_2( ).

    out-&gt;write( |lcl_b=&gt;stat_meth_2( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    lcl_c=&gt;stat_meth_2( ).

    out-&gt;write( |lcl_c=&gt;stat_meth_2( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    lcl_c=&gt;stat_meth_3( ).

    out-&gt;write( |lcl_c=&gt;stat_meth_3( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).
    out-&gt;write( |{ repeat( val = `*` occ = 75 ) }\n| ).

    out-&gt;write( |4b) Instance events\n| ).

    DATA(oref1) = NEW lcl_a( ).
    DATA(oref2) = NEW lcl_b( ).
    DATA(oref3) = NEW lcl_c( ).
    DATA(oref4) = NEW lcl_d( ).

    SET HANDLER oref4-&gt;inst_evt_handler FOR ALL INSTANCES.

    oref1-&gt;inst_meth_1( ).

    out-&gt;write( |oref1-&gt;inst_meth_1( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    oref2-&gt;inst_meth_1( ).

    out-&gt;write( |oref2-&gt;inst_meth_1( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    oref3-&gt;inst_meth_1( ).

    out-&gt;write( |oref3-&gt;inst_meth_1( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    oref2-&gt;inst_meth_2( ).

    out-&gt;write( |oref2-&gt;inst_meth_2( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    oref3-&gt;inst_meth_2( ).

    out-&gt;write( |oref3-&gt;inst_meth_2( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).

    oref3-&gt;inst_meth_3( ).

    out-&gt;write( |oref3-&gt;inst_meth_3( ): &quot;{ lcl_a=&gt;is_handled }&quot;| ).
    display_log_table( out ).
  ENDMETHOD.

  METHOD display_log_table.
    out-&gt;write( data = lcl_a=&gt;log_tab name = `lcl_a=&gt;log_tab` ).
    out-&gt;write( |\n\n| ).
    CLEAR lcl_a=&gt;log_tab.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;tr&gt; &lt;td&gt;</p>
<p>CCIMP include (Local Types tab in ADT)</p>
<p>&lt;/td&gt;</p>
<p>&lt;td&gt;</p>
<pre><code>*&amp;---------------------------------------------------------------------*
*&amp; Example class 1
*&amp;---------------------------------------------------------------------*

&quot;- The class declares four instance events without parameters.
&quot;- The same class also defines four event handler methods.
&quot;- Additionally, a separate method implements the raising of the four events.
&quot;- Based on the current time, a string (an instance attribute in the class) is
&quot;  populated with a greeting appropriate for the time of day.

CLASS lcl_greetings DEFINITION.
  PUBLIC SECTION.
    &quot;Attributes for display purposes
    DATA: greets TYPE string,
          time   TYPE t.

    &quot;Event declarations
    EVENTS: morning,
      afternoon,
      evening,
      night.

    &quot;Event handler methods
    METHODS: morning_greets FOR EVENT morning OF lcl_greetings,
      afternoon_greets FOR EVENT afternoon OF lcl_greetings,
      evening_greets FOR EVENT evening OF lcl_greetings,
      night_greets FOR EVENT night OF lcl_greetings.

    &quot;Method that includes event raising in the implementation
    METHODS: greetings.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl_greetings IMPLEMENTATION.
  METHOD greetings.
    time = cl_abap_context_info=&gt;get_system_time( ).

    IF time BETWEEN &#x27;050001&#x27; AND &#x27;120000&#x27;.
      RAISE EVENT morning.
    ELSEIF time BETWEEN &#x27;120001&#x27; AND &#x27;170000&#x27;.
      RAISE EVENT afternoon.
    ELSEIF time BETWEEN &#x27;170001&#x27; AND &#x27;210000&#x27;.
      RAISE EVENT evening.
    ELSEIF time BETWEEN &#x27;210001&#x27; AND &#x27;050000&#x27;.
      RAISE EVENT night.
    ENDIF.
  ENDMETHOD.

  METHOD morning_greets.
    greets = |Good morning, { sy-uname }. It&#x27;s { time TIME = ENVIRONMENT }.|.
  ENDMETHOD.

  METHOD afternoon_greets.
    greets = |Good afternoon, { sy-uname }. It&#x27;s { time TIME = ENVIRONMENT }.|.
  ENDMETHOD.

  METHOD evening_greets.
    greets = |Good evening, { sy-uname }. It&#x27;s { time TIME = ENVIRONMENT }.|.
  ENDMETHOD.

  METHOD night_greets.
    greets = |Good night, { sy-uname }. It&#x27;s { time TIME = ENVIRONMENT }.|.
  ENDMETHOD.
ENDCLASS.

*&amp;---------------------------------------------------------------------*
*&amp; Example class 2
*&amp;---------------------------------------------------------------------*

&quot;- The class lcl_1 declares instance and static events, some with and some without
&quot;  exporting parameters.
&quot;- The class also declares event handlers. The evt_handler_c method intentionally omits the num2
&quot;  formal parameter, demonstrating that it is optional to specify all parameters. Additionally,
&quot;  explicitly specifying the sender parameter is also optional.
&quot;- The example implementations populate a string table for visualization and display purposes.
&quot;- Note that for simplicity of the example, the instance method raise_event not only raises instance
&quot;  events but also static events.
&quot;- Based on the value of the importing parameter of the raise_event method, a specific event
&quot;  is raised.

CLASS lcl_1 DEFINITION.
  PUBLIC SECTION.
    &quot;Attributes for display purposes
    CLASS-DATA event_log_lcl_1 TYPE string_table.
    DATA some_text TYPE string.

    &quot;Event declarations
    &quot;Instance events
    EVENTS: inst_event_a,
      inst_event_b EXPORTING VALUE(num) TYPE i,
      inst_event_c EXPORTING VALUE(num1) TYPE i VALUE(num2) TYPE i VALUE(text) TYPE string.
    &quot;Static events
    CLASS-EVENTS: stat_event_d,
      stat_event_e EXPORTING VALUE(num) TYPE i.

    &quot;Event handler methods
    &quot;Instance methods
    METHODS: evt_handler_a FOR EVENT inst_event_a OF lcl_1,
      evt_handler_b FOR EVENT inst_event_b OF lcl_1 IMPORTING num,
      evt_handler_c FOR EVENT inst_event_c OF lcl_1 IMPORTING num1 text sender.
    &quot;Static events
    CLASS-METHODS: evt_handler_d FOR EVENT stat_event_d OF lcl_1,
      evt_handler_e FOR EVENT stat_event_e OF lcl_1 IMPORTING num.

    &quot;Method that includes event raising in the implementation
    METHODS raise_event IMPORTING int TYPE i
                                  txt TYPE string.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl_1 IMPLEMENTATION.
  METHOD evt_handler_a.
    APPEND `evt_handler_a: Event handled` TO event_log_lcl_1.
    APPEND INITIAL LINE TO event_log_lcl_1.
  ENDMETHOD.

  METHOD evt_handler_b.
    APPEND `evt_handler_b: Event handled` TO event_log_lcl_1.
    APPEND |Value of num passed: { num }| TO event_log_lcl_1.
    APPEND INITIAL LINE TO event_log_lcl_1.
  ENDMETHOD.

  METHOD evt_handler_c.
    APPEND `evt_handler_c: Event handled` TO event_log_lcl_1.
    APPEND |Value of num1 passed: { num1 }| TO event_log_lcl_1.
    APPEND |Value of text passed: { text }| TO event_log_lcl_1.
    DATA(sender_cl_name) = CAST cl_abap_classdescr( cl_abap_typedescr=&gt;describe_by_object_ref( sender ) )-&gt;get_relative_name( ).
    APPEND |Accessing sender reference variable; class name: { sender_cl_name }| TO event_log_lcl_1.
    APPEND |Value of instance attribute, accessed via sender: { sender-&gt;some_text }| TO event_log_lcl_1.
    APPEND INITIAL LINE TO event_log_lcl_1.
  ENDMETHOD.

  METHOD evt_handler_d.
    APPEND `evt_handler_d: Event handled` TO event_log_lcl_1.
    APPEND INITIAL LINE TO event_log_lcl_1.
  ENDMETHOD.

  METHOD evt_handler_e.
    APPEND `evt_handler_e: Event handled` TO event_log_lcl_1.
    APPEND |Value of num passed: { num }| TO event_log_lcl_1.
    APPEND INITIAL LINE TO event_log_lcl_1.
  ENDMETHOD.

  METHOD raise_event.
    CASE int.
      WHEN 1.
        RAISE EVENT inst_event_a.
      WHEN 2.
        RAISE EVENT inst_event_b EXPORTING num = int.
      WHEN 3.
        RAISE EVENT inst_event_c EXPORTING num1 = int num2 = int text = txt.
      WHEN 4.
        RAISE EVENT stat_event_d.
      WHEN 5.
        RAISE EVENT stat_event_e EXPORTING num = int.
      WHEN OTHERS.
        RAISE EVENT stat_event_d.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*&amp;---------------------------------------------------------------------*
*&amp; Example class 3
*&amp;---------------------------------------------------------------------*

&quot;This example class is setup similarly to lcl_1.

CLASS lcl_2 DEFINITION.
  PUBLIC SECTION.
    &quot;Attribute for display purposes
    CLASS-DATA event_log_lcl_2 TYPE string_table.

    &quot;Event declarations
    EVENTS: inst_event_f,
            inst_event_g.
    CLASS-EVENTS stat_event_h.

    &quot;Event handler methods
    METHODS: evt_handler_f FOR EVENT inst_event_f OF lcl_2,
           evt_handler_g FOR EVENT inst_event_g OF lcl_2.
    CLASS-METHODS: evt_handler_h FOR EVENT stat_event_h OF lcl_2.

    &quot;Method that includes event raising in the implementation
    METHODS raise_event IMPORTING num TYPE i.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS lcl_2 IMPLEMENTATION.
  METHOD evt_handler_f.
    APPEND `evt_handler_f: Event handled` TO event_log_lcl_2.
    APPEND INITIAL LINE TO event_log_lcl_2.
  ENDMETHOD.

  METHOD evt_handler_g.
    APPEND `evt_handler_g: Event handled` TO event_log_lcl_2.
    APPEND INITIAL LINE TO event_log_lcl_2.
  ENDMETHOD.

  METHOD evt_handler_h.
    APPEND `evt_handler_h: Event handled` TO event_log_lcl_2.
    APPEND INITIAL LINE TO event_log_lcl_2.
  ENDMETHOD.

  METHOD raise_event.
    CASE num.
      WHEN 1.
        RAISE EVENT inst_event_f.
      WHEN 2.
        RAISE EVENT inst_event_g.
      WHEN OTHERS.
        RAISE EVENT stat_event_h.
    ENDCASE.
  ENDMETHOD.
ENDCLASS.

*&amp;---------------------------------------------------------------------*
*&amp; Example classes 4 - 7
*&amp;---------------------------------------------------------------------*

    &quot;The example classes 4 - 6 represent an inheritance tree:
    &quot;LCL_A
    &quot;  |
    &quot;  |--LCL_B
    &quot;  |   |
    &quot;  |   |--LCL_C
    &quot;
    &quot;Example class 7, LCL_D, is a class that implements event handlers.

    &quot;Example notes:
    &quot;- Static and instance events are declared in the superclass lcl_a.
    &quot;- The superclass and its subclasses contain a static and an instance
    &quot;  method that raise the events.
    &quot;- The class lcl_d declares event handlers for the events in lcl_b, which
    &quot;  are inherited from the superclass lcl_a.
    &quot;- In the global class, different options are used to access methods.
    &quot;- A log table visualizes the method call flow. Additionally, a flag is
    &quot;  populated illustrating whether the event is handled.

CLASS lcl_a DEFINITION.
  PUBLIC SECTION.
    CLASS-DATA: log_tab TYPE string_table,
                is_handled TYPE abap_boolean.
    CLASS-EVENTS stat_event.
    CLASS-METHODS stat_meth_1.
    EVENTS  inst_event.
    METHODS inst_meth_1.
ENDCLASS.

CLASS lcl_b DEFINITION INHERITING FROM lcl_a.
  PUBLIC SECTION.
    CLASS-METHODS stat_meth_2.
    METHODS inst_meth_2.
ENDCLASS.

CLASS lcl_c DEFINITION INHERITING FROM lcl_b.
  PUBLIC SECTION.
    CLASS-METHODS stat_meth_3.
    METHODS inst_meth_3.
ENDCLASS.

CLASS lcl_d DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS stat_evt_handler FOR EVENT stat_event OF lcl_b.
    METHODS inst_evt_handler FOR EVENT inst_event OF lcl_b.
ENDCLASS.

CLASS lcl_a IMPLEMENTATION.
  METHOD stat_meth_1.
    CLEAR lcl_a=&gt;is_handled.
    APPEND `lcl_a/stat_meth_1 called` TO lcl_a=&gt;log_tab.
    RAISE EVENT stat_event.
  ENDMETHOD.
  METHOD inst_meth_1.
    CLEAR lcl_a=&gt;is_handled.
    APPEND `lcl_a/inst_meth_1 called` TO lcl_a=&gt;log_tab.
    RAISE EVENT inst_event.
  ENDMETHOD.
ENDCLASS.

CLASS lcl_b IMPLEMENTATION.
  METHOD stat_meth_2.
    CLEAR lcl_a=&gt;is_handled.
    APPEND `lcl_b/stat_meth_2 called` TO lcl_a=&gt;log_tab.
    RAISE EVENT stat_event.
  ENDMETHOD.
  METHOD inst_meth_2.
    CLEAR lcl_a=&gt;is_handled.
    APPEND `lcl_b/inst_event called` TO lcl_a=&gt;log_tab.
    RAISE EVENT inst_event.
  ENDMETHOD.
ENDCLASS.

CLASS lcl_c IMPLEMENTATION.
  METHOD stat_meth_3.
    CLEAR lcl_a=&gt;is_handled.
    APPEND `lcl_c/stat_meth_3 called` TO lcl_a=&gt;log_tab.
    RAISE EVENT stat_event.
  ENDMETHOD.
  METHOD inst_meth_3.
    CLEAR lcl_a=&gt;is_handled.
    APPEND `lcl_c/inst_meth_3 called` TO lcl_a=&gt;log_tab.
    RAISE EVENT inst_event.
  ENDMETHOD.
ENDCLASS.

CLASS lcl_d IMPLEMENTATION.
  METHOD stat_evt_handler.
    lcl_a=&gt;is_handled = abap_true.
    APPEND `lcl_d/stat_evt_handler called` TO lcl_a=&gt;log_tab.
    APPEND `--- Static event handled ---` TO lcl_a=&gt;log_tab.
  ENDMETHOD.
  METHOD inst_evt_handler.
    lcl_a=&gt;is_handled = abap_true.
    APPEND `lcl_d/inst_evt_handler called` TO lcl_a=&gt;log_tab.
    APPEND `--- Instance event handled ---` TO lcl_a=&gt;log_tab.
  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;/td&gt; &lt;/tr&gt;</p>
<p>&lt;/table&gt;</p>
<p>&lt;/details&gt;</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>Excursions</h2>
<h3>ABAP Examples of Design Patterns in Object-Oriented Programming</h3>
<p>Find ABAP examples of design patterns in object-oriented programming in <a href="34_OO_Design_Patterns.md">this ABAP cheat sheet</a>.</p>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Class-Based and Classic Exceptions</h3>
<pre><code>  &quot;ABAP class (creates UUIDs) raising a class-based exception
  TRY.
      DATA(uuid) = cl_system_uuid=&gt;create_uuid_x16_static( ).
    CATCH cx_uuid_error.
  ENDTRY.
  
  &quot;ABAP class (provides type information, see the Dynamic Programming cheat sheet)  specified with the EXCEPTIONS addition raising a non-class based exception 
  cl_abap_typedescr=&gt;describe_by_name( EXPORTING p_name = `TYPE_THAT_DOES_NOT_EXIST`
                                       RECEIVING p_descr_ref = DATA(tdo1)
                                       EXCEPTIONS type_not_found = 4 ).

  IF sy-subrc &lt;&gt; 0.
  &quot;Type not found
  ...
  ENDIF.

  cl_abap_typedescr=&gt;describe_by_name( EXPORTING p_name = `ABAP_BOOLEAN`
                                       RECEIVING p_descr_ref = DATA(tdo2)
                                       EXCEPTIONS type_not_found = 4 ).

  ASSERT sy-subrc = 0.</code></pre>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abencatchable_exception_glosry.htm">Catchable exceptions</a> are represented by exception objects of exception classes.</li>
<li>Predefined global exception classes exist, custom exception classes can be created. You can also create local exception classes.</li>
<li>Find an overview in the <a href="27_Exceptions.md">Exceptions and Runtime Errors</a> cheat sheet.</li>
<li>See the following code snippet for an ABAP method that specifies the <code>EXCEPTIONS</code> addition, which should not be specified anymore for new developments, raising classic, non-class based exceptions.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>ABAP Unit Tests</h3>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_unit_glosry.htm">ABAP Unit</a> is a test tool integrated into the ABAP runtime framework.</li>
<li>It can be used to run individual or mass tests, and to evaluate test results.</li>
<li>In ABAP programs, individual unit tests are implemented as <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentest_method_glosry.htm">test methods</a> of local <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abentest_class_glosry.htm">test classes</a>.</li>
<li>Find information on ABAP Unit tests in the <a href="14_ABAP_Unit_Tests.md">ABAP Unit Tests</a> cheat sheet.</li>
</ul>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>ABAP Doc Comments</h3>
<ul>
<li>The ABAP Doc documentation tool allows you to add special ABAP Doc comments to ABAP source code for documentation.</li>
<li>ABAP Doc comments consist of one or more lines starting with <code>&quot;!</code>.</li>
<li>You can place these comments before declarations (e.g., in classes and methods) to document functionality, add notes, and so on.</li>
<li>In ADT, click, for example, on class names or methods to display ABAP Doc comments (if available) in the <em>ABAP Element Info</em> tab, or choose F2 to display the information.</li>
<li>Find more information on ABAP Doc <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENDOCCOMMENT.html">here</a>.</li>
<li>The following example demonstrates ABAP Doc comments, showing various commenting options, including:</li>
<li>Using HTML tags for formatting. Only a selected set of HTML tags is supported. You can choose <em>CTRL + Space</em> in the ABAP Doc comment for input help.</li>
<li>Special tagging and linking options. Refer to the <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENDOCCOMMENT.html">documentation</a> for more details.</li>
<li>Special method signature specifications like <code>@parameter | ...</code> for parameters and <code>@raising | ...</code> for declared exceptions.</li>
</ul>
<pre><code>&quot;! &lt;p class=&quot;shorttext&quot;&gt;Demo ABAP Doc comments&lt;/p&gt;
&quot;!
&quot;! &lt;p&gt;This class serves as an example to illustrate comments for ABAP Doc.
&quot;! The comments begin with the string &lt;strong&gt;&quot;!&lt;/strong&gt;, a special form of regular comments introduced by &lt;strong&gt;&quot;&lt;/strong&gt;. &lt;br/&gt;&lt;br/&gt;
&quot;! The {@link zcl_demo_abap.METH:calculate} method of the example class performs a calculation. &lt;/p&gt;
&quot;! &lt;h2&gt;Notes&lt;/h2&gt;
&quot;! &lt;ul&gt;
&quot;! &lt;li&gt;ABAP Doc documents declarations in ABAP programs.&lt;/li&gt;
&quot;! &lt;li&gt;The ABAP development tools for Eclipse (ADT) support ABAP Doc.&lt;/li&gt;
&quot;! &lt;li&gt;The content of ABAP Doc comments is converted to HTML and displayed appropriately.&lt;/li&gt;
&quot;! &lt;li&gt;Check out the supported HTML tags by choosing &lt;em&gt;CTRL + Space&lt;/em&gt; after &lt;em&gt;&quot;!&lt;/em&gt;. h1 - h3 tags can also be used.&lt;/li&gt;
&quot;! &lt;li&gt;Escaping special characters:  &lt;strong&gt;&amp;quot;, &amp;apos;, &amp;lt;, &amp;gt;, &amp;#64;, &amp;#123;, &amp;#124;, and &amp;#125;&lt;/strong&gt;&lt;/li&gt;
&quot;! &lt;/ul&gt;
&quot;! &lt;h2&gt;Steps&lt;/h2&gt;
&quot;! &lt;ol&gt;
&quot;! &lt;li&gt;Create a demo class named &lt;em&gt;zcl_demo_abap&lt;/em&gt;&lt;/li&gt;
&quot;! &lt;li&gt;Copy and paste the code of this example and activate.&lt;/li&gt;
&quot;! &lt;li&gt;Click the class name to display the comments in the &lt;em&gt;ABAP Element Info&lt;/em&gt; ADT tab.&lt;/li&gt;
&quot;! &lt;li&gt;You can also choose F2 for the class name to display the information.&lt;/li&gt;
&quot;! &lt;/ol&gt;
&quot;! &lt;h3&gt;More examples&lt;/h3&gt;
&quot;! Find more code examples in ABAP cheat sheet demo classes, such as {@link zcl_demo_abap_objects} and others. &lt;br/&gt;
&quot;! &lt;h3&gt;Link examples&lt;/h3&gt;
&quot;! &lt;ul&gt;
&quot;! &lt;li&gt;Repository objects such as the following, and more:
&quot;! &lt;ul&gt;&lt;li&gt;Classes, e.g. {@link zcl_demo_abap}&lt;/li&gt;
&quot;! &lt;li&gt;Interfaces, e.g. {@link if_oo_adt_classrun}&lt;/li&gt;
&quot;! &lt;li&gt;CDS artifacts, e.g. {@link i_apisforclouddevelopment}&lt;/li&gt;
&quot;! &lt;li&gt;DDIC database tables, e.g. {@link zdemo_abap_carr}&lt;/li&gt;
&quot;! &lt;li&gt;DDIC data elements, e.g. {@link land1}&lt;/li&gt;&lt;/ul&gt;
&quot;! &lt;li&gt;Method: {@link zcl_demo_abap.METH:calculate}&lt;/li&gt;
&quot;! &lt;li&gt;Constant: {@link zcl_demo_abap.DATA:const}&lt;/li&gt;
&quot;! &lt;li&gt;Data object: {@link zcl_demo_abap.DATA:dobj}&lt;/li&gt;
&quot;! &lt;li&gt;Method parameter: {@link zcl_demo_abap.METH:calculate.DATA:operator}&lt;/li&gt;
&quot;! &lt;li&gt;Interface implemented in a class: {@link zcl_demo_abap.INTF:if_oo_adt_classrun}&lt;/li&gt;
&quot;! &lt;li&gt;Interface method implemented in a class: {@link zcl_demo_abap.INTF:if_oo_adt_classrun.METH:main}&lt;/li&gt;
&quot;! &lt;li&gt;DDIC domain: {@link DOMA:land1}&lt;/li&gt;
&quot;! &lt;li&gt;XSLT: {@link XSLT:id}&lt;/li&gt;
&quot;! &lt;/ul&gt;
CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

    TYPES: basetype TYPE c LENGTH 1,
           BEGIN OF ENUM t_enum_calc STRUCTURE en_calc BASE TYPE basetype,
             init     VALUE IS INITIAL,
             plus     VALUE &#x27;+&#x27;,
             minus    VALUE &#x27;-&#x27;,
             multiply VALUE &#x27;*&#x27;,
             divide   VALUE &#x27;/&#x27;,
           END OF ENUM t_enum_calc STRUCTURE en_calc.

    &quot;! &lt;p&gt;This method performs a calculation.&lt;/p&gt;
    &quot;! @parameter num1 | First number for the calcation
    &quot;! @parameter num2 | Second number for the calcation
    &quot;! @parameter operator | Operator. Only one of the four operators +, -, *, / is allowed.
    &quot;! The value is represented by an enumerated type. Note that there is no handling if
    &quot;! the assigned value is initial.
    &quot;! @parameter result | Result of the calculation
    &quot;! @raising cx_sy_zerodivide | Zero division
    &quot;! @raising cx_sy_arithmetic_overflow | Arithmetic overflow
    CLASS-METHODS calculate IMPORTING num1          TYPE i
                                      num2          TYPE i
                                      operator      TYPE t_enum_calc
                            RETURNING VALUE(result) TYPE decfloat34
                            RAISING   cx_sy_zerodivide cx_sy_arithmetic_overflow.

    &quot;! This is an ABAP Doc comment for a constant
    CONSTANTS const TYPE i VALUE 123.

    &quot;! This is an ABAP Doc comment for a static attribute of a class
    CLASS-DATA dobj TYPE i.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.
  METHOD if_oo_adt_classrun~main.

    DATA(result_plus) = calculate( num1 = 1 num2 = 10 operator = en_calc-plus ).
    DATA(result_minus) = calculate( num1 = 1 num2 = 10 operator = en_calc-minus ).
    DATA(result_multiply) = calculate( num1 = 2 num2 = 5 operator = en_calc-multiply ).
    DATA(result_divide) = calculate( num1 = 10 num2 = 5 operator = en_calc-divide ).

    out-&gt;write( data = result_plus name = `result_plus` ).
    out-&gt;write( data = result_minus name = `result_minus` ).
    out-&gt;write( data = result_multiply name = `result_multiply` ).
    out-&gt;write( data = result_divide name = `result_divide` ).

  ENDMETHOD.

  METHOD calculate.

    result = SWITCH #( operator
                       WHEN en_calc-plus THEN num1 + num2
                       WHEN en_calc-minus THEN num1 - num2
                       WHEN en_calc-multiply THEN num1 * num2
                       WHEN en_calc-divide THEN num1 / num2
                       ELSE &#x27;0&#x27; ).

  ENDMETHOD.
ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h3>Escape Character</h3>
<ul>
<li>You may encounter <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/ABENNAMES_ESCAPING.html"><code>!</code> characters</a> specified before operands, particularly in signatures of <a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenprocedure_glosry.htm">procedures</a>.</li>
<li>They are used to distinguish the operand&#x27;s name from ABAP words.</li>
<li>When compiling ABAP programs, the specifications with the escape character are not considered as ABAP words.</li>
<li>When executing the programs, the escape characters are ignored.</li>
</ul>
<p>The following nonsensical example shows various specifications with the escape character that emphasize in the program that the operands are not to be confused with ABAP words. These specifications are not mandatory in the example. The example only addresses escape characters you may encounter in (older) ABAP code.</p>
<pre><code>CLASS zcl_demo_abap DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES if_oo_adt_classrun.

    DATA num TYPE i.
    CLASS-DATA default TYPE i VALUE 1.
    METHODS meth1 IMPORTING !num TYPE i.
    METHODS !methods IMPORTING !raising   TYPE i OPTIONAL
                               !optional  TYPE i
                               !exporting TYPE i
                     EXPORTING !importing TYPE i
                               !changing  TYPE i
                     CHANGING  !default   TYPE i.

  PROTECTED SECTION.
  PRIVATE SECTION.

ENDCLASS.

CLASS zcl_demo_abap IMPLEMENTATION.

  METHOD if_oo_adt_classrun~main.
    meth1( 1 ).

    DATA !exporting TYPE i.
    DATA !changing TYPE i.
    methods(
      EXPORTING
        raising   = 1
        optional  = 5
        exporting = 10
      IMPORTING
        importing = !exporting
        changing  = !changing
     CHANGING
        default   = !default
    ).

  ENDMETHOD.
  METHOD meth1.
    DATA(a) = num.
    DATA(b) = me-&gt;num.
    DATA(c) = !num.

    me-&gt;num = num.
    me-&gt;num = !num.
  ENDMETHOD.

  METHOD methods.

    !importing = !raising + !optional.
    !changing = !exporting.
    !default += 1.

    importing = raising + optional.
    changing = exporting.
    default += 1.

  ENDMETHOD.

ENDCLASS.</code></pre>
<p>&lt;p align=&quot;right&quot;&gt;&lt;a href=&quot;#top&quot;&gt;‚¨ÜÔ∏è back to top&lt;/a&gt;&lt;/p&gt;</p>
<h2>More Information</h2>
<p>You can check the subtopics of</p>
<ul>
<li><a href="https://help.sap.com/doc/abapdocu_cp_index_htm/CLOUD/en-US/index.htm?file=abenabap_objects_oview.htm">ABAP Objects - Overview</a></li>
<li><a href="https://help.sap.com/doc/abapdocu_latest_index_htm/latest/en-US/index.htm?file=abenobj_oriented_gdl.htm">Programming Guidlines - Object-Oriented Programming (F1 docu for standard ABAP)</a></li>
</ul>
<p>in the ABAP Keyword Documentation.</p>
<h2>Executable Examples</h2>
<ul>
<li><a href="./src/zcl_demo_abap_objects.clas.abap">zcl_demo_abap_objects</a></li>
<li><a href="./src/zcl_demo_abap_objects_misc.clas.abap">zcl_demo_abap_objects_misc</a>: Additional syntax examples</li>
<li><a href="./src/zcl_demo_abap_oo_inheritance_1.clas.abap">zcl_demo_abap_oo_inheritance_1</a>: Focuses on inheritance; the inheritance tree consists of 4 example classes</li>
</ul>
<div class="note">[!NOTE] - The steps to import and run the code are outlined [here](README.md#-getting-started-with-the-examples). - [Disclaimer](./README.md#%EF%B8%8F-disclaimer)</div>
</body>
</html>